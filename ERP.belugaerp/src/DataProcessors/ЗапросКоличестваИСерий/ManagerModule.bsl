#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

&ИзменениеИКонтроль("ЗаполнитьСерииДляСтрокиТовары")
Процедура бг_ЗаполнитьСерииДляСтрокиТовары(Форма, КлючевыеПараметры, ИндексСтроки, ПараметрыУказанияСерий) Экспорт
	
	ПодборСерий = КлючевыеПараметры.ПодборСерий;
	Объект = Форма.Объект;
	Строка = Объект.Товары[ИндексСтроки];
	
	Если Не ПодборСерий
			Или Строка.СтатусУказанияСерий = 0
			Или Строка.НомерПервойСтрокиСерий <> 0
			Или Строка.Обособленно
				И Не ЗначениеЗаполнено(Строка.Назначение) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура("Номенклатура,Характеристика,Склад,Обособленно");
	ЗаполнитьЗначенияСвойств(СтруктураОтбора, Строка);
	
	ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.Номенклатура, "ВидНоменклатуры");
	
	ВариантПолученияДанных = Обработки.ПодборСерийВДокументы.ВариантПолучениеДанныхИзРегистровПоПараметрамФормы(
		ПараметрыУказанияСерий,
		Неопределено,
		Строка.Склад,
		ВидНоменклатуры);
		
	Если ЭтоАдресВременногоХранилища(Форма.Параметры.АдресТаблицыПодобраноРанее) Тогда
		
		Если КлючевыеПараметры.ПодборТоваров Тогда
			ПодобраноРанее = ПолучитьИзВременногоХранилища(Форма.Параметры.АдресТаблицыПодобраноРанее).Скопировать(СтруктураОтбора);
		Иначе
			ПодобраноРанее = ПолучитьИзВременногоХранилища(Форма.Параметры.АдресТаблицыПодобраноРанее);
		КонецЕсли;
		
	Иначе
		
		ПодобраноРанее = Новый ТаблицаЗначений();
		ПодобраноРанее.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
		ПодобраноРанее.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура",   Строка.Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Строка.Характеристика);
	Запрос.УстановитьПараметр("Назначение",     Строка.Назначение);
	Запрос.УстановитьПараметр("Склад",          Строка.Склад);
	Запрос.УстановитьПараметр("Помещение",      Справочники.СкладскиеПомещения.ПустаяСсылка());
	Запрос.УстановитьПараметр("Регистратор",    Объект.Регистратор);
	Запрос.УстановитьПараметр("Распоряжение",   Неопределено);
	Запрос.УстановитьПараметр("ПодобраноРанее", ПодобраноРанее);
	Запрос.УстановитьПараметр("ВсеСерии",       Истина);
	
	ТекстыЗапроса = Новый Массив();
	ТекстЗапроса = Обработки.ПодборСерийВДокументы.ТекстЗапросаФормированияТаблицыДанныеРегистров(
		ВариантПолученияДанных,
		Строка.Склад);
		
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Таблица.Серия      КАК Серия,
		|	Таблица.Количество КАК Количество
		|ПОМЕСТИТЬ ТаблицаТоваров
		|ИЗ
		|	&ПодобраноРанее КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Таблица.Серия             КАК Серия,
		|	СУММА(Таблица.Количество) КАК Количество
		|ПОМЕСТИТЬ ПодобраноРанее
		|ИЗ
		|	ТаблицаТоваров КАК Таблица
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Серия
		|ИНДЕКСИРОВАТЬ ПО
		|	Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеРегистров.Серия                                                    КАК Серия,
		|	ДанныеРегистров.СвободныйОстаток - ЕСТЬNULL(ПодобраноРанее.Количество,0) КАК Доступно,
		|	&ТекстЗапросаРеквизитыСерийВыборка
		|ИЗ
		|	ДанныеРегистров КАК ДанныеРегистров
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПодобраноРанее КАК ПодобраноРанее
		|		ПО ПодобраноРанее.Серия = ДанныеРегистров.Серия
#Вставка
		|ГДЕ
		|	&ТекстУсловияПодбораСерий 
#КонецВставки
		|УПОРЯДОЧИТЬ ПО
		|	ГоденДо,
		|	Номер";
	
	ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.Номенклатура, "ВидНоменклатуры");
	НастройкиИспользованияСерий = Справочники.ВидыНоменклатуры.НастройкиИспользованияСерий(
		ВидНоменклатуры,
		ПараметрыУказанияСерий,
		Новый Структура("Склад", Строка.Склад));
	Подстановка = Обработки.ПодборСерийВДокументы.ТекстЗапросаРеквизитыСерий("ДанныеРегистров", НастройкиИспользованияСерий, Ложь);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаРеквизитыСерийВыборка", Подстановка);
	
#Вставка
	Если бг_Номенклатура.ВедетсяУчетОстатковИРезервовВРазрезеУпаковокПаллет(Строка.Номенклатура) Тогда
		Запрос.УстановитьПараметр("Характеристика", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
		
		Если ЗначениеЗаполнено(Строка.Характеристика) Тогда
			УстановитьПривилегированныйРежим(Истина);
			УпаковкаПаллета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.Характеристика, "бг_УпаковкаПаллета");
			УстановитьПривилегированныйРежим(Ложь);
		Иначе	
		    УпаковкаПаллета = Неопределено;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(УпаковкаПаллета) Тогда
			Запрос.УстановитьПараметр("УпаковкаПаллета", УпаковкаПаллета);
			ТекстУсловияПодбораСерий = "ДанныеРегистров.Серия.бг_УпаковкаПаллета = &УпаковкаПаллета";
		Иначе	
			ТекстУсловияПодбораСерий = "ИСТИНА";
		КонецЕсли;	
	Иначе	
		ТекстУсловияПодбораСерий = "ИСТИНА";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстУсловияПодбораСерий", ТекстУсловияПодбораСерий);
#КонецВставки
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	ТаблицаОстатков = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаОстатков.Количество() = 0 Тогда
		НомерПервойСтрокиСерий = -1;
	Иначе
		НомерПервойСтрокиСерий = Объект.Серии.Количество() + 1;
	КонецЕсли;
	
	Для Каждого СтрокаСерий Из ТаблицаОстатков Цикл
		
		НоваяСтрокаСерий = Объект.Серии.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаСерий, Строка, "Номенклатура,Характеристика,Склад,Назначение");
		ЗаполнитьЗначенияСвойств(НоваяСтрокаСерий, СтрокаСерий);
		НоваяСтрокаСерий.ПроверятьДоступность = НоваяСтрокаСерий.Доступно > 0;
		
	КонецЦикла;
	
	УправлениеСвойствамиУТ.ЗаполнитьКолонкиДополнительныхРеквизитов(
		Форма,
		"Объект.Серии",
		"Серия");
	
	НайденныеСтроки = Объект.Товары.НайтиСтроки(СтруктураОтбора);
	Для Каждого Строка Из НайденныеСтроки Цикл
		Строка.НомерПервойСтрокиСерий = НомерПервойСтрокиСерий;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
