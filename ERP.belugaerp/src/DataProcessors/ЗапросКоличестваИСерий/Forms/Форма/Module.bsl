#Область ОбработчикиСобытийФормы

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	бг_ДобавитьИзменитьРеквизитыОбщие();
	бг_ДобавитьИзменитьРеквизитыФСМ();
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура бг_ДобавитьИзменитьРеквизитыОбщие()
	ПолеНаименование = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
				ЭтотОбъект,
				"бг_ОстаткиСерийТекущиеСерияНаименование",
				Элементы.Серии,
				"Объект.Серии.Серия.Наименование",
				,
				Элементы.СерииГоденДо);
	ПолеНаименование.Заголовок = НСтр("ru='Серия'");
	
	ПолеСтранаНазначения = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
				ЭтотОбъект,
				"бг_ОстаткиСерийТекущиеСерияСтранаНазначения",
				Элементы.Серии,
				"Объект.Серии.Серия.бг_СтранаНазначения",
				,
				Элементы.СерииГоденДо);
	ПолеСтранаНазначения.Заголовок = НСтр("ru='Страна назначения'");
КонецПроцедуры

&НаСервере
Процедура бг_ДобавитьИзменитьРеквизитыФСМ()
	ВидНоменклатурыФСМ = бг_КонстантыПовтИсп.ЗначениеКонстанты("ФедеральнаяСпецМарка");
	ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Номенклатура, "ВидНоменклатуры");
	Если ЗначениеЗаполнено(ВидНоменклатурыФСМ) И ВидНоменклатуры = ВидНоменклатурыФСМ Тогда
		РеквизитыФСМ = бг_СерииНоменклатуры.РеквизитыФСМ();
		ШаблонИмениРеквизита = "%1ОстаткиСерийТекущиеСерия%2";
		Для каждого Реквизит Из РеквизитыФСМ Цикл
			ПрефиксРеквизита = Лев(Реквизит.Имя, 3);
			ИмяРеквизитаБезПрефикса = Сред(Реквизит.Имя, 4);
			ИмяРеквизита = СтрШаблон(ШаблонИмениРеквизита, ПрефиксРеквизита, ИмяРеквизитаБезПрефикса);
			НовоеПоле = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
				ЭтотОбъект,
				ИмяРеквизита,
				Элементы.Серии,
				"Объект.Серии.Серия." + Реквизит.Имя);
			НовоеПоле.Заголовок = Реквизит.Синоним; 
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 
