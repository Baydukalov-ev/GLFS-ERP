
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Партнер", Партнер);
	Параметры.Свойство("ДатаОтгрузки", ДатаОтгрузки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблица

&НаКлиенте
Процедура ПартнерПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Партнер) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьКаналПродажПоУмолчанию();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ЕстьОшибки = Ложь;
	Если Не ЗначениеЗаполнено(Партнер) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не заполнен партнер';
								|en = 'Partner not filled'"));
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаОтгрузки) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не заполнена дата отгрузки';
								|en = 'Shipment date not filled'"));
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КаналПродаж) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не заполнен канал продаж';
								|en = 'Не заполнен канал продаж'"));
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Если ЕстьОшибки Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Партнер", Партнер);
	Результат.Вставить("ДатаОтгрузки", ДатаОтгрузки);
	Результат.Вставить("КаналПродаж", КаналПродаж);
	Результат.Вставить("Комментарий", Комментарий);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьКаналПродажПоУмолчанию()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаказКлиента.бг_КаналПродаж КАК КаналПродаж
	|ИЗ
	|	Документ.ЗаказКлиента КАК ЗаказКлиента
	|ГДЕ
	|	ЗаказКлиента.Партнер = &Партнер
	|	И ЗаказКлиента.Проведен
	|	И ЗаказКлиента.бг_КаналПродаж <> ЗНАЧЕНИЕ(Справочник.битКаналыПродаж.ПустаяСсылка)
	|	И ЗаказКлиента.бг_ДолгосрочныйРезерв
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаказКлиента.Дата УБЫВ";
	
	Запрос.УстановитьПараметр("Партнер", Партнер);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		КаналПродаж = Выборка.КаналПродаж;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти