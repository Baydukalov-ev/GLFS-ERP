
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КартинкиТиповУпаковок = бг_МаркируемаяПродукция.КартинкиТиповУпаковок();
	ЗначенияТиповУпаковок = бг_МаркируемаяПродукция.ЗначенияТиповУпаковок();
	ДлиныШтрихкодовМарок = бг_МаркируемаяПродукция.ДлиныШтрихкодовМарок();
	
	Параметры.Свойство("ОрганизацияЕГАИС", ОрганизацияЕГАИС);
	Параметры.Свойство("ДетализироватьРезультатДоМарок", ДетализироватьРезультатДоМарок);
	
	ИнициализироватьТабличныйДокумент();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Далее(Команда)

	Если Не ЗначениеЗаполнено(ОрганизацияЕГАИС) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Не заполнена организация ЕГАИС.'"),,
			"ОрганизацияЕГАИС");
		Возврат;
	КонецЕсли;
	
	ЗаполнитьШтрихкодыПоТабличномуДокументу();
	ЗаполнитьТоварыПоШтрихкодам();
	
	Элементы.Шаги.ТекущаяСтраница = Элементы.Шаг2;
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Элементы.Шаги.ТекущаяСтраница = Элементы.Шаг1;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	ШтрихкодыРезультат = ШтрихкодыРезультат();
	Если ШтрихкодыРезультат.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть(ШтрихкодыРезультат);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТоварыПоШтрихкодамПоДаннымМарок(Команда)
	ЗаполнитьТоварыПоШтрихкодам();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ШтрихкодыШтрихкодПриИзменении(Элемент)

	ТекущиеДанные = Элементы.Штрихкоды.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.Штрихкод = СокрЛП(ТекущиеДанные.Штрихкод);
	
	КонтекстЗаполненияДанныхТиповУпаковок = бг_МаркируемаяПродукцияКлиентСервер.КонтекстЗаполненияДанныхТиповУпаковок(
		ЭтаФорма,
		"Штрихкод",
		"ТипУпаковки",
		"КартинкаТипаУпаковки");
	
	бг_МаркируемаяПродукцияКлиентСервер.ЗаполнитьДанныеТипаУпаковкиВСтрокеТаблицы(
		ТекущиеДанные,
		КонтекстЗаполненияДанныхТиповУпаковок);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализироватьТабличныйДокумент()
	
	Макет = Обработки.бг_ЗаполнениеМарокИзТабличногоДокумента.ПолучитьМакет("МакетЗагрузкиДанных");
	
	ТабличныйДокумент.Очистить();
	
	Область = Макет.ПолучитьОбласть("Штрихкод");
	ТабличныйДокумент.Присоединить(Область);
	
	ТабличныйДокумент.ФиксацияСверху = 1;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьШтрихкодыПоТабличномуДокументу()
	
	Штрихкоды.Очистить();
	
	НомераКолонок = Новый Соответствие;
	НомераКолонок.Вставить("КолонкаШтрихкод", "C" + Формат(1, "ЧН=0; ЧГ=0"));
	
	ВысотаШапки = 1;
	НомерСтроки = ВысотаШапки + 1 ;
	СтроковыйНомерСтроки = Формат(НомерСтроки, "ЧН=0; ЧГ=0");
	
	СтроковыйНомерКолонкиШтрихкод = НомераКолонок.Получить("КолонкаШтрихкод");
	
	КонтекстЗаполненияДанныхТиповУпаковок = бг_МаркируемаяПродукцияКлиентСервер.КонтекстЗаполненияДанныхТиповУпаковок(
		ЭтаФорма,
		"Штрихкод",
		"ТипУпаковки",
		"КартинкаТипаУпаковки");
	
	Пока Истина Цикл
		
		ТекстЯчейки = ТабличныйДокумент.Область("R" + СтроковыйНомерСтроки + СтроковыйНомерКолонкиШтрихкод).Текст;
		Штрихкод = СокрЛП(ТекстЯчейки);
		
		Если Не ЗначениеЗаполнено(Штрихкод) Тогда
			Прервать;
		КонецЕсли;
		
		СтрокаШтрихкоды = Штрихкоды.Добавить();
		СтрокаШтрихкоды.Штрихкод = Штрихкод;
		
		бг_МаркируемаяПродукцияКлиентСервер.ЗаполнитьДанныеТипаУпаковкиВСтрокеТаблицы(
			СтрокаШтрихкоды,
			КонтекстЗаполненияДанныхТиповУпаковок);
		
		НомерСтроки = НомерСтроки + 1;
		СтроковыйНомерСтроки = Формат(НомерСтроки, "ЧН=0; ЧГ=0");
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТоварыПоШтрихкодам()
	
	ТоварыПоШтрихкодам.Очистить();
	
	СписокВалидныхШтрихкодов = Новый Массив;
	
	Для Каждого СтрокаШтрихкоды Из Штрихкоды Цикл
		Если ЗначениеЗаполнено(СтрокаШтрихкоды.ТипУпаковки)
			И СписокВалидныхШтрихкодов.Найти(СтрокаШтрихкоды.Штрихкод) = Неопределено Тогда
			СписокВалидныхШтрихкодов.Добавить(СтрокаШтрихкоды.Штрихкод);
		КонецЕсли;
	КонецЦикла;
	
	ТоварыПоШтрихкодамПоДаннымМарок = бг_МаркируемаяПродукция.ДанныеМарокПоШтрихкодам(
		СписокВалидныхШтрихкодов,
		ОрганизацияЕГАИС);
		
	ТоварыПоШтрихкодам.Загрузить(ТоварыПоШтрихкодамПоДаннымМарок);
		
	КонтекстЗаполненияУпаковок = бг_МаркируемаяПродукцияКлиентСервер.КонтекстЗаполненияДанныхТиповУпаковок(
		ЭтаФорма,
		"Штрихкод",
		"ТипУпаковки",
		"КартинкаТипаУпаковки");
		
	КонтекстЗаполненияУпаковокРодитель = бг_МаркируемаяПродукцияКлиентСервер.КонтекстЗаполненияДанныхТиповУпаковок(
		ЭтаФорма,
		"ШтрихкодРодитель",
		"ТипУпаковкиРодителя",
		"КартинкаТипаУпаковкиРодителя");
		
	Для Каждого СтрокаТоварыПоШтрихкодам Из ТоварыПоШтрихкодам Цикл
		
		бг_МаркируемаяПродукцияКлиентСервер.ЗаполнитьДанныеТипаУпаковкиВСтрокеТаблицы(
			СтрокаТоварыПоШтрихкодам,
			КонтекстЗаполненияУпаковок);
		
		бг_МаркируемаяПродукцияКлиентСервер.ЗаполнитьДанныеТипаУпаковкиВСтрокеТаблицы(
			СтрокаТоварыПоШтрихкодам,
			КонтекстЗаполненияУпаковокРодитель);
			
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ШтрихкодыРезультат()
	
	ШтрихкодыРезультат = Новый Массив;
	
	Если ДетализироватьРезультатДоМарок Тогда
		
		ЗаполнитьТоварыПоШтрихкодам();
		
		Для Каждого СтрокаТоварыПоШтрихкодам Из ТоварыПоШтрихкодам Цикл
			Если ЗначениеЗаполнено(СтрокаТоварыПоШтрихкодам.ТипУпаковки)
				И СтрокаТоварыПоШтрихкодам.ТипУпаковки = ЗначенияТиповУпаковок.Бутылка
				И ШтрихкодыРезультат.Найти(СтрокаТоварыПоШтрихкодам.Штрихкод) = Неопределено Тогда
				
				ШтрихкодыРезультат.Добавить(СтрокаТоварыПоШтрихкодам.Штрихкод);
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		Для Каждого СтрокаШтрихкоды Из Штрихкоды Цикл
			Если ЗначениеЗаполнено(СтрокаШтрихкоды.ТипУпаковки)
				И ШтрихкодыРезультат.Найти(СтрокаШтрихкоды.Штрихкод) = Неопределено Тогда
				
				ШтрихкодыРезультат.Добавить(СтрокаШтрихкоды.Штрихкод);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ШтрихкодыРезультат;
	
КонецФункции

#КонецОбласти
