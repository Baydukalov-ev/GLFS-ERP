#Область ПрограммныйИнтерфейс

// Удаляет из маршрутных листов заказы в которых отменены все строки
//
// Параметры:
//  МаршрутныеЛисты - Массив
//  НачалоПериода - Дата
//  КонецПериода - Дата
//
Процедура УдалитьОтмененныеЗаказыИзМаршрутныхЛистов(МаршрутныеЛисты = Неопределено,
					НачалоПериода = Неопределено, КонецПериода = Неопределено) Экспорт
		
	ОтмененныеЗаказы = Документы.битМаршрутныйЛист.ОтмененныеЗаказыМаршрутныхЛистов(
									МаршрутныеЛисты, НачалоПериода, КонецПериода);
	
	МаршрутныеЛисты = ОтмененныеЗаказы.Скопировать(, "МаршрутныйЛист");
	МаршрутныеЛисты.Свернуть("МаршрутныйЛист");
	
	ОтборОтмененныеЗаказы = Новый Структура("МаршрутныйЛист");
	Для Каждого СтрокаМаршрутныйЛист Из МаршрутныеЛисты Цикл
		Попытка
			ОтборОтмененныеЗаказы.МаршрутныйЛист = СтрокаМаршрутныйЛист.МаршрутныйЛист;
			ОтмененныеЗаказыМаршрутногоЛиста = ОтмененныеЗаказы.Скопировать(
						ОтборОтмененныеЗаказы, "Заказ").ВыгрузитьКолонку("Заказ");
						
			УдалитьОтмененныеЗаказыИзМаршрутногоЛиста(
					СтрокаМаршрутныйЛист.МаршрутныйЛист, ОтмененныеЗаказыМаршрутногоЛиста);						
		Исключение
			Событие = НСтр("ru = 'Удаление отмененных заказов из маршрутных листов'");
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ЗаписьЖурналаРегистрации(
						Событие,
						УровеньЖурналаРегистрации.Ошибка, ,
						СтрокаМаршрутныйЛист.МаршрутныйЛист,
						ТекстОшибки);
		КонецПопытки;
	КонецЦикла;	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УдалитьОтмененныеЗаказыИзМаршрутногоЛиста(МаршрутныйЛист, ОтмененныеЗаказы)
	ЗаблокироватьДанныеДляРедактирования(МаршрутныйЛист);
	МаршрутныйЛистОбъект = МаршрутныйЛист.ПолучитьОбъект();
	
	МаршрутныйЛистОбъект.УдалитьОтмененныеЗаказы(ОтмененныеЗаказы);
	МаршрутныйЛистОбъект.Записать(
		?(МаршрутныйЛистОбъект.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));	
КонецПроцедуры

#КонецОбласти
