#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоднятьФлаги(Команда)	
	Для Каждого Строка Из Объект.ПунктыНазначения Цикл
		Строка.Флаг = Истина;	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОпуститьФлаги(Команда)
	Для Каждого Строка Из Объект.ПунктыНазначения Цикл
		Строка.Флаг = Ложь;	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Отобрать(Команда)
	Если ЗначениеЗаполнено(Объект.МенеджерПоиска) Тогда
		ОтобратьНаСервере()
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Заменить(Команда)
	Если ЗначениеЗаполнено(Объект.МенеджерЗамены)
		И ЗначениеЗаполнено(ДатаЗамены) Тогда
		ЗаменитьНаСервере(); 
	КонецЕсли;
КонецПроцедуры

#КонецОбласти //ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИфункции 

&НаСервере
Процедура ОтобратьНаСервере()
	
	Запрос = Новый Запрос;	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИСТИНА КАК Флаг,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(бг_МенеджерыПунктовНазначения.ПунктНазначения.Клиент) = ТИП(Справочник.Контрагенты)
	|			ТОГДА ЕСТЬNULL(Контрагенты.Ссылка, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|		ИНАЧЕ ЕСТЬNULL(Организации.Ссылка, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|	КОНЕЦ КАК Контрагент,
	|	бг_МенеджерыПунктовНазначения.ПунктНазначения КАК ПунктНазначения,
	|	бг_МенеджерыПунктовНазначения.Менеджер КАК Менеджер,
	|	бг_МенеджерыПунктовНазначения.МаршрутТорговогоПредставителя КАК МаршрутТорговогоПредставителя,
	|	бг_МенеджерыПунктовНазначения.ПризнакЭТК КАК ПризнакЭТК
	|ИЗ
	|	РегистрСведений.бг_МенеджерыПунктовНазначения.СрезПоследних(&ДатаЗамены, ) КАК бг_МенеджерыПунктовНазначения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО бг_МенеджерыПунктовНазначения.ПунктНазначения.Клиент = Контрагенты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО бг_МенеджерыПунктовНазначения.ПунктНазначения.Клиент = Организации.Ссылка
	|ГДЕ
	|	бг_МенеджерыПунктовНазначения.Менеджер = &МенеджерПоиска";			   
	
	Запрос.УстановитьПараметр("МенеджерПоиска" , Объект.МенеджерПоиска);
	Запрос.УстановитьПараметр("ДатаЗамены"	   , ДатаЗамены);
	
	УстановитьПривилегированныйРежим(Истина);
	Объект.ПунктыНазначения.Загрузить(Запрос.Выполнить().Выгрузить());
	УстановитьПривилегированныйРежим(Ложь);	
	
КонецПроцедуры 

&НаСервере
Процедура ЗаменитьНаСервере()
	
	ЗаписанныеЗаписи = 0; 
	
	Для Каждого Строка из Объект.ПунктыНазначения Цикл  
		
		Если Строка.Флаг Тогда 
			
			МенеджерЗаписи = РегистрыСведений.бг_МенеджерыПунктовНазначения.СоздатьМенеджерЗаписи();
			
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Строка);	
			
			МенеджерЗаписи.Период   = ДатаЗамены;
			МенеджерЗаписи.Менеджер	= Объект.МенеджерЗамены;
			
			Если ЗначениеЗаполнено(Объект.МаршрутТорговогоПредставителяЗамены) Тогда
				МенеджерЗаписи.МаршрутТорговогоПредставителя = Объект.МаршрутТорговогоПредставителяЗамены;
			КонецЕсли;
 			
			Попытка
				
				МенеджерЗаписи.Записать();
				ЗаписанныеЗаписи = ЗаписанныеЗаписи + 1;
				
			Исключение      
				
				ТекстСообщения = НСтр("ru = 'Записать пункт назначения %1 не удалось. Ошибка: %2.'");
				ОбщегоНазначения.СообщитьПользователю(СтрШаблон(ТекстСообщения,
																Строка.ПунктНазначения,
																ОписаниеОшибки()));  
				
			КонецПопытки;
			
		КонецЕсли;	
		
	КонецЦикла;
	
	ТекстСообщения = НСтр("ru = 'Изменено %1 пунктов назначения.'");
	ОбщегоНазначения.СообщитьПользователю(СтрШаблон(ТекстСообщения, ЗаписанныеЗаписи));
	
КонецПроцедуры

#КонецОбласти //СлужебныеПроцедурыИфункции

