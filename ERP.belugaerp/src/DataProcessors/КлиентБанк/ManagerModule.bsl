#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Загрузка

&ИзменениеИКонтроль("ПодготовитьСтрокиВыписки")
Процедура бг_ПодготовитьСтрокиВыписки(ДокументыКЗагрузке, БанковскийСчет, НомерСчета)
	
	ТипыПлатежныхДокументовПоступления = Новый Соответствие;
	ТипыПлатежныхДокументовПоступления.Вставить("ПЛАТЕЖНОЕПОРУЧЕНИЕ", Перечисления.ТипыПлатежныхДокументов.ПлатежноеПоручение);
	ТипыПлатежныхДокументовПоступления.Вставить("ПЛАТЕЖНОЕТРЕБОВАНИЕ", Перечисления.ТипыПлатежныхДокументов.ПлатежноеТребование);
	ТипыПлатежныхДокументовПоступления.Вставить("ПЛАТЕЖНЫЙОРДЕР", Перечисления.ТипыПлатежныхДокументов.ПлатежныйОрдер);
	ТипыПлатежныхДокументовПоступления.Вставить("БАНКОВСКИЙОРДЕР", Перечисления.ТипыПлатежныхДокументов.ПлатежныйОрдер);
	ТипыПлатежныхДокументовПоступления.Вставить("МЕМОРИАЛЬНЫЙОРДЕР", Перечисления.ТипыПлатежныхДокументов.ПлатежныйОрдер);
	ТипыПлатежныхДокументовПоступления.Вставить("ИНКАССОВОЕПОРУЧЕНИЕ", Перечисления.ТипыПлатежныхДокументов.ИнкассовоеПоручение);
	
	ТипыПлатежныхДокументовСписания = Новый Соответствие;
	ТипыПлатежныхДокументовСписания.Вставить("ПЛАТЕЖНОЕПОРУЧЕНИЕ", Перечисления.ТипыПлатежныхДокументов.ПлатежноеПоручение);
	ТипыПлатежныхДокументовСписания.Вставить("ПЛАТЕЖНОЕТРЕБОВАНИЕ", Перечисления.ТипыПлатежныхДокументов.ПлатежноеТребование);
	ТипыПлатежныхДокументовСписания.Вставить("ПЛАТЕЖНЫЙОРДЕР", Перечисления.ТипыПлатежныхДокументов.ПлатежныйОрдер);
	ТипыПлатежныхДокументовСписания.Вставить("БАНКОВСКИЙОРДЕР", Перечисления.ТипыПлатежныхДокументов.БанковскийОрдер);
	ТипыПлатежныхДокументовСписания.Вставить("МЕМОРИАЛЬНЫЙОРДЕР", Перечисления.ТипыПлатежныхДокументов.БанковскийОрдер);
	ТипыПлатежныхДокументовСписания.Вставить("ИНКАССОВОЕПОРУЧЕНИЕ", Перечисления.ТипыПлатежныхДокументов.ИнкассовоеПоручение);
	
	СтатусыСоставителейТаможенныхПлатежей = Новый Массив;
	СтатусыСоставителейТаможенныхПлатежей.Добавить("06");
	СтатусыСоставителейТаможенныхПлатежей.Добавить("16");
	СтатусыСоставителейТаможенныхПлатежей.Добавить("17");
	СтатусыСоставителейТаможенныхПлатежей.Добавить("18");
	СтатусыСоставителейТаможенныхПлатежей.Добавить("19");
	СтатусыСоставителейТаможенныхПлатежей.Добавить("20");
	
	СобственныеСчета = СобственныеСчета(БанковскийСчет);
#Вставка
	Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчет, "Владелец");
	бг_ИспользоватьДопОтборы = бг_ИспользоватьДопОтборы(Организация);
	Если бг_ИспользоватьДопОтборы Тогда
		ДокументыКЗагрузке.Колонки.Добавить("бг_Загружать", Новый ОписаниеТипов("Булево"));
	КонецЕсли;
#КонецВставки
	
	// Подготовка строк выписки к поиску документов и контрагентов, заполнение общих реквизитов. Первичное распознавание.
	Для каждого СтрокаДокумента Из ДокументыКЗагрузке Цикл
#Вставка
		Если бг_ИспользоватьДопОтборы Тогда
			СтрокаДокумента.бг_Загружать = СтрНайти(СтрокаДокумента.ДанныеВыписки, "бг_Загружать=ИСТИНА") > 0;
		КонецЕсли;
#КонецВставки
		
		Если Не СтрокаДокумента.Загружать Или
			(Не СтрокаДокумента.ПлательщикСчет = НомерСчета
			И Не СтрокаДокумента.ПолучательСчет = НомерСчета) Тогда
			СтрокаДокумента.Загружать = Ложь;
			Продолжить;
		КонецЕсли;
		
		// Входящий или исходящий платеж
		Исходящий = (СтрокаДокумента.ПлательщикСчет = НомерСчета);
		СтрокаДокумента.Исходящий = Исходящий;
		
		// Дата документа
		Если Не ЗначениеЗаполнено(СтрокаДокумента.ДатаДок) Тогда
			СтрокаДокумента.ДатаДок = ДенежныеСредстваКлиентСервер.ПолучитьДатуИзСтроки(СтрокаДокумента.Дата);
			Если Не ЗначениеЗаполнено(СтрокаДокумента.ДатаДок) Тогда
				СтрокаДокумента.ДатаДок = ДенежныеСредстваКлиентСервер.ПолучитьДатуИзСтроки(
					?(Исходящий, СтрокаДокумента.ДатаСписано, СтрокаДокумента.ДатаПоступило));
			КонецЕсли;
		КонецЕсли;
		
		// Тип платежного документа
		ТипСтрокой = СтрЗаменить(ВРЕГ(СтрЗаменить(СокрЛП(СтрокаДокумента.Операция), " ", "")), "Ё", "Е");
		Если Исходящий Тогда
			ТипПлатежногоДокумента = ТипыПлатежныхДокументовСписания.Получить(ТипСтрокой);
		Иначе
			ТипПлатежногоДокумента = ТипыПлатежныхДокументовПоступления.Получить(ТипСтрокой);
		КонецЕсли;
		Если ТипПлатежногоДокумента = Неопределено Тогда
			ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов.ПлатежноеПоручение;
		КонецЕсли;

		СтрокаДокумента.ТипПлатежногоДокумента = ТипПлатежногоДокумента;
		
		// Хоз. операция по виду оплаты
		Если СтрокаДокумента.ВидОплаты = "03" Тогда
			СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.СнятиеНаличныхДенежныхСредств;
			
		ИначеЕсли СтрокаДокумента.ВидОплаты = "04" Тогда
			СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет;
			
		ИначеЕсли СтрокаДокумента.ВидОплаты = "13" Тогда
			Если Исходящий Тогда
				СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ВозвратОплатыНаПлатежнуюКарту;
			Иначе
				СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте;
			КонецЕсли;
		КонецЕсли;
		
		// Переводы
		Если СтрокаДокумента.Исходящий Тогда
			
			Если ЗначениеЗаполнено(СтрокаДокумента.ПолучательБИК) Тогда
				СтруктураПоиска = Новый Структура("НомерСчета, БИК", СтрокаДокумента.ПолучательСчет, СтрокаДокумента.ПолучательБИК);
			ИначеЕсли ЗначениеЗаполнено(СтрокаДокумента.ПолучательСВИФТ) Тогда
				СтруктураПоиска = Новый Структура("НомерСчета, СВИФТ", СтрокаДокумента.ПолучательСчет, СтрокаДокумента.ПолучательСВИФТ);
			Иначе
				СтруктураПоиска = Новый Структура("НомерСчета", СтрокаДокумента.ПолучательСчет);
			КонецЕсли;
			
			СобственныеСчетаПолучатели = СобственныеСчета.НайтиСтроки(СтруктураПоиска);
			Если СобственныеСчетаПолучатели.Количество() > 0 Тогда
				СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет;
				СтрокаДокумента.СчетКонтрагента = СобственныеСчетаПолучатели[0]["Ссылка"];
				Если Не ЗначениеЗаполнено(СтрокаДокумента.ДатаСписано) Тогда
					СтрокаДокумента.Загружать = Ложь;
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		Иначе
			
			Если ЗначениеЗаполнено(СтрокаДокумента.ПлательщикБИК) Тогда
				СтруктураПоиска = Новый Структура("НомерСчета, БИК", СтрокаДокумента.ПлательщикСчет, СтрокаДокумента.ПлательщикБИК);
			ИначеЕсли ЗначениеЗаполнено(СтрокаДокумента.ПлательщикСВИФТ) Тогда
				СтруктураПоиска = Новый Структура("НомерСчета, СВИФТ", СтрокаДокумента.ПлательщикСчет, СтрокаДокумента.ПлательщикСВИФТ);
			Иначе
				СтруктураПоиска = Новый Структура("НомерСчета", СтрокаДокумента.ПлательщикСчет);
			КонецЕсли;
			
			СобственныеСчетаОтправители = СобственныеСчета.НайтиСтроки(СтруктураПоиска);
			Если СобственныеСчетаОтправители.Количество() > 0 Тогда
				СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствСДругогоСчета;
				СтрокаДокумента.СчетКонтрагента = СобственныеСчетаОтправители[0]["Ссылка"];
				Если Не ЗначениеЗаполнено(СтрокаДокумента.ДатаПоступило) Тогда
					СтрокаДокумента.Загружать = Ложь;
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Инкассация
		Если Не ЗначениеЗаполнено(СтрокаДокумента.ВидОперации) Тогда
			
			БалансовыйСчет = Лев(СтрокаДокумента.ПлательщикСчет, 5);
			Если БалансовыйСчет    = "20202" // поступление/списание наличных из кассы
				Или БалансовыйСчет = "20208" // поступление/списание наличных из банкомата, устройства Cash-in
			Тогда
				Если СтрокаДокумента.Исходящий Тогда
					СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.СнятиеНаличныхДенежныхСредств;
				Иначе
					СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Контрагент
		Если Исходящий Тогда
			Если СтрокаДокумента.Получатель1 <> "" Тогда
				СтрокаДокумента.ИмяКонтрагента = СтрокаДокумента.Получатель1;
			Иначе
				СтрокаДокумента.ИмяКонтрагента = СтрокаДокумента.Получатель;
			КонецЕсли;
		Иначе
			Если СтрокаДокумента.Плательщик1 <> "" Тогда
				СтрокаДокумента.ИмяКонтрагента = СтрокаДокумента.Плательщик1;
			Иначе
				СтрокаДокумента.ИмяКонтрагента = СтрокаДокумента.Плательщик;
			КонецЕсли;
		КонецЕсли;
		СтруктураНаименования = ДенежныеСредстваСерверЛокализация.НаименованиеОрганизации(СтрокаДокумента.ИмяКонтрагента);
		СтрокаДокумента.СокрИмяКонтрагента = СтруктураНаименования.СокращенноеНаименование;
		
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Лев(СтрокаДокумента.ИмяКонтрагента, 10)) Тогда
			Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Лев(СтрокаДокумента.ИмяКонтрагента, 12)) Тогда
				СтрокаДокумента.ИмяКонтрагента = СокрЛП(Сред(СтрокаДокумента.ИмяКонтрагента, 13));
			Иначе
				СтрокаДокумента.ИмяКонтрагента = СокрЛП(Сред(СтрокаДокумента.ИмяКонтрагента, 11));
			КонецЕсли;
		КонецЕсли;
		
		ИННДляПоиска = ?(Исходящий, СтрокаДокумента.ПолучательИНН, СтрокаДокумента.ПлательщикИНН);
		СтрокаДокумента.ПроверятьИНН = ЗначениеЗаполнено(ИННДляПоиска);
	
		КППДляПоиска = ?(Исходящий, СтрокаДокумента.ПолучательКПП, СтрокаДокумента.ПлательщикКПП);
		СтрокаДокумента.ПроверятьКПП = (ЗначениеЗаполнено(КППДляПоиска) И КППДляПоиска <> "0");
		
		// УИП
		Если ЗначениеЗаполнено(СтрокаДокумента.Код) Тогда
			СтрокаДокумента.ЗаполненУИП = Истина;
		КонецЕсли;
		
		// Назначение платежа
		Если ПустаяСтрока(СтрокаДокумента.НазначениеПлатежа) Тогда
			СтрокаДокумента.НазначениеПлатежа = СтрокаДокумента.НазначениеПлатежа1;
			Для инд = 2 По 6 Цикл
				Если НЕ ЗначениеЗаполнено(СтрокаДокумента["НазначениеПлатежа" + инд]) Тогда
					Прервать;
				КонецЕсли;
				СтрокаДокумента.НазначениеПлатежа =
					СтрокаДокумента.НазначениеПлатежа + Символы.ПС + СтрокаДокумента["НазначениеПлатежа" + инд];
			КонецЦикла;
		КонецЕсли;
		
		СтрокаДокумента.ФорматированноеНазначениеПлатежа =
			ДенежныеСредстваСервер.ФорматироватьНазначениеПлатежа(СтрокаДокумента.НазначениеПлатежа);
		
		// Сумма
		Сумма = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СтрокаДокумента.Сумма);
		Если Сумма <> Неопределено Тогда
			Если Сумма < 0 Тогда
				Сумма = - Сумма;
			КонецЕсли;
			СтрокаДокумента.СуммаДокумента = Сумма;
			Если Исходящий Тогда
				СтрокаДокумента.СуммаСписано   = Сумма;
			Иначе
				СтрокаДокумента.СуммаПоступило = Сумма;
			КонецЕсли;
		Иначе
			ДобавитьЗамечание(СтрокаДокумента.ОшибкиЗагрузки,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Неверная сумма документа (%1)!';
						|en = 'Incorrect document amount (%1).'"), СтрокаДокумента.Сумма));
		КонецЕсли;
		
		//++ Локализация
		// Очередность платежа
		Буфер = СокрЛП(СтрокаДокумента.Очередность);
		Если Буфер <> "" И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Буфер) Тогда
			СтрокаДокумента.ОчередностьПлатежа = Число(Буфер);
		Иначе
			СтрокаДокумента.ОчередностьПлатежа = 5;
		КонецЕсли;
		
		// Платеж в бюджет
		Если ЗначениеЗаполнено(СтрокаДокумента.СтатусСоставителя) Тогда
			СтрокаДокумента.ПлатежВБюджет = Истина;
			
			Если СтатусыСоставителейТаможенныхПлатежей.Найти(СтрокаДокумента.СтатусСоставителя) <> Неопределено Тогда
				СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПеречислениеТаможне;
				СтрокаДокумента.ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.ТаможенныйПлатеж;
			ИначеЕсли СтрокаДокумента.СтатусСоставителя = "08" Тогда
				СтрокаДокумента.ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.ИнойПлатеж;
			Иначе
				СтрокаДокумента.ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.НалоговыйПлатеж;
			КонецЕсли;
			
			Если СтрокаДокумента.ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.НалоговыйПлатеж И Исходящий Тогда
				СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет;
			ИначеЕсли СтрокаДокумента.ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.НалоговыйПлатеж И Не Исходящий Тогда
				СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ВозвратНалогов;
			ИначеЕсли СтрокаДокумента.ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.ТаможенныйПлатеж И Исходящий Тогда
				СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПеречислениеТаможне;
			ИначеЕсли СтрокаДокумента.ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.ИнойПлатеж И Исходящий Тогда
				СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств;
			КонецЕсли;
		КонецЕсли;
		
		// Показатель даты бюджетного платежа
		Если НЕ ПустаяСтрока(СтрокаДокумента.ПоказательДаты) Тогда
			СтрокаДокумента.ПоказательДатыДок = ДенежныеСредстваКлиентСервер.ПолучитьДатуИзСтроки(СтрокаДокумента.ПоказательДаты);
			Если НЕ ЗначениеЗаполнено(СтрокаДокумента.ПоказательДатыДок) Тогда
				СтрокаДокумента.ПоказательДатыДок = Неопределено;
			КонецЕсли;
		КонецЕсли;
		//-- Локализация
		
		// Дата проведения банком
		Если Исходящий Тогда
			Если Не ЗначениеЗаполнено(СтрокаДокумента.Списано) Тогда
				СтрокаДокумента.Списано = ДенежныеСредстваКлиентСервер.ПолучитьДатуИзСтроки(
					СтрокаДокумента.ДатаСписано);
				Если ЗначениеЗаполнено(СтрокаДокумента.Списано) Тогда
					СтрокаДокумента.ДатаПроведения = СтрокаДокумента.Списано;
				КонецЕсли;
			КонецЕсли;
		Иначе
			Если Не ЗначениеЗаполнено(СтрокаДокумента.Поступило) Тогда
				СтрокаДокумента.Поступило = ДенежныеСредстваКлиентСервер.ПолучитьДатуИзСтроки(
					СтрокаДокумента.ДатаПоступило);
				Если ЗначениеЗаполнено(СтрокаДокумента.Поступило) Тогда
					СтрокаДокумента.ДатаПроведения = СтрокаДокумента.Поступило;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		//++ Локализация
		// Дата отсылки платежного требования
		Если Не ПустаяСтрока(СтрокаДокумента.ДатаОтсылкиДок) Тогда
			СтрокаДокумента.ДатаОтсылки = ДенежныеСредстваКлиентСервер.ПолучитьДатуИзСтроки(СтрокаДокумента.ДатаОтсылкиДок);
		КонецЕсли;
		//-- Локализация
#Вставка
		Если бг_ИспользоватьДопОтборы Тогда
			бг_ПереопределитьРеквизиты(СтрокаДокумента);
		КонецЕсли;
#КонецВставки
	КонецЦикла;
	
	// Ранее загруженные/введенные вручную документы
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	ТаблицаДокументов = ДокументыКЗагрузке.Скопировать(
		СтрокиКЗагрузке,
		"НомерСтроки, ДатаДок, Поступило, Списано, Номер, НомерСокр, Исходящий, ПолучательСчет, ПлательщикСчет, ТипПлатежногоДокумента,
		|ПолучательИНН, ПлательщикИНН, ПолучательКПП, ПлательщикКПП, ПроверятьИНН, ПроверятьКПП, ДанныеВыписки, Код, Операция");
	ЗаполнитьСсылкиНаДокументы(ДокументыКЗагрузке, ТаблицаДокументов, БанковскийСчет);
	
#Вставка
	бг_ЗаполнитьСсылкиНаДокументы(ДокументыКЗагрузке, ТаблицаДокументов, БанковскийСчет);
#КонецВставки
	// Основание платежа по УИП
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	Отбор.Вставить("Исходящий", Ложь);
	Отбор.Вставить("НайденДокументВБазе", Ложь);
	Отбор.Вставить("ЗаполненУИП", Истина);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	Если СтрокиКЗагрузке.Количество() Тогда
		ТаблицаДокументов = ДокументыКЗагрузке.Скопировать(СтрокиКЗагрузке, "НомерСтроки, Код");
		ЗаполнитьОснованиеПлатежейПоУИП(ДокументыКЗагрузке, ТаблицаДокументов, БанковскийСчет);
	КонецЕсли;
	
	// Контрагенты, счета контрагентов
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	ТаблицаДокументов = ДокументыКЗагрузке.Скопировать(
		СтрокиКЗагрузке,
		"НомерСтроки, Исходящий, ИмяКонтрагента, СокрИмяКонтрагента, Контрагент,
		|ПолучательСчет, ПлательщикСчет, ПолучательИНН, ПлательщикИНН, ПолучательКПП, ПлательщикКПП, ПроверятьИНН, ПроверятьКПП, ВидОперации");
	ЗаполнитьКонтрагентов(ДокументыКЗагрузке, ТаблицаДокументов);
	
	// Партнеры
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	Отбор.Вставить("НайденКонтрагент", Истина);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	ТаблицаДокументов = ДокументыКЗагрузке.Скопировать(
		СтрокиКЗагрузке,
		"НомерСтроки, Контрагент");
	ЗаполнитьПартнеров(ДокументыКЗагрузке, ТаблицаДокументов, БанковскийСчет);
	
	// Образцы документов для заполнения
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	Отбор.Вставить("НайденДокументВБазе", Ложь);
	Отбор.Вставить("НайденоОснованиеПлатежа", Ложь);
	Отбор.Вставить("НайденСчетКонтрагента", Истина);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	ЗаполнитьОбразцы(СтрокиКЗагрузке, БанковскийСчет);
	
	// Заполнение по косвенным данным
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	Отбор.Вставить("НайденДокументВБазе", Ложь);
	Отбор.Вставить("НайденоОснованиеПлатежа", Ложь);
	Отбор.Вставить("НайденОбразец", Ложь);
	Отбор.Вставить("НайденКонтрагент", Истина);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	ЗаполнитьПоКосвеннымДанным(СтрокиКЗагрузке, БанковскийСчет);
	
	// Поиск ПКО при инкассации
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	Отбор.Вставить("НайденДокументВБазе", Ложь);
	Отбор.Вставить("НайденоОснованиеПлатежа", Ложь);
	Отбор.Вставить("НайденОбразец", Ложь);
	Отбор.Вставить("ВидОперации", Перечисления.ХозяйственныеОперации.СнятиеНаличныхДенежныхСредств);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	ТаблицаДокументов = ДокументыКЗагрузке.Скопировать(
		СтрокиКЗагрузке,
		"НомерСтроки, СуммаДокумента, ДатаПроведения");
	ЗаполнитьКассовыйДокумент(ДокументыКЗагрузке, ТаблицаДокументов, БанковскийСчет, Истина);
	
	// Поиск РКО при инкассации
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	Отбор.Вставить("НайденДокументВБазе", Ложь);
	Отбор.Вставить("НайденоОснованиеПлатежа", Ложь);
	Отбор.Вставить("НайденОбразец", Ложь);
	Отбор.Вставить("ВидОперации", Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	ТаблицаДокументов = ДокументыКЗагрузке.Скопировать(
		СтрокиКЗагрузке,
		"НомерСтроки, СуммаДокумента, ДатаПроведения");
	ЗаполнитьКассовыйДокумент(ДокументыКЗагрузке, ТаблицаДокументов, БанковскийСчет, Ложь);
	
КонецПроцедуры

&ИзменениеИКонтроль("ОбновитьДокументы")
Процедура бг_ОбновитьДокументы(ДокументыКЗагрузке, БанковскийСчет, СоздаватьКонтрагентов, ПроводитьДокументы)
	
	Перем РеквизитыВсе, РеквизитыХозОперации;
	
	Отбор = Новый Структура("БанковскийСчет, Загружать, НайденДокументВБазе",
		БанковскийСчет, Истина, Истина);
#Вставка
		Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчет, "Владелец");
		Если бг_ИспользоватьДопОтборы(Организация) Тогда
			Отбор.Вставить("бг_Загружать", Истина);
		КонецЕсли;
#КонецВставки
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	
	Для каждого СтрокаДокумента Из СтрокиКЗагрузке Цикл
		
		ДокументОбъект = СтрокаДокумента.Документ.ПолучитьОбъект();
		
		СделатьНепроведенным = Ложь;
		
#Вставка
		бг_УстановитьСвойствоВставка(ДокументОбъект, "ХозяйственнаяОперация", СтрокаДокумента.ВидОперации, СделатьНепроведенным);
		бг_ЗаполнитьДоговорВДокументе(ДокументОбъект);
#КонецВставки
		
		УстановитьСвойство(ДокументОбъект, "ПроведеноБанком", Истина);
		УстановитьСвойство(ДокументОбъект, "ДатаПроведенияБанком", СтрокаДокумента.ДатаПроведения);
		
		ЗаменитьСтароеЗначение = Ложь;
		//++ Локализация
		ЗаменитьСтароеЗначение = (СтрокаДокумента.Операция <> "Операция по Яндекс.Кассе");
		//-- Локализация
		УстановитьСвойство(ДокументОбъект, "НомерВходящегоДокумента", СтрокаДокумента.Номер, ЗаменитьСтароеЗначение);
		
		Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПоступлениеБезналичныхДенежныхСредств") Тогда
			Документы.ПоступлениеБезналичныхДенежныхСредств.ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(
				ДокументОбъект.ХозяйственнаяОперация, РеквизитыВсе, РеквизитыХозОперации);
				
		ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СписаниеБезналичныхДенежныхСредств") Тогда
			Документы.СписаниеБезналичныхДенежныхСредств.ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(
				ДокументОбъект, РеквизитыВсе, РеквизитыХозОперации);
		КонецЕсли;
		
		// Контрагент
		Если РеквизитыХозОперации.Найти("Контрагент") <> Неопределено И Не ЗначениеЗаполнено(ДокументОбъект.Контрагент) Тогда
			Если ЗначениеЗаполнено(СтрокаДокумента.Контрагент) Тогда
				УстановитьСвойство(ДокументОбъект, "Контрагент", СтрокаДокумента.Контрагент);
			ИначеЕсли СоздаватьКонтрагентов И СтрокаДокумента.СоздаватьКонтрагента Тогда
				УстановитьСвойство(ДокументОбъект, "Контрагент", СоздатьКонтрагента(ДокументыКЗагрузке, СтрокаДокумента));
			Иначе
				УстановитьСвойство(ДокументОбъект, "ИмяКонтрагента", СтрокаДокумента.ИмяКонтрагента);
			КонецЕсли;
		КонецЕсли;
		
		// Счет контрагента
		Если РеквизитыХозОперации.Найти("БанковскийСчетКонтрагента") <> Неопределено И Не ЗначениеЗаполнено(ДокументОбъект.БанковскийСчетКонтрагента) Тогда
			Если ЗначениеЗаполнено(СтрокаДокумента.СчетКонтрагента) Тогда
				УстановитьСвойство(ДокументОбъект, "БанковскийСчетКонтрагента", СтрокаДокумента.СчетКонтрагента);
			ИначеЕсли ЗначениеЗаполнено(ДокументОбъект.Контрагент) Тогда
				УстановитьСвойство(ДокументОбъект, "БанковскийСчетКонтрагента",
				СоздатьБанковскийСчетКонтрагента(ДокументыКЗагрузке, СтрокаДокумента, ДокументОбъект.Контрагент));
			КонецЕсли;
		КонецЕсли;
		
		СтатьяДвиженияДенежныхСредств =
			Справочники.СтатьиДвиженияДенежныхСредств.СтатьяДвиженияДенежныхСредствПоХозяйственнойОперации(ДокументОбъект.ХозяйственнаяОперация);
		
		Для каждого СтрокаРасшифровки Из ДокументОбъект.РасшифровкаПлатежа Цикл
			
			// Партнер
			Если Не ЗначениеЗаполнено(СтрокаРасшифровки.Партнер) Тогда
				СтрокаРасшифровки.Партнер = СтрокаДокумента.Партнер;
			КонецЕсли;
			
			РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
																	СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
																	ДокументОбъект.Валюта,
																	СтрокаРасшифровки.ВалютаВзаиморасчетов,
																	ДокументОбъект.Организация,
																	ДокументОбъект.Дата);
			
			// Статья ДДС
			Если РеквизитыХозОперации.Найти("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств") <> Неопределено
				И Не ЗначениеЗаполнено(СтрокаРасшифровки.СтатьяДвиженияДенежныхСредств) Тогда
				
				Если ЗначениеЗаполнено(СтрокаРасшифровки.ОбъектРасчетов) Тогда
					
					РеквизитыОбъектаРасчетов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
									СтрокаРасшифровки.ОбъектРасчетов,
									"Договор.СтатьяДвиженияДенежныхСредств, Соглашение.СтатьяДвиженияДенежныхСредств");
					
					СтрокаРасшифровки.СтатьяДвиженияДенежныхСредств =
									?(ЗначениеЗаполнено(РеквизитыОбъектаРасчетов.ДоговорСтатьяДвиженияДенежныхСредств),
									РеквизитыОбъектаРасчетов.ДоговорСтатьяДвиженияДенежныхСредств,
									РеквизитыОбъектаРасчетов.СоглашениеСтатьяДвиженияДенежныхСредств);
					
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(СтрокаРасшифровки.СтатьяДвиженияДенежныхСредств) Тогда
					СтрокаРасшифровки.СтатьяДвиженияДенежныхСредств = СтатьяДвиженияДенежныхСредств;
				КонецЕсли;
				
			КонецЕсли;
			
#Вставка
			Если РеквизитыХозОперации.Найти("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств") <> Неопределено Тогда
				бг_УстановитьСвойствоТЧВставка(ДокументОбъект, СтрокаРасшифровки, 
					"СтатьяДвиженияДенежныхСредств", СтрокаДокумента.СтатьяДвиженияДенежныхСредств, СделатьНепроведенным);
			КонецЕсли;
#КонецВставки
			
		КонецЦикла;
		
		УстановитьСвойство(ДокументОбъект, "СуммаДокумента", СтрокаДокумента.СуммаДокумента, Истина);
		УстановитьСвойство(ДокументОбъект, "НазначениеПлатежа", СтрокаДокумента.НазначениеПлатежа, Истина);
		УстановитьСвойство(ДокументОбъект, "ФорматированноеНазначениеПлатежа", СтрокаДокумента.ФорматированноеНазначениеПлатежа, Истина);
		УстановитьСвойство(ДокументОбъект, "ДанныеВыписки", СтрокаДокумента.ДанныеВыписки, Истина);
		
		СуммыРазличаются = (СтрокаДокумента.СуммаДокумента <> ДокументОбъект.СуммаДокумента);
		Если СуммыРазличаются Тогда
			Если ДокументОбъект.РасшифровкаПлатежа.Количество() = 1 Тогда
				СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа[0];
				СтрокаРасшифровки.Сумма = ДокументОбъект.СуммаДокумента;
			Иначе
				СделатьНепроведенным = Истина;
				ОписаниеОшибки = НСтр("ru = 'Сумма документа отличается от суммы строк расшифровки платежа.';
										|en = 'Document amount differs from the total of payment details lines.'");
				ДобавитьЗамечание(СтрокаДокумента.ОшибкиЗагрузки, ОписаниеОшибки);
			КонецЕсли;
		КонецЕсли;
		
		УстановитьСвойство(ДокументОбъект, "Комментарий", НСтр("ru = '#Загружен из Клиент-Банка';
																|en = '#Imported from Client Bank'"));
		
		Если ДокументОбъект.Модифицированность() Тогда
			УстановитьСвойство(ДокументОбъект, "ДатаЗагрузки", ТекущаяДатаСеанса(), Истина);
		КонецЕсли;
		
		ДокументОбъект.ДополнительныеСвойства.Вставить("ОбменСБанками", Истина);
		ДокументОбъект.Движения.ДенежныеСредстваБезналичные.ДополнительныеСвойства.Вставить("ОбменСБанками", Истина);
		ДокументОбъект.ПроверитьЗаполнение();
		
		Если ДокументОбъект.ДополнительныеСвойства.Свойство("ОшибкиЗаполнения") Тогда
			УстановитьСвойство(ДокументОбъект, "ОшибкиЗагрузки", ДокументОбъект.ДополнительныеСвойства.ОшибкиЗаполнения, Истина);
		КонецЕсли;
		
		Если ПроводитьДокументы
			И СокрЛП(ДокументОбъект.ОшибкиЗагрузки) = "" Тогда
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
		Иначе
			РежимЗаписи = РежимЗаписиДокумента.Запись;
		КонецЕсли;
		
		ЗаписатьОбъект(ДокументОбъект, ?(СделатьНепроведенным, РежимЗаписиДокумента.ОтменаПроведения, РежимЗаписи), СтрокаДокумента);
	КонецЦикла;
	
КонецПроцедуры

&ИзменениеИКонтроль("СоздатьДокументы")
Процедура бг_СоздатьДокументы(ДокументыКЗагрузке, БанковскийСчет, СоздаватьКонтрагентов, ПроводитьДокументы)
	
	РеквизитыСчета = Справочники.БанковскиеСчетаОрганизаций.ПолучитьРеквизитыБанковскогоСчетаОрганизации(БанковскийСчет);
	РазрешитьПлатежиБезУказанияЗаявок = РеквизитыСчета.РазрешитьПлатежиБезУказанияЗаявок;
	
	Организация          = РеквизитыСчета.Организация;
	Префикс              = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "Префикс");
	Валюта               = РеквизитыСчета.Валюта;
	ТекущийПользователь  = Пользователи.ТекущийПользователь();
	
	Отбор = Новый Структура("БанковскийСчет, Загружать, НайденДокументВБазе",
		БанковскийСчет, Истина, Ложь);
#Вставка
		Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчет, "Владелец");
		Если бг_ИспользоватьДопОтборы(Организация) Тогда
			Отбор.Вставить("бг_Загружать", Истина);
		КонецЕсли;
#КонецВставки
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	
	Для каждого СтрокаДокумента Из СтрокиКЗагрузке Цикл
		
		Если СтрокаДокумента.Исходящий Тогда
			ДокументОбъект = Документы.СписаниеБезналичныхДенежныхСредств.СоздатьДокумент();
		Иначе
			ДокументОбъект = Документы.ПоступлениеБезналичныхДенежныхСредств.СоздатьДокумент();
		КонецЕсли;
		
		// Общие реквизиты
		ДатаВхДокумента = ДенежныеСредстваКлиентСервер.ПолучитьДатуИзСтроки(СтрокаДокумента.Дата);
		Если ЗначениеЗаполнено(ДатаВхДокумента) Тогда
			УстановитьСвойство(ДокументОбъект, "ДатаВходящегоДокумента", ДатаВхДокумента);
		Иначе
			УстановитьСвойство(ДокументОбъект, "ДатаВходящегоДокумента", СтрокаДокумента.ДатаДок);
		КонецЕсли;
		УстановитьСвойство(ДокументОбъект, "НомерВходящегоДокумента", СтрокаДокумента.Номер);
		
		УстановитьСвойство(ДокументОбъект, "Организация", Организация);
		УстановитьСвойство(ДокументОбъект, "БанковскийСчет", БанковскийСчет);
		УстановитьСвойство(ДокументОбъект, "Валюта", Валюта);
		
		УстановитьСвойство(ДокументОбъект, "ТипПлатежногоДокумента", СтрокаДокумента.ТипПлатежногоДокумента);
		УстановитьСвойство(ДокументОбъект, "СуммаДокумента", СтрокаДокумента.СуммаДокумента);
		
		УстановитьСвойство(ДокументОбъект, "НазначениеПлатежа", СтрокаДокумента.НазначениеПлатежа);
		УстановитьСвойство(ДокументОбъект, "ФорматированноеНазначениеПлатежа", СтрокаДокумента.ФорматированноеНазначениеПлатежа);
		УстановитьСвойство(ДокументОбъект, "ДанныеВыписки", СтрокаДокумента.ДанныеВыписки);
		УстановитьСвойство(ДокументОбъект, "ИдентификаторПлатежа", СтрокаДокумента.Код);
		
		УстановитьСвойство(ДокументОбъект, "Комментарий", НСтр("ru = '#Загружен из Клиент-Банка';
																|en = '#Imported from Client Bank'"));
		УстановитьСвойство(ДокументОбъект, "ДатаЗагрузки", ТекущаяДатаСеанса());
		
		УстановитьСвойство(ДокументОбъект, "ТипНалога", СтрокаДокумента.ТипНалога);
		УстановитьСвойство(ДокументОбъект, "НастройкаСчетовУчета", СтрокаДокумента.НастройкаСчетовУчета);
		
		УстановитьСвойство(ДокументОбъект, "ОшибкиЗагрузки", СтрокаДокумента.ОшибкиЗагрузки);
		
		УстановитьСвойство(ДокументОбъект, "Ответственный", ТекущийПользователь);
		
		// Индивидуальное заполнение
		Если СтрокаДокумента.Исходящий Тогда
			ЗаполнитьРеквизитыСписания(ДокументыКЗагрузке, СтрокаДокумента, ДокументОбъект, СоздаватьКонтрагентов, ПроводитьДокументы, Префикс);
		Иначе
			ЗаполнитьРеквизитыПоступления(ДокументыКЗагрузке, СтрокаДокумента, ДокументОбъект, СоздаватьКонтрагентов, ПроводитьДокументы);
		КонецЕсли;
		
		//++ Локализация
		Если СтрокаДокумента.Операция <> "Операция по Яндекс.Кассе" Тогда
		//-- Локализация
			УстановитьСвойство(ДокументОбъект, "ПроведеноБанком", Истина);
		//++ Локализация
		КонецЕсли;
		//-- Локализация
		
		Если ЗначениеЗаполнено(СтрокаДокумента.ДатаПроведения) Тогда
			УстановитьСвойство(ДокументОбъект, "ДатаПроведенияБанком", СтрокаДокумента.ДатаПроведения);
		Иначе
			УстановитьСвойство(ДокументОбъект, "ДатаПроведенияБанком", ДокументОбъект.Дата);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДокументОбъект.ДатаВходящегоДокумента) Тогда
			ДокументОбъект.ДатаВходящегоДокумента = ДокументОбъект.Дата;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ДокументОбъект.НомерВходящегоДокумента) Тогда
			ДокументОбъект.НомерВходящегоДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДокументОбъект.Номер);
		КонецЕсли;
		
		ДокументОбъект.ДополнительныеСвойства.Вставить("ОбменСБанками", Истина);
		ДокументОбъект.Движения.ДенежныеСредстваБезналичные.ДополнительныеСвойства.Вставить("ОбменСБанками", Истина);
		ДокументОбъект.ПроверитьЗаполнение();
		
		Если ДокументОбъект.ДополнительныеСвойства.Свойство("ОшибкиЗаполнения") Тогда
			ДокументОбъект.ОшибкиЗагрузки = СокрЛП(ДокументОбъект.ОшибкиЗагрузки + "
			|" + ДокументОбъект.ДополнительныеСвойства.ОшибкиЗаполнения);
		КонецЕсли;
		
		Если ПроводитьДокументы
			И СокрЛП(ДокументОбъект.ОшибкиЗагрузки) = "" Тогда
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
		Иначе
			РежимЗаписи = РежимЗаписиДокумента.Запись;
		КонецЕсли;
		
		Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации ИЛИ 
			ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию Тогда
			Если СтрокаДокумента.Исходящий Тогда
				ЗеркальныйПлатежСсылка = Документы.СписаниеБезналичныхДенежныхСредств.НайтиЗеркальныйПлатеж(ДокументОбъект.Ссылка);
				Если ЗначениеЗаполнено(ЗеркальныйПлатежСсылка) И ЗеркальныйПлатежСсылка.СуммаДокумента = ДокументОбъект.СуммаДокумента Тогда
					Документы.ПоступлениеБезналичныхДенежныхСредств.ПривестиВСоответствиеЗеркальныйПлатеж(ЗеркальныйПлатежСсылка, ДокументОбъект);
				КонецЕсли;
			Иначе
				ЗеркальныйПлатежСсылка = Документы.ПоступлениеБезналичныхДенежныхСредств.НайтиЗеркальныйПлатеж(ДокументОбъект.Ссылка);
				Если ЗначениеЗаполнено(ЗеркальныйПлатежСсылка) И ЗеркальныйПлатежСсылка.СуммаДокумента = ДокументОбъект.СуммаДокумента Тогда
					Документы.СписаниеБезналичныхДенежныхСредств.ПривестиВСоответствиеЗеркальныйПлатеж(ЗеркальныйПлатежСсылка, ДокументОбъект);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ЗаписатьОбъект(ДокументОбъект, РежимЗаписи, СтрокаДокумента);
		
		СтрокаДокумента.Документ = ДокументОбъект.Ссылка;
	КонецЦикла;
	
КонецПроцедуры

&ИзменениеИКонтроль("ЗаполнитьРеквизитыСписания")
Процедура бг_ЗаполнитьРеквизитыСписания(ДокументыКЗагрузке, СтрокаДокумента, ДокументОбъект, СоздаватьКонтрагентов, ПроводитьДокументы, Префикс)
	
	Перем РеквизитыВсе, РеквизитыХозОперации;
	
	УстановитьСвойство(ДокументОбъект, "Дата", ДокументОбъект.ДатаВходящегоДокумента);
	
#Вставка
	бг_УстановитьДатуДокументаПоДатеПроведенияБанком(ДокументОбъект, СтрокаДокумента.ДатаПроведения);
#КонецВставки
	
	Если НачалоДня(ДокументОбъект.Дата) = НачалоДня(ТекущаяДатаСеанса()) Тогда
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
	Иначе
		ДокументОбъект.Дата = КонецДня(ДокументОбъект.Дата);
	КонецЕсли;
	
	//++ Локализация
	// Контроль заявок
	Если ДокументОбъект.ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов.ИнкассовоеПоручение
		Или ДокументОбъект.ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов.ПлатежныйОрдер
		Или ДокументОбъект.ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов.БанковскийОрдер Тогда
	
		ДокументОбъект.НеКонтролироватьЗаполнениеЗаявки = Истина;
	Иначе
	//-- Локализация
		ДокументОбъект.ОплатаПоЗаявкам = ПолучитьФункциональнуюОпцию("ИспользоватьЗаявкиНаРасходованиеДенежныхСредств")
			И Не ДенежныеСредстваСервер.РазрешеныПлатежиБезУказанияЗаявок(ДокументОбъект);
	//++ Локализация
	КонецЕсли;
	
	РеквизитыПлательщика = ДенежныеСредстваПовтИспРФ.ДанныеПлательщика(ДокументОбъект.Организация, ДокументОбъект.БанковскийСчет);
	УстановитьСвойство(ДокументОбъект, "ИННПлательщика", РеквизитыПлательщика.ИННПлательщика);
	УстановитьСвойство(ДокументОбъект, "КПППлательщика", РеквизитыПлательщика.КПППлательщика);
	УстановитьСвойство(ДокументОбъект, "ТекстПлательщика", РеквизитыПлательщика.ТекстПлательщика);
	//-- Локализация
	
	// Хоз. операция
	Если ЗначениеЗаполнено(СтрокаДокумента.ВидОперации) Тогда
		ХозяйственнаяОперация = СтрокаДокумента.ВидОперации;
	Иначе
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоставщику;
	КонецЕсли;
	УстановитьСвойство(ДокументОбъект, "ХозяйственнаяОперация", ХозяйственнаяОперация);
	
	// Статья по умолчанию
	Если Не ЗначениеЗаполнено(СтрокаДокумента.СтатьяДвиженияДенежныхСредств) Тогда
		СтрокаДокумента.СтатьяДвиженияДенежныхСредств =
			Справочники.СтатьиДвиженияДенежныхСредств.СтатьяДвиженияДенежныхСредствПоХозяйственнойОперации(ХозяйственнаяОперация);
	КонецЕсли;
	
	Документы.СписаниеБезналичныхДенежныхСредств.ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(
		ДокументОбъект, РеквизитыВсе, РеквизитыХозОперации);
	
	// Контрагент
	Если РеквизитыХозОперации.Найти("Контрагент") <> Неопределено Тогда
		Если ЗначениеЗаполнено(СтрокаДокумента.Контрагент) Тогда
			УстановитьСвойство(ДокументОбъект, "Контрагент", СтрокаДокумента.Контрагент);
		ИначеЕсли СоздаватьКонтрагентов И СтрокаДокумента.СоздаватьКонтрагента Тогда
			УстановитьСвойство(ДокументОбъект, "Контрагент", СоздатьКонтрагента(ДокументыКЗагрузке, СтрокаДокумента));
		Иначе
			УстановитьСвойство(ДокументОбъект, "ИмяКонтрагента", СтрокаДокумента.ИмяКонтрагента);
		КонецЕсли;
	КонецЕсли;
	
	// Счет контрагента
	Если РеквизитыХозОперации.Найти("БанковскийСчетКонтрагента") <> Неопределено Тогда
		Если ЗначениеЗаполнено(СтрокаДокумента.СчетКонтрагента) Тогда
			УстановитьСвойство(ДокументОбъект, "БанковскийСчетКонтрагента", СтрокаДокумента.СчетКонтрагента);
		ИначеЕсли ЗначениеЗаполнено(ДокументОбъект.Контрагент) Тогда
			УстановитьСвойство(ДокументОбъект, "БанковскийСчетКонтрагента",
				СоздатьБанковскийСчетКонтрагента(ДокументыКЗагрузке, СтрокаДокумента, ДокументОбъект.Контрагент));
		КонецЕсли;
	КонецЕсли;
	
#Вставка
	бг_ЗаполнитьДоговорВДокументе(ДокументОбъект);
#КонецВставки
	
	// Подотчетное лицо
	Если РеквизитыХозОперации.Найти("ПодотчетноеЛицо") <> Неопределено Тогда
		Если СтрокаДокумента.НайденКонтрагент Тогда
			УстановитьСвойство(ДокументОбъект, "ПодотчетноеЛицо", СтрокаДокумента.Контрагент);
		Иначе
			УстановитьСвойство(ДокументОбъект, "ПодотчетноеЛицо", СтрокаДокумента.ПодотчетноеЛицо);
		КонецЕсли;
	КонецЕсли;
	
	Если РеквизитыХозОперации.Найти("ДатаАвансовогоОтчета") <> Неопределено Тогда
		УстановитьСвойство(ДокументОбъект, "ДатаАвансовогоОтчета",
			?(ЗначениеЗаполнено(ДокументОбъект.Дата), ДокументОбъект.Дата, ТекущаяДатаСеанса()) + 7 * 24 * 3600);
	КонецЕсли;
	
	// Банковский счет получатель
	Если РеквизитыХозОперации.Найти("БанковскийСчетПолучатель") <> Неопределено Тогда
		УстановитьСвойство(ДокументОбъект, "БанковскийСчетПолучатель", СтрокаДокумента.СчетКонтрагента);
	КонецЕсли;
	
	// Прочие реквизиты
	ЗаполнитьЗначенияСвойств(ДокументОбъект, СтрокаДокумента,
		"СтатьяДвиженияДенежныхСредств, Подразделение, КассаПолучатель");
	
	//++ Локализация
	Если ЗначениеЗаполнено(СтрокаДокумента.ОчередностьПлатежа) Тогда
		УстановитьСвойство(ДокументОбъект, "ОчередностьПлатежа", СтрокаДокумента.ОчередностьПлатежа);
	Иначе
		УстановитьСвойство(ДокументОбъект, "ОчередностьПлатежа", 5);
	КонецЕсли;
	УстановитьСвойство(ДокументОбъект, "КодВидаДохода", СтрокаДокумента.КодНазПлатежа);
	УстановитьСвойство(ДокументОбъект, "КодВыплат", СтрокаДокумента.ПоказательТипа);
	//-- Локализация
	
	//++ Локализация
	// Бюджетные реквизиты
	Если СтрокаДокумента.ПлатежВБюджет Тогда
		УстановитьСвойство(ДокументОбъект, "ПеречислениеВБюджет", Истина);
		УстановитьСвойство(ДокументОбъект, "ВидПеречисленияВБюджет", СтрокаДокумента.ВидПеречисленияВБюджет);
		УстановитьСвойство(ДокументОбъект, "КодБК", СтрокаДокумента.ПоказательКБК);
		УстановитьСвойство(ДокументОбъект, "КодОКАТО", СтрокаДокумента.ОКАТО);
		УстановитьСвойство(ДокументОбъект, "СтатусСоставителя", СтрокаДокумента.СтатусСоставителя);
		Если СтрокаДокумента.ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.ИнойПлатеж Тогда
			УстановитьСвойство(ДокументОбъект, "ПоказательОснования", "0");
			УстановитьСвойство(ДокументОбъект, "ПоказательПериода", "0");
			УстановитьСвойство(ДокументОбъект, "ПоказательНомера", "0");
			УстановитьСвойство(ДокументОбъект, "ПоказательДаты", "0");
		Иначе
			УстановитьСвойство(ДокументОбъект, "ПоказательОснования", СтрокаДокумента.ПоказательОснования);
			УстановитьСвойство(ДокументОбъект, "ПоказательПериода", СтрокаДокумента.ПоказательПериода);
			УстановитьСвойство(ДокументОбъект, "ПоказательНомера", СтрокаДокумента.ПоказательНомера);
			УстановитьСвойство(ДокументОбъект, "ПоказательДаты", СтрокаДокумента.ПоказательДаты);
		КонецЕсли;
		УстановитьСвойство(ДокументОбъект, "ПоказательТипа", СтрокаДокумента.ПоказательТипа);
	Иначе
		УстановитьСвойство(ДокументОбъект, "ВидПеречисленияВБюджет", Перечисления.ВидыПеречисленийВБюджет.ИнойПлатеж);
	КонецЕсли;
	
	Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет Тогда
		РеквизитыПлательщика = ДенежныеСредстваСерверЛокализация.РеквизитыПлательщика(ДокументОбъект);
		ЗаполнитьЗначенияСвойств(ДокументОбъект, РеквизитыПлательщика);
	КонецЕсли;
	//-- Локализация
	
	// Расшифровка платежа
	Если РеквизитыХозОперации.Найти("РасшифровкаПлатежа") <> Неопределено Тогда
		
		Если РеквизитыХозОперации.Найти("РасшифровкаПлатежа.ДоговорКредитаДепозита") <> Неопределено Тогда
			
			СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
			
			Запрос = Новый Запрос;
			Запрос.Текст = "
			|ВЫБРАТЬ
			|	ДанныеРегистра.ОбъектОплаты КАК ДоговорКредитаДепозита,
			|	ВЫРАЗИТЬ(ДанныеРегистра.ОбъектОплаты КАК Справочник.ДоговорыКредитовИДепозитов).Партнер КАК Партнер,
			|	ВЫРАЗИТЬ(ДанныеРегистра.ОбъектОплаты КАК Справочник.ДоговорыКредитовИДепозитов).СтатьяДДСПоступленияВыдачи КАК СтатьяДвиженияДенежныхСредств
			|ИЗ
			|	РегистрСведений.ГрафикПлатежей КАК ДанныеРегистра
			|ГДЕ
			|	ДанныеРегистра.БанковскийСчетКасса = &БанковскийСчет
			|	И ДанныеРегистра.ПлательщикПолучатель = &Контрагент
			|	И ДанныеРегистра.ПоступлениеСписание = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Списание)
			|	И ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаПоКредитам)
			|СГРУППИРОВАТЬ ПО
			|	ДанныеРегистра.ОбъектОплаты
			|ИМЕЮЩИЕ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеРегистра.ОбъектОплаты) = 1
			|";
			
			Запрос.УстановитьПараметр("БанковскийСчет", ДокументОбъект.БанковскийСчет);
			Запрос.УстановитьПараметр("Контрагент", ДокументОбъект.Контрагент);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, Выборка);
			Иначе
				СтрокиКЗагрузке = Новый Массив;
				СтрокиКЗагрузке.Добавить(СтрокаДокумента);
				ЗаполнитьПоКосвеннымДанным(СтрокиКЗагрузке, ДокументОбъект.БанковскийСчет);
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаДокумента,
					"Партнер, СтатьяДвиженияДенежныхСредств, ДоговорКредитаДепозита");
			КонецЕсли;
			
			СтрокаРасшифровки.Сумма = СтрокаДокумента.СуммаДокумента;
			
			РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
																	СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
																	ДокументОбъект.Валюта,
																	СтрокаРасшифровки.ВалютаВзаиморасчетов,
																	ДокументОбъект.Организация,
																	ДокументОбъект.Дата);
		
		ИначеЕсли РеквизитыХозОперации.Найти("РасшифровкаПлатежа.ОбъектРасчетов") = Неопределено Тогда
			
			СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаДокумента,
				"Партнер, СтатьяДвиженияДенежныхСредств, ДоговорКредитаДепозита, СтатьяРасходов, АналитикаРасходов, АналитикаАктивовПассивов, Подразделение");
			СтрокаРасшифровки.Сумма = СтрокаДокумента.СуммаДокумента;
			РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
																	СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
																	ДокументОбъект.Валюта,
																	СтрокаРасшифровки.ВалютаВзаиморасчетов,
																	ДокументОбъект.Организация,
																	ДокументОбъект.Дата);
		
		Иначе
			
			Реквизиты = Новый Структура;
			Реквизиты.Вставить("Дата", ДокументОбъект.Дата);
			Реквизиты.Вставить("Организация", ДокументОбъект.Организация);
			Реквизиты.Вставить("СуммаДокумента", 0);
			Реквизиты.Вставить("Валюта", ДокументОбъект.Валюта);
			Реквизиты.Вставить("Контрагент", ДокументОбъект.Контрагент);
			
			Если ТипЗнч(СтрокаДокумента.Контрагент) = Тип("СправочникСсылка.Организации") Тогда
				Реквизиты.Вставить("Партнер", Справочники.Партнеры.НашеПредприятие);
			Иначе
				Реквизиты.Вставить("Партнер", Неопределено);
			КонецЕсли;
			
			Реквизиты.Вставить("ХозяйственнаяОперация", ДокументОбъект.ХозяйственнаяОперация);
			
			Реквизиты.Вставить("ТипРасчетов", Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком);
			Реквизиты.Вставить("ПартнерПрочиеОтношения", Ложь);
			Реквизиты.Вставить("ПодборДебиторскойЗадолженности", Ложь);
			Реквизиты.Вставить("ПодборТолькоБезусловнойЗадолженности", Ложь);
			
			//++ Локализация
			//++ НЕ УТ
			Реквизиты.Вставить("ПлатежиПо275ФЗ", Ложь);
			Реквизиты.Вставить("ДоговорСУчастникомГОЗ", Ложь);
			//-- НЕ УТ
			//-- Локализация
			
			АдресПлатежейВХранилище = "";
			ТаблицаОстатковРасчетов = Неопределено;
			
			ВзаиморасчетыСервер.ЗаполнитьТаблицуОстатковРасчетов(Реквизиты, АдресПлатежейВХранилище, ТаблицаОстатковРасчетов);
			КолонкиТаблицыОстатковВзаиморасчетов = ТаблицаОстатковРасчетов.Колонки; // КоллекцияКолонокТаблицыЗначений 
			КолонкиТаблицыОстатковВзаиморасчетов.Добавить("Обработано", Новый ОписаниеТипов("Булево"));
			
			РаспределяемаяСумма = ДокументОбъект.СуммаДокумента;
		
			Для Каждого СтрокаТаблицы Из ТаблицаОстатковРасчетов Цикл
#Вставка
				Если бг_ИспользоватьДопОтборы(ДокументОбъект.Организация) Тогда
					//не подбираем документы по назначению платежа
					Прервать;
				КонецЕсли;
#КонецВставки
				Если ТипЗнч(СтрокаТаблицы.ОбъектРасчетов) = Тип("СправочникСсылка.ОбъектыРасчетов") Тогда
					
					НомерДокументаРасчетов = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СтрокаТаблицы.Номер, Истина, Истина);
					Если СтрЧислоВхождений(СтрокаДокумента.НазначениеПлатежа, НомерДокументаРасчетов) > 0 Тогда
						
						СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
						
						СуммаСтроки = Мин(РаспределяемаяСумма, СтрокаТаблицы.Сумма);
						РаспределяемаяСумма = РаспределяемаяСумма - СтрокаТаблицы.Сумма;
						
						ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицы,, "Сумма");
						СтрокаРасшифровки.Сумма = СуммаСтроки;
						
						РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
																				СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
																				ДокументОбъект.Валюта,
																				СтрокаРасшифровки.ВалютаВзаиморасчетов,
																				ДокументОбъект.Организация,
																				ДокументОбъект.Дата);
						
						СтрокаТаблицы.Обработано = Истина;
						
						Если РаспределяемаяСумма <= 0 Тогда
							Прервать;
						КонецЕсли;
						
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
#Удаление			
			Если ПроводитьДокументы И РаспределяемаяСумма > 0 Тогда // Подбор объектов расчетов
#КонецУдаления
#Вставка
			Если (ПроводитьДокументы Или бг_ИспользоватьДопОтборы(ДокументОбъект.Организация)) 
				И РаспределяемаяСумма > 0 Тогда // Подбор объектов расчетов
#КонецВставки
				Для Каждого СтрокаТаблицы Из ТаблицаОстатковРасчетов Цикл
					
					Если ЗначениеЗаполнено(СтрокаТаблицы.ОбъектРасчетов) Тогда
						
						Если СтрокаТаблицы.Обработано Тогда
							Продолжить;
						КонецЕсли;
						
						СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
						
						СуммаСтроки = Мин(РаспределяемаяСумма, СтрокаТаблицы.Сумма);
						РаспределяемаяСумма = РаспределяемаяСумма - СтрокаТаблицы.Сумма;
						
						ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицы, , "Сумма");
						СтрокаРасшифровки.Сумма = СуммаСтроки;
						
						РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
																				СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
																				ДокументОбъект.Валюта,
																				СтрокаРасшифровки.ВалютаВзаиморасчетов,
																				ДокументОбъект.Организация,
																				ДокументОбъект.Дата);
						
						СтрокаТаблицы.Обработано = Истина;
						
						Если РаспределяемаяСумма <= 0 Тогда
							Прервать;
						КонецЕсли;
						
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			Если РаспределяемаяСумма > 0 Или Не ДокументОбъект.РасшифровкаПлатежа.Количество() Тогда
				СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаДокумента,
					"Партнер, ОбъектРасчетов, СтатьяДвиженияДенежныхСредств");
				СтрокаРасшифровки.Сумма = РаспределяемаяСумма;
				РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
																		СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
																		ДокументОбъект.Валюта,
																		СтрокаРасшифровки.ВалютаВзаиморасчетов,
																		ДокументОбъект.Организация,
																		ДокументОбъект.Дата);
			КонецЕсли;
			
			ДенежныеСредстваСервер.ЗаполнитьНДСВРасшифровке(
				ДокументОбъект.РасшифровкаПлатежа,
				ДенежныеСредстваСервер.РасшифровкаПлатежаНДС(ДокументОбъект.Организация,
					ДокументОбъект.Дата, ДокументОбъект.Валюта, ДокументОбъект.РасшифровкаПлатежа.ВыгрузитьКолонку("ОбъектРасчетов"), Ложь));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&ИзменениеИКонтроль("ЗаполнитьРеквизитыПоступления")
Процедура бг_ЗаполнитьРеквизитыПоступления(ДокументыКЗагрузке, СтрокаДокумента, ДокументОбъект, СоздаватьКонтрагентов, ПроводитьДокументы)
	
	Перем РеквизитыВсе, РеквизитыХозОперации;
	
	УстановитьСвойство(ДокументОбъект, "Дата", ДокументОбъект.ДатаВходящегоДокумента);
#Вставка
	бг_УстановитьДатуДокументаПоДатеПроведенияБанком(ДокументОбъект, СтрокаДокумента.ДатаПроведения);
#КонецВставки
	
	Если НачалоДня(ДокументОбъект.Дата) = НачалоДня(ТекущаяДатаСеанса()) Тогда
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
	Иначе
		ДокументОбъект.Дата = КонецДня(ДокументОбъект.Дата);
	КонецЕсли;
	
	// Хоз. операция
	Если ЗначениеЗаполнено(СтрокаДокумента.ВидОперации) Тогда
		ХозяйственнаяОперация = СтрокаДокумента.ВидОперации;
	Иначе
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента;
	КонецЕсли;
	УстановитьСвойство(ДокументОбъект, "ХозяйственнаяОперация", ХозяйственнаяОперация);
	
	// Статья по умолчанию
	Если Не ЗначениеЗаполнено(СтрокаДокумента.СтатьяДвиженияДенежныхСредств) Тогда
		СтрокаДокумента.СтатьяДвиженияДенежныхСредств =
			Справочники.СтатьиДвиженияДенежныхСредств.СтатьяДвиженияДенежныхСредствПоХозяйственнойОперации(ХозяйственнаяОперация);
	КонецЕсли;
	
	Документы.ПоступлениеБезналичныхДенежныхСредств.ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(
		ХозяйственнаяОперация, РеквизитыВсе, РеквизитыХозОперации);
		
	// Контрагент
	Если РеквизитыХозОперации.Найти("Контрагент") <> Неопределено Тогда
		Если ЗначениеЗаполнено(СтрокаДокумента.Контрагент) Тогда
			УстановитьСвойство(ДокументОбъект, "Контрагент", СтрокаДокумента.Контрагент);
		ИначеЕсли СоздаватьКонтрагентов И СтрокаДокумента.СоздаватьКонтрагента Тогда
			УстановитьСвойство(ДокументОбъект, "Контрагент", СоздатьКонтрагента(ДокументыКЗагрузке, СтрокаДокумента));
		Иначе
			УстановитьСвойство(ДокументОбъект, "ИмяКонтрагента", СтрокаДокумента.ИмяКонтрагента);
		КонецЕсли;
	КонецЕсли;
	
	// Счет контрагента
	Если РеквизитыХозОперации.Найти("БанковскийСчетКонтрагента") <> Неопределено Тогда
		Если ЗначениеЗаполнено(СтрокаДокумента.СчетКонтрагента) Тогда
			УстановитьСвойство(ДокументОбъект, "БанковскийСчетКонтрагента", СтрокаДокумента.СчетКонтрагента);
		ИначеЕсли ЗначениеЗаполнено(ДокументОбъект.Контрагент) Тогда
			УстановитьСвойство(ДокументОбъект, "БанковскийСчетКонтрагента",
				СоздатьБанковскийСчетКонтрагента(ДокументыКЗагрузке, СтрокаДокумента, ДокументОбъект.Контрагент));
		КонецЕсли;
	КонецЕсли;
#Вставка
	бг_ЗаполнитьДоговорВДокументе(ДокументОбъект);
#КонецВставки
	
	// Подотчетное лицо
	Если РеквизитыХозОперации.Найти("ПодотчетноеЛицо") <> Неопределено Тогда
		Если СтрокаДокумента.НайденКонтрагент Тогда
			УстановитьСвойство(ДокументОбъект, "ПодотчетноеЛицо", СтрокаДокумента.Контрагент);
		Иначе
			УстановитьСвойство(ДокументОбъект, "ПодотчетноеЛицо", СтрокаДокумента.ПодотчетноеЛицо);
		КонецЕсли;
	КонецЕсли;
	
	// Банковский счет отправитель
	Если РеквизитыХозОперации.Найти("БанковскийСчетОтправитель") <> Неопределено Тогда
		УстановитьСвойство(ДокументОбъект, "БанковскийСчетОтправитель", СтрокаДокумента.СчетКонтрагента);
	КонецЕсли;
	
	// Прочие реквизиты
	ЗаполнитьЗначенияСвойств(ДокументОбъект, СтрокаДокумента,
		"СтатьяДвиженияДенежныхСредств, Подразделение, КассаОтправитель");
	
	// Поступление оплаты по эквайрингу
	Если РеквизитыХозОперации.Найти("ДоговорЭквайринга") <> Неопределено Тогда
		
		ОбработатьКомиссию = Ложь;
		
		Если ЗначениеЗаполнено(СтрокаДокумента.ДоговорЭквайринга) Тогда
			УстановитьСвойство(ДокументОбъект, "ДоговорЭквайринга", СтрокаДокумента.ДоговорЭквайринга);
			УстановитьСвойство(ДокументОбъект, "СтатьяРасходов", СтрокаДокумента.СтатьяРасходов);
			УстановитьСвойство(ДокументОбъект, "АналитикаРасходов", СтрокаДокумента.АналитикаРасходов);
			РеквизитыДоговора = Справочники.ДоговорыЭквайринга.РеквизитыДоговора(ДокументОбъект.ДоговорЭквайринга);
			УстановитьСвойство(ДокументОбъект, "ОтражатьКомиссию",
				РеквизитыДоговора.СпособОтраженияКомиссии = Перечисления.СпособыОтраженияЭквайринговойКомиссии.ПриЗачислении);
			ОбработатьКомиссию = ДокументОбъект.ОтражатьКомиссию;
		Иначе
			УстановитьСвойство(ДокументОбъект, "ДоговорЭквайринга",
				Справочники.ДоговорыЭквайринга.ДоговорПоУмолчанию(ДокументОбъект.Организация, ДокументОбъект.Контрагент));
			Если ЗначениеЗаполнено(ДокументОбъект.ДоговорЭквайринга) Тогда
				РеквизитыДоговора = Справочники.ДоговорыЭквайринга.РеквизитыДоговора(ДокументОбъект.ДоговорЭквайринга);
				УстановитьСвойство(ДокументОбъект, "Подразделение", РеквизитыДоговора.ПодразделениеРасходов);
				УстановитьСвойство(ДокументОбъект, "СтатьяРасходов", РеквизитыДоговора.СтатьяРасходов);
				УстановитьСвойство(ДокументОбъект, "АналитикаРасходов", РеквизитыДоговора.АналитикаРасходов);
				УстановитьСвойство(ДокументОбъект, "СтатьяДвиженияДенежныхСредств", РеквизитыДоговора.СтатьяДвиженияДенежныхСредствПоступлениеОплаты);
				УстановитьСвойство(ДокументОбъект, "ОтражатьКомиссию",
					РеквизитыДоговора.СпособОтраженияКомиссии = Перечисления.СпособыОтраженияЭквайринговойКомиссии.ПриЗачислении);
				ОбработатьКомиссию = ДокументОбъект.ОтражатьКомиссию;
			КонецЕсли;
		КонецЕсли;
		
		Если ОбработатьКомиссию И РеквизитыДоговора.ФиксированнаяСтавкаКомиссии Тогда
			СуммаКомиссии = Окр(ДенежныеСредстваКлиентСервер.РассчитатьСуммуКомиссии(
				ДокументОбъект.СуммаДокумента, РеквизитыДоговора.СтавкаКомиссии, Истина), 2);
			СуммаКомиссииСтрокой1 = Строка(СуммаКомиссии);
			СуммаКомиссииСтрокой2 = Строка(Цел(СуммаКомиссии));
			Если СтрНайти(ДокументОбъект.НазначениеПлатежа, СуммаКомиссииСтрокой1)
				Или СтрНайти(ДокументОбъект.НазначениеПлатежа, СуммаКомиссииСтрокой2) Тогда
				УстановитьСвойство(ДокументОбъект, "СуммаКомиссии", СуммаКомиссии);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	//++ Локализация
	// Бюджетные реквизиты
	Если СтрокаДокумента.ПлатежВБюджет Тогда
		УстановитьСвойство(ДокументОбъект, "ПеречислениеВБюджет", Истина);
		УстановитьСвойство(ДокументОбъект, "ВидПеречисленияВБюджет", СтрокаДокумента.ВидПеречисленияВБюджет);
		УстановитьСвойство(ДокументОбъект, "КодБК", СтрокаДокумента.ПоказательКБК);
		УстановитьСвойство(ДокументОбъект, "КодОКАТО", СтрокаДокумента.ОКАТО);
		УстановитьСвойство(ДокументОбъект, "СтатусСоставителя", СтрокаДокумента.СтатусСоставителя);
		Если СтрокаДокумента.ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.ИнойПлатеж Тогда
			УстановитьСвойство(ДокументОбъект, "ПоказательОснования", "0");
			УстановитьСвойство(ДокументОбъект, "ПоказательПериода", "0");
			УстановитьСвойство(ДокументОбъект, "ПоказательНомера", "0");
			УстановитьСвойство(ДокументОбъект, "ПоказательДаты", "0");
		Иначе
			УстановитьСвойство(ДокументОбъект, "ПоказательОснования", СтрокаДокумента.ПоказательОснования);
			УстановитьСвойство(ДокументОбъект, "ПоказательПериода", СтрокаДокумента.ПоказательПериода);
			УстановитьСвойство(ДокументОбъект, "ПоказательНомера", СтрокаДокумента.ПоказательНомера);
			УстановитьСвойство(ДокументОбъект, "ПоказательДаты", СтрокаДокумента.ПоказательДаты);
		КонецЕсли;
		УстановитьСвойство(ДокументОбъект, "ПоказательТипа", СтрокаДокумента.ПоказательТипа);
	Иначе
		УстановитьСвойство(ДокументОбъект, "ВидПеречисленияВБюджет", Перечисления.ВидыПеречисленийВБюджет.ИнойПлатеж);
	КонецЕсли;
	//-- Локализация
	
	// Расшифровка платежа
	Если РеквизитыХозОперации.Найти("РасшифровкаПлатежа") <> Неопределено Тогда
		
		Если СтрокаДокумента.НайденоОснованиеПлатежа Тогда // УИП
			
			СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаДокумента,
				"Партнер, ОснованиеПлатежа, ОбъектРасчетов, СтатьяДвиженияДенежныхСредств");
			СтрокаРасшифровки.УдалитьЗаказ = СтрокаДокумента.ОбъектРасчетов;
			СтрокаРасшифровки.Сумма = ДокументОбъект.СуммаДокумента;
			
			РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
																	СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
																	ДокументОбъект.Валюта,
																	СтрокаРасшифровки.ВалютаВзаиморасчетов,
																	ДокументОбъект.Организация,
																	ДокументОбъект.Дата);
			
			ДенежныеСредстваСервер.ЗаполнитьНДСВРасшифровке(
				ДокументОбъект.РасшифровкаПлатежа,
				ДенежныеСредстваСервер.РасшифровкаПлатежаНДС(ДокументОбъект.Организация,
					ДокументОбъект.Дата, ДокументОбъект.Валюта, ДокументОбъект.РасшифровкаПлатежа.ВыгрузитьКолонку("ОбъектРасчетов"), Истина));
					
		ИначеЕсли РеквизитыХозОперации.Найти("РасшифровкаПлатежа.ДоговорКредитаДепозита") <> Неопределено Тогда
			
			СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
			
			Запрос = Новый Запрос;
			Запрос.Текст = "
			|ВЫБРАТЬ
			|	ДанныеРегистра.ОбъектОплаты КАК ДоговорКредитаДепозита,
			|	ВЫРАЗИТЬ(ДанныеРегистра.ОбъектОплаты КАК Справочник.ДоговорыКредитовИДепозитов).Партнер КАК Партнер,
			|	ВЫРАЗИТЬ(ДанныеРегистра.ОбъектОплаты КАК Справочник.ДоговорыКредитовИДепозитов).СтатьяДДСПоступленияВыдачи КАК СтатьяДвиженияДенежныхСредств
			|ИЗ
			|	РегистрСведений.ГрафикПлатежей КАК ДанныеРегистра
			|ГДЕ
			|	ДанныеРегистра.БанковскийСчетКасса = &БанковскийСчет
			|	И ДанныеРегистра.ПлательщикПолучатель = &Контрагент
			|	И ДанныеРегистра.ПоступлениеСписание = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Поступление)
			|	И ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам)
			|СГРУППИРОВАТЬ ПО
			|	ДанныеРегистра.ОбъектОплаты
			|ИМЕЮЩИЕ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеРегистра.ОбъектОплаты) = 1
			|";
			
			Запрос.УстановитьПараметр("БанковскийСчет", ДокументОбъект.БанковскийСчет);
			Запрос.УстановитьПараметр("Контрагент", ДокументОбъект.Контрагент);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, Выборка);
			Иначе
				СтрокиКЗагрузке = Новый Массив;
				СтрокиКЗагрузке.Добавить(СтрокаДокумента);
				ЗаполнитьПоКосвеннымДанным(СтрокиКЗагрузке, ДокументОбъект.БанковскийСчет);
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаДокумента,
					"Партнер, СтатьяДвиженияДенежныхСредств, ДоговорКредитаДепозита");
			КонецЕсли;
			
			СтрокаРасшифровки.Сумма = СтрокаДокумента.СуммаДокумента;
			
			РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
																	СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
																	ДокументОбъект.Валюта,
																	СтрокаРасшифровки.ВалютаВзаиморасчетов,
																	ДокументОбъект.Организация,
																	ДокументОбъект.Дата);
			
		ИначеЕсли РеквизитыХозОперации.Найти("РасшифровкаПлатежа.ОбъектРасчетов") = Неопределено Тогда // Прочие платежи
			
			СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаДокумента,
				"Партнер, СтатьяДвиженияДенежныхСредств, ДоговорКредитаДепозита, СтатьяДоходов, АналитикаДоходов, АналитикаАктивовПассивов, Подразделение");
			СтрокаРасшифровки.Сумма = СтрокаДокумента.СуммаДокумента;
			РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
																	СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
																	ДокументОбъект.Валюта,
																	СтрокаРасшифровки.ВалютаВзаиморасчетов,
																	ДокументОбъект.Организация,
																	ДокументОбъект.Дата);
		
		Иначе
			
			Реквизиты = Новый Структура;
			Реквизиты.Вставить("Дата", ДокументОбъект.Дата);
			Реквизиты.Вставить("Организация", ДокументОбъект.Организация);
			Реквизиты.Вставить("СуммаДокумента", 0);
			Реквизиты.Вставить("Валюта", ДокументОбъект.Валюта);
			Реквизиты.Вставить("Контрагент", ДокументОбъект.Контрагент);
			
			Если ТипЗнч(СтрокаДокумента.Контрагент) = Тип("СправочникСсылка.Организации") Тогда
				Реквизиты.Вставить("Партнер", Справочники.Партнеры.НашеПредприятие);
			Иначе
				Реквизиты.Вставить("Партнер", Неопределено);
			КонецЕсли;
			
			Реквизиты.Вставить("ХозяйственнаяОперация", ДокументОбъект.ХозяйственнаяОперация);
			
			Реквизиты.Вставить("ТипРасчетов", Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом);
			Реквизиты.Вставить("ПартнерПрочиеОтношения", Ложь);
			Реквизиты.Вставить("ПодборДебиторскойЗадолженности", Истина);
			Реквизиты.Вставить("ПодборТолькоБезусловнойЗадолженности", Ложь);
			
			//++ Локализация
			//++ НЕ УТ
			Реквизиты.Вставить("ПлатежиПо275ФЗ", Ложь);
			Реквизиты.Вставить("ДоговорСУчастникомГОЗ", Ложь);
			//-- НЕ УТ
			//-- Локализация
			
			АдресПлатежейВХранилище = "";
			ТаблицаОстатковРасчетов = Неопределено;
			
			ВзаиморасчетыСервер.ЗаполнитьТаблицуОстатковРасчетов(Реквизиты, АдресПлатежейВХранилище, ТаблицаОстатковРасчетов);
#Вставка
			бг_СортироватьТаблицуОстатковРасчетов(ТаблицаОстатковРасчетов, ДокументОбъект.Организация);
#КонецВставки
			КолонкиТаблицыОстатковВзаиморасчетов = ТаблицаОстатковРасчетов.Колонки; // КоллекцияКолонокТаблицыЗначений
			КолонкиТаблицыОстатковВзаиморасчетов.Добавить("Обработано", Новый ОписаниеТипов("Булево"));
			КолонкиТаблицыОстатковВзаиморасчетов.Добавить("ОснованиеПлатежа");
			
			РаспределяемаяСумма = ДокументОбъект.СуммаДокумента;
#Вставка
			бг_ИспользоватьДопОтборы = бг_ИспользоватьДопОтборы(ДокументОбъект.Организация);
			бг_НеИскатьДокументыПоНазначениюПлатежа =
				бг_НеИскатьДокументыПоНазначениюПлатежа(ДокументОбъект.Организация);
#КонецВставки
			
			Для Каждого СтрокаТаблицы Из ТаблицаОстатковРасчетов Цикл
#Вставка
				Если бг_НеИскатьДокументыПоНазначениюПлатежа Тогда
					Прервать;
				КонецЕсли;
#КонецВставки
				
				Если ТипЗнч(СтрокаТаблицы.ОбъектРасчетов) = Тип("СправочникСсылка.ОбъектыРасчетов") Тогда
					
					СтрокаТаблицы.ОснованиеПлатежа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТаблицы.ОбъектРасчетов, "Объект");
					
					НомерДокументаРасчетов = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СтрокаТаблицы.Номер, Истина, Истина);
					Если СтрЧислоВхождений(СтрокаДокумента.НазначениеПлатежа, НомерДокументаРасчетов) > 0 Тогда
						
						СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
						
						СуммаСтроки = Мин(РаспределяемаяСумма, СтрокаТаблицы.Сумма);
						РаспределяемаяСумма = РаспределяемаяСумма - СтрокаТаблицы.Сумма;
						
						ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицы,, "Сумма");
						СтрокаРасшифровки.Сумма = СуммаСтроки;
						
						РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
																				СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
																				ДокументОбъект.Валюта,
																				СтрокаРасшифровки.ВалютаВзаиморасчетов,
																				ДокументОбъект.Организация,
																				ДокументОбъект.Дата);
						
						СтрокаТаблицы.Обработано = Истина;
						
						Если РаспределяемаяСумма <= 0 Тогда
							Прервать;
						КонецЕсли;
						
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
#Удаление
			Если ПроводитьДокументы И РаспределяемаяСумма > 0 Тогда // Подбор объектов расчетов
#КонецУдаления
#Вставка
			Если (ПроводитьДокументы Или бг_ИспользоватьДопОтборы)
				И РаспределяемаяСумма > 0 Тогда // Подбор объектов расчетов
#КонецВставки
				
				Для Каждого СтрокаТаблицы Из ТаблицаОстатковРасчетов Цикл
					
					Если ЗначениеЗаполнено(СтрокаТаблицы.ОбъектРасчетов) Тогда
						
						Если СтрокаТаблицы.Обработано Тогда
							Продолжить;
						КонецЕсли;
						
						СтрокаТаблицы.ОснованиеПлатежа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТаблицы.ОбъектРасчетов, "Объект");
						
						СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
						
						СуммаСтроки = Мин(РаспределяемаяСумма, СтрокаТаблицы.Сумма);
						РаспределяемаяСумма = РаспределяемаяСумма - СтрокаТаблицы.Сумма;
						
						ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаТаблицы,, "Сумма");
						СтрокаРасшифровки.Сумма = СуммаСтроки;
						
						РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
																				СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
																				ДокументОбъект.Валюта,
																				СтрокаРасшифровки.ВалютаВзаиморасчетов,
																				ДокументОбъект.Организация,
																				ДокументОбъект.Дата);
						
						СтрокаТаблицы.Обработано = Истина;
						
						Если РаспределяемаяСумма <= 0 Тогда
							Прервать;
						КонецЕсли;
						
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			Если РаспределяемаяСумма > 0 Или Не ДокументОбъект.РасшифровкаПлатежа.Количество() Тогда
				СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаДокумента,
					"Партнер, ОснованиеПлатежа, ОбъектРасчетов, СтатьяДвиженияДенежныхСредств");
				СтрокаРасшифровки.Сумма = РаспределяемаяСумма;
				РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
																	СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
																	ДокументОбъект.Валюта,
																	СтрокаРасшифровки.ВалютаВзаиморасчетов,
																	ДокументОбъект.Организация,
																	ДокументОбъект.Дата);
			КонецЕсли;
			
			ДенежныеСредстваСервер.ЗаполнитьНДСВРасшифровке(
				ДокументОбъект.РасшифровкаПлатежа,
				ДенежныеСредстваСервер.РасшифровкаПлатежаНДС(ДокументОбъект.Организация,
					ДокументОбъект.Дата, ДокументОбъект.Валюта, ДокументОбъект.РасшифровкаПлатежа.ВыгрузитьКолонку("ОбъектРасчетов"), Истина));
		КонецЕсли;
	КонецЕсли;
	
	//++ Локализация
	Если НЕ ПустаяСтрока(ДокументОбъект.НазначениеПлатежа) Тогда
		ДокументОбъект.ЗаполнитьКодВалютнойОперации();
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

&Перед("ЗаполнитьПартнеров")
Процедура бг_ЗаполнитьКонтрагентовВДокументахКЗагрузке(ДокументыКЗагрузке, ТаблицаДокументов, БанковскийСчет) Экспорт
	
	Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчет, "Владелец");
	
	Если Не ЗначениеЗаполнено(Организация) Или Не бг_ИспользоватьДопОтборы(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	//Если у контрагента есть банк.счёт - оставляем его, если такого банковского счёта нет - подставляем нашего контрагента.
	//И оставляем только ЮрЛица, не обособленные подразделения
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаДокументов.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокументов.Контрагент,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.Исходящий
	|			ТОГДА ТаблицаДокументов.ПолучательИНН
	|		ИНАЧЕ ТаблицаДокументов.ПлательщикИНН
	|	КОНЕЦ КАК ИНН,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.Исходящий
	|			ТОГДА ТаблицаДокументов.ПолучательКПП
	|		ИНАЧЕ ТаблицаДокументов.ПлательщикКПП
	|	КОНЕЦ КАК КПП,
	|	ТаблицаДокументов.СчетКонтрагента КАК СчетКонтрагента
	|ПОМЕСТИТЬ ТаблицаДокументов
	|ИЗ
	|	&ТаблицаДокументов КАК ТаблицаДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(Контрагенты.Ссылка, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК Контрагент,
	|	ЕСТЬNULL(КонтрагентыБезУчетаКПП.Ссылка, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК КонтрагентБезУчетаКПП,
	|	ТаблицаДокументов.Контрагент КАК КонтрагентПоПервоначальномуЗаполнению,
	|	ТаблицаДокументов.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ТаблицаДокументов КАК ТаблицаДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО ТаблицаДокументов.ИНН = Контрагенты.ИНН
	|			И ТаблицаДокументов.КПП = Контрагенты.КПП
	|			И ТаблицаДокументов.ИНН <> """"
	|			И Контрагенты.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|			И Не Контрагенты.ОбособленноеПодразделение
	|			И НЕ Контрагенты.ПометкаУдаления
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК КонтрагентыБезУчетаКПП
	|		ПО ТаблицаДокументов.ИНН = КонтрагентыБезУчетаКПП.ИНН
	|			И ТаблицаДокументов.ИНН <> """"
	|			И КонтрагентыБезУчетаКПП.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|			И Не КонтрагентыБезУчетаКПП.ОбособленноеПодразделение
	|			И НЕ КонтрагентыБезУчетаКПП.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	ТаблицаДокументов = ДокументыКЗагрузке.Скопировать(
		СтрокиКЗагрузке,
		"НомерСтроки, Исходящий, Контрагент,
		|СчетКонтрагента, ПолучательИНН, ПлательщикИНН, ПолучательКПП, ПлательщикКПП");
		
	Запрос.УстановитьПараметр("ТаблицаДокументов", ТаблицаДокументов);

	УстановитьПривилегированныйРежим(Истина);
	ТаблицаКонтрагентов = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	ПредыдущийНомерСтроки = 999999;
	ТекущийНомерСтроки = 0;
	
	Для Каждого СтрокаТаблицы Из ТаблицаКонтрагентов Цикл
		СтрокаДокумента = ДокументыКЗагрузке.Найти(СтрокаТаблицы.НомерСтроки, "НомерСтроки");
		Если СтрокаДокумента <> Неопределено Тогда
			Если ПредыдущийНомерСтроки = СтрокаТаблицы.НомерСтроки Тогда
				//т.к. в запросе применяется левое соединение, строки могут задублироваться
				//Поэтому исключаем повторную обработку строк
				Продолжить;
			КонецЕсли;
			//контрагент не найден
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.Контрагент) И Не ЗначениеЗаполнено(СтрокаТаблицы.КонтрагентБезУчетаКПП) Тогда
				Продолжить;
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаТаблицы.Контрагент) Тогда
				КонтрагентЮрЛицо = СтрокаТаблицы.Контрагент;
			Иначе
				КонтрагентЮрЛицо = СтрокаТаблицы.КонтрагентБезУчетаКПП;
			КонецЕсли;
			
			Если СтрокаДокумента.Контрагент = КонтрагентЮрЛицо Тогда
				//найден такой же контрагент, который указан в строке
				ПредыдущийНомерСтроки = СтрокаТаблицы.НомерСтроки;
				Продолжить;
			КонецЕсли;
			
			//контрагент найден, установливаем
			СтрокаДокумента.Контрагент = КонтрагентЮрЛицо;
			СтрокаДокумента.НайденКонтрагент = Истина;
			ПредыдущийНомерСтроки = СтрокаТаблицы.НомерСтроки;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура бг_ЗаполнитьСсылкиНаДокументы(ДокументыКЗагрузке, ТаблицаДокументов, БанковскийСчет)
	
	Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчет, "Владелец");
	
	Если Не ЗначениеЗаполнено(Организация) Или Не бг_ИспользоватьДопОтборы(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиКЗагрузке = бг_ОставшиесяСтрокиБезДокумента(ДокументыКЗагрузке);
	Если СтрокиКЗагрузке.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	ТаблицаДокументов = ДокументыКЗагрузке.Скопировать(
		СтрокиКЗагрузке,
		"НомерСтроки, ДатаДок, Поступило, Списано, Номер, НомерСокр, Исходящий, ПолучательСчет, ПлательщикСчет, ТипПлатежногоДокумента,
		|ПолучательИНН, ПлательщикИНН, ПолучательКПП, ПлательщикКПП, ПроверятьИНН, ПроверятьКПП, ДанныеВыписки, Код, Операция");
	ТаблицаДокументов.ЗаполнитьЗначения(Ложь, "ПроверятьКПП");
	ЗаполнитьСсылкиНаДокументы(ДокументыКЗагрузке, ТаблицаДокументов, БанковскийСчет);
	
	СтрокиКЗагрузке = бг_ОставшиесяСтрокиБезДокумента(ДокументыКЗагрузке);
	Если СтрокиКЗагрузке.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	ТаблицаДокументов = ДокументыКЗагрузке.Скопировать(
		СтрокиКЗагрузке,
		"НомерСтроки, ДатаДок, СуммаДокумента, Номер, НомерСокр, Исходящий");
	бг_ЗаполнитьСсылкиНаДокументыПоНомеруИДате(ДокументыКЗагрузке, ТаблицаДокументов);
	
КонецПроцедуры

#КонецОбласти

Функция бг_ИспользоватьДопОтборы(Организация)
	
	ИспользованиеДопОтборов = бг_КонстантыПовтИсп.ЗначениеКонстанты(
		"ИспользованиеДопОтборовПоПоступлениюСписаниюДС", Организация);
	
	Возврат ИспользованиеДопОтборов = Истина;
	
КонецФункции

Функция бг_НеИскатьДокументыПоНазначениюПлатежа(Организация)
	
	Результат = бг_КонстантыПовтИсп.ЗначениеКонстанты(
		"НеИскатьДокументыПоНазначениюПлатежаПриЗагрузкеБанковскойВыписки", Организация);
	
	Возврат Результат = Истина;
	
КонецФункции

Процедура бг_СортироватьТаблицуОстатковРасчетов(ТаблицаОстатковРасчетов, Организация)
	
	СортироватьПоДатеПлатежа = бг_КонстантыПовтИсп.ЗначениеКонстанты(
		"ВБанковскихДокументахПодбиратьОбъектыРасчетовВПорядкеПлановойДатыПлатежа", Организация);
	
	Если СортироватьПоДатеПлатежа <> Истина
		Или Не ЗначениеЗаполнено(ТаблицаОстатковРасчетов) Тогда
		Возврат;
	КонецЕсли;
	
	КолонкиТаблицыОстатковВзаиморасчетов = ТаблицаОстатковРасчетов.Колонки;
	ОписаниеТипаДатаВремя = ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя);
	КолонкиТаблицыОстатковВзаиморасчетов.Добавить("бг_ПлановаяДатаПлатежа", ОписаниеТипаДатаВремя);
	
	ВзаиморасчетыСервер.бг_ЗаполнитьПлановуюДатуПлатежаВТаблицеОстатковРасчетов(ТаблицаОстатковРасчетов);
	
	ТаблицаОстатковРасчетов.Сортировать("бг_ПлановаяДатаПлатежа");
	
	КолонкиТаблицыОстатковВзаиморасчетов.Удалить("бг_ПлановаяДатаПлатежа");
	
КонецПроцедуры

Процедура бг_ПереопределитьРеквизиты(СтрокаДокумента)
	
	ДанныеВыписки = СтрокаДокумента.ДанныеВыписки;
	Если Не ЗначениеЗаполнено(ДанныеВыписки) Тогда
		Возврат;
	КонецЕсли;
	
	//Вид операции
	бг_ЗаменитьЗначениеРеквизита(СтрокаДокумента, "ВидОперации", "бг_ВидОперации", ДанныеВыписки);
	
	//Статья ДДС
	бг_ЗаменитьЗначениеРеквизита(СтрокаДокумента, "СтатьяДвиженияДенежныхСредств", "бг_СтатьяДДС", ДанныеВыписки);
	
КонецПроцедуры

Процедура бг_ЗаменитьЗначениеРеквизита(СтрокаДокумента, ИмяРеквизита, ИмяРеквизитаЗамены, ДанныеВыписки)
	
	ПозицияНачала = СтрНайти(ДанныеВыписки, ИмяРеквизитаЗамены);
	Если ПозицияНачала = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПозицияОкончания = СтрНайти(ДанныеВыписки, "}",, ПозицияНачала);
	Если ПозицияОкончания = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТеговаяСтрока = Сред(ДанныеВыписки, ПозицияНачала, ПозицияОкончания - ПозицияНачала + 1);
	РезультатРазбора = СтрРазделить(ТеговаяСтрока, "=");
	Если ЗначениеЗаполнено(РезультатРазбора[1]) Тогда
		СтрокаДокумента[ИмяРеквизита] = ЗначениеИзСтрокиВнутр(РезультатРазбора[1]);
	КонецЕсли;
	
КонецПроцедуры

Процедура бг_УстановитьСвойствоВставка(ДокументОбъект, ИмяСвойства, ЗначениеСвойства, СделатьНепроведенным)
	
	Если Не бг_ИспользоватьДопОтборы(ДокументОбъект.Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойства) И ДокументОбъект[ИмяСвойства] <> ЗначениеСвойства Тогда
		ДокументОбъект[ИмяСвойства] = ЗначениеСвойства;
		
		Если ИмяСвойства = "ХозяйственнаяОперация" Тогда
			СделатьНепроведенным = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура бг_УстановитьСвойствоТЧВставка(ДокументОбъект, СтрокаТабличнойЧасти, ИмяСвойства, ЗначениеСвойства, СделатьНепроведенным)
	
	Если Не бг_ИспользоватьДопОтборы(ДокументОбъект.Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеСвойства) И СтрокаТабличнойЧасти[ИмяСвойства] <> ЗначениеСвойства Тогда
		СтрокаТабличнойЧасти[ИмяСвойства] = ЗначениеСвойства;
	КонецЕсли;
	
КонецПроцедуры

Процедура бг_УстановитьДатуДокументаПоДатеПроведенияБанком(ДокументОбъект, Знач ДатаПроведенияБанком)
	
	Если Не ЗначениеЗаполнено(ДатаПроведенияБанком) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаменитьДату = бг_КонстантыПовтИсп.ЗначениеКонстанты(
		"УстанавливатьДатуПоступленияСписанияДСРавнойДатеПроведенияБанком", ДокументОбъект.Организация);
	
	Если ЗаменитьДату = Истина Тогда
		ДокументОбъект.Дата = ДатаПроведенияБанком;
	КонецЕсли;
	
КонецПроцедуры

Процедура бг_ЗаполнитьДоговорВДокументе(Документ)
	Если Не бг_ИспользоватьДопОтборы(Документ.Организация) Тогда
		Возврат;
	КонецЕсли;
	ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.бг_ДоговорКонтрагентаПоОрганизации(Документ.Контрагент, Документ.Организация);
	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		Документ.Договор = ДоговорКонтрагента;
	КонецЕсли;
КонецПроцедуры

Функция бг_ОставшиесяСтрокиБезДокумента(ДокументыКЗагрузке)
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	Отбор.Вставить("Документ", Неопределено);
	Возврат ДокументыКЗагрузке.НайтиСтроки(Отбор);
КонецФункции

Процедура бг_ЗаполнитьСсылкиНаДокументыПоНомеруИДате(ДокументыКЗагрузке, ТаблицаДокументов)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаДокументов", ТаблицаДокументов);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаДокументов.НомерСтроки,
	|	ТаблицаДокументов.ДатаДок,
	|	ТаблицаДокументов.Номер,
	|	ТаблицаДокументов.НомерСокр,
	|	ТаблицаДокументов.Исходящий,
	|	ТаблицаДокументов.СуммаДокумента
	|ПОМЕСТИТЬ ТаблицаДокументов
	|
	|ИЗ
	|	&ТаблицаДокументов КАК ТаблицаДокументов
	|;
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаДокументов.НомерСтроки КАК НомерСтроки,
	|	ЕСТЬNULL(ПоступлениеБезналичныхДенежныхСредств.Ссылка, СписаниеБезналичныхДенежныхСредств.Ссылка) КАК ДокументСсылка
	|ИЗ
	|	ТаблицаДокументов КАК ТаблицаДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
	|		ПО (СписаниеБезналичныхДенежныхСредств.ДатаВходящегоДокумента = ТаблицаДокументов.ДатаДок)
	|			И (СписаниеБезналичныхДенежныхСредств.НомерВходящегоДокумента = ТаблицаДокументов.Номер
	|				ИЛИ СписаниеБезналичныхДенежныхСредств.НомерВходящегоДокумента = ТаблицаДокументов.НомерСокр)
	|			И (СписаниеБезналичныхДенежныхСредств.СуммаДокумента = ТаблицаДокументов.СуммаДокумента)
	|			И (Не СписаниеБезналичныхДенежныхСредств.ПометкаУдаления)
	|			И ТаблицаДокументов.Исходящий
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеБезналичныхДенежныхСредств КАК ПоступлениеБезналичныхДенежныхСредств
	|		ПО (ПоступлениеБезналичныхДенежныхСредств.ДатаВходящегоДокумента = ТаблицаДокументов.ДатаДок)
	|			И (ПоступлениеБезналичныхДенежныхСредств.НомерВходящегоДокумента = ТаблицаДокументов.Номер
	|				ИЛИ ПоступлениеБезналичныхДенежныхСредств.НомерВходящегоДокумента = ТаблицаДокументов.НомерСокр)
	|			И (ПоступлениеБезналичныхДенежныхСредств.СуммаДокумента = ТаблицаДокументов.СуммаДокумента)
	|			И (Не ПоступлениеБезналичныхДенежныхСредств.ПометкаУдаления)
	|			И Не ТаблицаДокументов.Исходящий
	|ГДЕ
	|	(НЕ ПоступлениеБезналичныхДенежныхСредств.Ссылка ЕСТЬ NULL
	|			ИЛИ НЕ СписаниеБезналичныхДенежныхСредств.Ссылка ЕСТЬ NULL)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Не ЗначениеЗаполнено(Выборка.ДокументСсылка) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаДокумента = ДокументыКЗагрузке.Найти(Выборка.НомерСтроки, "НомерСтроки");
		Если СтрокаДокумента <> Неопределено Тогда
			// Если документ найден, создавать новый не будем. Если найдено несколько документов, то обновлять данные документа не нужно.
			Если ЗначениеЗаполнено(СтрокаДокумента.Документ) Тогда
				СтрокаДокумента.Загружать = Ложь;
			КонецЕсли;
			СтрокаДокумента.Документ = Выборка.ДокументСсылка;
			СтрокаДокумента.НайденДокументВБазе = Истина;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
