#Область ОбработчикиСобытийФормы

&Перед("ПриСозданииНаСервере")
&НаСервере
Процедура бг_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	бг_ДобавитьРеквизитыФормы();
	бг_ДобавитьЭлементыФормы();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура бг_ДобавитьРеквизитыФормы()
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	ДлинаНомераВходящего = 
		Метаданные.Документы.ЗаказПоставщику.Реквизиты.НомерПоДаннымПоставщика.Тип.КвалификаторыСтроки.Длина;
	ДобавляемыеРеквизиты.Добавить(
		Новый РеквизитФормы(
			"НомерПоДаннымПоставщика", 
			ОбщегоНазначения.ОписаниеТипаСтрока(ДлинаНомераВходящего), 
			"СписокРаспоряжений", 
			НСтр("ru = 'Номер вх.'"), 
			Ложь));
	
	ДобавляемыеРеквизиты.Добавить(
		Новый РеквизитФормы(
			"ДатаПоДаннымПоставщика", 
			ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата), 
			"СписокРаспоряжений", 
			НСтр("ru = 'Дата вх.'"), 
			Ложь));
	
	бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьРеквизитыНаФорму(ЭтотОбъект, ДобавляемыеРеквизиты);
	
КонецПроцедуры

&НаСервере
Процедура бг_ДобавитьЭлементыФормы()
	
	ЭлементФормы = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"СписокРаспоряженийНомерПоДаннымПоставщика",
		Элементы.СписокРаспоряжений,
		"СписокРаспоряжений.НомерПоДаннымПоставщика",,
		Элементы.СписокРаспоряженийСуммаДокумента);
	
	ЭлементФормы.Ширина = 12;
	ЭлементФормы.РастягиватьПоГоризонтали = Ложь;
	ЭлементФормы.Подсказка = НСтр("ru = 'Номер заказа по данным поставщика'");
	
	ЭлементФормы = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"СписокРаспоряженийДатаПоДаннымПоставщика",
		Элементы.СписокРаспоряжений,
		"СписокРаспоряжений.ДатаПоДаннымПоставщика",,
		Элементы.СписокРаспоряженийСуммаДокумента);
	
	ЭлементФормы.Ширина = 8;
	ЭлементФормы.РастягиватьПоГоризонтали = Ложь;
	ЭлементФормы.Подсказка = НСтр("ru = 'Дата заказа по данным поставщика'");
	
КонецПроцедуры

&После("ЗаполнитьСписокРаспоряжений")
&НаСервере
Процедура бг_ЗаполнитьСписокРаспоряжений()
	
	// Заполним добавленные в таблицу реквизиты.
	Если СписокРаспоряжений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗаказыПоставщиков = СписокРаспоряжений.Выгрузить().ВыгрузитьКолонку("Ссылка");
	РеквизитыЗаказов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		ЗаказыПоставщиков,
		"НомерПоДаннымПоставщика,ДатаПоДаннымПоставщика");
	
	Для Каждого СтрокаТЧ Из СписокРаспоряжений Цикл
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, РеквизитыЗаказов.Получить(СтрокаТЧ.Ссылка));
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
