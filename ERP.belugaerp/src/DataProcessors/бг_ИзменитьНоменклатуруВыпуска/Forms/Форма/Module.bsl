#Область ОбработчикиСобытийФормы
	
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЗаполнитьДанныеФормы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДокументВыпускаПриИзменении(Элемент)
	ЗаполнитьДанныеФормы();
КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьЗамену(Команда)
	Если Не(ЗначениеЗаполнено(ДокументВыпуска) 
		И ЗначениеЗаполнено(НоваяСпецификация)) Тогда
		ПоказатьПредупреждение(, НСтр("ru='Заполните документ выпуска и новую спецификацию'"));
	ИначеЕсли НоваяСпецификация = СпецификацияВыпуск Тогда
		ПоказатьПредупреждение(, НСтр("ru='Новая спецификация совпадает с текущей'"));
	Иначе	
		ОповещениеОтвет = Новый ОписаниеОповещения("ВыполнитьЗаменуОтвет", ЭтотОбъект);
		ПоказатьВопрос(
			ОповещениеОтвет, 
			НСтр("ru='Будет выполнена замена. Продолжить?'"),
			РежимДиалогаВопрос.ДаНет,
			,
			КодВозвратаДиалога.Нет);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗаменуОтвет(Ответ, ПараметрыВопроса) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ВыполнитьЗаменуНаСервере();
		ОповеститьОбИзменении(СерияВыпуск);
		ОповеститьОбИзменении(ПартияВыпуск);
		ОповеститьОбИзменении(ЭтапВыпуск);
		ЗаполнитьДанныеФормы();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыполнитьЗаменуНаСервере()
	ПротоколРаботы.Очистить();
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	Результат = ОбработкаОбъект.ВыполнитьЗамену(ДокументВыпуска, НоваяСпецификация);
	ПротоколРаботы.УстановитьТекст(Результат.ПолучитьТекст());
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаПротокол;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеФормы()
	ДанныеВыпуска = ДанныеДокументаВыпуска();
	Элементы.ГруппаОшибки.Видимость = ДанныеВыпуска.ЕстьОшибки;
	Элементы.ОшибкаТекст.Заголовок = ДанныеВыпуска.ОшибкиТекст;
	Элементы.ВыполнитьЗамену.Доступность = Не ДанныеВыпуска.ЕстьОшибки;
	Элементы.ГруппаПредупреждения.Видимость = ДанныеВыпуска.ЕстьПредупреждения;
	Элементы.ПредупреждениеТекст.Заголовок = ДанныеВыпуска.ПредупрежденияТекст;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеВыпуска, , "ДокументВыпуска");                  
	ЗаполнитьПолеДокумент("СкладскойДокумент", ДанныеВыпуска);
	ЗаполнитьПолеДокумент("ФинансовыйДокумент", ДанныеВыпуска);
	ЗаполнитьПолеДокумент("ВыпускВЛабораторию", ДанныеВыпуска);
	ЗаполнитьПолеДокумент("ОтчетОПроизводствеЕГАИС", ДанныеВыпуска);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПолеДокумент(ИмяПоля, ДанныеВыпуска)
	ЭтотОбъект[ИмяПоля] = Неопределено;   
	Если ТипЗнч(ДанныеВыпуска[ИмяПоля]) = Тип("Массив") Тогда
		Для каждого СтруктураДокумент Из ДанныеВыпуска[ИмяПоля] Цикл
			Если СтруктураДокумент.Проведен Тогда
				ЭтотОбъект[ИмяПоля] = СтруктураДокумент.Ссылка;   
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ДанныеДокументаВыпуска()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	Результат = ОбработкаОбъект.ДанныеДокументаВыпуска(ДокументВыпуска);
	Возврат Результат;
КонецФункции

#КонецОбласти