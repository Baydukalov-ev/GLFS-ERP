
#Область ПрограммныйИнтерфейс

Процедура ВыполнитьКоманду(ИмяКоманды, ОбъектыНазначения, ПараметрыВыполнения) Экспорт
	
	ЗаполнитьСписокДокументов(ОбъектыНазначения);
	
	Если ИмяКоманды = "ОбработатьЗаказыEDI" Тогда
		
		ОбработатьЗаказыEDI(ПараметрыВыполнения);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСписокДокументов(ОбъектыНазначения) Экспорт
	
	Для каждого Ссылка Из ОбъектыНазначения Цикл
		СтрокаЗаказы = Заказы.Добавить();
		СтрокаЗаказы.ЗаказКлиента = Ссылка;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьЗаказыEDI(ПараметрыВыполнения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбработкаЗаполнитьУпаковки = Обработки.бг_ЗаполнитьУпаковки.Создать();
	
	ЕстьОшибки = Ложь;
	
	Для каждого СтрокаЗаказы Из Заказы Цикл
		
		Попытка
		
		ДокументОбъект = СтрокаЗаказы.ЗаказКлиента.ПолучитьОбъект();
		
		Если Не ДокументОбъект.бг_ИсточникЗаказа =
			ПредопределенноеЗначение("Перечисление.бг_ИсточникиЗагрузкиЗаказовКлиентов.EDI") Тогда
			
			ЕстьОшибки = Истина;
			СтрокаЗаказы.ИнформацияОбОшибке = НСтр("ru = 'Пропущен: заказ не EDI'");
			
			ДокументОбъект = Неопределено;
			Продолжить;
		КонецЕсли;
		
		// Заполнить упаковки
		ОбработкаЗаполнитьУпаковки.ЗаполнитьУпаковкиВДокументе(ДокументОбъект);
		
		// Рассчитать скидки наценки
		СтруктураПараметры = Новый Структура;
		СтруктураПараметры.Вставить("ПрименятьКОбъекту", Истина);
		СтруктураПараметры.Вставить("ТолькоПредварительныйРасчет", Ложь);
		СтруктураПараметры.Вставить("ВосстанавливатьУправляемыеСкидки", Истина);
		СтруктураПараметры.Вставить("УправляемыеСкидки", Неопределено);
		
		СкидкиНаценкиЗаполнениеСервер.Рассчитать(ДокументОбъект, СтруктураПараметры);
		
		// Провести
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
		
		Исключение
		
		СтрокаЗаказы.ИнформацияОбОшибке = СтрШаблон(НСтр("ru = 'Не проведён: %1'"), ОписаниеОшибки());
		ЕстьОшибки = Истина;
		
		КонецПопытки;
		
	КонецЦикла;
	
	Если ПараметрыВыполнения = Неопределено Тогда
		ПараметрыВыполнения = Новый Структура;
	КонецЕсли;
	
	ПараметрыВыполнения.Вставить("ЕстьОшибки", ЕстьОшибки);
	
КонецПроцедуры

#КонецОбласти
