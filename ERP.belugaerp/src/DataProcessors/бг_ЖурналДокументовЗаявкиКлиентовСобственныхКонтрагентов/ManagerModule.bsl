#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает сведения о сформированных заказах клиента ВГО по заявке (предзаказу).
//
//  Параметры:
//    ЗаявкаКлиента         - ДокументСсылка.битЗаявкаКлиента - ссылка на заявку клиента.
//    СортироватьПоУбыванию - Булево - Истина - строки заказов будут отсортированы в порядке убывания дат.
//                                     Ложь - в порядке возрастания дат.
//    ОтправкаПодтверждения - Булево - Истина - данные будут использоваться для формирования файлов 
//                                              подтверждения заказов. 
//
//  Возвращаемое значение:
//    Массив - содержит данные о сфорированных заказах по заявке.
//      * Элемент массива - Структура - сведения о сформированном заказе.
//
Функция СведенияПоСформированнымЗаказам(ЗаявкаКлиента, СортироватьПоУбыванию = Ложь, ОтправкаПодтверждения = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтрокиЗаказов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЗаявкаКлиента", ЗаявкаКлиента);
	Запрос.УстановитьПараметр(
		"ВозможноПодтверждениеЗаказовПоЗаявке",
		Документы.битЗаявкаКлиента.ВозможноПодтверждениеЗаказовПоЗаявке(ЗаявкаКлиента));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказКлиента.Ссылка КАК ЗаказКлиента,
	|	ЗаказКлиента.Проведен КАК Проведен,
	|	ЗаказКлиента.Номер КАК Номер,
	|	ЗаказКлиента.Дата КАК Дата,
	|	ЗаказКлиента.Склад КАК Склад,
	|	ЗаказКлиента.бг_ДатаОтправкиПодтвержденияСобственномуКонтрагенту КАК ДатаОтправкиПодтверждения,
	|	ЗаказКлиента.бг_ПунктНазначения КАК ПунктРазгрузки,
	|	ЗаказКлиента.ДатаОтгрузки КАК ДатаОтгрузки,
	|	ЗаказКлиента.бг_ОбъемБрутто КАК ОбъемБрутто,
	|	ЗаказКлиента.бг_ВесБрутто КАК ВесБрутто,
	|	ЗаказКлиента.бг_ПолныйРезерв КАК ПолныйРезерв,
	|	ВЫБОР
	|		КОГДА ЗаказКлиента.Проведен
	|			ТОГДА 0
	|		КОГДА ЗаказКлиента.ПометкаУдаления
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК СтандартнаяКартинка,
	|	ВЫБОР
	|		КОГДА ЗаказКлиента.Проведен
	|				И ЗаказКлиента.бг_ДатаОтправкиПодтвержденияСобственномуКонтрагенту = ДАТАВРЕМЯ(1, 1, 1)
	|				И &ВозможноПодтверждениеЗаказовПоЗаявке
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Пометка,
	|	ВЫБОР
	|		КОГДА ЗаказКлиента.Проведен
	|				И ЗаказКлиента.бг_ДатаОтправкиПодтвержденияСобственномуКонтрагенту = ДАТАВРЕМЯ(1, 1, 1)
	|				И &ВозможноПодтверждениеЗаказовПоЗаявке
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВозможноПодтверждениеЗаказа
	|ИЗ
	|	Документ.ЗаказКлиента КАК ЗаказКлиента
	|ГДЕ
	|	ЗаказКлиента.бг_ЗаявкаКлиента = &ЗаявкаКлиента
	|	И &УсловиеНеподтвержденные
	|
	|УПОРЯДОЧИТЬ ПО
	|	&ПолеУпорядочивания";
	
	Если СортироватьПоУбыванию Тогда
		ТекстПолеУпорядочивания = "Дата УБЫВ";
	Иначе
		ТекстПолеУпорядочивания = "Дата";
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПолеУпорядочивания", ТекстПолеУпорядочивания);
	
	Если ОтправкаПодтверждения Тогда
		ТекстУсловияНеподтвержденные = 
			"ЗаказКлиента.бг_ДатаОтправкиПодтвержденияСобственномуКонтрагенту = ДАТАВРЕМЯ(1, 1, 1)
			|	И &ВозможноПодтверждениеЗаказовПоЗаявке
			|	И ЗаказКлиента.Проведен";
	Иначе
		ТекстУсловияНеподтвержденные = "ИСТИНА";
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеНеподтвержденные", ТекстУсловияНеподтвержденные);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаТаблицы = ПустаяСтрокаТаблицыСформированныеЗаказы();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);
		СтрокиЗаказов.Добавить(СтрокаТаблицы);
	КонецЦикла;
	
	Возврат СтрокиЗаказов;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПустаяСтрокаТаблицыСформированныеЗаказы()
	
	СтрокаТаблицы = Новый Структура;
	СтрокаТаблицы.Вставить("ЗаказКлиента", Неопределено);
	СтрокаТаблицы.Вставить("ДатаОтправкиПодтверждения", '00010101');
	СтрокаТаблицы.Вставить("Склад", Неопределено);
	СтрокаТаблицы.Вставить("ПунктРазгрузки", Неопределено);
	СтрокаТаблицы.Вставить("Проведен", Ложь);
	СтрокаТаблицы.Вставить("СтандартнаяКартинка", 2);
	СтрокаТаблицы.Вставить("ДатаОтгрузки", '00010101');
	СтрокаТаблицы.Вставить("Пометка", Ложь);
	СтрокаТаблицы.Вставить("ОбъемБрутто", 0);
	СтрокаТаблицы.Вставить("ВесБрутто", 0);
	СтрокаТаблицы.Вставить("ПолныйРезерв", Ложь);
	СтрокаТаблицы.Вставить("ВозможноПодтверждениеЗаказа", Ложь);
	
	Возврат СтрокаТаблицы; 
	
КонецФункции

#КонецОбласти

#КонецЕсли
