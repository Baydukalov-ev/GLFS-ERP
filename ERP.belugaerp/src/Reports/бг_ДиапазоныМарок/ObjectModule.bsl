#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ПараметрСерия 		= Новый ПараметрКомпоновкиДанных("СерииФСМ");
	ПараметрЗаявление 	= Новый ПараметрКомпоновкиДанных("ЗаявленияОВыдачеФСМ");
	ПараметрНакладная 	= Новый ПараметрКомпоновкиДанных("НакладныеНаВыдачуФСМ");
	ПараметрРегистратор = Новый ПараметрКомпоновкиДанных("Регистратор");
	
	Серия = Неопределено;
	Заявление = Неопределено;
	Накладная = Неопределено;
	Регистратор = Неопределено;
	
	ОтборЗадан = Ложь;
	
	Для каждого ТекущийПараметр Из КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		Если Не ТекущийПараметр.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекущийПараметр.Параметр = ПараметрСерия Тогда
			Серия = ТекущийПараметр.Значение;
		ИначеЕсли ТекущийПараметр.Параметр = ПараметрЗаявление Тогда
			Заявление = ТекущийПараметр.Значение;
		ИначеЕсли ТекущийПараметр.Параметр = ПараметрНакладная Тогда
			Накладная = ТекущийПараметр.Значение;
		ИначеЕсли ТекущийПараметр.Параметр = ПараметрРегистратор Тогда
			Регистратор = ТекущийПараметр.Значение;
		Иначе
			Продолжить;
		КонецЕсли;
		
		ОтборЗадан = Истина;
	КонецЦикла;
	
	Если Не ОтборЗадан Тогда
		ВызватьИсключение НСтр("ru='Не указан ни один отбор, укажите хотя бы один'");
	КонецЕсли;
	
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ЕстьОтборПоСериям", ЗначениеЗаполнено(Серия));
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("МассивСерийФСМ", Серия);
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ЕстьОтборПоЗаявлениям", ЗначениеЗаполнено(Заявление));
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("МассивЗаявлений", Заявление);
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ЕстьОтборПоНакладным", ЗначениеЗаполнено(Накладная));
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("МассивНакладных", Накладная);
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ОтборПоРегистратору",
								ЗначениеЗаполнено(Регистратор));
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("Регистратор", Регистратор);
	
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("КороткаяСерия", 100000000);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
