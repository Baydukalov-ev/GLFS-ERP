#Область ОбработчикиСобытийФормы
 
&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	ХарактеристикиНоменклатуры =
	    ХарактеристикиНоменклатуры
		Или ПолучитьФункциональнуюОпцию("бг_ВестиУчетРезервовАлкогольнойПродукцииВРазрезеУпаковок");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&Перед("ДобавитьНоменклатуруПоОтборуНаСервере")
&НаСервере
Процедура бг_ДобавитьНоменклатуруПоОтборуНаСервере(АдресВоВременномХранилище)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВремТаблицаНоменклатура = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
	
	ТаблицаНоменклатура = ВремТаблицаНоменклатура.СкопироватьКолонки();

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОтобраннаяНоменклатура", ВремТаблицаНоменклатура.ВыгрузитьКолонку("Номенклатура"));	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	бг_ХарактеристикиУпаковокПаллет.Номенклатура КАК Номенклатура,
	|	бг_ХарактеристикиУпаковокПаллет.Характеристика КАК Характеристика
	|ИЗ
	|	РегистрСведений.бг_ХарактеристикиУпаковокПаллет КАК бг_ХарактеристикиУпаковокПаллет
	|ГДЕ
	|	бг_ХарактеристикиУпаковокПаллет.Номенклатура В(&ОтобраннаяНоменклатура)
	|	И ЕСТЬNULL(ВЫРАЗИТЬ(бг_ХарактеристикиУпаковокПаллет.Номенклатура КАК Справочник.Номенклатура).бг_УчетОстатковИРезервовВРазрезеУпаковокПаллет, ЛОЖЬ)";
	
	УпаковкиПаллетыНоменклатуры = Запрос.Выполнить().Выгрузить();
	СтруктураПоиска = Новый Структура;
	Для каждого СтрокаТаблицы Из ВремТаблицаНоменклатура Цикл
		СтруктураПоиска.Вставить("Номенклатура", СтрокаТаблицы.Номенклатура);
		НайденныеСтрокиУпаковокПаллетНоменклатуры = УпаковкиПаллетыНоменклатуры.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтрокиУпаковокПаллетНоменклатуры.Количество() > 0 Тогда
			Для каждого СтрокаУпаковокПаллетНоменклатуры Из НайденныеСтрокиУпаковокПаллетНоменклатуры Цикл
				НоваяСтрока = ТаблицаНоменклатура.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				НоваяСтрока.Характеристика = СтрокаУпаковокПаллетНоменклатуры.Характеристика;
			КонецЦикла;
		Иначе	
			ЗаполнитьЗначенияСвойств(ТаблицаНоменклатура.Добавить(), СтрокаТаблицы);	
		КонецЕсли;		
	КонецЦикла;

	ПоместитьВоВременноеХранилище(ТаблицаНоменклатура, АдресВоВременномХранилище);
	
КонецПроцедуры

#КонецОбласти
