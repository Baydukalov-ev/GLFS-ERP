#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

&Перед("ПриКомпоновкеРезультата")
Процедура бг_ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ТекстЗапроса = СхемаКомпоновкиДанных.НаборыДанных.Основной.Запрос;
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	КоличествоПакетов = СхемаЗапроса.ПакетЗапросов.Количество();
	Для ИндексПакета = 0 По КоличествоПакетов - 1 Цикл
		ЗапросВыбора = СхемаЗапроса.ПакетЗапросов[ИндексПакета];	
		Для каждого ОператорВыбора Из ЗапросВыбора.Операторы Цикл
			
			ИсточникТоварыКОтгрузке = ОператорВыбора.Источники.НайтиПоПсевдониму("ТоварыКОтгрузке");
			
			Если ИсточникТоварыКОтгрузке = Неопределено Тогда
				Продолжить;	
			КонецЕсли;
			
			Если СтрСравнить(ИсточникТоварыКОтгрузке.Источник.ИмяТаблицы, "РегистрНакопления.ТоварыКОтгрузке.Остатки") <> 0 Тогда
				Продолжить;	
			КонецЕсли;

			ОператорВыбора.Источники.Добавить("РегистрСведений.бг_ХарактеристикиУпаковокПаллет", "ХарактеристикиУпаковокПаллет");
			ИсточникХарактеристикиУпаковокПаллет = ОператорВыбора.Источники.НайтиПоПсевдониму("ХарактеристикиУпаковокПаллет");
			
			ИсточникТоварыКОтгрузке.Соединения.Добавить(
				ИсточникХарактеристикиУпаковокПаллет,
				ИсточникТоварыКОтгрузке.Источник.Псевдоним +
					".Номенклатура = ХарактеристикиУпаковокПаллет.Номенклатура");
			ИсточникТоварыКОтгрузке.Соединения.Добавить(
				ИсточникХарактеристикиУпаковокПаллет,
				ИсточникТоварыКОтгрузке.Источник.Псевдоним +
					".Серия.бг_УпаковкаПаллета = ХарактеристикиУпаковокПаллет.УпаковкаПаллета");
			
			ВыбираемыеПоля = ОператорВыбора.ВыбираемыеПоля;
			КоличествоПолей = ВыбираемыеПоля.Количество();
			Для ИндексПоля = 0 По КоличествоПолей - 1 Цикл
				ТекстПоля = Строка(ВыбираемыеПоля[ИндексПоля]);
				Если СтрСравнить(ТекстПоля, ИсточникТоварыКОтгрузке.Источник.Псевдоним + ".Характеристика") = 0 Тогда	
					ВыражениеПоляХарактеристика = Новый ВыражениеСхемыЗапроса(
						"ЕСТЬNULL(ХарактеристикиУпаковокПаллет.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))"); 
					ВыбираемыеПоля.Установить(ИндексПоля, ВыражениеПоляХарактеристика);	
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	СхемаКомпоновкиДанных.НаборыДанных.Основной.Запрос = ТекстЗапроса;
	
КонецПроцедуры	

#КонецОбласти

#КонецЕсли