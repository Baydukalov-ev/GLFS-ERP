#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("бг_УчетБанковскихГарантий");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос        = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		ТекстЗапросаТаблицаАкцизПоПриобретеннымЦенностям(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
		
	КонецЕсли;
		
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

// Возвращает таблицу рассчитанных авансов по акцизам
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация, для которой требуется рассчитать авансы по акцизам
//  Период - Дата - дата на которую выполняется расчет авансов по акцизам
//  ТекущийДокумент - ДокументСсылка.битРасчетАвансовПоАкцизам - ссылка на текущий документ
//
// Возвращаемое значение:
//  ТаблицаЗначений
//
Функция АвансыКОплате(Организация, Период, ТекущийДокумент = Неопределено) Экспорт
	Запрос = Новый Запрос;
	
	МассивТекстов = Новый Массив;
	МассивТекстов.Добавить(ТекстЗапросаСрокиДействияБанковскихГарантий());
	МассивТекстов.Добавить(ТекстЗапросаАвансыКОплате());
	
	Запрос.Текст = СтрСоединить(МассивТекстов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СостоянияСырьяВРеализованнойПродукцииПодтверждено",
				Перечисления.бг_СостоянияПодакцизногоСырья.СостоянияСырьяВРеализованнойПродукцииРФ());
	Запрос.УстановитьПараметр("СостоянияСырьяВОжиданииПодтвержденияЭкспорта",
				Перечисления.бг_СостоянияПодакцизногоСырья.СостоянияСырьяВОжиданииПодтвержденияЭкспорта());
	Запрос.УстановитьПараметр("СостоянияСырьяВРеализованнойПродукцииПодтвержденоЭкспорт",
				Перечисления.бг_СостоянияПодакцизногоСырья.СостоянияСырьяВРеализованнойПродукцииПодтвержденоЭкспорт());
	Запрос.УстановитьПараметр("СостоянияСырьяВыполненоСписание",
				Перечисления.бг_СостоянияПодакцизногоСырья.СостоянияСырьяВыполненоСписание());
	Запрос.УстановитьПараметр("Регистратор", ТекущийДокумент);
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("ПериодГраница", Новый Граница(Период, ВидГраницы.Включая));
	
	Результат = Запрос.Выполнить();
	Возврат Результат.Выгрузить();
КонецФункции

// Возвращает таблицу рассчитанных вычетов по выплаченным авансам
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация, для которой требуется рассчитать вычеты по авансам
//  Период - Дата - дата на которую выполняется расчет
//  ТекущийДокумент - ДокументСсылка.битРасчетАвансовПоАкцизам - ссылка на текущий документ
//
// Возвращаемое значение:
//  ТаблицаЗначений
//
Функция ВычетыПоАвансам(Организация, Период, ТекущийДокумент = Неопределено) Экспорт
	Запрос = Новый Запрос;
	
	МассивТекстов = Новый Массив;
	МассивТекстов.Добавить(ТекстЗапросаСрокиДействияБанковскихГарантий());
	МассивТекстов.Добавить(ТекстЗапросаВычетыПоАвансам());
	Запрос.Текст = СтрСоединить(МассивТекстов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Регистратор", ТекущийДокумент);
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("ПериодГраница", Новый Граница(Период, ВидГраницы.Включая));
	
	СостоянияСырьяКВозмещениюАвансов = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СостоянияСырьяКВозмещениюАвансов, Перечисления.бг_СостоянияПодакцизногоСырья.СостоянияСырьяВыполненоСписание());
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СостоянияСырьяКВозмещениюАвансов, Перечисления.бг_СостоянияПодакцизногоСырья.СостоянияСырьяВРеализованнойПродукцииРФ());
	Запрос.УстановитьПараметр("СостоянияСырьяКВозмещениюАвансов", СостоянияСырьяКВозмещениюАвансов);
	
	Запрос.УстановитьПараметр("СостоянияСырьяВОжиданииПодтвержденияЭкспорта",
			Перечисления.бг_СостоянияПодакцизногоСырья.СостоянияСырьяВОжиданииПодтвержденияЭкспорта());
	Запрос.УстановитьПараметр("СостоянияСырьяВРеализованнойПродукцииПодтвержденоЭкспорт",
			Перечисления.бг_СостоянияПодакцизногоСырья.СостоянияСырьяВРеализованнойПродукцииПодтвержденоЭкспорт());
	
	Результат = Запрос.Выполнить();
	Возврат Результат.Выгрузить();
КонецФункции

Функция ПолучитьСуммуДокумента(ДокументОбъект) Экспорт
	
	Возврат ДокументОбъект.Акцизы.Итог("СуммаАкциза");
	
КонецФункции

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	СУММА(ЕСТЬNULL(Строки.СуммаАкциза, 0)) КАК Сумма,
	|	СУММА(ЕСТЬNULL(Строки.СуммаАкциза, 0)) КАК СуммаУУ,
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НалогообложениеАкцизомОрганизаций.ВидОперацииНалогообложения.СчетДт КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Акцизы) КАК СчетКт,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог) КАК СубконтоКт1,
	|	Операция.Организация.РегистрацияВНалоговомОргане КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Начисление авансового акциза"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.битРасчетАвансовПоАкцизам КАК Операция
	|		ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|			И Операция.Дата >= Операция.Организация.бг_ДатаНачалаУчетаАкцизовПоПриобретеннымЦенностям
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.битРасчетАвансовПоАкцизам.Акцизы КАК Строки
	|		ПО (Строки.Ссылка = Операция.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.бг_НалогообложениеАкцизомОрганизаций КАК НалогообложениеАкцизомОрганизаций
	|		ПО (Операция.Организация = НалогообложениеАкцизомОрганизаций.Организация)
	|			И (Строки.СтатусАкциза = НалогообложениеАкцизомОрганизаций.СтатусАкциза)
	|			И (Строки.СостояниеСырья = НалогообложениеАкцизомОрганизаций.СостояниеСырья)
	|			И (Строки.СостояниеОплатыАванса.Статус = НалогообложениеАкцизомОрганизаций.СтатусНачисленияАванса)
	|			И (Строки.СостояниеОплатыАванса.ПродажаНаЭкспорт = НалогообложениеАкцизомОрганизаций.ПродажаНаЭкспорт)
	|			И (НалогообложениеАкцизомОрганизаций.Номенклатура В (Строки.Номенклатура, Строки.Номенклатура.ВидНоменклатуры))
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.бг_ОперацииРасчетаАвансовПоАкцизам.РасчетАвансов)
	|
	|СГРУППИРОВАТЬ ПО
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	Операция.Организация,
	|	НалогообложениеАкцизомОрганизаций.ВидОперацииНалогообложения.СчетДт
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	Операция.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	СУММА(ЕСТЬNULL(Строки.СуммаАкциза, 0)),
	|	СУММА(ЕСТЬNULL(Строки.СуммаАкциза, 0)),
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Акцизы),
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог),
	|	Операция.Организация.РегистрацияВНалоговомОргане,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка),
	|	НалогообложениеАкцизомОрганизаций.ВидОперацииНалогообложения.СчетКт,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	""Принят к вычету авансовый акциз""
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.битРасчетАвансовПоАкцизам КАК Операция
	|		ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|			И Операция.Дата >= Операция.Организация.бг_ДатаНачалаУчетаАкцизовПоПриобретеннымЦенностям
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.битРасчетАвансовПоАкцизам.Акцизы КАК Строки
	|		ПО (Строки.Ссылка = Операция.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.бг_НалогообложениеАкцизомОрганизаций КАК НалогообложениеАкцизомОрганизаций
	|		ПО (Операция.Организация = НалогообложениеАкцизомОрганизаций.Организация)
	|			И (Строки.СтатусАкциза = НалогообложениеАкцизомОрганизаций.СтатусАкциза)
	|			И (Строки.СостояниеСырья = НалогообложениеАкцизомОрганизаций.СостояниеСырья)
	|			И (Строки.СостояниеОплатыАванса.Статус = НалогообложениеАкцизомОрганизаций.СтатусНачисленияАванса)
	|			И (Строки.СостояниеОплатыАванса.ПродажаНаЭкспорт = НалогообложениеАкцизомОрганизаций.ПродажаНаЭкспорт)
	|			И (НалогообложениеАкцизомОрганизаций.Номенклатура В (Строки.Номенклатура, Строки.Номенклатура.ВидНоменклатуры))
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.бг_ОперацииРасчетаАвансовПоАкцизам.РасчетВозмещенияАвансов)
	|
	|СГРУППИРОВАТЬ ПО
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	Операция.Организация,
	|	НалогообложениеАкцизомОрганизаций.ВидОперацииНалогообложения.СчетКт
	|";

	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//	ТекстЗапроса - Строка - Текст запроса создания временных таблиц.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат "";
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

&Вместо("ОбработкаПолученияПолейПредставления")
Процедура бг_ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("Ссылка");
	Поля.Добавить("Номер");
	Поля.Добавить("Дата");
	Поля.Добавить("ХозяйственнаяОперация");
КонецПроцедуры

&Вместо("ОбработкаПолученияПредставления")
Процедура бг_ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Данные.ХозяйственнаяОперация) Тогда
		СтандартнаяОбработка = Ложь;
		
		Представление = СтрШаблон(
				НСтр("ru = '%1 %2 от %3';
					|en = '%1 %2 dated %3'"),
				Данные.ХозяйственнаяОперация,
				Данные.Номер,
				Данные.Дата);
		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Дата, Организация");
	Запрос.УстановитьПараметр("Период", СтруктураРеквизитов.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураРеквизитов.Организация);
	
КонецПроцедуры

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаАкцизПоПриобретеннымЦенностям(Запрос, ТекстыЗапроса, Регистры);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаАкцизПоПриобретеннымЦенностям(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "бг_АкцизПоПриобретеннымЦенностям";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Документ.Корректировка
	|			ТОГДА Документ.КорректируемыйПериод
	|		ИНАЧЕ Документ.Дата
	|	КОНЕЦ КАК Период,
	|	ВЫБОР
	|		КОГДА АкцизПоПриобретеннымЦенностям.Количество < 0
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	Документ.Организация КАК Организация,
	|	АкцизПоПриобретеннымЦенностям.Номенклатура КАК Номенклатура,
	|	АкцизПоПриобретеннымЦенностям.СерияНоменклатуры КАК СерияНоменклатуры,
	|	АкцизПоПриобретеннымЦенностям.Сделка КАК Сделка,
	|	АкцизПоПриобретеннымЦенностям.СостояниеСырья КАК СостояниеСырья,
	|	АкцизПоПриобретеннымЦенностям.СтатусАкциза КАК СтатусАкциза,
	|	АкцизПоПриобретеннымЦенностям.Количество КАК Количество,
	|	АкцизПоПриобретеннымЦенностям.СуммаАкциза КАК СуммаАкциза,
	|	АкцизПоПриобретеннымЦенностям.Продукция КАК Продукция,
	|	АкцизПоПриобретеннымЦенностям.СерияПродукции КАК СерияПродукции,
	|	АкцизПоПриобретеннымЦенностям.ДатаРеализации КАК ДатаРеализации,
	|	ВЫБОР
	|		КОГДА АкцизПоПриобретеннымЦенностям.Количество < 0
	|			ТОГДА АкцизПоПриобретеннымЦенностям.СостояниеОплатыАванса
	|		ИНАЧЕ АкцизПоПриобретеннымЦенностям.СостояниеОплатыАвансаИсходное
	|	КОНЕЦ КАК СостояниеОплатыАванса
	|ИЗ
	|	Документ.битРасчетАвансовПоАкцизам КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.битРасчетАвансовПоАкцизам.Акцизы КАК АкцизПоПриобретеннымЦенностям
	|		ПО (Документ.Ссылка = &Ссылка)
	|			И Документ.Ссылка = АкцизПоПриобретеннымЦенностям.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Документ.Корректировка
	|			ТОГДА Документ.КорректируемыйПериод
	|		ИНАЧЕ Документ.Дата
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	Документ.Организация,
	|	АкцизПоПриобретеннымЦенностям.Номенклатура,
	|	АкцизПоПриобретеннымЦенностям.СерияНоменклатуры,
	|	АкцизПоПриобретеннымЦенностям.Сделка,
	|	АкцизПоПриобретеннымЦенностям.СостояниеСырья,
	|	АкцизПоПриобретеннымЦенностям.СтатусАкциза,
	|	ВЫБОР
	|		КОГДА АкцизПоПриобретеннымЦенностям.Количество < 0
	|			ТОГДА -АкцизПоПриобретеннымЦенностям.Количество
	|		ИНАЧЕ АкцизПоПриобретеннымЦенностям.Количество
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА АкцизПоПриобретеннымЦенностям.Количество < 0
	|			ТОГДА -АкцизПоПриобретеннымЦенностям.СуммаАкциза
	|		ИНАЧЕ АкцизПоПриобретеннымЦенностям.СуммаАкциза
	|	КОНЕЦ,
	|	АкцизПоПриобретеннымЦенностям.Продукция,
	|	АкцизПоПриобретеннымЦенностям.СерияПродукции,
	|	АкцизПоПриобретеннымЦенностям.ДатаРеализации,
	|	ВЫБОР
	|		КОГДА АкцизПоПриобретеннымЦенностям.Количество < 0
	|			ТОГДА АкцизПоПриобретеннымЦенностям.СостояниеОплатыАвансаИсходное
	|		ИНАЧЕ АкцизПоПриобретеннымЦенностям.СостояниеОплатыАванса
	|	КОНЕЦ
	|ИЗ
	|	Документ.битРасчетАвансовПоАкцизам КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.битРасчетАвансовПоАкцизам.Акцизы КАК АкцизПоПриобретеннымЦенностям
	|		ПО (Документ.Ссылка = &Ссылка)
	|			И Документ.Ссылка = АкцизПоПриобретеннымЦенностям.Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                      КАК Период,
	|	&Организация                 КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСрокиДействияБанковскихГарантий()
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	БанковскиеГарантии.Сделка КАК БанковскаяГарантия,
	|	БанковскиеГарантии.Номенклатура КАК Номенклатура,
	|	БанковскиеГарантии.СерияНоменклатуры КАК СерияНоменклатуры,
	|	БанковскиеГарантии.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ БанковскиеГарантии
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям.Остатки(
	|			&ПериодГраница,
	|			Организация = &Организация
	|				И ТИПЗНАЧЕНИЯ(Сделка) = ТИП(Справочник.бг_БанковскиеГарантии)) КАК БанковскиеГарантии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АкцизОбороты.Период КАК Период,
	|	АкцизОбороты.Сделка КАК БанковскаяГарантия,
	|	АкцизОбороты.Номенклатура КАК Номенклатура,
	|	АкцизОбороты.СерияНоменклатуры КАК СерияНоменклатуры,
	|	АкцизОбороты.КоличествоПриход КАК КоличествоПриход
	|ПОМЕСТИТЬ ПериодыОтгрузкиОтПоставщика
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям.Обороты(
	|			,
	|			,
	|			Месяц,
	|			Организация = &Организация
	|				И СтатусАкциза = ЗНАЧЕНИЕ(Перечисление.бг_СостоянияОплатыВходящегоАкциза.АвансовыйАкцизНаТоварыВПути)
	|				И (Сделка, Номенклатура, СерияНоменклатуры) В
	|					(ВЫБРАТЬ
	|						БанковскиеГарантии.БанковскаяГарантия КАК БанковскаяГарантия,
	|						БанковскиеГарантии.Номенклатура КАК Номенклатура,
	|						БанковскиеГарантии.СерияНоменклатуры КАК СерияНоменклатуры
	|					ИЗ
	|						БанковскиеГарантии КАК БанковскиеГарантии)) КАК АкцизОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыОтгрузкиОтПоставщика.Номенклатура.ВидНоменклатуры.бг_ВидОперацииНалогообложенияАкцизом КАК ВидОперацииНалогообложения,
	|	ПериодыОтгрузкиОтПоставщика.БанковскаяГарантия КАК БанковскаяГарантия,
	|	ПериодыОтгрузкиОтПоставщика.Номенклатура КАК Номенклатура,
	|	ПериодыОтгрузкиОтПоставщика.СерияНоменклатуры КАК СерияНоменклатуры,
	|	ДОБАВИТЬКДАТЕ(ДОБАВИТЬКДАТЕ(ПериодыОтгрузкиОтПоставщика.Период, МЕСЯЦ, 1),
	|			ДЕНЬ, ПериодыОтгрузкиОтПоставщика.Номенклатура.ВидНоменклатуры.бг_ВидОперацииНалогообложенияАкцизом.РасчетныйСрокБГ) КАК СрокДействия
	|ПОМЕСТИТЬ СрокиДействияБанковскихГарантий
	|ИЗ
	|	ПериодыОтгрузкиОтПоставщика КАК ПериодыОтгрузкиОтПоставщика
	|ГДЕ
	|	НЕ ПериодыОтгрузкиОтПоставщика.Номенклатура.ВидНоменклатуры.бг_ВидОперацииНалогообложенияАкцизом.РасчетныйСрокБГ ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СрокиДействияБанковскихГарантий.ВидОперацииНалогообложения КАК ВидОперацииНалогообложения,
	|	СрокиДействияБанковскихГарантий.БанковскаяГарантия КАК БанковскаяГарантия,
	|	СрокиДействияБанковскихГарантий.Номенклатура КАК Номенклатура,
	|	СрокиДействияБанковскихГарантий.СерияНоменклатуры КАК СерияНоменклатуры
	|ПОМЕСТИТЬ ИстекшиеБанковскиеГарантии
	|ИЗ
	|	СрокиДействияБанковскихГарантий КАК СрокиДействияБанковскихГарантий
	|ГДЕ
	|	СрокиДействияБанковскихГарантий.СрокДействия <= &Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СрокиДействияБанковскихГарантий.ВидОперацииНалогообложения КАК ВидОперацииНалогообложения,
	|	СрокиДействияБанковскихГарантий.БанковскаяГарантия КАК БанковскаяГарантия,
	|	СрокиДействияБанковскихГарантий.Номенклатура КАК Номенклатура,
	|	СрокиДействияБанковскихГарантий.СерияНоменклатуры КАК СерияНоменклатуры
	|ПОМЕСТИТЬ ДействующиеБанковскиеГарантии
	|ИЗ
	|	СрокиДействияБанковскихГарантий КАК СрокиДействияБанковскихГарантий
	|ГДЕ
	|	СрокиДействияБанковскихГарантий.СрокДействия > &Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СрокиДействияБанковскихГарантий.ВидОперацииНалогообложения КАК ВидОперацииНалогообложения,
	|	СрокиДействияБанковскихГарантий.БанковскаяГарантия КАК БанковскаяГарантия,
	|	СрокиДействияБанковскихГарантий.Номенклатура КАК Номенклатура,
	|	СрокиДействияБанковскихГарантий.СерияНоменклатуры КАК СерияНоменклатуры
	|ПОМЕСТИТЬ ИстекшиеВТекущемПериодеБанковскиеГарантии
	|ИЗ
	|	СрокиДействияБанковскихГарантий КАК СрокиДействияБанковскихГарантий
	|ГДЕ
	|	СрокиДействияБанковскихГарантий.СрокДействия <= &Период
	|	И СрокиДействияБанковскихГарантий.СрокДействия > НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаАвансыКОплате()
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КодыВидовОперацийНалогообложенияСрезПоследних.ВидОперацииНалогообложенияАкцизом КАК ВидОперацииНалогообложенияАкцизом,
	|	КодыВидовОперацийНалогообложенияСрезПоследних.ВидПодакцизныхТоваров КАК ВидПодакцизныхТоваров,
	|	КодыВидовОперацийНалогообложенияСрезПоследних.ВидПодакцизныхТоваров.НалоговаяСтавка КАК НалоговаяСтавка
	|ПОМЕСТИТЬ НалоговыеСтавки
	|ИЗ
	|	РегистрСведений.бг_КодыВидовОперацийНалогообложенияАкцизом.СрезПоследних(
	|			&Период,
	|			ВидОперацииНалогообложенияАкцизом В
	|				(ВЫБРАТЬ
	|					ИстекшиеБанковскиеГарантии.ВидОперацииНалогообложения КАК ВидОперацииНалогообложения
	|				ИЗ
	|					ИстекшиеБанковскиеГарантии КАК ИстекшиеБанковскиеГарантии)) КАК КодыВидовОперацийНалогообложенияСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АкцизОстатки.Сделка КАК Сделка,
	|	АкцизОстатки.Номенклатура КАК Номенклатура,
	|	АкцизОстатки.СерияНоменклатуры КАК СерияНоменклатуры,
	|	АкцизОстатки.СостояниеСырья КАК СостояниеСырья,
	|	АкцизОстатки.СтатусАкциза КАК СтатусАкциза,
	|	АкцизОстатки.Продукция КАК Продукция,
	|	АкцизОстатки.СерияПродукции КАК СерияПродукции,
	|	АкцизОстатки.ДатаРеализации КАК ДатаРеализации,
	|	АкцизОстатки.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ Данные
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям.Остатки(
	|			&ПериодГраница,
	|			Организация = &Организация
	|				И (Сделка, Номенклатура, СерияНоменклатуры) В
	|					(ВЫБРАТЬ
	|						ИстекшиеБанковскиеГарантии.БанковскаяГарантия КАК БанковскаяГарантия,
	|						ИстекшиеБанковскиеГарантии.Номенклатура КАК Номенклатура,
	|						ИстекшиеБанковскиеГарантии.СерияНоменклатуры КАК СерияНоменклатуры
	|					ИЗ
	|						ИстекшиеБанковскиеГарантии КАК ИстекшиеБанковскиеГарантии)
	|				И НЕ СостояниеСырья В (&СостоянияСырьяВРеализованнойПродукцииПодтверждено)
	|				И НЕ СостояниеСырья В (&СостоянияСырьяВыполненоСписание)
	|				И НЕ СтатусАкциза = ЗНАЧЕНИЕ(Перечисление.бг_СостоянияОплатыВходящегоАкциза.АкцизПоВозвращеннойПродукции)
	|				И СостояниеОплатыАванса = ЗНАЧЕНИЕ(Справочник.бг_СостоянияОплатыАванса.ПустаяСсылка)) КАК АкцизОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АкцизПоПриобретеннымЦенностям.Сделка,
	|	АкцизПоПриобретеннымЦенностям.Номенклатура,
	|	АкцизПоПриобретеннымЦенностям.СерияНоменклатуры,
	|	АкцизПоПриобретеннымЦенностям.СостояниеСырья,
	|	АкцизПоПриобретеннымЦенностям.СтатусАкциза,
	|	АкцизПоПриобретеннымЦенностям.Продукция,
	|	АкцизПоПриобретеннымЦенностям.СерияПродукции,
	|	АкцизПоПриобретеннымЦенностям.ДатаРеализации,
	|	АкцизПоПриобретеннымЦенностям.Количество
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям КАК АкцизПоПриобретеннымЦенностям
	|ГДЕ
	|	АкцизПоПриобретеннымЦенностям.Регистратор = &Регистратор
	|	И АкцизПоПриобретеннымЦенностям.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Данные.Сделка КАК Сделка,
	|	Данные.Номенклатура КАК Номенклатура,
	|	Данные.СерияНоменклатуры КАК СерияНоменклатуры,
	|	Данные.СостояниеСырья КАК СостояниеСырья,
	|	Данные.СтатусАкциза КАК СтатусАкциза,
	|	Данные.Продукция КАК Продукция,
	|	Данные.СерияПродукции КАК СерияПродукции,
	|	Данные.ДатаРеализации,
	|	НалоговыеСтавки.НалоговаяСтавка КАК НалоговаяСтавка,
	|	ВЫБОР
	|		КОГДА Данные.СостояниеСырья В (&СостоянияСырьяВОжиданииПодтвержденияЭкспорта, &СостоянияСырьяВРеализованнойПродукцииПодтвержденоЭкспорт)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПродажаНаЭкспорт,
	|	СУММА(Данные.Количество) КАК Количество,
	|	СУММА(Данные.Количество * ЕСТЬNULL(НалоговыеСтавки.НалоговаяСтавка, 0) * 10) КАК СуммаАкциза
	|ИЗ
	|	Данные КАК Данные
	|		ЛЕВОЕ СОЕДИНЕНИЕ НалоговыеСтавки КАК НалоговыеСтавки
	|		ПО Данные.Номенклатура.ВидНоменклатуры.бг_ВидОперацииНалогообложенияАкцизом = НалоговыеСтавки.ВидОперацииНалогообложенияАкцизом
	|
	|СГРУППИРОВАТЬ ПО
	|	Данные.Сделка,
	|	Данные.Номенклатура,
	|	Данные.СерияНоменклатуры,
	|	Данные.ДатаРеализации,
	|	НалоговыеСтавки.НалоговаяСтавка,
	|	Данные.СостояниеСырья,
	|	Данные.СтатусАкциза,
	|	Данные.Продукция,
	|	Данные.СерияПродукции";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаВычетыПоАвансам()
	ТекстЗапроса =
	// реализация и списание ГП
	"ВЫБРАТЬ
	|	АкцизОстатки.Сделка КАК Сделка,
	|	АкцизОстатки.СостояниеСырья КАК СостояниеСырья,
	|	АкцизОстатки.СтатусАкциза КАК СтатусАкциза,
	|	АкцизОстатки.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	АкцизОстатки.Номенклатура КАК Номенклатура,
	|	АкцизОстатки.СерияНоменклатуры КАК СерияНоменклатуры,
	|	АкцизОстатки.Продукция КАК Продукция,
	|	АкцизОстатки.СерияПродукции КАК СерияПродукции,
	|	АкцизОстатки.ДатаРеализации КАК ДатаРеализации,
	|	АкцизОстатки.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ ОстаткиАкцизаПредварительно
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям.Остатки(
	|			&ПериодГраница,
	|			Организация = &Организация
	|				И НЕ (Сделка, Номенклатура, СерияНоменклатуры) В
	|						(ВЫБРАТЬ
	|							ИстекшиеВТекущемПериодеБанковскиеГарантии.БанковскаяГарантия КАК БанковскаяГарантия,
	|							ИстекшиеВТекущемПериодеБанковскиеГарантии.Номенклатура КАК Номенклатура,
	|							ИстекшиеВТекущемПериодеБанковскиеГарантии.СерияНоменклатуры КАК СерияНоменклатуры
	|						ИЗ
	|							ИстекшиеВТекущемПериодеБанковскиеГарантии КАК ИстекшиеВТекущемПериодеБанковскиеГарантии)
	|				И СостояниеСырья В (&СостоянияСырьяКВозмещениюАвансов)
	|				И Продукция <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|				И СостояниеОплатыАванса.Статус = ЗНАЧЕНИЕ(Перечисление.бг_СтатусыНачисленияАвансов.НачисленАванс)) КАК АкцизОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// списание спирта по норме
	|ВЫБРАТЬ
	|	АкцизОстатки.Сделка КАК Сделка,
	|	АкцизОстатки.СостояниеСырья КАК СостояниеСырья,
	|	АкцизОстатки.СтатусАкциза КАК СтатусАкциза,
	|	АкцизОстатки.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	АкцизОстатки.Номенклатура КАК Номенклатура,
	|	АкцизОстатки.СерияНоменклатуры КАК СерияНоменклатуры,
	|	АкцизОстатки.Продукция КАК Продукция,
	|	АкцизОстатки.СерияПродукции КАК СерияПродукции,
	|	АкцизОстатки.ДатаРеализации КАК ДатаРеализации,
	|	АкцизОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям.Остатки(
	|			&ПериодГраница,
	|			Организация = &Организация
	|				И НЕ (Сделка, Номенклатура, СерияНоменклатуры) В
	|						(ВЫБРАТЬ
	|							ИстекшиеВТекущемПериодеБанковскиеГарантии.БанковскаяГарантия КАК БанковскаяГарантия,
	|							ИстекшиеВТекущемПериодеБанковскиеГарантии.Номенклатура КАК Номенклатура,
	|							ИстекшиеВТекущемПериодеБанковскиеГарантии.СерияНоменклатуры КАК СерияНоменклатуры
	|						ИЗ
	|							ИстекшиеВТекущемПериодеБанковскиеГарантии КАК ИстекшиеВТекущемПериодеБанковскиеГарантии)
	|				И СостояниеСырья = ЗНАЧЕНИЕ(Перечисление.бг_СостоянияПодакцизногоСырья.ВСписанныхМПЗПоНорме)
	|				И Продукция = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|				И СостояниеОплатыАванса.Статус = ЗНАЧЕНИЕ(Перечисление.бг_СтатусыНачисленияАвансов.НачисленАванс)) КАК АкцизОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// в ожидании подтверждения экспорта
	|ВЫБРАТЬ
	|	АкцизОстатки.Сделка,
	|	АкцизОстатки.СостояниеСырья,
	|	АкцизОстатки.СтатусАкциза,
	|	АкцизОстатки.СостояниеОплатыАванса,
	|	АкцизОстатки.Номенклатура,
	|	АкцизОстатки.СерияНоменклатуры,
	|	АкцизОстатки.Продукция,
	|	АкцизОстатки.СерияПродукции,
	|	АкцизОстатки.ДатаРеализации КАК ДатаРеализации,
	|	АкцизОстатки.КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям.Остатки(
	|			&ПериодГраница,
	|			Организация = &Организация
	|				И НЕ (Сделка, Номенклатура, СерияНоменклатуры) В
	|						(ВЫБРАТЬ
	|							ИстекшиеВТекущемПериодеБанковскиеГарантии.БанковскаяГарантия КАК БанковскаяГарантия,
	|							ИстекшиеВТекущемПериодеБанковскиеГарантии.Номенклатура КАК Номенклатура,
	|							ИстекшиеВТекущемПериодеБанковскиеГарантии.СерияНоменклатуры КАК СерияНоменклатуры
	|						ИЗ
	|							ИстекшиеВТекущемПериодеБанковскиеГарантии КАК ИстекшиеВТекущемПериодеБанковскиеГарантии)
	|				И СостояниеСырья В (&СостоянияСырьяВОжиданииПодтвержденияЭкспорта, &СостоянияСырьяВРеализованнойПродукцииПодтвержденоЭкспорт)
	|				И СостояниеОплатыАванса.Статус = ЗНАЧЕНИЕ(Перечисление.бг_СтатусыНачисленияАвансов.НачисленАванс)) КАК АкцизОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// реализация и списание спирта и ГП, где не заполнено СостояниеОплатыАванса
	|ВЫБРАТЬ
	|	АкцизОстатки.Сделка,
	|	АкцизОстатки.СостояниеСырья,
	|	АкцизОстатки.СтатусАкциза,
	|	АкцизОстатки.СостояниеОплатыАванса,
	|	АкцизОстатки.Номенклатура,
	|	АкцизОстатки.СерияНоменклатуры,
	|	АкцизОстатки.Продукция,
	|	АкцизОстатки.СерияПродукции,
	|	АкцизОстатки.ДатаРеализации КАК ДатаРеализации,
	|	АкцизОстатки.КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям.Остатки(
	|			&ПериодГраница,
	|			Организация = &Организация
	|				И СтатусАкциза = ЗНАЧЕНИЕ(Перечисление.бг_СостоянияОплатыВходящегоАкциза.АкцизПоОплаченнымМЦ)
	|				И СостояниеОплатыАванса = ЗНАЧЕНИЕ(Справочник.бг_СостоянияОплатыАванса.ПустаяСсылка)
	|				И (СостояниеСырья = ЗНАЧЕНИЕ(Перечисление.бг_СостоянияПодакцизногоСырья.ВСписанныхМПЗПоНорме)
	|					ИЛИ Продукция <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))) КАК АкцизОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АкцизПоПриобретеннымЦенностям.Сделка,
	|	АкцизПоПриобретеннымЦенностям.СостояниеСырья,
	|	АкцизПоПриобретеннымЦенностям.СтатусАкциза,
	|	АкцизПоПриобретеннымЦенностям.СостояниеОплатыАванса,
	|	АкцизПоПриобретеннымЦенностям.Номенклатура,
	|	АкцизПоПриобретеннымЦенностям.СерияНоменклатуры,
	|	АкцизПоПриобретеннымЦенностям.Продукция,
	|	АкцизПоПриобретеннымЦенностям.СерияПродукции,
	|	АкцизПоПриобретеннымЦенностям.ДатаРеализации,
	|	АкцизПоПриобретеннымЦенностям.Количество
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям КАК АкцизПоПриобретеннымЦенностям
	|ГДЕ
	|	АкцизПоПриобретеннымЦенностям.Регистратор = &Регистратор
	|	И АкцизПоПриобретеннымЦенностям.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиАкцизаПредварительно.Сделка КАК Сделка,
	|	ОстаткиАкцизаПредварительно.СостояниеСырья КАК СостояниеСырья,
	|	ОстаткиАкцизаПредварительно.СтатусАкциза КАК СтатусАкциза,
	|	ОстаткиАкцизаПредварительно.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	ОстаткиАкцизаПредварительно.Номенклатура КАК Номенклатура,
	|	ОстаткиАкцизаПредварительно.Номенклатура.ВидНоменклатуры.бг_ВидОперацииНалогообложенияАкцизом КАК ВидОперацииНалогообложения,
	|	ОстаткиАкцизаПредварительно.СерияНоменклатуры КАК СерияНоменклатуры,
	|	ОстаткиАкцизаПредварительно.Продукция КАК Продукция,
	|	ОстаткиАкцизаПредварительно.СерияПродукции КАК СерияПродукции,
	|	ОстаткиАкцизаПредварительно.ДатаРеализации,
	|	СУММА(ОстаткиАкцизаПредварительно.Количество) КАК Количество
	|ПОМЕСТИТЬ ОстаткиАкциза
	|ИЗ
	|	ОстаткиАкцизаПредварительно КАК ОстаткиАкцизаПредварительно
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиАкцизаПредварительно.Сделка,
	|	ОстаткиАкцизаПредварительно.СостояниеСырья,
	|	ОстаткиАкцизаПредварительно.СтатусАкциза,
	|	ОстаткиАкцизаПредварительно.СостояниеОплатыАванса,
	|	ОстаткиАкцизаПредварительно.Номенклатура,
	|	ОстаткиАкцизаПредварительно.Номенклатура.ВидНоменклатуры.бг_ВидОперацииНалогообложенияАкцизом,
	|	ОстаткиАкцизаПредварительно.СерияНоменклатуры,
	|	ОстаткиАкцизаПредварительно.Продукция,
	|	ОстаткиАкцизаПредварительно.СерияПродукции,
	|	ОстаткиАкцизаПредварительно.ДатаРеализации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидОперацииНалогообложения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиАкциза.Сделка КАК Сделка,
	|	ОстаткиАкциза.ВидОперацииНалогообложения КАК ВидОперацииНалогообложения,
	|	МАКСИМУМ(КодыВидовОперацийНалогообложения.Период) КАК ПериодНалоговыеСтавки
	|ПОМЕСТИТЬ ПриобретениеТоваровУслугСтавкиПериоды
	|ИЗ
	|	ОстаткиАкциза КАК ОстаткиАкциза
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.бг_КодыВидовОперацийНалогообложенияАкцизом КАК КодыВидовОперацийНалогообложения
	|		ПО ОстаткиАкциза.ВидОперацииНалогообложения = КодыВидовОперацийНалогообложения.ВидОперацииНалогообложенияАкцизом
	|			И ОстаткиАкциза.Сделка Ссылка Документ.ПриобретениеТоваровУслуг
	|			И ВЫРАЗИТЬ(ОстаткиАкциза.Сделка КАК Документ.ПриобретениеТоваровУслуг).Дата >= КодыВидовОперацийНалогообложения.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиАкциза.Сделка,
	|	ОстаткиАкциза.ВидОперацииНалогообложения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидОперацииНалогообложения,
	|	ПериодНалоговыеСтавки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриобретениеТоваровУслугСтавкиПериоды.Сделка КАК Сделка,
	|	ПриобретениеТоваровУслугСтавкиПериоды.ВидОперацииНалогообложения КАК ВидОперацииНалогообложения,
	|	КодыВидовОперацийНалогообложения.ВидПодакцизныхТоваров.НалоговаяСтавка КАК НалоговаяСтавка
	|ПОМЕСТИТЬ СтавкиНалогообложенияПриобретениеТоваровУслуг
	|ИЗ
	|	ПриобретениеТоваровУслугСтавкиПериоды КАК ПриобретениеТоваровУслугСтавкиПериоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.бг_КодыВидовОперацийНалогообложенияАкцизом КАК КодыВидовОперацийНалогообложения
	|		ПО ПриобретениеТоваровУслугСтавкиПериоды.ВидОперацииНалогообложения = КодыВидовОперацийНалогообложения.ВидОперацииНалогообложенияАкцизом
	|			И ПриобретениеТоваровУслугСтавкиПериоды.ПериодНалоговыеСтавки = КодыВидовОперацийНалогообложения.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сделка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КодыВидовОперацийНалогообложения.ВидОперацииНалогообложенияАкцизом КАК ВидОперацииНалогообложения,
	|	ЕСТЬNULL(КодыВидовОперацийНалогообложения.ВидПодакцизныхТоваров.НалоговаяСтавка, 0) КАК НалоговаяСтавка
	|ПОМЕСТИТЬ СтавкиНалогообложения
	|ИЗ
	|	РегистрСведений.бг_КодыВидовОперацийНалогообложенияАкцизом.СрезПоследних(
	|			&ПериодГраница,
	|			ВидОперацииНалогообложенияАкцизом В
	|				(ВЫБРАТЬ
	|					ОстаткиАкциза.ВидОперацииНалогообложения
	|				ИЗ
	|					ОстаткиАкциза КАК ОстаткиАкциза)) КАК КодыВидовОперацийНалогообложения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидОперацииНалогообложения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиАкциза.Сделка КАК Сделка,
	|	ОстаткиАкциза.СостояниеСырья КАК СостояниеСырья,
	|	ОстаткиАкциза.СтатусАкциза КАК СтатусАкциза,
	|	ОстаткиАкциза.СостояниеОплатыАванса КАК СостояниеОплатыАвансаИсходное,
	|	ВЫБОР
	|		КОГДА НЕ ОстаткиАкциза.СостояниеОплатыАванса.НалоговаяСтавка ЕСТЬ NULL
	|			ТОГДА ОстаткиАкциза.СостояниеОплатыАванса.НалоговаяСтавка
	|		КОГДА ОстаткиАкциза.Сделка Ссылка Документ.ПриобретениеТоваровУслуг
	|			ТОГДА ISNULL(СтавкиНалогообложенияПриобретениеТоваровУслуг.НалоговаяСтавка, 0)
	|		ИНАЧЕ ISNULL(СтавкиНалогообложения.НалоговаяСтавка, 0)
	|	КОНЕЦ КАК НалоговаяСтавка,
	|	ЕСТЬNULL(ОстаткиАкциза.СостояниеОплатыАванса.ПродажаНаЭкспорт, ЛОЖЬ) КАК ПродажаНаЭкспорт,
	|	ЕСТЬNULL(ОстаткиАкциза.СостояниеОплатыАванса.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК ПериодНачисленияАванса,
	|	ОстаткиАкциза.Номенклатура КАК Номенклатура,
	|	ОстаткиАкциза.СерияНоменклатуры КАК СерияНоменклатуры,
	|	ОстаткиАкциза.Продукция КАК Продукция,
	|	ОстаткиАкциза.СерияПродукции КАК СерияПродукции,
	|	ОстаткиАкциза.ДатаРеализации,
	|	ОстаткиАкциза.Количество КАК Количество,
	|	ОстаткиАкциза.Количество * ВЫБОР
	|		КОГДА НЕ ОстаткиАкциза.СостояниеОплатыАванса.НалоговаяСтавка ЕСТЬ NULL
	|			ТОГДА ОстаткиАкциза.СостояниеОплатыАванса.НалоговаяСтавка
	|		КОГДА ОстаткиАкциза.Сделка Ссылка Документ.ПриобретениеТоваровУслуг
	|			ТОГДА ISNULL(СтавкиНалогообложенияПриобретениеТоваровУслуг.НалоговаяСтавка, 0)
	|		ИНАЧЕ ISNULL(СтавкиНалогообложения.НалоговаяСтавка, 0)
	|	КОНЕЦ * 10 КАК СуммаАкциза
	|ИЗ
	|	ОстаткиАкциза КАК ОстаткиАкциза
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтавкиНалогообложения КАК СтавкиНалогообложения
	|		ПО ОстаткиАкциза.ВидОперацииНалогообложения = СтавкиНалогообложения.ВидОперацииНалогообложения
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтавкиНалогообложенияПриобретениеТоваровУслуг КАК СтавкиНалогообложенияПриобретениеТоваровУслуг
	|		ПО ОстаткиАкциза.ВидОперацииНалогообложения = СтавкиНалогообложенияПриобретениеТоваровУслуг.ВидОперацииНалогообложения
	|			И ОстаткиАкциза.Сделка = СтавкиНалогообложенияПриобретениеТоваровУслуг.Сделка";
	
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти

#КонецЕсли
