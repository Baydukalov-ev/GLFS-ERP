#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

&ИзменениеИКонтроль("ДанныеДокументаДляПроведения")
Функция бг_ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
	КонецЕсли;
	
	ДобавитьТекстыЗапросовТаблицСторноДвижений(Запрос, ТекстыЗапроса, Регистры);
	ДобавитьТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
	
	СторноЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
#Удаление	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
#КонецУдаления
#Вставка
	ТаблицыДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(
		Запрос, ТекстыЗапроса, ДопПараметры);
	бг_ЗаполнитьТаблицыДвиженийДаннымиРучнойКорректировки(ТаблицыДвижений, ДопПараметры);	
		
	Возврат ТаблицыДвижений;
#КонецВставки
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура бг_ЗаполнитьДопПараметрыПроведения(ДопПараметры, ДокументОбъект) Экспорт
	
	ДопПараметры.Вставить(
		"бг_ТаблицыДвиженийСРучнойКорректировкой", бг_ТаблицыДвиженийСРучнойКорректировкой(ДокументОбъект));

КонецПроцедуры

Функция бг_ТаблицыДвиженийСРучнойКорректировкой(ДокументОбъект)
	
	ТаблицыДвижений = Новый Структура;
	
	Для Каждого СтрокаТаблицы Из ДокументОбъект.бг_РегистрыДвижений Цикл
		Если СтрокаТаблицы.ДвиженияСкорректированы
			И ДокументОбъект.Движения.Найти(СтрокаТаблицы.ИмяРегистра) <> Неопределено Тогда
			ТаблицыДвижений.Вставить(СтрокаТаблицы.ИмяРегистра, ДокументОбъект.Движения[СтрокаТаблицы.ИмяРегистра].Выгрузить());
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицыДвижений;
	
КонецФункции

Процедура бг_ЗаполнитьТаблицыДвиженийДаннымиРучнойКорректировки(ТаблицыДвижений, ДопПараметры)

	Если ДопПараметры.Свойство("бг_ТаблицыДвиженийСРучнойКорректировкой") Тогда
		
		Для Каждого ДанныеТаблицы Из ДопПараметры.бг_ТаблицыДвиженийСРучнойКорректировкой Цикл 
			ИмяТаблицыДвижений = "Таблица" + ДанныеТаблицы.Ключ;
			ДвиженияТаблицы = ДанныеТаблицы.Значение;
			Если ТаблицыДвижений.Свойство(ИмяТаблицыДвижений) Тогда
				ТаблицыДвижений.Вставить(ИмяТаблицыДвижений, ДвиженияТаблицы);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли