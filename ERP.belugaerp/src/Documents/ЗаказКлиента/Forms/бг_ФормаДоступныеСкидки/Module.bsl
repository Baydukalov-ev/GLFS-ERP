
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СкидкиНаценки = ДоступныеСкидки(Параметры);
	// Если в доступных скидках выбрана пустая скидка, то это означает, что скидки не должны применяться
	НеПрименятьСкидки = Параметры.ДоступныеСкидки.Количество() <> 0 
		И Параметры.ДоступныеСкидки[0] = Справочники.СкидкиНаценки.ПустаяСсылка();
	
	Для Каждого СтрокаСкидка Из СкидкиНаценки Цикл		
		НоваяСтрока = ДоступныеСкидки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСкидка);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ДоступныеСкидкиПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеСкидкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьВсе(Команда)
	
	Для Каждого СтрокаСкидка Из ДоступныеСкидки Цикл
		СтрокаСкидка.Пометка = Истина; 	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
		
	Для Каждого СтрокаСкидка Из ДоступныеСкидки Цикл
		СтрокаСкидка.Пометка = Ложь; 	
	КонецЦикла;
	
КонецПроцедуры


// Команда сохранения результатов исполнения
&НаКлиенте
Процедура КомандаОК(Команда)
	
	Закрыть(ВыбранныеДоступныеСкидки());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВыбранныеДоступныеСкидки()
	Если НеПрименятьСкидки = Истина Тогда
		ПустыеСкидки = Новый Массив;
		ПустыеСкидки.Добавить(Справочники.СкидкиНаценки.ПустаяСсылка());
		Возврат ПустыеСкидки;
	Иначе
		Возврат ДоступныеСкидки.Выгрузить(Новый Структура("Пометка", Истина)).ВыгрузитьКолонку("СкидкаНаценка");
	КонецЕсли;
КонецФункции
	
&НаСервере
Функция ДоступныеСкидки(Параметры)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВсеСкидки", Параметры.ВсеСкидки);
	Запрос.УстановитьПараметр("ДоступныеСкидки", Параметры.ДоступныеСкидки);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СкидкиНаценки.Ссылка КАК СкидкаНаценка,
	|	ВЫБОР
	|		КОГДА СкидкиНаценки.Ссылка В (&ДоступныеСкидки)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Пометка
	|ИЗ
	|	Справочник.СкидкиНаценки КАК СкидкиНаценки
	|ГДЕ
	|	СкидкиНаценки.Ссылка В(&ВсеСкидки)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СкидкаНаценка
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти


