#Область ОбработчикиСобытийФормы

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	бг_ДобавитьРеквизитыИнвойса();
	бг_ДобавитьЭлементыИнвойса();
	бг_ЗаполнитьЗначенияРеквизитовИнвойса();
	бг_УстановитьПараметрыВыбораОтветственногоЛица();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
&Вместо("ИзменяемыеРеквизиты")
Функция бг_ИзменяемыеРеквизиты(Источник)
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("АдресДоставки",                  Источник.АдресДоставки);
	СтруктураПараметров.Вставить("БанковскийСчет",                 Источник.БанковскийСчет);
	СтруктураПараметров.Вставить("БанковскийСчетГрузоотправителя", Источник.БанковскийСчетГрузоотправителя);
	СтруктураПараметров.Вставить("БанковскийСчетГрузополучателя",  Источник.БанковскийСчетГрузополучателя);
	СтруктураПараметров.Вставить("БанковскийСчетКонтрагента",      Источник.БанковскийСчетКонтрагента);
	СтруктураПараметров.Вставить("Грузоотправитель",               Источник.Грузоотправитель);
	СтруктураПараметров.Вставить("Грузополучатель",                Источник.Грузополучатель);
	СтруктураПараметров.Вставить("Руководитель",                   Источник.Руководитель);
	СтруктураПараметров.Вставить("ГлавныйБухгалтер",               Источник.ГлавныйБухгалтер);
	СтруктураПараметров.Вставить("НазначениеПлатежа",              Источник.НазначениеПлатежа);
	СтруктураПараметров.Вставить("НомерЗаказа",			           Источник.НомерЗаказа);
	СтруктураПараметров.Вставить("ДатаЗаказа",			           Источник.ДатаЗаказа);	
	СтруктураПараметров.Вставить("бг_ОтветственныйЗаПодписаниеИнвойса", Источник.бг_ОтветственныйЗаПодписаниеИнвойса);
	СтруктураПараметров.Вставить("бг_УсловияПоставки",			    Источник.бг_УсловияПоставки);
	
	Возврат СтруктураПараметров;
	
КонецФункции

&НаСервере
Процедура бг_ДобавитьРеквизитыИнвойса()
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(
		Новый РеквизитФормы(
			"бг_ОтветственныйЗаПодписаниеИнвойса", 	
			Новый ОписаниеТипов("СправочникСсылка.ОтветственныеЛицаОрганизаций"), 
			, 
			, 
			Истина));
	ДобавляемыеРеквизиты.Добавить(
		Новый РеквизитФормы(
			"бг_УсловияПоставки", 	
			Новый ОписаниеТипов("СправочникСсылка.бг_УсловияПоставки"), 
			, 
			, 
			Истина));
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
КонецПроцедуры

&НаСервере
Процедура бг_ДобавитьЭлементыИнвойса()
	бг_ГруппаИнвойс = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьГруппуНаФорму(
						ЭтотОбъект,
						"бг_ГруппаИнвойс", 
						Элементы.ГруппаПечатьПраво, 
						ВидГруппыФормы.ОбычнаяГруппа);
										
	бг_ГруппаИнвойс.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	бг_ГруппаИнвойс.Заголовок   = "Инвойс";
	бг_ГруппаИнвойс.Поведение   = ПоведениеОбычнойГруппы.Свертываемая;
	бг_ГруппаИнвойс.Скрыть();
	
	бг_ОтветственныйЗаПодписаниеИнвойса = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
											ЭтотОбъект,
											"бг_ОтветственныйЗаПодписаниеИнвойса", 
											Элементы.бг_ГруппаИнвойс, 
											"бг_ОтветственныйЗаПодписаниеИнвойса");
									
	бг_ОтветственныйЗаПодписаниеИнвойса.Заголовок = "Подписант";
										
	бг_УсловияПоставки = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
							ЭтотОбъект,
							"бг_УсловияПоставки", 
							Элементы.бг_ГруппаИнвойс, 
							"бг_УсловияПоставки");
							
	бг_УсловияПоставки.Заголовок = "Условия поставки";
КонецПроцедуры

&НаСервере
Процедура бг_ЗаполнитьЗначенияРеквизитовИнвойса()
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры, "бг_ОтветственныйЗаПодписаниеИнвойса, бг_УсловияПоставки");	
КонецПроцедуры

&НаСервере
Процедура бг_УстановитьПараметрыВыбораОтветственногоЛица()
	Связи = Новый Массив;
	НоваяСвязь   = Новый СвязьПараметраВыбора("Отбор.Организация", "Организация");
	Связи.Добавить(НоваяСвязь); 
	
	ПараметрыВыбора = Новый Массив;
    НовыйПараметр = Новый ПараметрВыбора("Отбор.ОтветственноеЛицо", 
		Перечисления.ОтветственныеЛицаОрганизаций.бг_ОтветственныйЗаПодписаниеИнвойса);
    ПараметрыВыбора.Добавить(НовыйПараметр);
	
	Элементы.бг_ОтветственныйЗаПодписаниеИнвойса.СвязиПараметровВыбора = Новый ФиксированныйМассив(Связи);
	Элементы.бг_ОтветственныйЗаПодписаниеИнвойса.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
КонецПроцедуры

#КонецОбласти
