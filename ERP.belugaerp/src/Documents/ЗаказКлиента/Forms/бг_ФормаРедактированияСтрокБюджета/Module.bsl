
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Пользователи.РолиДоступны("бг_ИспользованиеПодбораКодовСтрокБюджета, ПолныеПрава", Пользователи.ТекущийПользователь()) Тогда
		ВызватьИсключение НСтр("ru = 'Недостаточно прав для рассчета скидок по коду строк бюджета.'");	
	КонецЕсли;
    	
	СкидкиНаценки = ДоступныеСкидкиДляМобильнойТорговли(Параметры);
	
	Для Каждого СтрокаСкидка Из СкидкиНаценки Цикл		
		НоваяСтрока = ИнформацияОСкидкахНаценках.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСкидка.Ссылка);
		НоваяСтрока.Пометка = ?(Параметры.КодыСтрокБюджета.Найти(СокрЛП(СтрокаСкидка.бг_КодСтрокиБюджета)) = Неопределено, Ложь, Истина);		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Рассчитать(Команда)
		
	Закрыть(КодыСтрокБюджетаДляРасчетаСкидокНаценок()); 
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВсе(Команда)
	
	Для Каждого СтрокаСкидка Из ИнформацияОСкидкахНаценках Цикл
		СтрокаСкидка.Пометка = Истина; 	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
		
	Для Каждого СтрокаСкидка Из ИнформацияОСкидкахНаценках Цикл
		СтрокаСкидка.Пометка = Ложь; 	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДоступныеСкидкиДляМобильнойТорговли(Параметры)
	
	СкидкиНаценки = СкидкиНаценкиСервер.бг_СкидкиНаценкиДляОптовойТорговли(Параметры);	
	ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(СкидкиНаценки);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", СкидкиНаценки);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СкидкиНаценки.Ссылка КАК Ссылка,
	|	СкидкиНаценки.бг_КодСтрокиБюджета КАК бг_КодСтрокиБюджета
	|ИЗ
	|	Справочник.СкидкиНаценки КАК СкидкиНаценки
	|ГДЕ
	|	СкидкиНаценки.Ссылка В(&Ссылка)
	|	И СкидкиНаценки.бг_ТребуетсяПодтверждениеМТ
	|
	|УПОРЯДОЧИТЬ ПО
	|	СкидкиНаценки.бг_ДатаСоздания";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервере
Функция КодыСтрокБюджетаДляРасчетаСкидокНаценок()
   	
	Если ИнформацияОСкидкахНаценках.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	КодыСтрокБюджетаДляРасчета = Новый СписокЗначений;
	
	НайденныеСтроки = ИнформацияОСкидкахНаценках.НайтиСтроки(Новый Структура("Пометка", Истина));
	
	Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		КодыСтрокБюджетаДляРасчета.Добавить(НайденнаяСтрока.Ссылка);
	КонецЦикла;
	
	Возврат КодыСтрокБюджетаДляРасчета;

КонецФункции

#КонецОбласти


