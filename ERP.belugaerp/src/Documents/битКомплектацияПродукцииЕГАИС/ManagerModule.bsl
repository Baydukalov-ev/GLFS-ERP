
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет в таблице штрихкодов текущую номенклатуру и серию по идентификатору марки.
//
// Параметры:
//  ТаблицаШтрихкодов - ТаблицаЗначений, ДанныеФормыКоллекция - заполняемая таблица.
//  ОрганизацияЕГАИС - СправочникСсылка.КлассификаторОрганизацийЕГАИС - Организация ЕГАИС
//  Дата - Дата - дата получения данных товаров.
//  Показатели - Строка - Список заполняемых показателей.
//
Процедура ЗаполнитьТекущиеДанныеТоваровВШтрихкодах(ТаблицаШтрихкодов, ОрганизацияЕГАИС, Дата,
	Показатели = Неопределено) Экспорт
	
	Если Показатели = Неопределено Тогда
		Показатели = ВсеПоказателиДанныхТоваровВШтрихкодах();
	КонецЕсли;
	
	СтрокиШтрихкодов = Новый Массив;
	Для каждого СтрокаШтрихкоды Из ТаблицаШтрихкодов Цикл
		СтрокиШтрихкодов.Добавить(СтрокаШтрихкоды);
	КонецЦикла;
	
	ЗаполнитьТекущиеДанныеТоваровВШтрихкодахОбщая(СтрокиШтрихкодов, ОрганизацияЕГАИС, Дата, Показатели);
	
КонецПроцедуры

// Заполняет в строке штрихкодов текущую номенклатуру и серию по идентификатору марки.
//
// Параметры:
//  СтрокаШтрихкоды - СтрокаТаблицыЗначений - строка таблицы штрихкодов,
//   для которой нужно выполнить расчет.
//  ОрганизацияЕГАИС - СправочникСсылка.КлассификаторОрганизацийЕГАИС - Организация ЕГАИС
//  Дата - Дата - дата получения данных товаров.
//  Показатели - Строка - Список заполняемых показателей.
//
Процедура ЗаполнитьТекущиеДанныеТовараВСтрокеШтрихкоды(СтрокаШтрихкоды, ОрганизацияЕГАИС, Дата,
	Показатели = Неопределено) Экспорт
	
	Если Показатели = Неопределено Тогда
		Показатели = ВсеПоказателиДанныхТоваровВШтрихкодах();
	КонецЕсли;
	
	СтрокиШтрихкодов = Новый Массив;
	СтрокиШтрихкодов.Добавить(СтрокаШтрихкоды);
	
	ЗаполнитьТекущиеДанныеТоваровВШтрихкодахОбщая(СтрокиШтрихкодов, ОрганизацияЕГАИС, Дата, Показатели);
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьТекущиеДанныеТоваровВШтрихкодахОбщая(СтрокиШтрихкодов, ОрганизацияЕГАИС, Дата, ПоказателиСтрокой)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Штрихкоды = Новый Массив;
	
	Для каждого СтрокаШтрихкоды Из СтрокиШтрихкодов Цикл
		Штрихкоды.Добавить(СтрокаШтрихкоды.Штрихкод);
	КонецЦикла;
	
	СтатусыМарок = Перечисления.бг_СтатусыАкцизныхМарок.СтатусыПоОперации(
		Метаданные.Документы.битКомплектацияПродукцииЕГАИС.Имя);
	
	ТекущиеДанныеШтрихкодов = бг_МаркируемаяПродукция.ДанныеМарокПоШтрихкодам(
		Штрихкоды,
		ОрганизацияЕГАИС,
		СтатусыМарок.СтатусыККомплектации,
		Дата);
		
	Если ТекущиеДанныеШтрихкодов.Количество() > 1 Тогда
		ТекущиеДанныеШтрихкодов.Индексы.Добавить("Штрихкод");
	КонецЕсли;
	
	СоответствиеПоказателей = СоответствиеПоказателейДанныхТоваровВШтрихкодах(ПоказателиСтрокой);
	
	Для каждого СтрокаШтрихкоды Из СтрокиШтрихкодов Цикл
		ЗаполнитьТекущиеДанныеТовараВСтрокеШтрихкодыОбщая(СтрокаШтрихкоды, ТекущиеДанныеШтрихкодов, СоответствиеПоказателей);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьТекущиеДанныеТовараВСтрокеШтрихкодыОбщая(СтрокаШтрихкоды, ТекущиеДанныеШтрихкодов, СоответствиеПоказателей)
	
	ПараметрыПоискаШтрихкода = Новый Структура("Штрихкод", СтрокаШтрихкоды.Штрихкод);
	НайденныеСтрокиДанныхШтрихкода = ТекущиеДанныеШтрихкодов.НайтиСтроки(ПараметрыПоискаШтрихкода);
	
	Если НайденныеСтрокиДанныхШтрихкода.Количество() = 1 Тогда
		
		Для каждого СоответствиеПоказателя Из СоответствиеПоказателей Цикл
			СтрокаШтрихкоды[СоответствиеПоказателя.Ключ] = НайденныеСтрокиДанныхШтрихкода[0][СоответствиеПоказателя.Значение];
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ВсеПоказателиДанныхТоваровВШтрихкодах()
	
	Возврат "СтараяНоменклатура, СтараяСерия, ШтрихкодРодитель, ТипУпаковки, ГУИДМарки, СтатусМарки";
	
КонецФункции

Функция СоответствиеПоказателейДанныхТоваровВШтрихкодах(ПоказателиСтрокой)
	
	СоответствиеПоказателей = Новый Структура;
	
	Если Найти(ПоказателиСтрокой, "СтараяНоменклатура") > 0 Тогда
		СоответствиеПоказателей.Вставить("СтараяНоменклатура", "Номенклатура");
	КонецЕсли;
	
	Если Найти(ПоказателиСтрокой, "СтараяСерия") > 0 Тогда
		СоответствиеПоказателей.Вставить("СтараяСерия", "Серия");
	КонецЕсли;
	
	Если Найти(ПоказателиСтрокой, "ШтрихкодРодитель") > 0 Тогда
		СоответствиеПоказателей.Вставить("ШтрихкодРодитель", "ШтрихкодРодитель");
	КонецЕсли;
	
	Если Найти(ПоказателиСтрокой, "ГУИДМарки") > 0 Тогда
		СоответствиеПоказателей.Вставить("ГУИДМарки", "ГУИДМарки");
	КонецЕсли;
	
	Если Найти(ПоказателиСтрокой, "СтатусМарки") > 0 Тогда
		СоответствиеПоказателей.Вставить("СтатусМарки", "СтатусМарки");
	КонецЕсли;
	
	Возврат СоответствиеПоказателей;
	
КонецФункции

#КонецОбласти

#КонецЕсли

