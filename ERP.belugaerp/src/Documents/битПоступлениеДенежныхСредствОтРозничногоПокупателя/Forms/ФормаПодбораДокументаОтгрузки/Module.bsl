
#Область ОбработчикиСобытийФормы

 &НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Список.Отбор.Элементы.Очистить();
	
	Если Параметры.Свойство("ОтборСписка") Тогда
		
		Для Каждого Отбор из Параметры.ОтборСписка Цикл
			ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(Отбор.Ключ);
			ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
			ЭлементОтбора.Использование  = Истина;
			ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
			ЭлементОтбора.ПравоеЗначение = Отбор.Значение;
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

 &НаКлиенте
Процедура Выбрать(Команда)
	
	РезультатВыбора = РезультатВыбора();

	ОповеститьОВыборе(РезультатВыбора);	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийСписка

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)	
	
	РезультатВыбора = РезультатВыбора();
		
	ОповеститьОВыборе(РезультатВыбора);
	СтандартнаяОбработка = Ложь;	

КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция РезультатВыбора()
	
	КонтрагентДоговор = Новый Структура();	
	КонтрагентДоговор.Вставить("Контрагент");
	КонтрагентДоговор.Вставить("Договор");
	КонтрагентДоговор.Вставить("СуммаВРозницу");
	
	РезультатВыбора = Новый Структура(); 	
	РезультатВыбора.Вставить("ДокументОтгрузки", Неопределено);
	РезультатВыбора.Вставить("Сумма", 0);
	РезультатВыбора.Вставить("КонтрагентДоговор", КонтрагентДоговор);
	
	ЗаполнитьЗначенияСвойств(РезультатВыбора, Элементы.Список.ТекущиеДанные);
	ЗаполнитьЗначенияСвойств(РезультатВыбора.КонтрагентДоговор, Элементы.Список.ТекущиеДанные);
	
	Возврат РезультатВыбора;
	
КонецФункции

#КонецОбласти
