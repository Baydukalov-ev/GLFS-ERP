
#Область ОбработчикиСобытий

&Перед("ОбработкаЗаполнения")
Процедура бг_ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	ЗаполненНаОснованииДокумента = Ложь;
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.КорректировкаРеализации") Тогда

		ЗаполнитьДокументНаОснованииКорректировкиРеализации(ДанныеЗаполнения);
		
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииКорректировкиРеализации(Знач ДокументОснование)
	
	Склад = ДокументОснование.Склад;
	бг_ДокументОснование = ДокументОснование;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", ДокументОснование);
	Запрос.УстановитьПараметр("Склад", ДокументОснование.Склад);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТоварыКОформлениюИзлишков.Регистратор КАК Регистратор,
	|	ТоварыКОформлениюИзлишков.Склад КАК Склад,
	|	ТоварыКОформлениюИзлишков.Номенклатура КАК Номенклатура,
	|	ТоварыКОформлениюИзлишков.Характеристика КАК Характеристика,
	|	ТоварыКОформлениюИзлишков.Назначение КАК Назначение,
	|	ТоварыКОформлениюИзлишков.Серия КАК Серия,
	|	ТоварыКОформлениюИзлишков.КОформлениюАктовРасход КАК КОформлениюАктовРасход
	|ПОМЕСТИТЬ ТоварыКОформлениюИзлишковКорректировка
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюИзлишковНедостач.ОстаткиИОбороты(, , Регистратор, , Склад = &Склад) КАК ТоварыКОформлениюИзлишков
	|ГДЕ
	|	(ВЫРАЗИТЬ(ТоварыКОформлениюИзлишков.Регистратор КАК Документ.КорректировкаРеализации)) = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ТоварыКОформлениюИзлишковНедостачОстаткиИОбороты.Регистратор КАК Документ.ОрдерНаОтражениеИзлишковТоваров).бг_ДокументОснование КАК ДокументОснование,
	|	ТоварыКОформлениюИзлишковНедостачОстаткиИОбороты.Склад КАК Склад,
	|	ТоварыКОформлениюИзлишковНедостачОстаткиИОбороты.Номенклатура КАК Номенклатура,
	|	ТоварыКОформлениюИзлишковНедостачОстаткиИОбороты.Характеристика КАК Характеристика,
	|	ТоварыКОформлениюИзлишковНедостачОстаткиИОбороты.Назначение КАК Назначение,
	|	ТоварыКОформлениюИзлишковНедостачОстаткиИОбороты.Серия КАК Серия,
	|	СУММА(ТоварыКОформлениюИзлишковНедостачОстаткиИОбороты.КОформлениюАктовПриход) КАК КОформлениюАктовПриход
	|ПОМЕСТИТЬ ТоварыКОформлениюИзлишковОрдер
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюИзлишковНедостач.ОстаткиИОбороты(, , Регистратор, , ) КАК ТоварыКОформлениюИзлишковНедостачОстаткиИОбороты
	|ГДЕ
	|	ВЫРАЗИТЬ(ТоварыКОформлениюИзлишковНедостачОстаткиИОбороты.Регистратор КАК Документ.ОрдерНаОтражениеИзлишковТоваров).бг_ДокументОснование = &Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлениюИзлишковНедостачОстаткиИОбороты.Склад,
	|	ТоварыКОформлениюИзлишковНедостачОстаткиИОбороты.Номенклатура,
	|	ТоварыКОформлениюИзлишковНедостачОстаткиИОбороты.Характеристика,
	|	ТоварыКОформлениюИзлишковНедостачОстаткиИОбороты.Серия,
	|	ТоварыКОформлениюИзлишковНедостачОстаткиИОбороты.Назначение,
	|	ВЫРАЗИТЬ(ТоварыКОформлениюИзлишковНедостачОстаткиИОбороты.Регистратор КАК Документ.ОрдерНаОтражениеИзлишковТоваров).бг_ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлениюИзлишковКорректировка.Склад КАК Склад,
	|	ТоварыКОформлениюИзлишковКорректировка.Номенклатура КАК Номенклатура,
	|	ТоварыКОформлениюИзлишковКорректировка.Характеристика КАК Характеристика,
	|	ТоварыКОформлениюИзлишковКорректировка.Назначение КАК Назначение,
	|	ТоварыКОформлениюИзлишковКорректировка.Серия КАК Серия,
	|	ТоварыКОформлениюИзлишковКорректировка.КОформлениюАктовРасход - ЕСТЬNULL(ТоварыКОформлениюИзлишковОрдер.КОформлениюАктовПриход, 0) КАК КоличествоУпаковок
	|ПОМЕСТИТЬ ТоварыКОформлениюИзлишковОстаток
	|ИЗ
	|	ТоварыКОформлениюИзлишковКорректировка КАК ТоварыКОформлениюИзлишковКорректировка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыКОформлениюИзлишковОрдер КАК ТоварыКОформлениюИзлишковОрдер
	|		ПО (ТоварыКОформлениюИзлишковКорректировка.Регистратор = ТоварыКОформлениюИзлишковОрдер.ДокументОснование
	|				И ТоварыКОформлениюИзлишковКорректировка.Номенклатура = ТоварыКОформлениюИзлишковОрдер.Номенклатура
	|				И ТоварыКОформлениюИзлишковКорректировка.Характеристика = ТоварыКОформлениюИзлишковОрдер.Характеристика
	|				И ТоварыКОформлениюИзлишковКорректировка.Назначение = ТоварыКОформлениюИзлишковОрдер.Назначение
	|				И ТоварыКОформлениюИзлишковКорректировка.Серия = ТоварыКОформлениюИзлишковОрдер.Серия
	|				И ТоварыКОформлениюИзлишковКорректировка.Склад = ТоварыКОформлениюИзлишковОрдер.Склад)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КоличествоУпаковок,
	|	Склад,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлениюИзлишковОстаток.Склад КАК Склад,
	|	ТоварыКОформлениюИзлишковОстаток.Номенклатура КАК Номенклатура,
	|	ТоварыКОформлениюИзлишковОстаток.Характеристика КАК Характеристика,
	|	ТоварыКОформлениюИзлишковОстаток.Назначение КАК Назначение,
	|	ТоварыКОформлениюИзлишковОстаток.Серия КАК Серия,
	|	ТоварыКОформлениюИзлишковОстаток.КоличествоУпаковок КАК Количество,
	|	ТоварыКОформлениюИзлишковОстаток.КоличествоУпаковок КАК КоличествоУпаковок
	|
	|ИЗ
	|	ТоварыКОформлениюИзлишковОстаток КАК ТоварыКОформлениюИзлишковОстаток
	|ГДЕ
	|	ТоварыКОформлениюИзлишковОстаток.КоличествоУпаковок > 0";
	
	Товары.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры

#КонецОбласти
