#Область СлужебныеПроцедурыИФункции

&ИзменениеИКонтроль("СформироватьВременнуюТаблицуИсходныхДанных")
Процедура бг_СформироватьВременнуюТаблицуИсходныхДанных(МенеджерВременныхТаблиц, ДокументОснование)
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ТекстЗапросаПоОснованию = ТекстЗапросаПоИсходнымДаннымРеализацияТоваровУслуг();
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.АктВыполненныхРабот") Тогда
		ТекстЗапросаПоОснованию = ТекстЗапросаПоИсходнымДаннымАктВыполненныхРабот();
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов") Тогда
		ТекстЗапросаПоОснованию = ТекстЗапросаПоИсходнымДаннымРеализацияУслугПрочихАктивов();
	//++ НЕ УТКА
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетДавальцу") Тогда
		ТекстЗапросаПоОснованию = ТекстЗапросаПоИсходнымДаннымОтчетаДавальцу();
	//-- НЕ УТКА
	КонецЕсли;
	
#Вставка
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ТекстЗапросаПоОснованию = СтрЗаменить(
			ТекстЗапросаПоОснованию, 
			"КАК СуммаНДС,", 
			"КАК СуммаНДС,
			|ТаблицаТовары.бг_СуммаАкциза КАК бг_СуммаАкциза,");
	Иначе
		ТекстЗапросаПоОснованию = СтрЗаменить(
			ТекстЗапросаПоОснованию, 
			"КАК СуммаНДС,", 
			"КАК СуммаНДС,
			|0 КАК бг_СуммаАкциза,");
	КонецЕсли;
#КонецВставки
	
	ТекстЗапроса = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	КорректировкаРеализации.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ДанныеПоследнейКорректировки
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.РеестрДокументов КАК РеестрДокументовСторноИсправление
	|	ПО
	|		КорректировкаРеализации.Ссылка = РеестрДокументовСторноИсправление.СторнируемыйДокумент
	|		И РеестрДокументовСторноИсправление.Проведен
	|		И НЕ РеестрДокументовСторноИсправление.ДополнительнаяЗапись
	|ГДЕ
	|	КорректировкаРеализации.Проведен
	|	И КорректировкаРеализации.ВидКорректировки <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПерепоставленногоТовара)
	|	И КорректировкаРеализации.ВидКорректировки <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара)
	|	И КорректировкаРеализации.ДокументОснование = &ДокументОснование
	|	И РеестрДокументовСторноИсправление.Ссылка ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	КорректировкаРеализации.МоментВремени УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки				   КАК НомерСтроки,
	|	ТаблицаТовары.НоменклатураНабора		   КАК НоменклатураНабора,
	|	ТаблицаТовары.ХарактеристикаНабора		   КАК ХарактеристикаНабора,
	|	ТаблицаТовары.Номенклатура		 		   КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаТовары.НоменклатураПартнера         КАК НоменклатураПартнера,
	|	ТаблицаТовары.Характеристика			   КАК Характеристика,
	|	ТаблицаТовары.Назначение				   КАК Назначение,
	|	ТаблицаТовары.Серия						   КАК Серия,
	|	ТаблицаТовары.СтатусУказанияСерий		   КАК СтатусУказанияСерий,
	|	ТаблицаТовары.Содержание				   КАК Содержание,
	|	ТаблицаТовары.Упаковка					   КАК Упаковка,
	|	ТаблицаТовары.КоличествоУпаковок		   КАК КоличествоУпаковок,
	|	ТаблицаТовары.Количество				   КАК Количество,
	|	ТаблицаТовары.Сумма						   КАК Сумма,
	|	ТаблицаТовары.СтавкаНДС					   КАК СтавкаНДС,
	|	ТаблицаТовары.СтатьяДоходов				   КАК СтатьяДоходов,
	|	ТаблицаТовары.АналитикаДоходов			   КАК АналитикаДоходов,
	|	ТаблицаТовары.СуммаНДС					   КАК СуммаНДС,
#Вставка
	|	ТаблицаТовары.бг_СуммаАкциза               КАК бг_СуммаАкциза,
#КонецВставки
	|	ТаблицаТовары.СуммаСНДС					   КАК СуммаСНДС,
	|	ТаблицаТовары.КодСтроки					   КАК КодСтроки,
	|	ТаблицаТовары.Склад						   КАК Склад,
	|	ТаблицаТовары.Подразделение				   КАК Подразделение,
	|	ТаблицаТовары.ЗаказКлиента				   КАК ЗаказКлиента,
	|	ТаблицаТовары.КодТНВЭД					   КАК КодТНВЭД,
	|	ТаблицаТовары.Цена						   КАК Цена,
	|	ТаблицаТовары.ВидЦены					   КАК ВидЦены
	|
	|ПОМЕСТИТЬ ИсходныеДанные
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|		ПО ТаблицаТовары.Ссылка = ДанныеПоследнейКорректировки.Ссылка
	|";
	
	ТекстЗапроса = ТекстЗапроса + "
	|ОБЪЕДИНИТЬ ВСЕ
	|" + ТекстЗапросаПоОснованию + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеПоследнейКорректировки
	|";

	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	//++ НЕ УТКА
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетДавальцу") Тогда
		Период = НачалоМесяца(ДокументОснование.Дата);
		Запрос.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоНазначениям",
			РасчетСебестоимостиПовтИсп.СебестоимостьТоваровПоНазначениям(Период));
	КонецЕсли;
	//-- НЕ УТКА
	
	Запрос.Выполнить();
	
КонецПроцедуры

&Вместо("ТекстОтраженияВРеглУчете")
Функция бг_ТекстОтраженияВРеглУчете()
	
	ТекстЗапроса = ПродолжитьВызов();
	
	ТекстЗапроса = ТекстЗапроса
		+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении()
		+ КорректировкаРеализацииЛокализация.бг_ТекстОтраженияНачисленияАкциза();
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти
