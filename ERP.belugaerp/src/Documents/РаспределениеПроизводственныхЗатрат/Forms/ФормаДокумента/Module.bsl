#Область ОбработчикиСобытийФормы

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	бг_ДобавитьКомандуРаспределенияМатериалов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура бг_РаспределитьМатериалыПоПартиямПроизводства(Команда)
	бг_РаспределитьМатериалыПоПартиямПроизводстваНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура бг_ПересчитатьРаспределениеМатериаловПоПартиямПроизводства(Команда)
	бг_ПересчитатьРаспределениеМатериаловПоПартиямПроизводстваНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура бг_ДобавитьКомандуРаспределенияМатериалов()
	бг_ГруппаКоманд = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьГруппуНаФорму(
								ЭтотОбъект,
								"бг_ГруппаКомандРаспредениеМатериаловПоПартиямПроизводства",
								Элементы.ПартииПроизводстваКоманднаяПанель,
								ВидГруппыФормы.Подменю);
	бг_ГруппаКоманд.Заголовок = НСтр("ru = 'Распределение материалов'");
	
	ИмяКоманды       = "бг_РаспределитьМатериалыПоПартиямПроизводства";
	ЗаголовокКоманды = НСтр("ru = 'Распределить материалы по партиям производства'");
	бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьКнопкуНаФорму(
								ЭтотОбъект,
								ИмяКоманды,
								бг_ГруппаКоманд,
								ЗаголовокКоманды,
								ИмяКоманды,
								ИмяКоманды, ,
								ВидКнопкиФормы.КнопкаКоманднойПанели);
	
	ИмяКоманды       = "бг_ПересчитатьРаспределениеМатериаловПоПартиямПроизводства";
	ЗаголовокКоманды = НСтр("ru = 'Пересчитать распределение материалов по партиям производства'");
	бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьКнопкуНаФорму(
								ЭтотОбъект,
								ИмяКоманды,
								бг_ГруппаКоманд,
								ЗаголовокКоманды,
								ИмяКоманды,
								ИмяКоманды, ,
								ВидКнопкиФормы.КнопкаКоманднойПанели);
КонецПроцедуры

&НаСервере
Процедура бг_РаспределитьМатериалыПоПартиямПроизводстваНаСервере()
	БазаРаспределенияМатериалов = бг_БазаРаспределенияМатериалов();
	бг_РаспределениеПроизводственныхЗатрат.бг_РаспределитьМатериалыПоПартиямПроизводства(Объект, Объект.Распределить, БазаРаспределенияМатериалов);
	
	Объект.Распределить = 0;
КонецПроцедуры

&НаСервере
Функция бг_БазаРаспределенияМатериалов(ПартииПроизводства = Неопределено)
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВыпускПродукцииОбороты.Распоряжение КАК Распоряжение,
	|	ВЫРАЗИТЬ(ВыпускПродукцииОбороты.Распоряжение КАК Документ.ЭтапПроизводства2_2).Спецификация КАК Спецификация,
	|	ВЫРАЗИТЬ(ВыпускПродукцииОбороты.Распоряжение КАК Документ.ЭтапПроизводства2_2).ПартияПроизводства КАК ПартияПроизводства,
	|	ВЫРАЗИТЬ(ВыпускПродукцииОбороты.Распоряжение КАК Документ.ЭтапПроизводства2_2).Подразделение КАК Подразделение,
	|	ВыпускПродукцииОбороты.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВыпускПродукцииОбороты.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	ВыпускПродукцииОбороты.КоличествоОборот КАК Коэффициент
	|ИЗ
	|	РегистрНакопления.ВыпускПродукции.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Организация = &Организация
	|				И ТИПЗНАЧЕНИЯ(Распоряжение) = ТИП(Документ.ЭтапПроизводства2_2)
	|				И (НЕ &ОтборПоПартиямПроизводства
	|					ИЛИ ВЫРАЗИТЬ(Распоряжение КАК Документ.ЭтапПроизводства2_2).ПартияПроизводства
	|							В (&ПартииПроизводства))) КАК ВыпускПродукцииОбороты";
	Запрос.УстановитьПараметр("НачалоПериода",              НачалоМесяца(Объект.Дата));
	Запрос.УстановитьПараметр("КонецПериода",               КонецМесяца(Объект.Дата));
	Запрос.УстановитьПараметр("Организация",                Объект.Организация);
	Запрос.УстановитьПараметр("ПартииПроизводства",         ПартииПроизводства);
	Запрос.УстановитьПараметр("ОтборПоПартиямПроизводства", ПартииПроизводства <> Неопределено);
	
	Результат = Запрос.Выполнить();
	ТаблицаВыпущеннаяПродукция = Результат.Выгрузить();
	
	ТаблицаСписокНоменклатуры = Справочники.РесурсныеСпецификации.СписокНоменклатуры();
	Для Каждого СтрокаПродукция Из ТаблицаВыпущеннаяПродукция Цикл
		НоваяСтрокаСписокНоменклатуры = ТаблицаСписокНоменклатуры.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаСписокНоменклатуры, СтрокаПродукция);
		НоваяСтрокаСписокНоменклатуры.Количество = СтрокаПродукция.Коэффициент;
	КонецЦикла;
	
	ПараметрыВыборкиДанных = Справочники.РесурсныеСпецификации.ПараметрыВыборкиДанных("МатериалыИУслуги");
	ДанныеСпецификаций = Справочники.РесурсныеСпецификации.ДанныеСпецификацииПоСпискуНоменклатуры(ТаблицаСписокНоменклатуры, ПараметрыВыборкиДанных);
	
	БазаРаспределенияМатериалов = Новый ТаблицаЗначений;
	БазаРаспределенияМатериалов.Колонки.Добавить("Распоряжение", Новый ОписаниеТипов("ДокументСсылка.ЭтапПроизводства2_2"));
	БазаРаспределенияМатериалов.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	БазаРаспределенияМатериалов.Колонки.Добавить("Спецификация", Новый ОписаниеТипов("СправочникСсылка.РесурсныеСпецификации"));
	БазаРаспределенияМатериалов.Колонки.Добавить("ПартияПроизводства", Новый ОписаниеТипов("СправочникСсылка.ПартииПроизводства"));
	БазаРаспределенияМатериалов.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
	БазаРаспределенияМатериалов.Колонки.Добавить("Коэффициент", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3, ДопустимыйЗнак.Неотрицательный));
	БазаРаспределенияМатериалов.Колонки.Добавить("СтатьяКалькуляции", Новый ОписаниеТипов("СправочникСсылка.СтатьиКалькуляции"));
	
	Для ИндексСтроки = 0 По ТаблицаВыпущеннаяПродукция.Количество() - 1 Цикл
		СтрокаВыпущеннаяПродукция = ТаблицаВыпущеннаяПродукция[ИндексСтроки];
		ДанныеСпецификации = ДанныеСпецификаций[ИндексСтроки];
		
		ОтборМатериалы  = Новый Структура("Номенклатура, Характеристика", Объект.Номенклатура, Объект.Характеристика);
		СтрокиМатериалы = ДанныеСпецификации.МатериалыИУслуги.НайтиСтроки(ОтборМатериалы);
		
		Для Каждого СтрокаМатериал Из СтрокиМатериалы Цикл
			НоваяСтрокаБазаРаспределения = БазаРаспределенияМатериалов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаБазаРаспределения, СтрокаВыпущеннаяПродукция);
			
			ЗаполнитьЗначенияСвойств(НоваяСтрокаБазаРаспределения, СтрокаМатериал);
			НоваяСтрокаБазаРаспределения.Коэффициент = СтрокаМатериал.КоличествоУпаковок;
		КонецЦикла;
	КонецЦикла;
	
	Если БазаРаспределенияМатериалов.Количество() = 0 Тогда
		БазаРаспределенияМатериалов = ТаблицаВыпущеннаяПродукция;
	КонецЕсли;
	
	Возврат БазаРаспределенияМатериалов;
КонецФункции

&НаСервере
Процедура бг_ПересчитатьРаспределениеМатериаловПоПартиямПроизводстваНаСервере()
	КоличествоРаспределить = Объект.Распределить + Объект.Количество;
	
	Партии = Объект.ПартииПроизводства.Выгрузить(, "ПартияПроизводства").ВыгрузитьКолонку("ПартияПроизводства");
	БазаРаспределенияМатериалов  = бг_БазаРаспределенияМатериалов(Партии);
	
	бг_РаспределениеПроизводственныхЗатрат.бг_РаспределитьМатериалыПоПартиямПроизводства(
																	Объект,
																	КоличествоРаспределить,
																	БазаРаспределенияМатериалов);
	
	Объект.Распределить = 0;
КонецПроцедуры

#КонецОбласти
