#Область ОбработчикиСобытийФормы

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	бг_ДобавитьКомандуВызовПомощникаРаспределенияМатериалов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура бг_ОткрытьПомощникРаспределенияМатериалов(Команда)
	ПараметрыФормы = Новый Структура("Период, АдресМатериалыКРаспределению", Период, бг_АдресМатериалыКРаспределению());
	
	ОткрытьФорму("Обработка.бг_РаспределениеМатериалов.Форма.Форма",
			ПараметрыФормы,
			ЭтотОбъект, , , ,
			Новый ОписаниеОповещения("бг_РаспределениеМатериаловЗавершение", ЭтотОбъект),
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура бг_РаспределениеМатериаловЗавершение(Результат, ДополнительныеПараметры) Экспорт
	ЗаполнитьМатериалы();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура бг_ДобавитьКомандуВызовПомощникаРаспределенияМатериалов()
	ИмяКоманды       = "бг_ОткрытьПомощникРаспределенияМатериалов";
	ЗаголовокКоманды = НСтр("ru = 'Помощник распределения материалов'");
	бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьКнопкуНаФорму(ЭтотОбъект, ИмяКоманды,
								Элементы.ЗатратыГруппаРасшифровка, ЗаголовокКоманды,
								ИмяКоманды, ИмяКоманды, , ВидКнопкиФормы.КнопкаКоманднойПанели);
КонецПроцедуры

&НаСервере
Функция бг_АдресМатериалыКРаспределению()
	бг_Материалы = Материалы.Выгрузить();
	
	Возврат ПоместитьВоВременноеХранилище(бг_Материалы, УникальныйИдентификатор);
КонецФункции

#КонецОбласти
