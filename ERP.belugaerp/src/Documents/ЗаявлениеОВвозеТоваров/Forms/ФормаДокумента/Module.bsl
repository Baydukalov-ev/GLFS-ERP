
#Область ОбработчикиСобытийФормы

&НаСервере
&После("ПриСозданииНаСервере")
Процедура бг_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	бг_УстановитьДействиеТоварыСуммаАкцизаПриИзменении();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
&После("ТоварыНалоговаяБазаАкцизыПриИзменении")
Процедура бг_ТоварыНалоговаяБазаАкцизыПриИзменении(Элемент)
	
	бг_ПересчитатьДанныеВСтрокеПриИзмененииСуммыАкциза(Элемент);
	
КонецПроцедуры

&НаКлиенте
&После("ТоварыТвердаяСтавкаАкцизаПриИзменении")
Процедура бг_ТоварыТвердаяСтавкаАкцизаПриИзменении(Элемент)
	
	бг_ПересчитатьДанныеВСтрокеПриИзмененииСуммыАкциза(Элемент);
	
КонецПроцедуры

&НаКлиенте
&После("ТоварыАдвалорнаяСтавкаАкцизаПриИзменении")
Процедура бг_ТоварыАдвалорнаяСтавкаАкцизаПриИзменении(Элемент)
	
	бг_ПересчитатьДанныеВСтрокеПриИзмененииСуммыАкциза(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ТоварыСуммаАкцизаПриИзменении(Элемент)
	
	бг_ПересчитатьДанныеВСтрокеПриИзмененииСуммыАкциза(Элемент);
	
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("РасчитатьСуммыВТабличнойЧастиНаСервере")
Процедура бг_РасчитатьСуммыВТабличнойЧастиНаСервере(СтрокаТабличнойЧасти, ДанныеОбъекта)

	// Рассчитаем по фактурной стоимости налоговую базу в рублях на дату принятия на учет.
	Если ДанныеОбъекта.Валюта <> ДанныеОбъекта.ВалютаРегламентированногоУчета Тогда
		СтруктураКурса = РаботаСКурсамиВалютУТ.ПолучитьКурсВалюты(ДанныеОбъекта.Валюта, СтрокаТабличнойЧасти.ДатаПринятияНаУчет, ДанныеОбъекта.ВалютаРегламентированногоУчета);
		СтрокаТабличнойЧасти.НалоговаяБазаНДС = СтрокаТабличнойЧасти.ФактурнаяСтоимость * (СтруктураКурса.КурсЧислитель/СтруктураКурса.КурсЗнаменатель);
	Иначе
		СтрокаТабличнойЧасти.НалоговаяБазаНДС = СтрокаТабличнойЧасти.ФактурнаяСтоимость;
	КонецЕсли; 
#Вставка
	СтрокаТабличнойЧасти.НалоговаяБазаНДС = СтрокаТабличнойЧасти.НалоговаяБазаНДС + СтрокаТабличнойЧасти.СуммаАкциза;
#КонецВставки

	// Рассчитаем по налоговой базе и ставке сумму НДС
	СтрокаТабличнойЧасти.СуммаНДС = УчетНДСУПКлиентСервер.РассчитатьСуммуНДС(
	СтрокаТабличнойЧасти.НалоговаяБазаНДС,
	УчетНДСУПКлиентСервер.ЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС),
	Ложь);

	// Рассчитаем по налоговой базе в рублях статистическую стоимость в долларах на дату принятия на учет.
	Если ДанныеОбъекта.ВалютаДолларыСША <> Справочники.Валюты.ПустаяСсылка() Тогда
		СтруктураКурса = РаботаСКурсамиВалютУТ.ПолучитьКурсВалюты(ДанныеОбъекта.ВалютаДолларыСША, СтрокаТабличнойЧасти.ДатаПринятияНаУчет, ДанныеОбъекта.ВалютаРегламентированногоУчета);
		СтрокаТабличнойЧасти.СтатСтоимостьДолларыСША = СтрокаТабличнойЧасти.НалоговаяБазаНДС * (СтруктураКурса.КурсЗнаменатель/СтруктураКурса.КурсЧислитель);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура бг_УстановитьДействиеТоварыСуммаАкцизаПриИзменении()
	
	Элементы.ТоварыСуммаАкциза.УстановитьДействие("ПриИзменении", "бг_ТоварыСуммаАкцизаПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ПересчитатьДанныеВСтрокеПриИзмененииСуммыАкциза(Элемент)
	
	бг_ПересчитатьДанныеВСтрокеПриИзмененииСуммыАкцизаНаСервере(Элементы.Товары.ТекущиеДанные.НомерСтроки);
	
КонецПроцедуры

&НаСервере
Процедура бг_ПересчитатьДанныеВСтрокеПриИзмененииСуммыАкцизаНаСервере(НомерСтроки)
	
	ТекущаяСтрока = Объект.Товары[НомерСтроки - 1];
	
	ДанныеОбъекта = Новый Структура(
		"ВалютаРегламентированногоУчета, ВалютаДолларыСША, Валюта",
		ВалютаРегламентированногоУчета, ВалютаДолларыСША, ТекущаяСтрока.Валюта);
	
	РасчитатьСуммыВТабличнойЧастиНаСервере(ТекущаяСтрока, ДанныеОбъекта);
	
КонецПроцедуры

#КонецОбласти
