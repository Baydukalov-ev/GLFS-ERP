#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

&После("ОбработкаЗаполнения")
Процедура бг_ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

&После("ПередЗаписью")
Процедура бг_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	ИнтеграцияЕГАИСПереопределяемый.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
	РаботаСФайлами.УстановитьПометкуУдаленияПрисоединенныхФайловДокументов(
		ЭтотОбъект,
		Отказ,
		РежимЗаписи,
		РежимПроведения);
	
КонецПроцедуры

&После("ПриЗаписи")
Процедура бг_ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияЕГАИС.ЗаписатьСтатусДокументаЕГАИСПоУмолчанию(ЭтотОбъект);

КонецПроцедуры

&После("ПриКопировании")
Процедура бг_ПриКопировании(ОбъектКопирования)
	
	ИнициализироватьДокумент();
	
	ИдентификаторЕГАИС = "";
	
	// Присоединенные файлы не копируются вместе с документом.
	// Очистим файлы, чтобы в документе не было ссылок на присоединенные файлы другого владельца. 
	Для Каждого СтрокаТЧ Из Емкости Цикл
		СтрокаТЧ.Этикетка = Неопределено;
	КонецЦикла;
	Для Каждого СтрокаТЧ Из ТехнологическиеИнструкции Цикл
		СтрокаТЧ.Файл = Неопределено;
	КонецЦикла;
	
	// Так как у этих реквизитов в диалоговой форме отключена видимость,
	// будем их очищать при копировании, чтобы избежать скрытых от пользователя данных.
	// Если будем оформлять уведомления на пиво, то этот код надо убрать,
	// а у соответствующих реквизитов формы документа включить видимость. 
	ТипПива = "";
	СпособОбработкиПива = "";
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьДокумент()
	
	Ответственный = Пользователи.ТекущийПользователь();
	ДатаПервойПоставки = 
		Документы.битУведомлениеОНачалеОборотаНаТерриторииРФАПЕГАИС.ДатаПервойПоставки(ТекущаяДатаСеанса());
	
	Если Не ЗначениеЗаполнено(ТипОрганизации) Тогда
		СписокВыбора = Новый СписокЗначений;
		бг_ИнтеграцияЕГАИС.ЗаполнитьСписокВыбораТипаОрганизацииПроизводителя(СписокВыбора);
		ТипОрганизации = СписокВыбора[0].Значение;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтранаПроисхождения) Тогда
		СтранаПроисхождения = ПредопределенноеЗначение("Справочник.СтраныМира.Россия");
	КонецЕсли;
	
	ЗаполнитьТехническиеРегламенты();
	
КонецПроцедуры

Процедура ЗаполнитьТехническиеРегламенты()
	
	ТехническиеРегламенты.Очистить();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	битТехническиеРегламенты.Ссылка КАК Регламент,
	|	битТехническиеРегламенты.Номер КАК Номер,
	|	битТехническиеРегламенты.НаименованиеПолное КАК НаименованиеПолное
	|ИЗ
	|	Справочник.битТехническиеРегламенты КАК битТехническиеРегламенты
	|ГДЕ
	|	НЕ битТехническиеРегламенты.ПометкаУдаления";
	
	ТехническиеРегламенты.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли


