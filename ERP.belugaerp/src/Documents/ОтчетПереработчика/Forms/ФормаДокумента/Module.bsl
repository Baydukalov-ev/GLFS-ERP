
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	бг_СоздатьЭлементыФормы();
	бг_ОбновитьНадписьСтатусМагистраль();
	
КонецПроцедуры

&НаСервере
Процедура бг_ПриЧтенииНаСервереПосле(ТекущийОбъект)
	
	Если ТекущийОбъект.ЭтоНовый() Тогда
		бг_УстановитьНастройкуФормированияЗаявокНаДСМагистральПоУмолчанию();
	КонецЕсли;
	
	бг_ОбновитьНадписьСтатусМагистраль();
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ПередЗаписьюПеред(Отказ, ПараметрыЗаписи)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	бг_ПередЗаписьюМагистраль(Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура бг_ОбработкаПроверкиЗаполненияНаСервереПеред(Отказ, ПроверяемыеРеквизиты)
	
	Если Объект.бг_АвтоматическиФормироватьЗаявкиНаРасходованиеДС Тогда
		
		Если Не ЗначениеЗаполнено(Объект.ДатаПоДаннымПартнера) Тогда
			ТекстОшибки = НСтр("ru = 'Не заполнена дата входящего документа'");
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , "Объект.ДатаПоДаннымПартнера", , Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Объект.НомерПоДаннымПартнера) Тогда
			ТекстОшибки = НСтр("ru = 'Не заполнен номер входящего документа'");
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , "Объект.НомерПоДаннымПартнера", , Отказ);
		КонецЕсли;
		
		бг_Магистраль.ПроверитьРеквизитыДоговора(Объект.Договор, "Объект.Договор", Отказ);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
&После("ПриИзмененииДоговораСервер")
Процедура бг_ПриИзмененииДоговораСервер()
	
	бг_УстановитьНастройкуФормированияЗаявокНаДСМагистральПоУмолчанию();
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ДекорацияСтатусМагистральОбработкаНавигационнойСсылки(Элемент,
	НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(НавигационнаяСсылкаФорматированнойСтроки) Тогда
		ПерейтиПоНавигационнойСсылке(НавигационнаяСсылкаФорматированнойСтроки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура бг_СоздатьЭлементыФормы()
	
	бг_ДобавитьПоляМагистраль();
	
КонецПроцедуры

&НаСервере
Процедура бг_ДобавитьПоляМагистраль()
	
	бг_ГруппаШапкаМагистраль = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьГруппуНаФорму(
		ЭтотОбъект,
		"бг_ГруппаШапкаМагистраль",
		Элементы.ГруппаОсновное,
		ВидГруппыФормы.ОбычнаяГруппа,
		Элементы.Комментарий);
	бг_ГруппаШапкаМагистраль.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	бг_ГруппаШапкаМагистраль.Заголовок = НСтр("ru = 'Проект ""Магистраль""'");
	
	бг_АвтоматическиФормироватьЗаявкиНаРасходованиеДС =
		бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_АвтоматическиФормироватьЗаявкиНаРасходованиеДС",
		бг_ГруппаШапкаМагистраль,
		"Объект.бг_АвтоматическиФормироватьЗаявкиНаРасходованиеДС",
		,
		,
		"ПолеФлажка");
	бг_АвтоматическиФормироватьЗаявкиНаРасходованиеДС.ВидФлажка = ВидФлажка.Выключатель;
	
	бг_ДекорацияСтатусМагистраль =
		бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьДекорациюНаФорму(
		ЭтотОбъект,
		"бг_ДекорацияСтатусМагистраль",
		бг_ГруппаШапкаМагистраль,
		НСтр("ru = 'n/a'"));
	
	бг_ДекорацияСтатусМагистраль.УстановитьДействие(
		"ОбработкаНавигационнойСсылки", "бг_ДекорацияСтатусМагистральОбработкаНавигационнойСсылки");
	
	бг_Магистраль.УстановитьДоступностьРеквизитовМагистральНаФорме(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура бг_УстановитьНастройкуФормированияЗаявокНаДСМагистральПоУмолчанию()
	
	Объект.бг_АвтоматическиФормироватьЗаявкиНаРасходованиеДС =
		бг_АвтоматическиФормироватьЗаявкиНаРасходованиеДС(Объект.Договор);
	
	бг_Магистраль.УстановитьДоступностьРеквизитовМагистральНаФорме(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ПередЗаписьюМагистраль(Отказ, ПараметрыЗаписи)
	
	Если Модифицированность
		И ЗначениеЗаполнено(ПараметрыЗаписи)
		И ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение
		И ЗначениеЗаполнено(Объект.ДатаПоДаннымПартнера)
		И Объект.бг_АвтоматическиФормироватьЗаявкиНаРасходованиеДС Тогда
		
		ДатаДокумента = Объект.Дата;
		Если Не ЗначениеЗаполнено(ДатаДокумента) Тогда
			ДатаДокумента = ОбщегоНазначенияКлиент.ДатаСеанса();
		КонецЕсли;
		
		Если НачалоДня(Объект.ДатаПоДаннымПартнера) <> НачалоДня(ДатаДокумента)
			И Не ПараметрыЗаписи.Свойство("бг_ПередЗаписьюМагистральПропуститьПроверку") Тогда
			
			Отказ = Истина;
			
			ТекстВопроса = НСтр("ru = 'Дата документа отличается от даты вх. документа по данным поставщика.
                                 |Отсрочка платежа будет рассчитана от даты документа.
                                 |Провести документ?'");
			
			ОписаниеОповещения = Новый ОписаниеОповещения("бг_ПередЗаписьюПередМагистральОтвет", ЭтотОбъект, ПараметрыЗаписи);
			
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ПередЗаписьюПередМагистральОтвет(РезультатВопроса, ПараметрыЗаписи) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ПараметрыЗаписи.Вставить("бг_ПередЗаписьюМагистральПропуститьПроверку");
		Записать(ПараметрыЗаписи);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура бг_ОбновитьНадписьСтатусМагистраль()
	
	Если ЭтотОбъект.Элементы.Найти("бг_ДекорацияСтатусМагистраль") <> Неопределено
		И (Объект.бг_АвтоматическиФормироватьЗаявкиНаРасходованиеДС
			Или бг_АвтоматическиФормироватьЗаявкиНаРасходованиеДС(Объект.Договор)) Тогда
		
		ЭтотОбъект.Элементы.бг_ДекорацияСтатусМагистраль.Заголовок =
			бг_Магистраль.НадписьСтатусЗаявкиНаРасходованиеДС(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция бг_АвтоматическиФормироватьЗаявкиНаРасходованиеДС(Договор)
	
	Возврат бг_Магистраль.АвтоматическиФормироватьЗаявкиНаРасходованиеДС(Договор);
	
КонецФункции

#КонецОбласти
