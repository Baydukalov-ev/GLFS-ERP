#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает признак наличия маркируемой алкогольной продукции в акте постановки на баланс ЕГАИС (необходимы движения по маркам).
//
// Параметры:
//  АктПостановкиНаБалансЕГАИС - ДокументСсылка.АктПостановкиНаБалансЕГАИС - Ссылка на документ АктПостановкиНаБалансЕГАИС.
//
// Возвращаемое значение:
//   Булево - признак наличия маркируемой алкогольной продукции.
//
Функция бг_ЕстьМаркируемаяАлкогольнаяПродукция(АктПостановкиНаБалансЕГАИС) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	АктПостановкиНаБалансЕГАИСТовары.АлкогольнаяПродукция КАК АлкогольнаяПродукция
	|ИЗ
	|	Документ.АктПостановкиНаБалансЕГАИС.Товары КАК АктПостановкиНаБалансЕГАИСТовары
	|ГДЕ
	|	АктПостановкиНаБалансЕГАИСТовары.АлкогольнаяПродукция.ВидПродукции.Маркируемый
	|	И АктПостановкиНаБалансЕГАИСТовары.Ссылка = &Ссылка
	|	И АктПостановкиНаБалансЕГАИСТовары.АлкогольнаяПродукция.ВидПродукции.ВидЛицензии = ЗНАЧЕНИЕ(Перечисление.ВидыЛицензийАлкогольнойПродукции.АлкогольнаяПродукция)";
	
	Запрос.УстановитьПараметр("Ссылка", АктПостановкиНаБалансЕГАИС);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

// Возвращает данные по списываемым штрихкодам по акту постановки на баланс.
//
// Параметры:
//  АктПостановкиНаБалансЕГАИС - ДокументСсылка.АктПостановкиНаБалансЕГАИС - Ссылка на документ АктПостановкиНаБалансЕГАИС.
//  ТолькоЗаполненныеМарки - Булево - Флаг, означающий, что результат по маркам не должен содержать строк,
//    в которых содержатся строки алкогольной продукции по акту без найденного соответствия марок.
//
// Возвращаемое значение:
//   Структура - Описание:
//    * Марки - ТаблицаЗначений.
//    * Упаковки - ТаблицаЗначений.
//
Функция бг_ДанныеПриходуемыхШтрихкодов(АктПостановкиНаБалансЕГАИС, ТолькоЗаполненныеМарки = Истина) Экспорт
	
	РегистраторыДвиженияМарок = бг_ДокументыПостановкиНаБалансWMS(АктПостановкиНаБалансЕГАИС);
	Если РегистраторыДвиженияМарок.Количество() <> 1 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = бг_ТекстЗапросаДанныеПриходуемыхШтрихкодов();
	
	Запрос.УстановитьПараметр("РегистраторыДвиженияМарок", РегистраторыДвиженияМарок);
	Запрос.УстановитьПараметр("АктПостановкиНаБалансЕГАИС", АктПостановкиНаБалансЕГАИС);
	
	МоментВремениРегистратораДвижений = РегистраторыДвиженияМарок[РегистраторыДвиженияМарок.ВГраница()].МоментВремени();
	Запрос.УстановитьПараметр(
		"Период",
		МоментВремениРегистратораДвижений.Дата);
		
	ПакетРезультатовЗапроса = Запрос.ВыполнитьПакет();
	
	ВерхнийИндексПакета = ПакетРезультатовЗапроса.ВГраница();
	
	ДанныеПриходуемыхШтрихкодов = Новый Структура;
	ДанныеПриходуемыхШтрихкодов.Вставить("Марки", ПакетРезультатовЗапроса[ВерхнийИндексПакета - 1].Выгрузить());
	ДанныеПриходуемыхШтрихкодов.Вставить("Упаковки", ПакетРезультатовЗапроса[ВерхнийИндексПакета].Выгрузить());
	
	Если ТолькоЗаполненныеМарки Тогда
		ПараметрыПоискаНеНайденныхМарок = Новый Структура("НетСоответствияТовараАктаДляМарки", Истина);
		СтрокиСНенайденнымиМарками = ДанныеПриходуемыхШтрихкодов.Марки.НайтиСтроки(ПараметрыПоискаНеНайденныхМарок);
		Для каждого СтрокаСНенайденнойМаркой Из СтрокиСНенайденнымиМарками Цикл
			ДанныеПриходуемыхШтрихкодов.Марки.Удалить(СтрокаСНенайденнойМаркой);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ДанныеПриходуемыхШтрихкодов;
	
КонецФункции

// Возвращает признак соответствия марок товарам акту постановки на баланс.
//
// Параметры:
//  АктПостановкиНаБалансЕГАИС - ДокументСсылка.АктПостановкиНаБалансЕГАИС - Ссылка на документ АктПостановкиНаБалансЕГАИС.
//
// Возвращаемое значение:
//   Булево - признак соответствия марок товарам акту постановки на баланс ЕГАИС.
//
Функция бг_МаркиСоответствуютАлкогольнойПродукцииАкта(Марки, АктПостановкиНаБалансЕГАИС, Отказ = Ложь) Экспорт
	
	МаркиСоответствуютАлкогольнойПродукцииАкта = Истина;
	
	ВыведенныеОшибкиПоАП = Новый Массив;
	
	Для каждого ДанныеМарки Из Марки Цикл
		
		Если ДанныеМарки.НесоответствиеКоличества
			И ВыведенныеОшибкиПоАП.Найти(ДанныеМарки.АлкогольнаяПродукция) = Неопределено Тогда
			
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(
					НСтр("ru='Отгружаемая алкогольная продукция %1 по акту постановки на баланс не соответствует данным штрихкодов'"),
					?(ЗначениеЗаполнено(ДанныеМарки.АлкогольнаяПродукция),
						ДанныеМарки.АлкогольнаяПродукция,
						НСтр("ru='<нет сопоставления с движениями марок>'"))),
				АктПостановкиНаБалансЕГАИС,,,
				Отказ);
				
			МаркиСоответствуютАлкогольнойПродукцииАкта = Ложь;
			
			// Не дублировать выведенные ошибки по одной алкогольной продукции.
			ВыведенныеОшибкиПоАП.Добавить(ДанныеМарки.АлкогольнаяПродукция);
		КонецЕсли;
		
		Если ДанныеМарки.НеЗаполненыПоляТоваровДокумента
			И ВыведенныеОшибкиПоАП.Найти(ДанныеМарки.АлкогольнаяПродукция) = Неопределено Тогда
			
			Если ЗначениеЗаполнено(ДанныеМарки.АлкогольнаяПродукция) Тогда
				
				ТекстОшибки = СтрШаблон(
					НСтр("ru='Несоответствие ключевых полей товаров акта и данных штрихкодов по:
					| Номенклатура: %1, серия %2:, алкогольная продукция: %3'"),
					ДанныеМарки.Номенклатура,
					ДанныеМарки.Серия,
					ДанныеМарки.АлкогольнаяПродукция);
					
			Иначе
				
				ТекстОшибки = СтрШаблон(
					НСтр("ru='Несоответствие ключевых полей товаров акта и данных штрихкодов по:
					| Номенклатура: %1, серия %2:'"),
					ДанныеМарки.Номенклатура,
					ДанныеМарки.Серия);
				
			КонецЕсли;
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстОшибки,
				АктПостановкиНаБалансЕГАИС,,,
				Отказ);
				
			МаркиСоответствуютАлкогольнойПродукцииАкта = Ложь;
			
			// Не дублировать выведенные ошибки по одной алкогольной продукции.
			ВыведенныеОшибкиПоАП.Добавить(ДанныеМарки.АлкогольнаяПродукция);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МаркиСоответствуютАлкогольнойПродукцииАкта;
	
КонецФункции

// Возвращает признак необходимости использования механизма движений марок вместо типового механизма ШУТов. 
//
// Параметры:
//  АктПостановкиНаБалансЕГАИС - ДокументСсылка.АктПостановкиНаБалансЕГАИС - Ссылка на документ АктПостановкиНаБалансЕГАИС.
//
// Возвращаемое значение:
//   Булево - признак использования.
//
Функция бг_ИспользоватьМеханизмДвиженийМарок(АктПостановкиНаБалансЕГАИС) Экспорт
	
	Если Не ЗначениеЗаполнено(АктПостановкиНаБалансЕГАИС) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат бг_ИнтеграцияЕГАИСПовтИсп.ЕстьМаркируемаяАлкогольнаяПродукция(АктПостановкиНаБалансЕГАИС);
	
КонецФункции

// Возвращает дату, на которую должны быть сформированы движения по акту постановки на баланс.
//
// Параметры:
//  АктПостановкиНаБалансЕГАИС - ДокументСсылка.АктПостановкиНаБалансЕГАИС - Ссылка на документ АктПостановкиНаБалансЕГАИС.
//  Дата - Дата - Дата из АктПостановкиНаБалансЕГАИС.
//  ДатаРегистрацииДвижений - Дата - ДатаРегистрацииДвижений из АктПостановкиНаБалансЕГАИС.
//
// Возвращаемое значение:
//  Дата
//
Функция бг_ДатаДвиженияМарок(АктПостановкиНаБалансЕГАИС, Дата = Неопределено, ДатаРегистрацииДвижений = Неопределено) Экспорт
	
	Если Дата = Неопределено Тогда
		ДатыАкта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(АктПостановкиНаБалансЕГАИС, "ДатаРегистрацииДвижений, Дата");
		Дата = ДатыАкта.Дата;
		ДатаРегистрацииДвижений = ДатыАкта.ДатаРегистрацииДвижений;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаРегистрацииДвижений) Тогда
		ДатаДвиженияМарок = КонецДня(ДатаРегистрацииДвижений);
	Иначе
		ДатаДвиженияМарок = КонецДня(Дата);
	КонецЕсли;
	
	ПредыдущиеРегистраторыМарок = бг_ДокументыПостановкиНаБалансWMS(АктПостановкиНаБалансЕГАИС);
	
	Если ПредыдущиеРегистраторыМарок.Количество() > 0 Тогда
		
		ПоследнийРегистратор = ПредыдущиеРегистраторыМарок[ПредыдущиеРегистраторыМарок.ВГраница()];
		
		МаксимальнаяДатаДвиженийПредыдущихСтатусовМарок = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ПоследнийРегистратор,
			"Дата");
			
		Если МаксимальнаяДатаДвиженийПредыдущихСтатусовМарок >= ДатаДвиженияМарок Тогда
			ДатаДвиженияМарок = МаксимальнаяДатаДвиженийПредыдущихСтатусовМарок + 1;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДатаДвиженияМарок;
	
КонецФункции

// Возвращает документы постановки на баланс WMS, которые являются регистраторами предыдущих
// в цепочке движений по РС "Движение марок" статусов марок.
//
// Параметры:
//
// Возвращаемое значение:
//   Массив - элементов с типом ДокументСсылка.битИнвентаризацияПродукцииЕГАИС.
//
Функция бг_ДокументыПостановкиНаБалансWMS(АктПостановкиНаБалансЕГАИС) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	битИнвентаризацияПродукцииЕГАИС.Ссылка КАК ИнвентаризацияПродукцииЕГАИС
	|ИЗ
	|	Документ.АктПостановкиНаБалансЕГАИС КАК АктПостановкиНаБалансЕГАИС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрочееОприходованиеТоваров КАК ПрочееОприходованиеТоваров
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.битИнвентаризацияПродукцииЕГАИС КАК битИнвентаризацияПродукцииЕГАИС
	|			ПО ПрочееОприходованиеТоваров.бг_ИнвентаризацияПродукцииЕГАИС = битИнвентаризацияПродукцииЕГАИС.Ссылка
	|		ПО АктПостановкиНаБалансЕГАИС.ДокументОснование = ПрочееОприходованиеТоваров.Ссылка
	|			И (АктПостановкиНаБалансЕГАИС.Ссылка = &АктПостановкиНаБалансЕГАИС)";
	
	Запрос.УстановитьПараметр("АктПостановкиНаБалансЕГАИС", АктПостановкиНаБалансЕГАИС);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИнвентаризацияПродукцииЕГАИС");
	
КонецФункции

// Возвращает массив статусов, при которых акт постановки на баланс является подтвержденным в ЕГАИС.
//
// Параметры:
//
// Возвращаемое значение:
//	Массив - элементы с типом ПеречислениеСсылка.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.
//
Функция бг_СтатусыПодтвержденные() Экспорт

	СтатусыПодтвержденные = Новый Массив;
	
	СтатусыПодтвержденные.Добавить(Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ПроведенЕГАИС);
	
	Возврат СтатусыПодтвержденные;

КонецФункции

// Возвращает массив статусов, при которых акт постановки на баланс является отмененным в ЕГАИС.
//
// Параметры:
//
// Возвращаемое значение:
//	Массив - элементы с типом ПеречислениеСсылка.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.
//
Функция бг_СтатусыОтменыЕГАИС() Экспорт
	
	СтатусыОтменыЕГАИС = Новый Массив;
	
	СтатусыОтменыЕГАИС.Добавить(Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.Отменен);
	
	Возврат СтатусыОтменыЕГАИС;
	
КонецФункции

// Возвращает признак, что статус акта постановки на баланс является подтвержденным.
//
// Параметры:
//
// Возвращаемое значение:
//	Булево
//
Функция бг_СтатусЯвляетсяПодтвержденным(Статус) Экспорт
	
	Возврат бг_СтатусыПодтвержденные().Найти(Статус) <> Неопределено;
	
КонецФункции

// Актуализирует данные серий, если они изменились в документе.
//
Процедура бг_АктуализироватьСерииСправки(АктПостановкиНаБалансЕГАИС) Экспорт
	
	ДанныеСправочниковКАктуализации = бг_СерииСправкиКАктуализации(АктПостановкиНаБалансЕГАИС);
	
	Для каждого ДанныеКАктуализации Из ДанныеСправочниковКАктуализации Цикл
		бг_АктуализироватьСериюСправку(ДанныеКАктуализации);
	КонецЦикла;
	
КонецПроцедуры

// Интерфейс для отложенной обработки
//
// Параметры:
//   Документ - ДокументСсылка.АктПостановкиНаБалансЕГАИС
//   ВариантОбработки - ПеречислениеСсылка.бг_ВариантыОтложеннойОбработкиОбъектов, Неопределено - вариант обработки
//   Отказ - Булево - отказ от обработки
//
Процедура бг_ОтложеннаяОбработкаОбъекта(Документ, ВариантОбработки, Отказ,
	ДополнительныеСведения = Неопределено) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ВариантОбработки = Перечисления.бг_ВариантыОтложеннойОбработкиОбъектов.ОтправитьЗапросыСправки1ЕГАИС Тогда
		бг_ИнтеграцияЕГАИС.ОтправитьЗапросыСправок1ВУТМ(Документ);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает признак о смене статуса на один вниз по стандартной цепочке статусов Акта постановки на баланс ЕГАИС.
// Обычно это происходит всвязи с ошибкой в последовательности обработки статусов ЕГАИС.
//
Функция бг_ЭтоЗапретныйДаунгрейдСтатуса(ПредыдущийСтатус, НовыйСтатус) Экспорт
	
	ВсеСтатусы = Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС;
	
	Возврат ПредыдущийСтатус = ВсеСтатусы.ПроведенЕГАИС
		И НовыйСтатус = ВсеСтатусы.ОбрабатываетсяЕГАИС;
	
КонецФункции

#КонецОбласти

#Область ДействияПриОбменеЕГАИС

&После("ПриИзмененииСтатусаДокумента")
Процедура бг_ПриИзмененииСтатусаДокумента(ДокументСсылка, ПредыдущийСтатус, НовыйСтатус, ПараметрыОбновленияСтатуса)
	
	бг_ПриИзмененииСтатусаДокументаДвижениеМарок(ДокументСсылка, НовыйСтатус);
	
	Если бг_СтатусЯвляетсяПодтвержденным(НовыйСтатус) Тогда
		бг_АктуализироватьСерииСправки(ДокументСсылка);
	КонецЕсли;
	
	Если Не бг_СтатусЯвляетсяПодтвержденным(ПредыдущийСтатус) И бг_СтатусЯвляетсяПодтвержденным(НовыйСтатус) Тогда
		бг_ИнтеграцияЕГАИС.ЗарегистрироватьДокументДляОтложенногоЗапросаСправок1(ДокументСсылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СообщенияЕГАИС

&ИзменениеИКонтроль("АктПостановкиНаБалансЕГАИСXML")
Функция бг_АктПостановкиНаБалансЕГАИСXML(ДокументСсылка)
	
	СообщенияXML = Новый Массив;
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ЕГАИСПрисоединенныеФайлы.Документ      КАК Ссылка,
		|	КОЛИЧЕСТВО(ЕГАИСПрисоединенныеФайлы.Ссылка) КАК ПоследнийНомер
		|ПОМЕСТИТЬ Версии
		|ИЗ
		|	Справочник.ЕГАИСПрисоединенныеФайлы КАК ЕГАИСПрисоединенныеФайлы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктПостановкиНаБалансЕГАИС КАК Шапка
		|		ПО Шапка.Ссылка       = &Ссылка
		|		 И Шапка.ВидДокумента = ЕГАИСПрисоединенныеФайлы.Операция
		|		 И Шапка.Ссылка       = ЕГАИСПрисоединенныеФайлы.Документ
		|ГДЕ
		|	ЕГАИСПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
		|СГРУППИРОВАТЬ ПО
		|	ЕГАИСПрисоединенныеФайлы.Документ
		|;
		|
		|//#РезультатЗапроса#////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Шапка.Номер                           КАК Номер,
		|	Шапка.Дата                            КАК Дата,
		|	ЕСТЬNULL(Версии.ПоследнийНомер, 0)    КАК ПоследнийНомерВерсии,
		|	Шапка.Идентификатор                   КАК Идентификатор,
		|	Шапка.ВидДокумента                    КАК Операция,
		|	Шапка.ДокументОснование               КАК ДокументОснование,
		|	
		|	Шапка.ПричинаПостановкиНаБаланс КАК ПричинаПостановкиНаБаланс,
		|	ВЫБОР
		|		КОГДА Шапка.ПричинаПостановкиНаБаланс = ЗНАЧЕНИЕ(Перечисление.ПричиныПостановкиНаБалансЕГАИС.Пересортица)
		|			ТОГДА ЕСТЬNULL(Шапка.АктСписанияЕГАИС.ИдентификаторЕГАИС, НЕОПРЕДЕЛЕНО)
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ИдентификаторАктаСписания,
		|	ВЫРАЗИТЬ(Шапка.Комментарий КАК Строка(500)) КАК Комментарий,
		|	
		|	Шапка.ОрганизацияЕГАИС              КАК ОрганизацияЕГАИС,
		|	Шапка.ОрганизацияЕГАИС.Код          КАК ИдентификаторФСРАР,
		|	Шапка.ОрганизацияЕГАИС.ФорматОбмена КАК ФорматОбмена,
		|	Шапка.Ответственный                 КАК Ответственный
		|ИЗ
		|	Документ.АктПостановкиНаБалансЕГАИС КАК Шапка,
		|		ЛЕВОЕ СОЕДИНЕНИЕ Версии КАК Версии
		|		ПО Шапка.Ссылка = Версии.Ссылка
		|ГДЕ
		|	Шапка.Ссылка = &Ссылка
		|",
		"Шапка");
	
	ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	Товары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	Товары.Номенклатура         КАК Номенклатура,
		|	Товары.Характеристика       КАК Характеристика,
		|	Товары.Серия                КАК Серия
		|ПОМЕСТИТЬ ВТТовары
		|ИЗ
		|	Документ.АктПостановкиНаБалансЕГАИС.Товары КАК Товары
		|ГДЕ
		|	Товары.Ссылка = &Ссылка
		|");
	
	ТекстыЗапроса.Добавить(
		ИнтеграцияЕГАИС.ТекстЗапросаВТКоэффициентыПересчетаВЕдиницыЕГАИС(
			"ВТТовары",
			"ВТКоэффициентыПересчетаВЕдиницыЕГАИС"));
	
	ТекстЗапросаАлкогольнаяПродукция = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.АлкогольнаяПродукция КАК АлкогольнаяПродукция
	|ПОМЕСТИТЬ АлкогольнаяПродукция
	|ИЗ
	|	ВТТовары КАК Товары
	|;
	|
	|//#РезультатЗапроса#////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	// Выгрузка информации о товарах в сокращенном виде
	|	Продукция.Код                                          КАК ПродукцияКод,
	|	ВЫРАЗИТЬ(Продукция.НаименованиеПолное КАК Строка(255)) КАК ПродукцияНаименованиеПолное,
	|	Продукция.ТипПродукции                                 КАК ПродукцияТипПродукции,
	|	ЕСТЬNULL(ВидыПродукции.Код, """")                      КАК ПродукцияКодВидаПродукции,
	|	
	|	Производители.Код                                            КАК ПроизводительКод,
	|	ВЫРАЗИТЬ(Производители.Наименование КАК Строка(64))          КАК ПроизводительНаименование,
	|	ВЫРАЗИТЬ(Производители.НаименованиеПолное КАК Строка(255))   КАК ПроизводительНаименованиеПолное,
	|	Производители.ИНН                                            КАК ПроизводительИНН,
	|	Производители.КПП                                            КАК ПроизводительКПП,
	|	Производители.КодСтраны                                      КАК ПроизводительКодСтраны,
	|	Производители.КодРегиона                                     КАК ПроизводительКодРегиона,
	|	Производители.ПочтовыйИндекс                                 КАК ПроизводительПочтовыйИндекс,
	|	ВЫРАЗИТЬ(Производители.ПредставлениеАдреса КАК Строка(1000)) КАК ПроизводительПредставлениеАдреса,
	|	Производители.ТипОрганизации                                 КАК ПроизводительТипОрганизации,
	|	Производители.ИдентификаторОрганизацииТС                     КАК ПроизводительИдентификаторОрганизацииТС
	|ИЗ
	|	АлкогольнаяПродукция КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК Продукция
	|		ПО Продукция.Ссылка = Товары.АлкогольнаяПродукция
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыАлкогольнойПродукции КАК ВидыПродукции
	|		ПО Продукция.ВидПродукции = ВидыПродукции.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК Производители
	|		ПО Производители.Ссылка = Продукция.Производитель
	|";
	
	ТекстыЗапроса.Добавить(
		ТекстЗапросаАлкогольнаяПродукция,
		"АлкогольнаяПродукция");
		
	ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	Товары.НомерСтроки                      КАК НомерСтроки,
		|	Товары.ИдентификаторСтроки              КАК ИдентификаторСтроки,
		|	Товары.АлкогольнаяПродукция             КАК АлкогольнаяПродукция,
		|	Товары.Количество
		|	* ЕСТЬNULL(ЕдиницыЕГАИС.Коэффициент, 1) КАК Количество,
		|	Товары.КоличествоПоСправке1             КАК КоличествоПоСправке1,
		|	Товары.НомерТТН                         КАК НомерТТН,
		|	Товары.ДатаТТН                          КАК ДатаТТН,
		|	Товары.ДатаРозлива                      КАК ДатаРозлива,
		|	Товары.НомерПодтвержденияЕГАИС          КАК НомерПодтвержденияЕГАИС,
		|	Товары.ДатаПодтвержденияЕГАИС           КАК ДатаПодтвержденияЕГАИС,
		|	Товары.Справка2                         КАК Справка2,
		|	Товары.Справка2.НомерСправки1           КАК НомерСправки1,
		|	Товары.Справка2.РегистрационныйНомер    КАК НомерСправки2,
		|	
		|	ЕСТЬNULL(ЕдиницыЕГАИС.ПроверятьОбъемДАЛ, ЛОЖЬ) КАК ПроверятьОбъемДАЛ,
		|	ЕСТЬNULL(ЕдиницыЕГАИС.ОбъемДАЛ, 0)             КАК ОбъемДАЛ
		|ИЗ
		|	Документ.АктПостановкиНаБалансЕГАИС.Товары КАК Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКоэффициентыПересчетаВЕдиницыЕГАИС КАК ЕдиницыЕГАИС
		|		ПО ЕдиницыЕГАИС.АлкогольнаяПродукция = Товары.АлкогольнаяПродукция
		|		 И ЕдиницыЕГАИС.Номенклатура = Товары.Номенклатура
		|		 И ЕдиницыЕГАИС.Характеристика = Товары.Характеристика
		|		 И ЕдиницыЕГАИС.Серия = Товары.Серия
		|ГДЕ
		|	Товары.Ссылка = &Ссылка
		|",
		"Товары");
	
	ПараметрыФормированияТекстаЗапроса = ШтрихкодированиеЕГАИС.ПараметрыФормированияТекстаЗапросаВложенныхШтрихкодов();
	ПараметрыФормированияТекстаЗапроса.ДокументСсылка                  = ДокументСсылка;
	ПараметрыФормированияТекстаЗапроса.ИспользоватьИдентификаторСтроки = Истина;
#Удаление
	ТекстыЗапроса.Добавить(
		ШтрихкодированиеЕГАИС.ТекстЗапросаВложенныхШтрихкодовПоДокументу(ПараметрыФормированияТекстаЗапроса),
		"ВложенныеШтрихкоды");
#КонецУдаления
#Вставка
	Если Не бг_ИнтеграцияЕГАИСПовтИсп.ИспользоватьМеханизмДвиженийМарок(ДокументСсылка) Тогда
		ТекстыЗапроса.Добавить(
			ШтрихкодированиеЕГАИС.ТекстЗапросаВложенныхШтрихкодовПоДокументу(ПараметрыФормированияТекстаЗапроса),
			"ВложенныеШтрихкоды");
	КонецЕсли;
#КонецВставки
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ПустыеЗначенияНоменклатуры", ИнтеграцияИС.НезаполненныеЗначенияОпределяемогоТипа("Номенклатура"));
	РезультатыЗапроса = ИнтеграцияИС.ВыполнитьПакетЗапросов(Запрос, ТекстыЗапроса);
	
	//@skip-warning
	Шапка                = РезультатыЗапроса["Шапка"].Выбрать();
	//@skip-warning
	АлкогольнаяПродукция = РезультатыЗапроса["АлкогольнаяПродукция"].Выгрузить();
	//@skip-warning
	Товары               = РезультатыЗапроса["Товары"].Выгрузить();
	
	Если Не Шапка.Следующий()
		Или Товары.Количество() = 0 Тогда
		
		СообщениеXML = ИнтеграцияЕГАИС.СтруктураСообщенияXML();
		СообщениеXML.Документ = ДокументСсылка;
		СообщениеXML.Описание = ИнтеграцияЕГАИС.ОписаниеОперацииПередачиДанных(
			Шапка.Операция, ДокументСсылка);
		
		ИнтеграцияЕГАИСКлиентСервер.ДобавитьТекстОшибки(СообщениеXML, НСтр("ru = 'Нет данных для выгрузки.';
																			|en = 'Нет данных для выгрузки.'"));
		
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
		
	КонецЕсли;
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	ФорматОбмена = ИнтеграцияЕГАИСКлиентСервер.ФорматОбмена(Шапка.ФорматОбмена);
	
	СообщениеXML = ИнтеграцияЕГАИС.СтруктураСообщенияXML();
	СообщениеXML.Описание = ИнтеграцияЕГАИС.ОписаниеОперацииПередачиДанных(
		Шапка.Операция, ДокументСсылка, НомерВерсии);
	
	ПространствоИмен = Перечисления.ВидыДокументовЕГАИС.ПространствоИмен(Шапка.Операция, ФорматОбмена);
	ИмяТипа          = Перечисления.ВидыДокументовЕГАИС.ТипЕГАИС(Шапка.Операция, ФорматОбмена);
	
	Если ПространствоИмен = Неопределено
		Или ИмяТипа = Неопределено Тогда
		ИнтеграцияЕГАИСКлиентСервер.ДобавитьТекстОшибки(
			СообщениеXML,
			СтрШаблон(НСтр("ru = 'Операция не поддерживается в версии формата обмена: %1.';
							|en = 'Операция не поддерживается в версии формата обмена: %1.'"), ФорматОбмена));
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
	КонецЕсли;
	
	Если Шапка.Операция = Перечисления.ВидыДокументовЕГАИС.АктПостановкиНаБалансВРегистр1 Тогда
		
		#Область ПодготовкаДанных
		
		ТоварыИтоги = Новый ТаблицаЗначений;
		ТоварыИтоги.Колонки.Добавить("АлкогольнаяПродукция");
		ТоварыИтоги.Колонки.Добавить("КоличествоПоСправке1");
		ТоварыИтоги.Колонки.Добавить("ДатаРозлива");
		ТоварыИтоги.Колонки.Добавить("НомерТТН");
		ТоварыИтоги.Колонки.Добавить("ДатаТТН");
		ТоварыИтоги.Колонки.Добавить("НомерПодтвержденияЕГАИС");
		ТоварыИтоги.Колонки.Добавить("ДатаПодтвержденияЕГАИС");
		ТоварыИтоги.Колонки.Добавить("Количество");
		
		Для Каждого СтрокаТЧ Из Товары Цикл
			
			Если СтрокаТЧ.ПроверятьОбъемДАЛ
				И Не ЗначениеЗаполнено(СтрокаТЧ.ОбъемДАЛ) Тогда
				ИнтеграцияЕГАИСКлиентСервер.ДобавитьТекстОшибки(СообщениеXML, СтрШаблон(НСтр("ru = 'Для номенклатуры в строке %1 не установлен объем в декалитрах.';
																							|en = 'Для номенклатуры в строке %1 не установлен объем в декалитрах.'"), СтрокаТЧ.НомерСтроки));
			КонецЕсли;
			
			НоваяСтрока = ТоварыИтоги.Добавить();
			НоваяСтрока.АлкогольнаяПродукция    = СтрокаТЧ.АлкогольнаяПродукция;
			НоваяСтрока.КоличествоПоСправке1    = СтрокаТЧ.КоличествоПоСправке1;
			НоваяСтрока.ДатаРозлива             = СтрокаТЧ.ДатаРозлива;
			НоваяСтрока.НомерТТН                = СтрокаТЧ.НомерТТН;
			НоваяСтрока.ДатаТТН                 = СтрокаТЧ.ДатаТТН;
			НоваяСтрока.НомерПодтвержденияЕГАИС = СтрокаТЧ.НомерПодтвержденияЕГАИС;
			НоваяСтрока.ДатаПодтвержденияЕГАИС  = СтрокаТЧ.ДатаПодтвержденияЕГАИС;
			
			НоваяСтрока.Количество = СтрокаТЧ.Количество;
			
		КонецЦикла;
		
		ТоварыИтоги.Свернуть(
			"АлкогольнаяПродукция,
			|КоличествоПоСправке1,
			|ДатаРозлива,
			|НомерТТН,
			|ДатаТТН,
			|НомерПодтвержденияЕГАИС,
			|ДатаПодтвержденияЕГАИС",
			"Количество");
		
		#КонецОбласти
		
		#Область ПодготовкаДанныхАкцизныеМарки
		
		АкцизныеМарки = Новый ТаблицаЗначений;
		
		АкцизныеМарки.Колонки.Добавить("АлкогольнаяПродукция");
		АкцизныеМарки.Колонки.Добавить("КоличествоПоСправке1");
		АкцизныеМарки.Колонки.Добавить("ДатаРозлива");
		АкцизныеМарки.Колонки.Добавить("НомерТТН");
		АкцизныеМарки.Колонки.Добавить("ДатаТТН");
		АкцизныеМарки.Колонки.Добавить("НомерПодтвержденияЕГАИС");
		АкцизныеМарки.Колонки.Добавить("ДатаПодтвержденияЕГАИС");
		АкцизныеМарки.Колонки.Добавить("КодАкцизнойМарки");
		
		//@skip-warning
#Удаление
		Выборка = РезультатыЗапроса["ВложенныеШтрихкоды"].Выбрать();
		ВложенныеШтрихкодыУпаковок = ШтрихкодированиеЕГАИС.ВложенныеШтрихкодыУпаковокПоВыборкеИМенеджеруВТ(
									 Выборка, МенеджерВременныхТаблиц);
#КонецУдаления
#Вставка
		Если бг_ИнтеграцияЕГАИСПовтИсп.ИспользоватьМеханизмДвиженийМарок(ДокументСсылка) Тогда
			// Выполняется подмена данных для формирования XML сообщения:
			//  типовой алгоритм опирался на справочник ШтрихкодыУпаковокТоваров;
			//  свой алгоритм опирается на регистры бг_ДвижениеМарок и бг_СоставУпаковок.
			ВложенныеШтрихкодыУпаковок = бг_ИнтеграцияЕГАИС.ДанныеШтрихкодовДляГенерацииЕГАИСXML(
				ДокументСсылка,
				СообщениеXML);
		Иначе
			// Типовой алгоритм
			Выборка = РезультатыЗапроса["ВложенныеШтрихкоды"].Выбрать();
			ВложенныеШтрихкодыУпаковок = ШтрихкодированиеЕГАИС.ВложенныеШтрихкодыУпаковокПоВыборкеИМенеджеруВТ(
				Выборка,
				МенеджерВременныхТаблиц);
		КонецЕсли;
#КонецВставки
		
		Для Каждого СтрокаТЧ Из ВложенныеШтрихкодыУпаковок.МаркированныеТовары Цикл
			
			Если ЗначениеЗаполнено(СтрокаТЧ.Статус) Тогда
				ИнтеграцияЕГАИСКлиентСервер.ДобавитьТекстОшибки(
					СообщениеXML,
					СтрШаблон(
						НСтр("ru = 'Акцизная марка %1 со статусом %2 не может быть поставлена на баланс в регистр №1';
							|en = 'Акцизная марка %1 со статусом %2 не может быть поставлена на баланс в регистр №1'"),
						СтрокаТЧ.Штрихкод,
						СтрокаТЧ.Статус));
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаТЧ.ИдентификаторСтроки) Тогда
				ИнтеграцияЕГАИСКлиентСервер.ДобавитьТекстОшибки(СообщениеXML, НСтр("ru = 'Не заполнен идентификатор строки акцизной марки.';
																					|en = 'Не заполнен идентификатор строки акцизной марки.'"));
			КонецЕсли;
			
			НайденнаяСтрока = Товары.Найти(СтрокаТЧ.ИдентификаторСтроки, "ИдентификаторСтроки");
			Если НайденнаяСтрока = Неопределено Тогда
				ИнтеграцияЕГАИСКлиентСервер.ДобавитьТекстОшибки(СообщениеXML, НСтр("ru = 'Ошибка при связывании акцизных марок и алкогольной продукции';
																					|en = 'Ошибка при связывании акцизных марок и алкогольной продукции'"));
			КонецЕсли;
			
			НоваяСтрока = АкцизныеМарки.Добавить();
			НоваяСтрока.КодАкцизнойМарки = СтрокаТЧ.Штрихкод;
			
			НоваяСтрока.АлкогольнаяПродукция    = НайденнаяСтрока.АлкогольнаяПродукция;
			НоваяСтрока.КоличествоПоСправке1    = НайденнаяСтрока.КоличествоПоСправке1;
			НоваяСтрока.ДатаРозлива             = НайденнаяСтрока.ДатаРозлива;
			НоваяСтрока.НомерТТН                = НайденнаяСтрока.НомерТТН;
			НоваяСтрока.ДатаТТН                 = НайденнаяСтрока.ДатаТТН;
			НоваяСтрока.НомерПодтвержденияЕГАИС = НайденнаяСтрока.НомерПодтвержденияЕГАИС;
			НоваяСтрока.ДатаПодтвержденияЕГАИС  = НайденнаяСтрока.ДатаПодтвержденияЕГАИС;
			
		КонецЦикла;
		
		#КонецОбласти
		
		#Область АктПостановкиНаБалансВРегистр1
		
		Если ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V1 Тогда
			
			#Область ФорматОбмена_V1
			
			АктXDTO = ИнтеграцияЕГАИС.ОбъектXDTO(ПространствоИмен, "ActChargeOnType");
			
			ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO, "Identity", Шапка.Идентификатор, СообщениеXML, 3);
			
			АктXDTO.Header = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(АктXDTO, "Header");
			
			ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "Number",  СокрЛП(Шапка.Номер), СообщениеXML);
			ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "ActDate", Шапка.Дата, СообщениеXML);
			ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "Note",    Шапка.Комментарий, СообщениеXML);
			
			АктXDTO.Content = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(АктXDTO, "Content");
			
			Для Каждого СтрокаТЧ Из ТоварыИтоги Цикл
				
				НомерСтроки = Формат(ТоварыИтоги.Индекс(СтрокаТЧ) + 1, "ЧГ=0");
				
				Position = ИнтеграцияЕГАИС.ОбъектXDTO(ПространствоИмен, "ActChargeOnPositionType");
				
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Identity", НомерСтроки, СообщениеXML, 5);
				Position.Product = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(Position, "Product");
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Quantity", СтрокаТЧ.Количество, СообщениеXML);
				
				Position.InformAB = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(Position, "InformAB");
				Position.InformAB.InformABReg = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(Position.InformAB, "InformABReg");
				Position.InformAB.InformABReg.InformA = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(Position.InformAB.InformABReg, "InformA");
				
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position.InformAB.InformABReg.InformA, "Quantity",       СтрокаТЧ.КоличествоПоСправке1,    СообщениеXML);
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position.InformAB.InformABReg.InformA, "BottlingDate",   СтрокаТЧ.ДатаРозлива,             СообщениеXML);
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position.InformAB.InformABReg.InformA, "TTNNumber",      СтрокаТЧ.НомерТТН,                СообщениеXML);
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position.InformAB.InformABReg.InformA, "TTNDate",        СтрокаТЧ.ДатаТТН,                 СообщениеXML);
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position.InformAB.InformABReg.InformA, "EGAISFixNumber", СтрокаТЧ.НомерПодтвержденияЕГАИС, СообщениеXML);
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position.InformAB.InformABReg.InformA, "EGAISFixDate",   СтрокаТЧ.ДатаПодтвержденияЕГАИС,  СообщениеXML);
				
				ИнтеграцияЕГАИС.ЗаполнитьВXDTOАлкогольнуюПродукцию_v1(
					Position.Product,
					АлкогольнаяПродукция.Найти(СтрокаТЧ.АлкогольнаяПродукция, "АлкогольнаяПродукция"),
					"Продукция", СообщениеXML);
				
				ПараметрыОтбораАкцизныМарок = Новый Структура;
				ПараметрыОтбораАкцизныМарок.Вставить("АлкогольнаяПродукция",    СтрокаТЧ.АлкогольнаяПродукция);
				ПараметрыОтбораАкцизныМарок.Вставить("КоличествоПоСправке1",    СтрокаТЧ.КоличествоПоСправке1);
				ПараметрыОтбораАкцизныМарок.Вставить("ДатаРозлива",             СтрокаТЧ.ДатаРозлива);
				ПараметрыОтбораАкцизныМарок.Вставить("НомерТТН",                СтрокаТЧ.НомерТТН);
				ПараметрыОтбораАкцизныМарок.Вставить("ДатаТТН",                 СтрокаТЧ.ДатаТТН);
				ПараметрыОтбораАкцизныМарок.Вставить("НомерПодтвержденияЕГАИС", СтрокаТЧ.НомерПодтвержденияЕГАИС);
				ПараметрыОтбораАкцизныМарок.Вставить("ДатаПодтвержденияЕГАИС",  СтрокаТЧ.ДатаПодтвержденияЕГАИС);
				
				ИнтеграцияЕГАИС.ЗаполнитьВXDTOАкцизныеМаркиПоОтбору(Position, ПараметрыОтбораАкцизныМарок, АкцизныеМарки, "MarkCode", СообщениеXML);
				
				АктXDTO.Content.Position.Добавить(Position);
				
			КонецЦикла;
			
			#КонецОбласти
			
		Иначе
			
			#Область ФорматОбмена_V2
			
			АктXDTO = ИнтеграцияЕГАИС.ОбъектXDTO(ПространствоИмен, "ActChargeOnType_v2");
			
			ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO, "Identity", Шапка.Идентификатор, СообщениеXML, 3);
			
			АктXDTO.Header = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(АктXDTO, "Header");
			
			ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "Number",       СокрЛП(Шапка.Номер),                     СообщениеXML);
			ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "ActDate",      Шапка.Дата,                              СообщениеXML);
#Удаление
			ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "TypeChargeOn", Строка(Шапка.ПричинаПостановкиНаБаланс), СообщениеXML);
#КонецУдаления
#Вставка
			ПричинаПостановкиНаБалансСтрокой = бг_ПричинаПостановкиНаБалансСтрокой(Шапка.ПричинаПостановкиНаБаланс);	
			ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "TypeChargeOn", ПричинаПостановкиНаБалансСтрокой, СообщениеXML);
#КонецВставки
			Если Шапка.ПричинаПостановкиНаБаланс = Перечисления.ПричиныПостановкиНаБалансЕГАИС.Пересортица Тогда
				Если ЗначениеЗаполнено(Шапка.ИдентификаторАктаСписания) Тогда
					ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "ActWriteOff", Шапка.ИдентификаторАктаСписания, СообщениеXML);
				Иначе
					ИнтеграцияЕГАИСКлиентСервер.ДобавитьТекстОшибки(СообщениеXML, НСтр("ru = 'Акт списания не проведен в ЕГАИС.';
																						|en = 'Акт списания не проведен в ЕГАИС.'"));
				КонецЕсли;
			КонецЕсли;
			
			ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "Note", Шапка.Комментарий, СообщениеXML);
			
			АктXDTO.Content = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(АктXDTO, "Content");
			
			Для Каждого СтрокаТЧ Из ТоварыИтоги Цикл
				
				НомерСтроки = Формат(ТоварыИтоги.Индекс(СтрокаТЧ) + 1, "ЧГ=0");
				
				Position = ИнтеграцияЕГАИС.ОбъектXDTO(ПространствоИмен, "ActChargeOnPositionType");
				
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Identity", НомерСтроки,         СообщениеXML, 5);
				Position.Product = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(Position, "Product");
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Quantity", СтрокаТЧ.Количество, СообщениеXML);
				
				Position.InformF1F2 = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(Position, "InformF1F2");
				Position.InformF1F2.InformF1F2Reg = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(Position.InformF1F2, "InformF1F2Reg");
				Position.InformF1F2.InformF1F2Reg.InformF1 = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(Position.InformF1F2.InformF1F2Reg, "InformF1");
				
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position.InformF1F2.InformF1F2Reg.InformF1, "Quantity",       СтрокаТЧ.КоличествоПоСправке1,    СообщениеXML);
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position.InformF1F2.InformF1F2Reg.InformF1, "BottlingDate",   СтрокаТЧ.ДатаРозлива,             СообщениеXML);
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position.InformF1F2.InformF1F2Reg.InformF1, "TTNNumber",      СтрокаТЧ.НомерТТН,                СообщениеXML);
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position.InformF1F2.InformF1F2Reg.InformF1, "TTNDate",        СтрокаТЧ.ДатаТТН,                 СообщениеXML);
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position.InformF1F2.InformF1F2Reg.InformF1, "EGAISFixNumber", СтрокаТЧ.НомерПодтвержденияЕГАИС, СообщениеXML);
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position.InformF1F2.InformF1F2Reg.InformF1, "EGAISFixDate",   СтрокаТЧ.ДатаПодтвержденияЕГАИС,  СообщениеXML);
				
				ИнтеграцияЕГАИС.ЗаполнитьВXDTOАлкогольнуюПродукцию_v2(
					Position.Product,
					АлкогольнаяПродукция.Найти(СтрокаТЧ.АлкогольнаяПродукция, "АлкогольнаяПродукция"),
					"Продукция", СообщениеXML);
				
				ПараметрыОтбораАкцизныМарок = Новый Структура;
				ПараметрыОтбораАкцизныМарок.Вставить("АлкогольнаяПродукция",    СтрокаТЧ.АлкогольнаяПродукция);
				ПараметрыОтбораАкцизныМарок.Вставить("КоличествоПоСправке1",    СтрокаТЧ.КоличествоПоСправке1);
				ПараметрыОтбораАкцизныМарок.Вставить("ДатаРозлива",             СтрокаТЧ.ДатаРозлива);
				ПараметрыОтбораАкцизныМарок.Вставить("НомерТТН",                СтрокаТЧ.НомерТТН);
				ПараметрыОтбораАкцизныМарок.Вставить("ДатаТТН",                 СтрокаТЧ.ДатаТТН);
				ПараметрыОтбораАкцизныМарок.Вставить("НомерПодтвержденияЕГАИС", СтрокаТЧ.НомерПодтвержденияЕГАИС);
				ПараметрыОтбораАкцизныМарок.Вставить("ДатаПодтвержденияЕГАИС",  СтрокаТЧ.ДатаПодтвержденияЕГАИС);
				
				ИнтеграцияЕГАИС.ЗаполнитьВXDTOАкцизныеМаркиПоОтбору(Position, ПараметрыОтбораАкцизныМарок, АкцизныеМарки, "MarkCode", СообщениеXML);
				
				АктXDTO.Content.Position.Добавить(Position);
				
			КонецЦикла;
			
			#КонецОбласти
			
		КонецЕсли;
		
		#КонецОбласти
	
	ИначеЕсли Шапка.Операция = Перечисления.ВидыДокументовЕГАИС.АктПостановкиНаБалансВРегистр2 Тогда
		
		#Область ПодготовкаДанных
		
		ТоварыИтоги = Новый ТаблицаЗначений;
		ТоварыИтоги.Колонки.Добавить("АлкогольнаяПродукция");
		ТоварыИтоги.Колонки.Добавить("Количество");
		
		Для Каждого СтрокаТЧ Из Товары Цикл
			
			Если СтрокаТЧ.ПроверятьОбъемДАЛ
				И Не ЗначениеЗаполнено(СтрокаТЧ.ОбъемДАЛ) Тогда
				ИнтеграцияЕГАИСКлиентСервер.ДобавитьТекстОшибки(СообщениеXML, СтрШаблон(НСтр("ru = 'Для номенклатуры в строке %1 не установлен объем в декалитрах.';
																							|en = 'Для номенклатуры в строке %1 не установлен объем в декалитрах.'"), СтрокаТЧ.НомерСтроки));
			КонецЕсли;
			
			НоваяСтрока = ТоварыИтоги.Добавить();
			НоваяСтрока.АлкогольнаяПродукция = СтрокаТЧ.АлкогольнаяПродукция;
			НоваяСтрока.Количество           = СтрокаТЧ.Количество;
			
		КонецЦикла;
		
		ТоварыИтоги.Свернуть(
			"АлкогольнаяПродукция",
			"Количество");
		
		#КонецОбласти
		
		#Область АктПостановкиНаБалансВРегистр2
		
		АктXDTO = ИнтеграцияЕГАИС.ОбъектXDTO(ПространствоИмен, "ActChargeOnShopType_v2");
		
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO, "Identity", Шапка.Идентификатор, СообщениеXML, 3);
		
		АктXDTO.Header = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(АктXDTO, "Header");
		
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "Number",       СокрЛП(Шапка.Номер),                     СообщениеXML);
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "ActDate",      Шапка.Дата,                              СообщениеXML);
#Удаление
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "TypeChargeOn", Строка(Шапка.ПричинаПостановкиНаБаланс), СообщениеXML);
#КонецУдаления
#Вставка
		ПричинаПостановкиНаБалансСтрокой = бг_ПричинаПостановкиНаБалансСтрокой(Шапка.ПричинаПостановкиНаБаланс);	
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "TypeChargeOn", ПричинаПостановкиНаБалансСтрокой, СообщениеXML);
#КонецВставки
		
		Если Шапка.ПричинаПостановкиНаБаланс = Перечисления.ПричиныПостановкиНаБалансЕГАИС.Пересортица Тогда
			Если ЗначениеЗаполнено(Шапка.ИдентификаторАктаСписания) Тогда
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "ActWriteOff", Шапка.ИдентификаторАктаСписания, СообщениеXML);
			Иначе
				ИнтеграцияЕГАИСКлиентСервер.ДобавитьТекстОшибки(СообщениеXML, НСтр("ru = 'Акт списания не проведен в ЕГАИС.';
																					|en = 'Акт списания не проведен в ЕГАИС.'"));
			КонецЕсли;
		КонецЕсли;
		
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "Note", Шапка.Комментарий, СообщениеXML);
		
		АктXDTO.Content = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(АктXDTO, "Content");
		
		Для Каждого СтрокаТЧ Из ТоварыИтоги Цикл
			
			НомерСтроки = Формат(ТоварыИтоги.Индекс(СтрокаТЧ) + 1, "ЧГ=0");
			
			Position = ИнтеграцияЕГАИС.ОбъектXDTO(ПространствоИмен, "ActChargeOnShopPositionType");
			
			ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Identity", НомерСтроки, СообщениеXML, 5);
			Position.Product = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(Position, "Product");
			ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Quantity", СтрокаТЧ.Количество, СообщениеXML);
			
			ИнтеграцияЕГАИС.ЗаполнитьВXDTOАлкогольнуюПродукцию_v2(
				Position.Product,
				АлкогольнаяПродукция.Найти(СтрокаТЧ.АлкогольнаяПродукция, "АлкогольнаяПродукция"),
				"Продукция", СообщениеXML);
			
			АктXDTO.Content.Position.Добавить(Position);
			
		КонецЦикла;
		
		#КонецОбласти
	
	ИначеЕсли Шапка.Операция = Перечисления.ВидыДокументовЕГАИС.АктПостановкиНаБалансВРегистр3 Тогда
		
		#Область ПодготовкаДанных
		
		ТоварыИтоги = Новый ТаблицаЗначений;
		ТоварыИтоги.Колонки.Добавить("АлкогольнаяПродукция");
		ТоварыИтоги.Колонки.Добавить("Справка2");
		ТоварыИтоги.Колонки.Добавить("НомерСправки2");
		ТоварыИтоги.Колонки.Добавить("Количество");
		
		Для Каждого СтрокаТЧ Из Товары Цикл
			
			Если СтрокаТЧ.ПроверятьОбъемДАЛ
				И Не ЗначениеЗаполнено(СтрокаТЧ.ОбъемДАЛ) Тогда
				ИнтеграцияЕГАИСКлиентСервер.ДобавитьТекстОшибки(СообщениеXML, СтрШаблон(НСтр("ru = 'Для номенклатуры в строке %1 не установлен объем в декалитрах.';
																							|en = 'Для номенклатуры в строке %1 не установлен объем в декалитрах.'"), СтрокаТЧ.НомерСтроки));
			КонецЕсли;
			
			НоваяСтрока = ТоварыИтоги.Добавить();
			НоваяСтрока.АлкогольнаяПродукция = СтрокаТЧ.АлкогольнаяПродукция;
			НоваяСтрока.Справка2             = СтрокаТЧ.Справка2;
			НоваяСтрока.НомерСправки2        = СтрокаТЧ.НомерСправки2;
			НоваяСтрока.Количество           = СтрокаТЧ.Количество;
			
		КонецЦикла;
		
		ТоварыИтоги.Свернуть("АлкогольнаяПродукция, Справка2, НомерСправки2", "Количество");
		
		АкцизныеМарки = Новый ТаблицаЗначений;
		АкцизныеМарки.Колонки.Добавить("Справка2");
		АкцизныеМарки.Колонки.Добавить("КодАкцизнойМарки");
		
		СтатусыДоступныхАкцизныхМарок = СтатусыДоступныхАкцизныхМарок();
		
		//@skip-warning
		ВложенныеШтрихкодыУпаковок = ШтрихкодированиеЕГАИС.ВложенныеШтрихкодыУпаковокПоВыборкеИМенеджеруВТ(
			РезультатыЗапроса["ВложенныеШтрихкоды"].Выбрать(), МенеджерВременныхТаблиц);
		
		Для Каждого СтрокаТЧ Из ВложенныеШтрихкодыУпаковок.МаркированныеТовары Цикл
			
			Справка2 = Неопределено;
			
			Если СтатусыДоступныхАкцизныхМарок.Найти(СтрокаТЧ.Статус) = Неопределено Тогда
				ИнтеграцияЕГАИСКлиентСервер.ДобавитьТекстОшибки(
					СообщениеXML,
					СтрШаблон(
						НСтр("ru = 'Акцизная марка %1 со статусом %2 не может быть поставлена на баланс в регистр №3';
							|en = 'Акцизная марка %1 со статусом %2 не может быть поставлена на баланс в регистр №3'"),
						СтрокаТЧ.Штрихкод,
						СтрокаТЧ.Статус));
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаТЧ.Справка2) Тогда
				Справка2 = СтрокаТЧ.Справка2;
			Иначе
				НайденнаяСтрока = Товары.Найти(СтрокаТЧ.ИдентификаторСтроки, "ИдентификаторСтроки");
				Если НайденнаяСтрока = Неопределено Тогда
					ИнтеграцияЕГАИСКлиентСервер.ДобавитьТекстОшибки(
						СообщениеXML,
						СтрШаблон(
							НСтр("ru = 'Не удалось определить справку 2 для акцизной марки %1';
								|en = 'Не удалось определить справку 2 для акцизной марки %1'"),
							СтрокаТЧ.Штрихкод));
				Иначе
					Справка2 = НайденнаяСтрока.Справка2;
				КонецЕсли;
			КонецЕсли;
			
			Если Справка2 = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = АкцизныеМарки.Добавить();
			НоваяСтрока.Справка2         = Справка2;
			НоваяСтрока.КодАкцизнойМарки = СтрокаТЧ.Штрихкод;
			
		КонецЦикла;
		
		#КонецОбласти
		
		#Область АктПостановкиНаБалансВРегистр3
		
		АктXDTO = ИнтеграцияЕГАИС.ОбъектXDTO(ПространствоИмен, "ActFixBarCode");
		
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO, "Identity", Шапка.Идентификатор, СообщениеXML, 3);
		
		АктXDTO.Header = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(АктXDTO, "Header");
		
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "Number",  СокрЛП(Шапка.Номер),    СообщениеXML);
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "ActDate", Шапка.Дата,             СообщениеXML);
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(АктXDTO.Header, "Note",    Шапка.Комментарий,      СообщениеXML);
		
		АктXDTO.Content = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(АктXDTO, "Content");
		
		Для Каждого СтрокаТЧ Из ТоварыИтоги Цикл
			
			НомерСтроки = Формат(ТоварыИтоги.Индекс(СтрокаТЧ) + 1, "ЧГ=0");
			
			Position = ИнтеграцияЕГАИС.ОбъектXDTO(ПространствоИмен, "ActFixBarCodePositionType");
			
			ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Identity",     НомерСтроки,            СообщениеXML, 5);
			ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Inform2RegId", СтрокаТЧ.НомерСправки2, СообщениеXML);
			
			ИнтеграцияЕГАИС.ЗаполнитьВXDTOАкцизныеМаркиПоСправке2_v3(Position, СтрокаТЧ, АкцизныеМарки, СообщениеXML);
			
			АктXDTO.Content.Position.Добавить(Position);
			
		КонецЦикла;
		
		#КонецОбласти
	
	КонецЕсли;
	
	ТекстСообщенияXML = ИнтеграцияЕГАИС.ОбъектXDTOВXML(АктXDTO, Шапка.ИдентификаторФСРАР, ПространствоИмен, ИмяТипа);
	
	СообщениеXML.ТекстСообщенияXML = ТекстСообщенияXML;
	СообщениеXML.ТипСообщения      = Перечисления.ТипыЗапросовИС.Исходящий;
	СообщениеXML.ОрганизацияЕГАИС  = Шапка.ОрганизацияЕГАИС;
	СообщениеXML.Операция          = Шапка.Операция;
	СообщениеXML.ФорматОбмена      = ФорматОбмена;
	СообщениеXML.Документ          = ДокументСсылка;
	СообщениеXML.ДокументОснование = Шапка.ДокументОснование;
	СообщениеXML.Версия            = НомерВерсии;
	
	СообщенияXML.Добавить(СообщениеXML);
	
	Возврат СообщенияXML;
	
КонецФункции

#КонецОбласти

#Область Отчеты

&После("ДобавитьКомандыОтчетов")
Процедура бг_ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры)
	
	Отчеты.бг_ДвижениеМарок.ДобавитьКомандуДвижениеМарокПоДокументу(КомандыОтчетов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДанныеШтрихкодовДвиженияМарок

Процедура бг_ПриИзмененииСтатусаДокументаДвижениеМарок(АктПостановкиНаБалансЕГАИС, НовыйСтатус)
	
	ПрочееОприходованиеТоваров = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		АктПостановкиНаБалансЕГАИС,
		"ДокументОснование");
	
	Если ТипЗнч(ПрочееОприходованиеТоваров) <> Тип("ДокументСсылка.ПрочееОприходованиеТоваров") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не бг_ИнтеграцияЕГАИСПовтИсп.ИспользоватьМеханизмДвиженийМарок(АктПостановкиНаБалансЕГАИС) Тогда
		Возврат;
	КонецЕсли;
	
	Если бг_СтатусЯвляетсяПодтвержденным(НовыйСтатус) Тогда
		
		ДанныеПриходуемыхШтрихкодов = бг_ДанныеПриходуемыхШтрихкодов(
			АктПостановкиНаБалансЕГАИС,
			Ложь);
			
		Если ДанныеПриходуемыхШтрихкодов = Неопределено Тогда
			ВызватьИсключение СтрШаблон(
				НСтр("ru='Ошибка обновления статуса документа %1: не найдены приходуемые штрихкоды'"),
				АктПостановкиНаБалансЕГАИС);
		КонецЕсли;
		
		МаркиСоответствуютАлкогольнойПродукцииАкта = бг_МаркиСоответствуютАлкогольнойПродукцииАкта(
			ДанныеПриходуемыхШтрихкодов.Марки,
			АктПостановкиНаБалансЕГАИС);
			
		Если Не МаркиСоответствуютАлкогольнойПродукцииАкта Тогда
			ВызватьИсключение СтрШаблон(
				НСтр("ru='Ошибка обновления статуса документа %1: приходуемые штрихкоды не соответствуют алкогольной продукции'"),
				АктПостановкиНаБалансЕГАИС);
		КонецЕсли;
		
		// Записать движения марок.
		СтатусыМарокПоОперации = Перечисления.бг_СтатусыАкцизныхМарок.СтатусыПоОперации(
			Метаданные.Документы.АктПостановкиНаБалансЕГАИС.Имя);
			
		СтатусМаркиНаБалансе = СтатусыМарокПоОперации.СтатусНаБалансе;
		
		НаборЗаписейДвижениеМарок = РегистрыСведений.бг_ДвижениеМарок.СоздатьНаборЗаписей();
		НаборЗаписейДвижениеМарок.Отбор.Регистратор.Установить(АктПостановкиНаБалансЕГАИС);
		
		ДатаДвиженияМарок = бг_ДатаДвиженияМарок(АктПостановкиНаБалансЕГАИС);
		
		Для каждого СтрокаМарки Из ДанныеПриходуемыхШтрихкодов.Марки Цикл
			Запись = НаборЗаписейДвижениеМарок.Добавить();
			Запись.Период = ДатаДвиженияМарок;
			ЗаполнитьЗначенияСвойств(Запись, СтрокаМарки);
			Запись.СтатусМарки = СтатусМаркиНаБалансе;
		КонецЦикла;
		
		НаборЗаписейДвижениеМарок.Записать();
		
	Иначе
		
		НаборЗаписейМарки = РегистрыСведений.бг_ДвижениеМарок.СоздатьНаборЗаписей();
		НаборЗаписейМарки.Отбор.Регистратор.Установить(АктПостановкиНаБалансЕГАИС);
		НаборЗаписейМарки.Прочитать();
		
		Если НаборЗаписейМарки.Количество() > 0 Тогда
			НаборЗаписейМарки.Очистить();
			НаборЗаписейМарки.Записать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция бг_ТекстЗапросаДанныеПриходуемыхШтрихкодов()
	
	// Предполагается, что в акте постановки на баланс ЕГАИС ТЧ Товары всегда свернута,
	// и по одной Номенклатуре + Серии + АП + Справке2 не может быть нескольких строк с разными идентификаторами.
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АктПостановкиНаБалансЕГАИСТовары.НомерСтроки КАК НомерСтрокиАкта,
	|	АктПостановкиНаБалансЕГАИСТовары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	АктПостановкиНаБалансЕГАИСТовары.Справка2 КАК Справка2,
	|	АктПостановкиНаБалансЕГАИСТовары.Номенклатура КАК Номенклатура,
	|	АктПостановкиНаБалансЕГАИСТовары.Серия КАК Серия,
	|	АктПостановкиНаБалансЕГАИСТовары.Ссылка.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	СУММА(АктПостановкиНаБалансЕГАИСТовары.Количество) КАК Количество,
	|	АктПостановкиНаБалансЕГАИСТовары.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ТоварыАкта
	|ИЗ
	|	Документ.АктПостановкиНаБалансЕГАИС.Товары КАК АктПостановкиНаБалансЕГАИСТовары
	|ГДЕ
	|	АктПостановкиНаБалансЕГАИСТовары.Ссылка = &АктПостановкиНаБалансЕГАИС
	|
	|СГРУППИРОВАТЬ ПО
	|	АктПостановкиНаБалансЕГАИСТовары.АлкогольнаяПродукция,
	|	АктПостановкиНаБалансЕГАИСТовары.Справка2,
	|	АктПостановкиНаБалансЕГАИСТовары.Ссылка.ОрганизацияЕГАИС,
	|	АктПостановкиНаБалансЕГАИСТовары.Номенклатура,
	|	АктПостановкиНаБалансЕГАИСТовары.Серия,
	|	АктПостановкиНаБалансЕГАИСТовары.НомерСтроки,
	|	АктПостановкиНаБалансЕГАИСТовары.ИдентификаторСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	бг_ДвижениеМарок.ГУИДМарки КАК ГУИДМарки,
	|	бг_ДвижениеМарок.Серия.бг_Номенклатура КАК Номенклатура,
	|	бг_ДвижениеМарок.Серия КАК Серия,
	|	бг_ДвижениеМарок.КодУпаковки КАК КодУпаковки
	|ПОМЕСТИТЬ ДвижениеМарок
	|ИЗ
	|	РегистрСведений.бг_ДвижениеМарок КАК бг_ДвижениеМарок
	|ГДЕ
	|	бг_ДвижениеМарок.Активность
	|	И бг_ДвижениеМарок.Регистратор В(&РегистраторыДвиженияМарок)
	|	И бг_ДвижениеМарок.СтатусМарки = ЗНАЧЕНИЕ(Перечисление.бг_СтатусыАкцизныхМарок.ВключенаВАктПостановкиНаБаланс)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ГУИДМарки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвижениеМарок.ГУИДМарки КАК ГУИДМарки,
	|	бг_ИдентификаторыМарок.ИдентификаторМарки КАК ИдентификаторМарки,
	|	ДвижениеМарок.Номенклатура КАК Номенклатура,
	|	ДвижениеМарок.Серия КАК Серия,
	|	ДвижениеМарок.КодУпаковки КАК КодУпаковки
	|ПОМЕСТИТЬ Марки
	|ИЗ
	|	ДвижениеМарок КАК ДвижениеМарок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.бг_ИдентификаторыМарок КАК бг_ИдентификаторыМарок
	|		ПО ДвижениеМарок.ГУИДМарки = бг_ИдентификаторыМарок.ГУИДМарки
	|ГДЕ
	|	ДвижениеМарок.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И ДвижениеМарок.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Марки.Номенклатура КАК Номенклатура,
	|	Марки.Серия КАК Серия,
	|	СУММА(1) КАК Количество
	|ПОМЕСТИТЬ МаркиИтоги
	|ИЗ
	|	Марки КАК Марки
	|
	|СГРУППИРОВАТЬ ПО
	|	Марки.Серия,
	|	Марки.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Марки.ГУИДМарки КАК ГУИДМарки,
	|	Марки.ИдентификаторМарки КАК ИдентификаторМарки,
	|	Марки.Номенклатура КАК Номенклатура,
	|	Марки.Серия КАК Серия,
	|	Марки.КодУпаковки КАК КодУпаковки,
	|	МаркиИтоги.Количество КАК КоличествоПоТовару
	|ПОМЕСТИТЬ МаркиСИтогами
	|ИЗ
	|	Марки КАК Марки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаркиИтоги КАК МаркиИтоги
	|		ПО Марки.Номенклатура = МаркиИтоги.Номенклатура
	|			И Марки.Серия = МаркиИтоги.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	бг_СоставУпаковок.КодУпаковки КАК КодУпаковки,
	|	МАКСИМУМ(бг_СоставУпаковок.Период) КАК Период
	|ПОМЕСТИТЬ СоставУпаковокМаксимальныйПериод
	|ИЗ
	|	Марки КАК Марки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.бг_СоставУпаковок КАК бг_СоставУпаковок
	|		ПО Марки.КодУпаковки = бг_СоставУпаковок.КодУпаковки
	|ГДЕ
	|	бг_СоставУпаковок.Период <= &Период
	|
	|СГРУППИРОВАТЬ ПО
	|	бг_СоставУпаковок.КодУпаковки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КодУпаковки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТоварыАкта.ОрганизацияЕГАИС, ЗНАЧЕНИЕ(Справочник.КлассификаторОрганизацийЕГАИС.ПустаяСсылка)) КАК ОрганизацияЕГАИС,
	|	ТоварыАкта.НомерСтрокиАкта КАК НомерСтрокиАкта,
	|	ЕСТЬNULL(МаркиСИтогами.Номенклатура, ТоварыАкта.Номенклатура) КАК Номенклатура,
	|	ЕСТЬNULL(МаркиСИтогами.Серия, ТоварыАкта.Серия) КАК Серия,
	|	МаркиСИтогами.ГУИДМарки КАК ГУИДМарки,
	|	ЕСТЬNULL(МаркиСИтогами.ИдентификаторМарки, """") КАК ИдентификаторМарки,
	|	ЕСТЬNULL(МаркиСИтогами.КодУпаковки, """") КАК КодУпаковки,
	|	ЕСТЬNULL(ТоварыАкта.АлкогольнаяПродукция, ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)) КАК АлкогольнаяПродукция,
	|	ЕСТЬNULL(ТоварыАкта.Справка2, ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка)) КАК Справка2,
	|	ТоварыАкта.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ЕСТЬNULL(МаркиСИтогами.КоличествоПоТовару, 0) КАК КоличествоМарок,
	|	ЕСТЬNULL(ТоварыАкта.Количество, 0) КАК КоличествоАкта,
	|	ЕСТЬNULL(ТоварыАкта.Количество, 0) <> ЕСТЬNULL(МаркиСИтогами.КоличествоПоТовару, 0) КАК НесоответствиеКоличества,
	|	ТоварыАкта.АлкогольнаяПродукция = ЗНАЧЕНИЕ(Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.ПустаяСсылка)
	|		ИЛИ ТоварыАкта.АлкогольнаяПродукция ЕСТЬ NULL
	|		ИЛИ ТоварыАкта.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|		ИЛИ ТоварыАкта.Номенклатура ЕСТЬ NULL
	|		ИЛИ ТоварыАкта.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		ИЛИ ТоварыАкта.Серия ЕСТЬ NULL
	|		ИЛИ ТоварыАкта.ИдентификаторСтроки ЕСТЬ NULL КАК НеЗаполненыПоляТоваровДокумента,
	|	МаркиСИтогами.ГУИДМарки ЕСТЬ NULL
	|		ИЛИ МаркиСИтогами.ИдентификаторМарки ЕСТЬ NULL
	|		ИЛИ МаркиСИтогами.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|		ИЛИ МаркиСИтогами.Номенклатура ЕСТЬ NULL
	|		ИЛИ МаркиСИтогами.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		ИЛИ МаркиСИтогами.Серия ЕСТЬ NULL КАК НеЗаполненыПоляМарок,
	|	МаркиСИтогами.ГУИДМарки ЕСТЬ NULL КАК НетСоответствияТовараАктаДляМарки
	|ИЗ
	|	ТоварыАкта КАК ТоварыАкта
	|		ПОЛНОЕ СОЕДИНЕНИЕ МаркиСИтогами КАК МаркиСИтогами
	|		ПО ТоварыАкта.Номенклатура = МаркиСИтогами.Номенклатура
	|			И ТоварыАкта.Серия = МаркиСИтогами.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	бг_СоставУпаковок.КодУпаковки КАК КодУпаковки,
	|	бг_СоставУпаковок.КодВышестоящейУпаковки КАК КодВышестоящейУпаковки
	|ИЗ
	|	СоставУпаковокМаксимальныйПериод КАК СоставУпаковокМаксимальныйПериод
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.бг_СоставУпаковок КАК бг_СоставУпаковок
	|		ПО СоставУпаковокМаксимальныйПериод.КодУпаковки = бг_СоставУпаковок.КодУпаковки
	|			И СоставУпаковокМаксимальныйПериод.Период = бг_СоставУпаковок.Период
	|ГДЕ
	|	бг_СоставУпаковок.КодВышестоящейУпаковки <> """"";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти // Конец ДанныеШтрихкодовДвиженияМарок

#Область АктуализацияСерий

Функция бг_СерииСправкиКАктуализации(АктПостановкиНаБалансЕГАИС)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	АктПостановкиНаБалансЕГАИСТовары.Серия КАК Серия,
	|	АктПостановкиНаБалансЕГАИСТовары.Справка2 КАК Справка2,
	|	АктПостановкиНаБалансЕГАИСТовары.АлкогольнаяПродукция КАК АлкогольнаяПродукция
	|ПОМЕСТИТЬ СерииДокумента
	|ИЗ
	|	Документ.АктПостановкиНаБалансЕГАИС.Товары КАК АктПостановкиНаБалансЕГАИСТовары
	|ГДЕ
	|	АктПостановкиНаБалансЕГАИСТовары.Ссылка = &АктПостановкиНаБалансЕГАИС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Серия,
	|	АлкогольнаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СерииДокумента.Серия КАК Серия,
	|	СправочникСерииНоменклатуры.бг_НомерГТД КАК СерияНомерГТД,
	|	СправочникСерииНоменклатуры.Справка2ЕГАИС КАК СерияСправка2,
	|	СправочникСерииНоменклатуры.ПроизводительЕГАИС КАК СерияПроизводитель,
	|	СерииДокумента.Справка2 КАК Справка2,
	|	КлассификаторАлкогольнойПродукцииЕГАИС.Крепость КАК АлкогольнаяПродукцияКрепость,
	|	КлассификаторАлкогольнойПродукцииЕГАИС.Производитель КАК АлкогольнаяПродукцияПроизводитель,
	|	Справки1ЕГАИС.Ссылка КАК Справка1,
	|	Справки1ЕГАИС.бг_Крепость КАК Справка1Крепость,
	|	Справки1ЕГАИС.бг_НомерГТД КАК Справка1НомерГТД
	|ИЗ
	|	Справочник.СерииНоменклатуры КАК СправочникСерииНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СерииДокумента КАК СерииДокумента
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
	|			ПО (КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка = СерииДокумента.АлкогольнаяПродукция)
	|		ПО (СерииДокумента.Серия = СправочникСерииНоменклатуры.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Справки2ЕГАИС КАК Справки2ЕГАИС
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Справки1ЕГАИС КАК Справки1ЕГАИС
	|			ПО Справки2ЕГАИС.Справка1 = Справки1ЕГАИС.Ссылка
	|		ПО (СерииДокумента.Справка2 = Справки2ЕГАИС.Ссылка)
	|ГДЕ
	|	(КлассификаторАлкогольнойПродукцииЕГАИС.Производитель <> СправочникСерииНоменклатуры.ПроизводительЕГАИС
	|			ИЛИ СерииДокумента.Справка2 <> СправочникСерииНоменклатуры.Справка2ЕГАИС
	|			ИЛИ КлассификаторАлкогольнойПродукцииЕГАИС.Крепость <> ЕСТЬNULL(Справки1ЕГАИС.бг_Крепость, 0)
	|			ИЛИ СправочникСерииНоменклатуры.бг_НомерГТД <> ЕСТЬNULL(Справки1ЕГАИС.бг_НомерГТД, ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)))";
	
	Запрос.УстановитьПараметр("АктПостановкиНаБалансЕГАИС", АктПостановкиНаБалансЕГАИС);
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура бг_АктуализироватьСериюСправку(Данные)
	
	НеобходимоИзменитьСправку1 = ЗначениеЗаполнено(Данные.Справка1)
		И (Данные.Справка1Крепость <> Данные.АлкогольнаяПродукцияКрепость
			Или Данные.Справка1НомерГТД <> Данные.СерияНомерГТД);
	
	Если НеобходимоИзменитьСправку1 Тогда
		Справка1Объект = Данные.Справка1.ПолучитьОбъект();
		Справка1Объект.бг_Крепость = Данные.АлкогольнаяПродукцияКрепость;
		Справка1Объект.бг_НомерГТД = Данные.СерияНомерГТД;
		Справка1Объект.Записать();
	КонецЕсли;
	
	Если Данные.СерияСправка2 <> Данные.Справка2
		Или Данные.СерияПроизводитель <> Данные.АлкогольнаяПродукцияПроизводитель
		Или НеобходимоИзменитьСправку1 Тогда
		
		// Изменение справки1 может повлиять на наименование серии, указанное в шаблоне, поэтому серию нужно перезаписать,
		// если были изменена справка 1.
		СерияОбъект = Данные.Серия.ПолучитьОбъект();
		СерияОбъект.ПроизводительЕГАИС = Данные.АлкогольнаяПродукцияПроизводитель;
		СерияОбъект.Справка2ЕГАИС = Данные.Справка2;
		СерияОбъект.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // Конец АктуализацияСерий

Функция бг_ПричинаПостановкиНаБалансСтрокой(ПричинаПостановки)
	
	ПричинаПостановкиСтрокой = Строка(ПричинаПостановки);
	Если ПричинаПостановки = Перечисления.ПричиныПостановкиНаБалансЕГАИС.Производство_Сливы Тогда
		ПричинаПостановкиСтрокой = "Производство_Сливы";
	КонецЕсли;
	
	Возврат ПричинаПостановкиСтрокой;  
	
КонецФункции

#КонецОбласти

#КонецЕсли
