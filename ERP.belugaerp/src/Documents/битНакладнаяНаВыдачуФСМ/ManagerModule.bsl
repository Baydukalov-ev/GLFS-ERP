#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Функция ПодчиненныеДокументы(НакладнаяНаВыдачуФСМ) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПриобретениеТоваровУслуг.Ссылка КАК ПриобретениеТоваровУслуг,
	|	ПриобретениеТоваровУслуг.Проведен КАК Проведен,
	|	ПриобретениеТоваровУслуг.ПометкаУдаления КАК ПометкаУдаления
	|ПОМЕСТИТЬ ВТ_ПТУ
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
	|ГДЕ
	|	ПриобретениеТоваровУслуг.бг_НакладнаяНаВыдачуФСМ = &НакладнаяНаВыдачуФСМ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	СчетФактураПолученныйДокументыОснования.Ссылка КАК Документ,
	|	СчетФактураПолученныйДокументыОснования.Ссылка.Проведен КАК Проведен,
	|	СчетФактураПолученныйДокументыОснования.Ссылка.ПометкаУдаления КАК ПометкаУдаления,
	|	""СчетФактураПолученный"" КАК ИмяДокумента
	|ИЗ
	|	Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученныйДокументыОснования
	|ГДЕ
	|	СчетФактураПолученныйДокументыОснования.ДокументОснование В
	|			(ВЫБРАТЬ
	|				ВТ_ПТУ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
	|			ИЗ
	|				ВТ_ПТУ КАК ВТ_ПТУ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВТ_ПТУ.ПриобретениеТоваровУслуг,
	|	ВТ_ПТУ.Проведен,
	|	ВТ_ПТУ.ПометкаУдаления,
	|	""ПриобретениеТоваровУслуг""
	|ИЗ
	|	ВТ_ПТУ КАК ВТ_ПТУ";	
		
	Запрос.УстановитьПараметр("НакладнаяНаВыдачуФСМ", НакладнаяНаВыдачуФСМ);
	
	РезультатЗапроса = Запрос.Выполнить();
	ПодчиненныеДокументы = РезультатЗапроса.Выгрузить();
	
	Возврат ПодчиненныеДокументы;

КонецФункции

&Вместо("ОбработкаПолученияПредставления")
Процедура бг_ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	ДополнительныеРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Данные.Ссылка, "ДатаВходящегоДокумента,НомерВходящегоДокумента");
	
	Если ЗначениеЗаполнено(ДополнительныеРеквизиты.ДатаВходящегоДокумента) 
		И ЗначениеЗаполнено(ДополнительныеРеквизиты.НомерВходящегоДокумента) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Представление = "Накладная на выдачу ФСМ №%1 от %2";
		Представление = СтрШаблон(Представление, ДополнительныеРеквизиты.НомерВходящегоДокумента,
			Формат(ДополнительныеРеквизиты.ДатаВходящегоДокумента, "ДФ=dd.MM.yyyy"));
			
	КонецЕсли;
	
КонецПроцедуры

#Область ПанельОбменСЕГАИС

// Возвращает массив дальнейших действий с документом, требующих участия пользователя
// 
// Возвращаемое значение:
// 	Массив из ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - дальшейшие действия
//
Функция ВсеТребующиеДействия() Экспорт
	
	Возврат Новый Массив;
	
КонецФункции

// Возвращает массив дальнейших действий с документом, не требующих участия пользователя
// 
// Возвращаемое значение:
// 	Массив из ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - дальшейшие действия
//
Функция ВсеТребующиеОжидания() Экспорт
	
	Возврат Новый Массив;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Партнер)
	|	И ЗначениеРазрешено(Склад)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли
