#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Печать

&После("ДобавитьКомандыПечати")
Процедура бг_ДобавитьКомандыПечати(КомандыПечати)
	
	// ТОРГ-2
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьАктыОРасхождениях";
	КомандаПечати.Идентификатор = "ТОРГ2";
	КомандаПечати.Представление = НСтр("ru = 'ТОРГ-2';
										|en = 'TORG-2'");
	КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВНоменклатуреПоставщика", Ложь);
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// ТОРГ-3
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьАктыОРасхождениях";
	КомандаПечати.Идентификатор = "ТОРГ3";
	КомандаПечати.Представление = НСтр("ru = 'ТОРГ-3';
										|en = 'TORG-3'");
	КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВНоменклатуреПоставщика", Ложь);
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры	

// Возвращает данные, необходимые для печатной формы "ТОРГ-2".
// 
// Параметры:
// 	ПараметрыПечати - Структура - дополнительные настройки печати
// 	МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать
// 	
// Возвращаемое значение:
// 	Структура - Содержит в себе данные документа:
// 	* ДанныеСчетовФактур          - РезультатЗапроса - 
// 	* ДанныеПечати                - РезультатЗапроса - 
// 	* ДанныеТовары                - РезультатЗапроса - 
// 	* ДанныеТоваровПоДокументам   - РезультатЗапроса - 
// 	* ДанныеКурсовВалют           - РезультатЗапроса - 
//
Функция бг_ПолучитьДанныеДляПечатнойФормыТОРГ2(ПараметрыПечати, МассивОбъектов) Экспорт
	
	Запрос = Новый Запрос;
	
	МассивЗапросов = Новый Массив;
	МассивЗапросов.Добавить(бг_ТекстЗапросаОбщийТорг());
	МассивЗапросов.Добавить(бг_ТекстЗапросаДополнениеТорг2());
	
	Разделитель = Символы.ПС + "///////////////////////////////////////" + Символы.ПС;	
	
	Запрос.Текст = СтрСоединить(МассивЗапросов, Разделитель); 
		
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("БазоваяВалюта", ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию());
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	
	ДанныеПечати = Новый Структура;
	ДанныеПечати.Вставить("ДанныеСчетовФактур",         ПакетРезультатов[2]);
	ДанныеПечати.Вставить("ДанныеПечати",               ПакетРезультатов[6]);
	ДанныеПечати.Вставить("ДанныеТовары",               ПакетРезультатов[7]);
	ДанныеПечати.Вставить("ДанныеТоваровПоДокументам",  ПакетРезультатов[8]);
	ДанныеПечати.Вставить("ДанныеКурсовВалют",          ПакетРезультатов[9]);
	
	Возврат ДанныеПечати;
	
КонецФункции

// Возвращает данные, необходимые для печатной формы "ТОРГ-3".
// 
// Параметры:
// 	ПараметрыПечати - Структура - дополнительные настройки печати
// 	МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать
// 	
// Возвращаемое значение:
// 	Структура - Содержит в себе данные документа:
// 	* ДанныеСчетовФактур          - РезультатЗапроса - 
// 	* ДанныеПечати                - РезультатЗапроса - 
// 	* ДанныеТовары                - РезультатЗапроса - 
//
Функция бг_ПолучитьДанныеДляПечатнойФормыТОРГ3(ПараметрыПечати, МассивОбъектов) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = бг_ТекстЗапросаОбщийТорг();
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	
	ДанныеПечати = Новый Структура;
	ДанныеПечати.Вставить("ДанныеСчетовФактур",   ПакетРезультатов[2]);
	ДанныеПечати.Вставить("ДанныеПечати",         ПакетРезультатов[6]);
	ДанныеПечати.Вставить("ДанныеТовары",         ПакетРезультатов[7]);
	
	Возврат ДанныеПечати;
	
КонецФункции

#КонецОбласти

#Область ТекстыЗапросов

Функция бг_ТекстЗапросаОбщийТорг()
	
	Возврат
	
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументКорректировкаПриобретения.ДокументОснование КАК ДокументОснование,
	|	ДокументКорректировкаПриобретения.Ссылка КАК КорректировкаПриобретения,
	|	МАКСИМУМ(ТТНВходящаяЕГАИС.Ссылка) КАК ТТНВходящая
	|ПОМЕСТИТЬ ДокументыОснованияКорректировки
	|ИЗ
	|	Документ.КорректировкаПриобретения КАК ДокументКорректировкаПриобретения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТТНВходящаяЕГАИС КАК ТТНВходящаяЕГАИС
	|		ПО (ДокументКорректировкаПриобретения.ДокументОснование = ТТНВходящаяЕГАИС.ДокументОснование)
	|ГДЕ
	|	ДокументКорректировкаПриобретения.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументКорректировкаПриобретения.ДокументОснование,
	|	ДокументКорректировкаПриобретения.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыОснованияКорректировки.КорректировкаПриобретения КАК КорректировкаПриобретения,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(КорректировкаПриобретенияРасхождения.Количество, 0) <> 0
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьРасхождения
	|ПОМЕСТИТЬ ЕстьРасхожденияПоДокументам
	|ИЗ
	|	ДокументыОснованияКорректировки КАК ДокументыОснованияКорректировки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения.Расхождения КАК КорректировкаПриобретенияРасхождения
	|		ПО ДокументыОснованияКорректировки.КорректировкаПриобретения = КорректировкаПриобретенияРасхождения.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыОснованияКорректировки.КорректировкаПриобретения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументыОснованияКорректировки.КорректировкаПриобретения КАК ДокументОснование,
	|	СтрокиОснований.ДокументОснование КАК ДокументОснованиеРеализация,
	|	СчетФактураПолученный.НомерИсправления КАК НомерИсправления,
	|	СчетФактураПолученный.Номер КАК НомерСчетаФактуры,
	|	СчетФактураПолученный.Дата КАК ДатаСчетаФактуры
	|ИЗ
	|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК СтрокиОснований
	|		ПО (СтрокиОснований.Ссылка = СчетФактураПолученный.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыОснованияКорректировки КАК ДокументыОснованияКорректировки
	|		ПО (ДокументыОснованияКорректировки.ДокументОснование = СтрокиОснований.ДокументОснование)
	|ГДЕ
	|	НЕ СчетФактураПолученный.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыОснованияКорректировки.КорректировкаПриобретения,
	|	СтрокиОснований.ДокументОснование,
	|	СчетФактураПолученный.НомерИсправления,
	|	СчетФактураПолученный.Номер,
	|	СчетФактураПолученный.Дата
	|ИЗ
	|	Документ.СчетФактураПолученныйНалоговыйАгент КАК СчетФактураПолученный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученныйНалоговыйАгент.ДокументыОснования КАК СтрокиОснований
	|		ПО (СтрокиОснований.Ссылка = СчетФактураПолученный.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыОснованияКорректировки КАК ДокументыОснованияКорректировки
	|		ПО (ДокументыОснованияКорректировки.ДокументОснование = СтрокиОснований.ДокументОснование)
	|ГДЕ
	|	НЕ СчетФактураПолученный.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	СчетФактураПолученный.НомерИсправления УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректировкаПриобретенияТовары.Ссылка КАК Ссылка,
	|	КорректировкаПриобретенияТовары.Номенклатура КАК Номенклатура,
	|	КорректировкаПриобретенияТовары.НоменклатураПартнера КАК НоменклатураПартнера,
	|	КорректировкаПриобретенияТовары.Серия.бг_УпаковкаПаллета.Родитель КАК ПаллетаРодитель,
	|	КорректировкаПриобретенияТовары.Характеристика КАК Характеристика,
	|	КорректировкаПриобретенияТовары.Серия КАК Серия,
	|	КорректировкаПриобретенияТовары.КоличествоУпаковок КАК КоличествоУпаковокПоДокументу,
	|	КорректировкаПриобретенияТовары.Количество КАК КоличествоПоДокументам,
	|	КорректировкаПриобретенияТовары.Цена КАК Цена,
	|	КорректировкаПриобретенияТовары.Цена КАК ЦенаПоФакту,
	|	КорректировкаПриобретенияТовары.Сумма КАК СуммаПоДокументам,
	|	КорректировкаПриобретенияТовары.СтавкаНДС КАК СтавкаНДС,
	|	КорректировкаПриобретенияТовары.СуммаНДС КАК СуммаНДСПоДокументам,
	|	КорректировкаПриобретенияТовары.СуммаСНДС КАК СуммаСНДСПоДокументам,
	|	КорректировкаПриобретенияТовары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	КорректировкаПриобретенияТовары.Количество КАК КоличествоПоФакту,
	|	КорректировкаПриобретенияТовары.Сумма КАК СуммаПоФакту,
	|	КорректировкаПриобретенияТовары.СуммаНДС КАК СуммаНДСПоФакту,
	|	КорректировкаПриобретенияТовары.СуммаСНДС КАК СуммаСНДСПоФакту,
	|	0 КАК КоличествоНедостача,
	|	0 КАК КоличествоИзлишек,
	|	0 КАК СуммаНедостача,
	|	0 КАК СуммаИзлишек,
	|	0 КАК КоличествоБой,
	|	0 КАК КоличествоБрак
	|ПОМЕСТИТЬ ТабличныеДанные
	|ИЗ
	|	Документ.КорректировкаПриобретения.Товары КАК КорректировкаПриобретенияТовары
	|ГДЕ
	|	КорректировкаПриобретенияТовары.Ссылка В (&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаПриобретенияРасхождения.Ссылка,
	|	КорректировкаПриобретенияРасхождения.Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка),
	|	КорректировкаПриобретенияРасхождения.Серия.бг_УпаковкаПаллета.Родитель,
	|	КорректировкаПриобретенияРасхождения.Характеристика,
	|	КорректировкаПриобретенияРасхождения.Серия,
	|	-КорректировкаПриобретенияРасхождения.КоличествоУпаковок,
	|	-КорректировкаПриобретенияРасхождения.Количество,
	|	КорректировкаПриобретенияРасхождения.Цена,
	|	КорректировкаПриобретенияРасхождения.Цена,
	|	-КорректировкаПриобретенияРасхождения.Сумма,
	|	КорректировкаПриобретенияРасхождения.СтавкаНДС,
	|	-КорректировкаПриобретенияРасхождения.СуммаНДС,
	|	-КорректировкаПриобретенияРасхождения.СуммаСНДС,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА КорректировкаПриобретенияРасхождения.КоличествоУпаковок < 0
	|			ТОГДА -КорректировкаПриобретенияРасхождения.КоличествоУпаковок
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА КорректировкаПриобретенияРасхождения.КоличествоУпаковок > 0
	|			ТОГДА КорректировкаПриобретенияРасхождения.КоличествоУпаковок
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА КорректировкаПриобретенияРасхождения.СуммаСНДС < 0
	|			ТОГДА -КорректировкаПриобретенияРасхождения.СуммаСНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА КорректировкаПриобретенияРасхождения.СуммаСНДС > 0
	|			ТОГДА КорректировкаПриобретенияРасхождения.СуммаСНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	КорректировкаПриобретенияРасхождения.бг_КоличествоБой,
	|	КорректировкаПриобретенияРасхождения.бг_КоличествоБрак
	|ИЗ
	|	Документ.КорректировкаПриобретения.Расхождения КАК КорректировкаПриобретенияРасхождения
	|ГДЕ
	|	КорректировкаПриобретенияРасхождения.Ссылка В (&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличныеДанные.Ссылка КАК Ссылка,
	|	ТабличныеДанные.Номенклатура КАК Номенклатура,
	|	ТабличныеДанные.Номенклатура.ЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	МАКСИМУМ(ТабличныеДанные.НоменклатураПартнера) КАК НоменклатураПартнера,
	|	ТабличныеДанные.Характеристика КАК Характеристика,
	|	ТабличныеДанные.Серия КАК Серия,
	|	ТабличныеДанные.Номенклатура.ВидНоменклатуры.бг_ЕдиницаИзмеренияМест КАК Упаковка,
	|	ТабличныеДанные.ПаллетаРодитель КАК ПаллетаРодитель,
	|	ВЫБОР
	|		КОГДА ТабличныеДанные.Номенклатура.ВидНоменклатуры.бг_ЕдиницаИзмеренияМест = ТабличныеДанные.ПаллетаРодитель.ЕдиницаИзмерения
	|			ТОГДА ЕСТЬNULL(ТабличныеДанные.ПаллетаРодитель.КоличествоУпаковок, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоМест,
	|	СУММА(ТабличныеДанные.КоличествоУпаковокПоДокументу) КАК КоличествоУпаковокПоДокументу,
	|	СУММА(ТабличныеДанные.КоличествоПоДокументам) КАК КоличествоПоДокументам,
	|	МАКСИМУМ(ТабличныеДанные.Цена) КАК Цена,
	|	МАКСИМУМ(ТабличныеДанные.ЦенаПоФакту) КАК ЦенаПоФакту,
	|	СУММА(ТабличныеДанные.СуммаПоДокументам) КАК СуммаПоДокументам,
	|	ТабличныеДанные.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ТабличныеДанные.СуммаНДСПоДокументам) КАК СуммаНДСПоДокументам,
	|	СУММА(ТабличныеДанные.СуммаСНДСПоДокументам) КАК СуммаСНДСПоДокументам,
	|	СУММА(ТабличныеДанные.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(ТабличныеДанные.КоличествоПоФакту) КАК КоличествоПоФакту,
	|	СУММА(ТабличныеДанные.СуммаПоФакту) КАК СуммаПоФакту,
	|	СУММА(ТабличныеДанные.СуммаНДСПоФакту) КАК СуммаНДСПоФакту,
	|	СУММА(ТабличныеДанные.СуммаСНДСПоФакту) КАК СуммаСНДСПоФакту,
	|	СУММА(ТабличныеДанные.КоличествоНедостача) КАК КоличествоНедостача,
	|	СУММА(ТабличныеДанные.КоличествоИзлишек) КАК КоличествоИзлишек,
	|	СУММА(ТабличныеДанные.СуммаНедостача) КАК СуммаНедостача,
	|	СУММА(ТабличныеДанные.СуммаИзлишек) КАК СуммаИзлишек,
	|	СУММА(ТабличныеДанные.КоличествоБой) КАК КоличествоБой,
	|	СУММА(ТабличныеДанные.КоличествоБрак) КАК КоличествоБрак
	|ПОМЕСТИТЬ РасхожденияПоДокументам
	|ИЗ
	|	ТабличныеДанные КАК ТабличныеДанные
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабличныеДанные.Ссылка,
	|	ТабличныеДанные.Номенклатура,
	|	ТабличныеДанные.Номенклатура.ВидНоменклатуры.бг_ЕдиницаИзмеренияМест,
	|	ТабличныеДанные.ПаллетаРодитель,
	|	ТабличныеДанные.Характеристика,
	|	ВЫБОР
	|		КОГДА ТабличныеДанные.Номенклатура.ВидНоменклатуры.бг_ЕдиницаИзмеренияМест = ТабличныеДанные.ПаллетаРодитель.ЕдиницаИзмерения
	|			ТОГДА ЕСТЬNULL(ТабличныеДанные.ПаллетаРодитель.КоличествоУпаковок, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТабличныеДанные.СтавкаНДС,
	|	ТабличныеДанные.Номенклатура.ЕдиницаИзмерения.Наименование,
	|	ТабличныеДанные.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТабличныеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументКорректировкаПриобретения.Ссылка КАК Ссылка,
	|	ДокументКорректировкаПриобретения.Номер КАК НомерДокумента,
	|	ДокументКорректировкаПриобретения.Дата КАК ДатаДокумента,
	|	ДокументКорректировкаПриобретения.Дата КАК ДатаОснования,
	|	ДокументКорректировкаПриобретения.Договор.Номер КАК НомерДоговораПоставки,
	|	ДокументКорректировкаПриобретения.Договор.Дата КАК ДатаДоговораПоставки,
	|	ДокументКорректировкаПриобретения.ДокументОснование КАК ДокументОснование,
	|	ДокументКорректировкаПриобретения.ДокументОснование.ДатаВходящегоДокумента КАК ДатаДокументаОснования,
	|	ДокументКорректировкаПриобретения.ДокументОснование.НомерВходящегоДокумента КАК НомерДокументаОснования,
	|	ДокументКорректировкаПриобретения.Организация КАК Организация,
	|	ДокументКорректировкаПриобретения.Организация.Префикс КАК Префикс,
	|	ДокументыОснованияКорректировки.ТТНВходящая.Грузополучатель.бг_ПунктРазгрузки КАК ГрузополучательПунктРазгрузки,
	|	ДокументКорректировкаПриобретения.Организация.КодПоОКПО КАК ОрганизацияПоОКПО,
	|	ВЫРАЗИТЬ(ДокументКорректировкаПриобретения.ДокументОснование КАК Документ.ПриобретениеТоваровУслуг).Контрагент КАК Поставщик,
	|	ДокументКорректировкаПриобретения.Подразделение КАК Подразделение,
	|	ДокументКорректировкаПриобретения.Подразделение.Наименование КАК ПодразделениеНаименование,
	|	ДокументКорректировкаПриобретения.Дата КАК ДатаПрибытияВПунктНазначения,
	|	ДокументКорректировкаПриобретения.Дата КАК ДатаВскрытия,
	|	ДокументКорректировкаПриобретения.Дата КАК ДатаДоставкиНаСклад,
	|	ДокументКорректировкаПриобретения.Дата КАК ДатаНачалаРазгрузки,
	|	ДАТАВРЕМЯ(1, 1, 1, 13, 0, 0) КАК ДатаНачалаПриемки,
	|	ДАТАВРЕМЯ(1, 1, 1, 17, 0, 0) КАК ДатаОкончанияПриемки,
	|	ДокументКорректировкаПриобретения.Дата КАК ДатаДокументаОВызовеПредставителяПартнера,
	|	ДокументКорректировкаПриобретения.Валюта КАК Валюта,
	|	ДокументКорректировкаПриобретения.Контрагент КАК Контрагент,
	|	ДокументКорректировкаПриобретения.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	ДокументКорректировкаПриобретения.Дата КАК ДатаДоставкиТоваров,
	|	ДокументКорректировкаПриобретения.Дата КАК ДатаОтправленияТоваров,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаКоммерческогоАкта,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаВетеринарногоСвидетельства,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаЖелезнодорожнойНакладной,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаКоносамента,
	|	""Счетом мест, внутренним и внешним осмотром тары"" КАК СпособОпределенияКоличества,
	|	ДокументКорректировкаПриобретения.Дата КАК ДатаВскрытияТары,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаВыдачиДокументаУдостоверяющегоПолномочияПредставителяПартнера,
	|	"""" КАК ВызываемыйПредставительПартнера,
	|	"""" КАК ВидДокументаОВызовеПредставителяПартнера,
	|	"""" КАК ОснованиеДляСоставленияАкта,
	|	"""" КАК УсловияХраненияТовараДоВскрытия,
	|	"""" КАК СостояниеТарыИУпаковки,
	|	"""" КАК СодержаниеНаружнойМаркировки,
	|	"""" КАК ОрганизацияВзвесившаяИОпломбировавшаяТовар,
	|	"""" КАК ПорядокОтбораТовараДляВыборочнойПроверки,
	|	"""" КАК МестоОпределенияКоличества,
	|	"""" КАК СведенияОбИсправностиВесоизмерительныхПриборов,
	|	"""" КАК ПрочиеДанные,
	|	"""" КАК Приложение,
	|	"""" КАК КладовщикПринявшийТовар,
	|	0 КАК ТемператураПриРазгрузке,
	|	ЕстьРасхожденияПоДокументам.ЕстьРасхождения КАК ЕстьРасхождения,
	|	ДокументКорректировкаПриобретения.бг_ГлавныйБухгалтер КАК ГлавныйБухгалтер,
	|	ВЫРАЗИТЬ(ДокументКорректировкаПриобретения.ДокументОснование КАК Документ.ПриобретениеТоваровУслуг).Грузоотправитель КАК Грузоотправитель,
	|	Производители.Производитель КАК Производитель,
	|	ДокументКорректировкаПриобретения.бг_ДолжностьПредседателяКомиссии КАК ДолжностьПредседателяКомиссии,
	|	ДокументКорректировкаПриобретения.бг_Руководитель.Должность КАК ДолжностьРуководителя,
	|	ДокументКорректировкаПриобретения.бг_ДолжностьЧленаКомиссии1 КАК ДолжностьЧленаКомиссии1,
	|	ДокументКорректировкаПриобретения.бг_ДолжностьЧленаКомиссии2 КАК ДолжностьЧленаКомиссии2,
	|	ДокументКорректировкаПриобретения.бг_ДолжностьЧленаКомиссии3 КАК ДолжностьЧленаКомиссии3,
	|	ДокументКорректировкаПриобретения.бг_ПредседательКомиссии КАК ПредседательКомиссии,
	|	ДокументКорректировкаПриобретения.бг_Руководитель КАК Руководитель,
	|	ДокументКорректировкаПриобретения.бг_ЧленКомиссии1 КАК ЧленКомиссии1,
	|	ДокументКорректировкаПриобретения.бг_ЧленКомиссии2 КАК ЧленКомиссии2,
	|	ДокументКорректировкаПриобретения.бг_ЧленКомиссии3 КАК ЧленКомиссии3,
	|	ДокументыОснованияКорректировки.ТТНВходящая КАК ТТНВходящая,
	|	ДокументыОснованияКорректировки.ТТНВходящая.ТипТранспорта КАК СпособДоставки,
	|	ДокументыОснованияКорректировки.ТТНВходящая.НомерТранспортногоСредства КАК НомерТранспортногоСредства,
	|	ДокументыОснованияКорректировки.ТТНВходящая.Номер КАК НомерТТН,
	|	ДокументыОснованияКорректировки.ТТНВходящая.Дата КАК ДатаТТН
	|ИЗ
	|	ЕстьРасхожденияПоДокументам КАК ЕстьРасхожденияПоДокументам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения КАК ДокументКорректировкаПриобретения
	|			ЛЕВОЕ СОЕДИНЕНИЕ ДокументыОснованияКорректировки КАК ДокументыОснованияКорректировки
	|			ПО ДокументКорректировкаПриобретения.Ссылка = ДокументыОснованияКорректировки.КорректировкаПриобретения
	|			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				МАКСИМУМ(РасхожденияПоДокументам.Номенклатура.бг_КонтрагентПроизводитель) КАК Производитель,
	|				РасхожденияПоДокументам.Ссылка КАК КорректировкаПриобретения
	|			ИЗ
	|				РасхожденияПоДокументам КАК РасхожденияПоДокументам
	|			
	|			СГРУППИРОВАТЬ ПО
	|				РасхожденияПоДокументам.Ссылка) КАК Производители
	|			ПО ДокументКорректировкаПриобретения.Ссылка = Производители.КорректировкаПриобретения
	|		ПО ЕстьРасхожденияПоДокументам.КорректировкаПриобретения = ДокументКорректировкаПриобретения.Ссылка
	|ГДЕ
	|	ЕстьРасхожденияПоДокументам.КорректировкаПриобретения.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасхожденияПоДокументам.Ссылка КАК Ссылка,
	|	РасхожденияПоДокументам.Номенклатура КАК Номенклатура,
	|	РасхожденияПоДокументам.НоменклатураПартнера КАК НоменклатураПартнера,
	|	РасхожденияПоДокументам.НоменклатураПартнера.Артикул КАК НоменклатураПартнераАртикул,
	|	РасхожденияПоДокументам.Упаковка КАК Упаковка,
	|	РасхожденияПоДокументам.КоличествоМест КАК КоличествоМест,
	|	РасхожденияПоДокументам.НоменклатураПартнера.Идентификатор КАК Идентификатор,
	|	РасхожденияПоДокументам.Характеристика КАК Характеристика,
	|	РасхожденияПоДокументам.Серия КАК Серия,
	|	РасхожденияПоДокументам.КоличествоУпаковокПоДокументу КАК КоличествоУпаковокПоДокументу,
	|	РасхожденияПоДокументам.КоличествоПоДокументам КАК КоличествоПоДокументам,
	|	РасхожденияПоДокументам.Цена КАК Цена,
	|	РасхожденияПоДокументам.ЦенаПоФакту КАК ЦенаПоФакту,
	|	РасхожденияПоДокументам.СуммаПоДокументам КАК СуммаПоДокументам,
	|	РасхожденияПоДокументам.СтавкаНДС КАК СтавкаНДС,
	|	РасхожденияПоДокументам.СуммаНДСПоДокументам КАК СуммаНДСПоДокументам,
	|	РасхожденияПоДокументам.СуммаСНДСПоДокументам КАК СуммаСНДСПоДокументам,
	|	РасхожденияПоДокументам.КоличествоУпаковок КАК КоличествоУпаковок,
	|	РасхожденияПоДокументам.КоличествоПоФакту КАК КоличествоПоФакту,
	|	РасхожденияПоДокументам.СуммаПоФакту КАК СуммаПоФакту,
	|	РасхожденияПоДокументам.СуммаНДСПоФакту КАК СуммаНДСПоФакту,
	|	РасхожденияПоДокументам.СуммаСНДСПоФакту КАК СуммаСНДСПоФакту,
	|	ВЫБОР
	|		КОГДА РасхожденияПоДокументам.КоличествоНедостача - РасхожденияПоДокументам.КоличествоБой - РасхожденияПоДокументам.КоличествоБрак > 0
	|			ТОГДА РасхожденияПоДокументам.КоличествоНедостача - РасхожденияПоДокументам.КоличествоБой - РасхожденияПоДокументам.КоличествоБрак
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоНедостача,
	|	ВЫБОР
	|		КОГДА РасхожденияПоДокументам.КоличествоИзлишек - РасхожденияПоДокументам.КоличествоБой - РасхожденияПоДокументам.КоличествоБрак > 0
	|			ТОГДА РасхожденияПоДокументам.КоличествоИзлишек - РасхожденияПоДокументам.КоличествоБой - РасхожденияПоДокументам.КоличествоБрак
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоИзлишек,
	|	ВЫБОР
	|		КОГДА РасхожденияПоДокументам.КоличествоНедостача - РасхожденияПоДокументам.КоличествоБой - РасхожденияПоДокументам.КоличествоБрак > 0
	|			ТОГДА РасхожденияПоДокументам.СуммаНедостача / РасхожденияПоДокументам.КоличествоНедостача * (РасхожденияПоДокументам.КоличествоНедостача - РасхожденияПоДокументам.КоличествоБой - РасхожденияПоДокументам.КоличествоБрак)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНедостача,
	|	ВЫБОР
	|		КОГДА РасхожденияПоДокументам.КоличествоИзлишек - РасхожденияПоДокументам.КоличествоБой - РасхожденияПоДокументам.КоличествоБрак > 0
	|			ТОГДА РасхожденияПоДокументам.СуммаИзлишек / РасхожденияПоДокументам.КоличествоИзлишек * (РасхожденияПоДокументам.КоличествоНедостача - РасхожденияПоДокументам.КоличествоБой - РасхожденияПоДокументам.КоличествоБрак)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаИзлишек,
	|	РасхожденияПоДокументам.КоличествоБой КАК КоличествоБой,
	|	ВЫБОР
	|		КОГДА РасхожденияПоДокументам.КоличествоНедостача <> 0
	|			ТОГДА ВЫРАЗИТЬ(РасхожденияПоДокументам.СуммаНедостача / РасхожденияПоДокументам.КоличествоНедостача * РасхожденияПоДокументам.КоличествоБой КАК ЧИСЛО(31, 2))
	|		КОГДА РасхожденияПоДокументам.КоличествоИзлишек <> 0
	|			ТОГДА ВЫРАЗИТЬ(РасхожденияПоДокументам.СуммаИзлишек / РасхожденияПоДокументам.КоличествоИзлишек * РасхожденияПоДокументам.КоличествоБой КАК ЧИСЛО(31, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБой,
	|	РасхожденияПоДокументам.КоличествоБрак КАК КоличествоБрак,
	|	ВЫБОР
	|		КОГДА РасхожденияПоДокументам.КоличествоНедостача <> 0
	|			ТОГДА ВЫРАЗИТЬ(РасхожденияПоДокументам.СуммаНедостача / РасхожденияПоДокументам.КоличествоНедостача * РасхожденияПоДокументам.КоличествоБрак КАК ЧИСЛО(31, 2))
	|		КОГДА РасхожденияПоДокументам.КоличествоИзлишек <> 0
	|			ТОГДА ВЫРАЗИТЬ(РасхожденияПоДокументам.СуммаИзлишек / РасхожденияПоДокументам.КоличествоИзлишек * РасхожденияПоДокументам.КоличествоБрак КАК ЧИСЛО(31, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБрак,
	|	РасхожденияПоДокументам.Номенклатура.Код КАК Код,
	|	РасхожденияПоДокументам.Номенклатура.Артикул КАК Артикул,
	|	РасхожденияПоДокументам.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА РасхожденияПоДокументам.Номенклатура.АлкогольнаяПродукция
	|				И РасхожденияПоДокументам.Номенклатура.ВидАлкогольнойПродукции.ВидЛицензии = ЗНАЧЕНИЕ(Перечисление.ВидыЛицензийАлкогольнойПродукции.АлкогольнаяПродукция)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК АлкогольнаяПродукция,
	|	РасхожденияПоДокументам.ЕдиницаИзмеренияНаименование КАК ЕдиницаИзмеренияНаименование,
	|	РасхожденияПоДокументам.Номенклатура.ЕдиницаИзмерения.Код КАК КодПоОКЕИ,
	|	РасхожденияПоДокументам.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|	РасхожденияПоДокументам.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	РасхожденияПоДокументам.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	РасхожденияПоДокументам.Ссылка.ЦенаВключаетНДС КАК ЦенаВключаетНДСПоФакту,
	|	РасхожденияПоДокументам.Ссылка.ЦенаВключаетНДС КАК ЦенаВключаетНДСПоДокументам,
	|	РасхожденияПоДокументам.Ссылка.ДокументОснование КАК ДокументОснование,
	|	"""" КАК ТекстовоеОписание
	|ИЗ
	|	РасхожденияПоДокументам КАК РасхожденияПоДокументам
	|;";
	
КонецФункции	

Функция бг_ТекстЗапросаДополнениеТорг2()

	Возврат
	
	"ВЫБРАТЬ
	|	РасхожденияПоДокументам.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА РасхожденияПоДокументам.Номенклатура.АлкогольнаяПродукция
	|				И РасхожденияПоДокументам.Номенклатура.ВидАлкогольнойПродукции.ВидЛицензии = ЗНАЧЕНИЕ(Перечисление.ВидыЛицензийАлкогольнойПродукции.АлкогольнаяПродукция)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК АлкогольнаяПродукция,
	|	СУММА(ВЫБОР
	|			КОГДА РасхожденияПоДокументам.КоличествоМест > 0
	|				ТОГДА РасхожденияПоДокументам.КоличествоУпаковокПоДокументу / РасхожденияПоДокументам.КоличествоМест
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоМест,
	|	РасхожденияПоДокументам.ЕдиницаИзмеренияНаименование КАК ЕдиницаИзмерения,
	|	РасхожденияПоДокументам.Упаковка КАК Упаковка
	|ИЗ
	|	РасхожденияПоДокументам КАК РасхожденияПоДокументам
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА РасхожденияПоДокументам.Номенклатура.АлкогольнаяПродукция
	|				И РасхожденияПоДокументам.Номенклатура.ВидАлкогольнойПродукции.ВидЛицензии = ЗНАЧЕНИЕ(Перечисление.ВидыЛицензийАлкогольнойПродукции.АлкогольнаяПродукция)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	РасхожденияПоДокументам.ЕдиницаИзмеренияНаименование,
	|	РасхожденияПоДокументам.Упаковка,
	|	РасхожденияПоДокументам.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыКурсовВалютДокументов.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ПериодыКурсовВалютДокументов.Ссылка.Валюта = КурсыВалют.Валюта
	|				ТОГДА ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1)
	|		КОНЕЦ) КАК КурсВалютыДокумента,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ПериодыКурсовВалютДокументов.Ссылка.Валюта = КурсыВалют.Валюта
	|				ТОГДА ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1)
	|		КОНЕЦ) КАК КратностьВалютыДокумента,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ПериодыКурсовВалютДокументов.ВалютаРегламентированногоУчета = КурсыВалют.Валюта
	|				ТОГДА ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1)
	|		КОНЕЦ) КАК КурсВалютыРегламентированногоУчета,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ПериодыКурсовВалютДокументов.ВалютаРегламентированногоУчета = КурсыВалют.Валюта
	|				ТОГДА ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1)
	|		КОНЕЦ) КАК КратностьВалютыРегламентированногоУчета,
	|	ВЫБОР
	|		КОГДА ПериодыКурсовВалютДокументов.ВалютаРегламентированногоУчета = ПериодыКурсовВалютДокументов.Ссылка.Валюта
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПересчетНеТребуется
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДанныеДокумента.Ссылка КАК Ссылка,
	|		ДанныеДокумента.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	|		МАКСИМУМ(ВЫБОР
	|				КОГДА ДанныеДокумента.Валюта = КурсыВалют.Валюта
	|					ТОГДА КурсыВалют.Период
	|			КОНЕЦ) КАК ПериодВалютаДокумента,
	|		МАКСИМУМ(ВЫБОР
	|				КОГДА ДанныеДокумента.Организация.ВалютаРегламентированногоУчета = КурсыВалют.Валюта
	|					ТОГДА КурсыВалют.Период
	|			КОНЕЦ) КАК ПериодВалютаРегламентированногоУчета
	|	ИЗ
	|		Документ.КорректировкаПриобретения КАК ДанныеДокумента
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|			ПО ДанныеДокумента.Дата >= КурсыВалют.Период
	|				И (ДанныеДокумента.Валюта = КурсыВалют.Валюта
	|					ИЛИ ДанныеДокумента.Организация.ВалютаРегламентированногоУчета = КурсыВалют.Валюта)
	|				И (КурсыВалют.БазоваяВалюта = &БазоваяВалюта)
	|	ГДЕ
	|		ДанныеДокумента.Ссылка В(&МассивОбъектов)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДанныеДокумента.Ссылка,
	|		ДанныеДокумента.Организация.ВалютаРегламентированногоУчета) КАК ПериодыКурсовВалютДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО (ПериодыКурсовВалютДокументов.Ссылка.Валюта = КурсыВалют.Валюта
	|					И ПериодыКурсовВалютДокументов.ПериодВалютаДокумента = КурсыВалют.Период
	|				ИЛИ ПериодыКурсовВалютДокументов.ВалютаРегламентированногоУчета = КурсыВалют.Валюта
	|					И ПериодыКурсовВалютДокументов.ПериодВалютаРегламентированногоУчета = КурсыВалют.Период)
	|			И (КурсыВалют.БазоваяВалюта = &БазоваяВалюта)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПериодыКурсовВалютДокументов.Ссылка,
	|	ВЫБОР
	|		КОГДА ПериодыКурсовВалютДокументов.ВалютаРегламентированногоУчета = ПериодыКурсовВалютДокументов.Ссылка.Валюта
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";
		
КонецФункции	

#КонецОбласти

#КонецОбласти

#КонецЕсли
