#Область ОписаниеПеременных

&НаКлиенте
Перем ВыполняетсяЗакрытие;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ДокументОбъект") 
		Или Не ТипЗнч(Параметры.ДокументОбъект) = Тип("ДанныеФормыСтруктура")
		Или Не Параметры.ДокументОбъект.Свойство("Ссылка") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ДокументОбъект);
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ВыполняетсяЗакрытие 
		И Модифицированность 
		И Не СохранитьПараметры Тогда
		
		СохранитьПараметры = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстПредупреждения = НСтр("ru = 'Реквизиты печати корректировки приобретения были изменены. 
									|Перенести изменения?'");
		
		ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы, ТекстПредупреждения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	ВыполняетсяЗакрытие = Истина;
	СохранитьПараметры  = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если СохранитьПараметры И Не ЗавершениеРаботы Тогда
		
		ОповеститьОВыборе(СтруктураВозвращаемыхДанных());
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЧленыКомиссииПриИзменении(Элемент)
	
	ИмяРеквизита = "бг_" + Элемент.Имя;
	
	ЗначениеРеквизита = ЭтотОбъект[ИмяРеквизита];
	
	Если ЗначениеЗаполнено(ЗначениеРеквизита) 
		И ТипЗнч(ЗначениеРеквизита) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		
		ИменаДолжностей = ИменаДолжностейЧленовКомиссии();
		
		Должность = ДолжностьФизическогоЛица(ЗначениеРеквизита);
		
		ЭтотОбъект[ИменаДолжностей.Получить(ИмяРеквизита)] = Должность;	
		
	КонецЕсли;		
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не ТолькоПросмотр Тогда
		СохранитьПараметры = Истина;
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ДолжностьФизическогоЛица(ФизическоеЛицо)		

	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФизическоеЛицо, "бг_Должность");
	
КонецФункции	

&НаКлиенте
Функция ИменаДолжностейЧленовКомиссии()

	ИменаДолжностей = Новый Соответствие;
	
	ИменаДолжностей.Вставить("бг_ПредседательКомиссии", "бг_ДолжностьПредседателяКомиссии");
	ИменаДолжностей.Вставить("бг_ЧленКомиссии1", 		"бг_ДолжностьЧленаКомиссии1");
	ИменаДолжностей.Вставить("бг_ЧленКомиссии2", 		"бг_ДолжностьЧленаКомиссии2");
	ИменаДолжностей.Вставить("бг_ЧленКомиссии3", 		"бг_ДолжностьЧленаКомиссии3");
	
	Возврат ИменаДолжностей;
	
КонецФункции	

&НаСервере
Функция СтруктураВозвращаемыхДанных()
	
	СтруктураВозврата = Новый Структура;
	
	РеквизитыФормы = ПолучитьРеквизиты();
	Для Каждого РеквизитФормы Из РеквизитыФормы Цикл
		
		СтруктураВозврата.Вставить(РеквизитФормы.Имя, ЭтотОбъект[РеквизитФормы.Имя]);
		
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти

#Область Инициализация

ВыполняетсяЗакрытие = Ложь;

#КонецОбласти
