
#Область ОбработчикиСобытийФормы

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура бг_ПриЧтенииНаСервереПеред(ТекущийОбъект)
	
	бг_ДобавитьРеквизитыФормы();
	бг_ЗаполнитьСлужебныеДанные();
	
КонецПроцедуры

&НаСервере
Процедура бг_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		бг_ДобавитьРеквизитыФормы();
		бг_ЗаполнитьСлужебныеДанные();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	бг_ДобавитьПоляДвижениеМарок();
	
	бг_УстановитьВидимостьПолейАлкогольнойПродукции();
	
КонецПроцедуры

&НаСервере
Процедура бг_ПослеЗаписиНаСервереПосле(ТекущийОбъект, ПараметрыЗаписи)
	
	бг_УстановитьВидимостьПолейАлкогольнойПродукции();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура бг_ПроверкаСписываемыхШтрихкодовНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Или Модифицированность Тогда
		
		ОповещениеПоЗавершении = Новый ОписаниеОповещения(
			"бг_ПроверкаСписываемыхШтрихкодовНажатиеПослеВопроса",
			ЭтотОбъект);
		
		ПоказатьВопрос(
			ОповещениеПоЗавершении,
			НСтр("ru='Документ предварительно необходимо записать. Записать?'"),
			РежимДиалогаВопрос.ДаНет);
			
	Иначе
			
		бг_ПроверкаСписываемыхШтрихкодовНажатиеЗавершение();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ПроверкаСписываемыхШтрихкодовНажатиеПослеВопроса(Результат, ДополнительныеПараметры) Экспорт

	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияУТКлиент.Записать(ЭтаФорма, Истина);

	бг_ПроверкаСписываемыхШтрихкодовНажатиеЗавершение();
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ПроверкаСписываемыхШтрихкодовНажатиеЗавершение()

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АктСписанияЕГАИС", Объект.Ссылка);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("КлючВарианта", "СписываемыеМарки");
	
	ОткрытьФорму(
		"Отчет.бг_АнализСписываемыхМарок.ФормаОбъекта",
		ПараметрыФормы,
		ЭтаФорма,
		УникальныйИдентификатор,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура бг_ДобавитьРеквизитыФормы()
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтотОбъект, "бг_ЭтоКонтролерЕГАИС") Тогда
		
		ДобавляемыеРеквизиты.Добавить(
			Новый РеквизитФормы(
				"бг_ЭтоКонтролерЕГАИС",
				Новый ОписаниеТипов("Булево")));
	
	КонецЕсли;
	
	Если ДобавляемыеРеквизиты.Количество() > 0 Тогда
		ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура бг_ДобавитьПоляДвижениеМарок()
	
	ГиперссылкаПроверкиСписываемыхШтрихкодов = бг_ГиперссылкаПроверкиСписываемыхШтрихкодов();
	
КонецПроцедуры

&НаСервере
Процедура бг_ЗаполнитьСлужебныеДанные()
	
	ЭтотОбъект["бг_ЭтоКонтролерЕГАИС"] = РольДоступна("бг_КонтролерЕГАИС");
	
КонецПроцедуры

&НаСервере
Процедура бг_УстановитьВидимостьПолейАлкогольнойПродукции()
	
	Элементы.ГруппаСканированиеИПроверкаАлкогольнойПродукции.Видимость = Ложь;
	Элементы.ТоварыИндексАкцизнойМарки.Видимость = Ложь;
	
	бг_ГиперссылкаПроверкиСписываемыхШтрихкодов().Видимость =
		ЗначениеЗаполнено(Объект.Ссылка)
		И бг_ИнтеграцияЕГАИСПовтИсп.ИспользоватьМеханизмДвиженийМарок(Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Функция бг_ГиперссылкаПроверкиСписываемыхШтрихкодов()
	
	ГиперссылкаПроверкиСписываемыхШтрихкодов = Элементы.Найти("бг_ПроверкаСписываемыхШтрихкодов");
	Если ГиперссылкаПроверкиСписываемыхШтрихкодов <> Неопределено Тогда
		Возврат ГиперссылкаПроверкиСписываемыхШтрихкодов;
	КонецЕсли;
	
	ГиперссылкаПроверкиСписываемыхШтрихкодов = Элементы.Вставить(
		"бг_ПроверкаСписываемыхШтрихкодов",
		Тип("ДекорацияФормы"),
		Элементы.СтраницаТовары,
		Элементы.ГруппаСканированиеИПроверкаАлкогольнойПродукции);
		
	ГиперссылкаПроверкиСписываемыхШтрихкодов.Вид = ВидДекорацииФормы.Надпись;
	ГиперссылкаПроверкиСписываемыхШтрихкодов.Гиперссылка = Истина;
	ГиперссылкаПроверкиСписываемыхШтрихкодов.Заголовок = НСтр("ru='Проверить списываемые штрихкоды'");
	ГиперссылкаПроверкиСписываемыхШтрихкодов.УстановитьДействие("Нажатие", "бг_ПроверкаСписываемыхШтрихкодовНажатие");
	
	Возврат ГиперссылкаПроверкиСписываемыхШтрихкодов;
	
КонецФункции

#Область СканированиеАлкогольнойПродукции

&НаСервере
&После("ОбновитьИнформациюОткрытияФормыСканирования")
Процедура бг_ОбновитьИнформациюОткрытияФормыСканирования(Прочитать)
	
	бг_УстановитьВидимостьПолейАлкогольнойПродукции();
	
КонецПроцедуры

#КонецОбласти // Конец СканированиеАлкогольнойПродукции

#КонецОбласти
