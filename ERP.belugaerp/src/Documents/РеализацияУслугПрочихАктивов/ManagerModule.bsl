#Область СлужебныйПрограммныйИнтерфейс

// Проверяет продажу ОС и НМА в одном документе
//
// Параметры:
//  ТаблицаЗначений - Таблица Значений с колонками табличной части проверяемого документа.
// 
// Возвращаемое значение:
//   - Структура, где Результат - Булево,
//                    ЕстьОсновныеСредства - Булево,
//                    ЕстьНМА - Булево.
//
Функция бг_ЭтоПродажиНМАиОСВОдномДокументе(ТаблицаЗначений) Экспорт
	
	Структура = Новый Структура("Результат, ЕстьОсновныеСредства, ЕстьНМА", Ложь, Ложь, Ложь);
		
	Если Не ЗначениеЗаполнено(ТаблицаЗначений) Тогда		
		Возврат Структура;
	КонецЕсли;		

	Запрос = бг_ПодготовитьЗапросДляПроверкиПродажиОСиНМАВОдномДокументе();
	
	ВариантыОсновныхСредств = Новый Массив;
	ВариантыОсновныхСредств.Добавить(Перечисления.ВидыВнеоборотныхАктивов.ОсновноеСредство);
	ВариантыОсновныхСредств.Добавить(Перечисления.ВидыВнеоборотныхАктивов.ОбъектыСтроительства);
	ВариантыОсновныхСредств.Добавить(Перечисления.ВидыВнеоборотныхАктивов.ИнвестиционноеИмущество);
	
	ВариантыНМА				= Новый Массив;
	ВариантыНМА.Добавить(Перечисления.ВидыВнеоборотныхАктивов.НМА);
	
	Запрос.УстановитьПараметр("ДокТЧ", ТаблицаЗначений);
	Запрос.УстановитьПараметр("ВариантыОсновныхСредств", ВариантыОсновныхСредств);
	Запрос.УстановитьПараметр("ВариантыНМА", ВариантыНМА);		
		
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Структура;
	Иначе 		
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Количество() > 1 Тогда
			Структура.Результат = Истина;
		КонецЕсли;
		
		Пока Выборка.Следующий() Цикл
			Если Выборка.ВидАктива = "ЕстьОсновныеСредства" Тогда
				Структура.ЕстьОсновныеСредства = Истина;				
			ИначеЕсли Выборка.ВидАктива = "ЕстьНМА" Тогда
				Структура.ЕстьНМА = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Возврат Структура;
	
КонецФункции

Функция бг_ПодготовитьЗапросДляПроверкиПродажиОСиНМАВОдномДокументе() 
		
	Запрос 		 = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДокТЧ.ВидАктива КАК ВидАктива
	|ПОМЕСТИТЬ ДокТЧ
	|ИЗ
	|	&ДокТЧ КАК ДокТЧ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ДокТЧ.ВидАктива В (&ВариантыОсновныхСредств)
	|				ТОГДА ""ЕстьОсновныеСредства"" 
	|			КОГДА ДокТЧ.ВидАктива В (&ВариантыНМА)
	|				ТОГДА ""ЕстьНМА""
	|			ИНАЧЕ """"
	|		КОНЕЦ) КАК ВидАктива
	|ПОМЕСТИТЬ ВидыАктивовВТабличнойЧасти
	|ИЗ
	|	ДокТЧ КАК ДокТЧ
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокТЧ.ВидАктива
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВидыАктивовВТабличнойЧасти.ВидАктива КАК ВидАктива
	|ИЗ
	|	ВидыАктивовВТабличнойЧасти КАК ВидыАктивовВТабличнойЧасти
	|ГДЕ
	|	ВидыАктивовВТабличнойЧасти.ВидАктива <> """"";
	
	Возврат Запрос;
	
КонецФункции

#КонецОбласти
