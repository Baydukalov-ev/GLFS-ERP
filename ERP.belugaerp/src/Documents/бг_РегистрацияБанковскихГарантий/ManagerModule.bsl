
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("бг_УчетБанковскихГарантий");
		
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		ТекстЗапросаТаблицаБанковскиеГарантии(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаАкцизПоПриобретеннымЦенностям(Запрос, ТекстыЗапроса, Регистры);
		
	КонецЕсли;
		
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("Ссылка");
	Поля.Добавить("Номер");
	Поля.Добавить("Дата");
	Поля.Добавить("Операция");
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Данные.Операция) Тогда
		СтандартнаяОбработка = Ложь;
		
		Представление = СтрШаблон(
				НСтр("ru = '%1 %2 от %3';
					|en = '%1 %2 dated %3'"),
				Данные.Операция,
				Данные.Номер,
				Данные.Дата);
		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Период", ДокументСсылка.Дата);
	
КонецПроцедуры

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаБанковскиеГарантии(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаАкцизПоПриобретеннымЦенностям(Запрос, ТекстыЗапроса, Регистры);
		
КонецПроцедуры

Функция ТекстЗапросаТаблицаБанковскиеГарантии(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "бг_БанковскиеГарантии";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	ТабличнаяЧасть.БанковскаяГарантия КАК БанковскаяГарантия,
	|	ТабличнаяЧасть.Сумма КАК Сумма,
	|	ТабличнаяЧасть.Количество КАК Количество,
	|	бг_РегистрацияБанковскихГарантий.Организация КАК Организация
	|ИЗ
	|	Документ.бг_РегистрацияБанковскихГарантий.РаспределениеАкциза КАК ТабличнаяЧасть
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.бг_РегистрацияБанковскихГарантий КАК бг_РегистрацияБанковскихГарантий
	|		ПО ТабличнаяЧасть.Ссылка = бг_РегистрацияБанковскихГарантий.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|	И (бг_РегистрацияБанковскихГарантий.Операция = ЗНАЧЕНИЕ(Перечисление.бг_ОперацииРегистрацииБанковскихГарантий.РегистрацияБанковскихГарантий)
	|			ИЛИ бг_РегистрацияБанковскихГарантий.Операция = ЗНАЧЕНИЕ(Перечисление.бг_ОперацииРегистрацииБанковскихГарантий.ПустаяСсылка))";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаАкцизПоПриобретеннымЦенностям(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "бг_АкцизПоПриобретеннымЦенностям";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегистрацияБанковскихГарантий.Организация КАК Организация,
	|	РегистрацияБанковскихГарантий.Операция КАК Операция,
	|	ТабличнаяЧасть.БанковскаяГарантия КАК Сделка,
	|	ТабличнаяЧасть.Количество КАК Количество
	|ПОМЕСТИТЬ БанковскиеГарантий
	|ИЗ
	|	Документ.бг_РегистрацияБанковскихГарантий.РаспределениеАкциза КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.бг_РегистрацияБанковскихГарантий КАК РегистрацияБанковскихГарантий
	|		ПО (ТабличнаяЧасть.Ссылка = &Ссылка)
	|			И ТабличнаяЧасть.Ссылка = РегистрацияБанковскихГарантий.Ссылка
	|			И (РегистрацияБанковскихГарантий.Организация.бг_ВестиУчетАкцизовПоПриобретеннымЦенностям)
	|			И (РегистрацияБанковскихГарантий.Дата >= РегистрацияБанковскихГарантий.Организация.бг_ДатаНачалаУчетаАкцизовПоПриобретеннымЦенностям)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	БанковскиеГарантий.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК СерияНоменклатуры,
	|	БанковскиеГарантий.Сделка КАК Сделка,
	|	ЗНАЧЕНИЕ(Перечисление.бг_СостоянияПодакцизногоСырья.ПустаяСсылка) КАК СостояниеСырья,
	|	ЗНАЧЕНИЕ(Справочник.бг_СостоянияОплатыАванса.ПустаяСсылка) КАК СостояниеОплатыАванса,
	|	ЗНАЧЕНИЕ(Перечисление.бг_СостоянияОплатыВходящегоАкциза.ЗарегистрированаБанковскаяГарантияЗаАкцизыВБюджет) КАК СтатусАкциза,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Продукция,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК СерияПродукции,
	|	ДатаВремя(01,01,01) КАК ДатаРеализации,
	|	БанковскиеГарантий.Количество КАК Количество
	|ИЗ
	|	БанковскиеГарантий КАК БанковскиеГарантий
	|ГДЕ
	|	БанковскиеГарантий.Операция = ЗНАЧЕНИЕ(Перечисление.бг_ОперацииРегистрацииБанковскихГарантий.РегистрацияБанковскихГарантий)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Период,
	|	АкцизОстатки.Организация,
	|	АкцизОстатки.Номенклатура,
	|	АкцизОстатки.СерияНоменклатуры,
	|	АкцизОстатки.Сделка,
	|	АкцизОстатки.СостояниеСырья,
	|	АкцизОстатки.СостояниеОплатыАванса,
	|	АкцизОстатки.СтатусАкциза,
	|	АкцизОстатки.Продукция,
	|	АкцизОстатки.СерияПродукции,
	|	АкцизОстатки.ДатаРеализации,
	|	АкцизОстатки.КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям.Остатки(
	|			,
	|			(Организация, Сделка) В
	|				(ВЫБРАТЬ
	|					БанковскиеГарантий.Организация,
	|					БанковскиеГарантий.Сделка КАК Сделка
	|				ИЗ
	|					БанковскиеГарантий КАК БанковскиеГарантий
	|				ГДЕ
	|					БанковскиеГарантий.Операция =
	|						ЗНАЧЕНИЕ(Перечисление.бг_ОперацииРегистрацииБанковскихГарантий.ЗакрытиеБанковскихГарантий))) КАК АкцизОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Акциз.ВидДвижения,
	|	&Период,
	|	Акциз.Организация,
	|	Акциз.Номенклатура,
	|	Акциз.СерияНоменклатуры,
	|	Акциз.Сделка,
	|	Акциз.СостояниеСырья,
	|	Акциз.СостояниеОплатыАванса,
	|	Акциз.СтатусАкциза,
	|	Акциз.Продукция,
	|	Акциз.СерияПродукции,
	|	Акциз.ДатаРеализации,
	|	Акциз.Количество
	|ИЗ
	|	Документ.бг_РегистрацияБанковскихГарантий КАК РегистрацияБанковскихГарантий
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям КАК Акциз
	|		ПО (РегистрацияБанковскихГарантий.Ссылка = &Ссылка)
	|			И (РегистрацияБанковскихГарантий.Операция = ЗНАЧЕНИЕ(Перечисление.бг_ОперацииРегистрацииБанковскихГарантий.ЗакрытиеБанковскихГарантий))
	|			И РегистрацияБанковскихГарантий.Ссылка = Акциз.Регистратор";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецЕсли
