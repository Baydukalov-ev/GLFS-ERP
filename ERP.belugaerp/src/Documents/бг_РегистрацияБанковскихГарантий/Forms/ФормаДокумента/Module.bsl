
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	УстановитьВидимостьДоступностьЭлементов();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// Обработчик механизма "ДатыЗапретаИзменения"
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
				
	СобытияФорм.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
		
	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
		
	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДиапазонБанковскихГарантийПриИзменении(Элемент)
	
	Если Объект.РаспределениеАкциза.Количество() Тогда 
		ТекстВопроса = НСтр("ru = 'Табличная часть документа будет очищена и перезаполнена. Продолжить?';
		|en = 'Tabular section of the document will be cleared and refilled. Continue?'");
		ОбработчикОповещения = Новый ОписаниеОповещения("ЗаполнитьПоДиапозону", ЭтаФорма);
		ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьТабличнуюЧастьРаспределенияАкциза();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацияПриИзменении(Элемент)
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура РаспределениеАкцизаКоличествоПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.РаспределениеАкциза.ТекущиеДанные;
	СтавкаДиапазона = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ДиапазонБанковскихГарантий, "НалоговаяСтавка");
	
	Если СтавкаДиапазона <> 0 Тогда
    	ТекущаяСтрока.Сумма=ТекущаяСтрока.Количество*СтавкаДиапазона;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РаспределениеАкцизаСуммаПриИзменении(Элемент)

	ТекущаяСтрока = Элементы.РаспределениеАкциза.ТекущиеДанные;
	СтавкаДиапазона =  ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ДиапазонБанковскихГарантий, "НалоговаяСтавка");
	
	Если СтавкаДиапазона <> 0 Тогда
    	ТекущаяСтрока.Количество=ТекущаяСтрока.Сумма/СтавкаДиапазона;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементов()
	ЗакрытиеБанковскихГарантий = (Объект.Операция = Перечисления.бг_ОперацииРегистрацииБанковскихГарантий.ЗакрытиеБанковскихГарантий);
	
	Элементы.РаспределениеАкцизаДанныеПродукции.Видимость = Не ЗакрытиеБанковскихГарантий;
	Элементы.НалоговаяСтавка.Видимость = Не ЗакрытиеБанковскихГарантий;
	Элементы.ГруппаИтог.Видимость = Не ЗакрытиеБанковскихГарантий;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДиапозону(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ЗаполнитьТабличнуюЧастьРаспределенияАкциза();
				
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличнуюЧастьРаспределенияАкциза()
	
	Объект.РаспределениеАкциза.Очистить();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	бг_БанковскиеГарантии.Ссылка КАК БанковскаяГарантия,
	|	бг_БанковскиеГарантииДиапазон.Поставщик КАК Поставщик,
	|	бг_БанковскиеГарантииДиапазон.НалоговаяСтавка КАК НалоговаяСтавка
	|ИЗ
	|	Справочник.бг_БанковскиеГарантии КАК бг_БанковскиеГарантииДиапазон
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.бг_БанковскиеГарантии КАК бг_БанковскиеГарантии
	|		ПО бг_БанковскиеГарантииДиапазон.Ссылка = бг_БанковскиеГарантии.Родитель
	|ГДЕ
	|	бг_БанковскиеГарантииДиапазон.Ссылка = &Диапазон");
	
	Запрос.УстановитьПараметр("Диапазон", Объект.ДиапазонБанковскихГарантий);
	Результат = Запрос.Выполнить();
	
	Если не Результат.Пустой() Тогда
		
		ТЗБанковскиеГарантии = Результат.Выгрузить();
		Объект.РаспределениеАкциза.Загрузить(ТЗБанковскиеГарантии);
		Объект.НалоговаяСтавка = ТЗБанковскиеГарантии[0].НалоговаяСтавка;	
		Объект.Поставщик = ТЗБанковскиеГарантии[0].Поставщик;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти