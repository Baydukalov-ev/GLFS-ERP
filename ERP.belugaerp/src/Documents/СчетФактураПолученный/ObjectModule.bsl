#Область ОбработчикиСобытий

&После("ПередЗаписью")
Процедура бг_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЭтоНовый() Тогда
		бг_ДатаСоздания = ТекущаяДатаСеанса();
	КонецЕсли;
КонецПроцедуры

&После("ОбработкаЗаполнения")
Процедура бг_ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") 
		Или Не (ДанныеЗаполнения.Свойство("ДокументОснование")
			Или ДанныеЗаполнения.Свойство("ДокументыОснования")) Тогда
			Возврат;
	КонецЕсли;
	бг_ЗаполнитьНомерДатуПоДокументуОснованию();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура бг_ЗаполнитьНомерДатуПоДокументуОснованию()
	Если ДокументыОснования.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	ПервыйДокументОснования = ДокументыОснования[0].ДокументОснование;
	Если Не ЗначениеЗаполнено(ПервыйДокументОснования) Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(ПервыйДокументОснования));
	Если Не Метаданные.Документы.Содержит(ОбъектМетаданных) Тогда
		Возврат;
	КонецЕсли;
	РеквизитОрганизацияНайден = ОбъектМетаданных.Реквизиты.Найти("Организация") <> Неопределено;
	Если РеквизитОрганизацияНайден Тогда
		ОрганизацияДокументаОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПервыйДокументОснования, "Организация");
		Если Не ЗначениеЗаполнено(ОрганизацияДокументаОснования) Тогда
			Возврат;
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;
	
	РеквизитДатаВхДокНайден = Ложь;
	РеквизитНомерВхДокНайден = Ложь;
	СписокРеквизитовСтрокой = "";
	Если бг_КонстантыПовтИсп.ЗначениеКонстанты("АвтоматическоеЗаполнениеДатыСФ", ОрганизацияДокументаОснования) Тогда
		СписокРеквизитовСтрокой = "Дата";
		РеквизитДатаВхДокНайден = ОбъектМетаданных.Реквизиты.Найти("ДатаВходящегоДокумента") <> Неопределено;
		Если РеквизитДатаВхДокНайден Тогда
			СписокРеквизитовСтрокой = СписокРеквизитовСтрокой + ", ДатаВходящегоДокумента";
		КонецЕсли;
	КонецЕсли;
	
	Если бг_КонстантыПовтИсп.ЗначениеКонстанты("АвтоматическоеЗаполнениеНомераСФ", ОрганизацияДокументаОснования) Тогда
		РеквизитНомерВхДокНайден = ОбъектМетаданных.Реквизиты.Найти("НомерВходящегоДокумента") <> Неопределено;
		Если РеквизитНомерВхДокНайден Тогда
			Если ЗначениеЗаполнено(СписокРеквизитовСтрокой) Тогда
				СписокРеквизитовСтрокой = СписокРеквизитовСтрокой + ", НомерВходящегоДокумента";
			Иначе
				СписокРеквизитовСтрокой = "НомерВходящегоДокумента";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(СписокРеквизитовСтрокой) Тогда
		СтруктураПараметров = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПервыйДокументОснования, СписокРеквизитовСтрокой);
		Если СтруктураПараметров.Свойство("Дата") Тогда
			Дата = СтруктураПараметров.Дата;
			Если РеквизитДатаВхДокНайден Тогда
				ДатаСоставления = СтруктураПараметров.ДатаВходящегоДокумента;
			Иначе 
				ДатаСоставления = СтруктураПараметров.Дата;
			КонецЕсли;
		КонецЕсли;
		Если РеквизитНомерВхДокНайден Тогда
			Номер = СтруктураПараметров.НомерВходящегоДокумента;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти
