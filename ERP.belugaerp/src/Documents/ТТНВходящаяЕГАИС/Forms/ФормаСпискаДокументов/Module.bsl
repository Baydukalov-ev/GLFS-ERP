
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	бг_ИзменитьТекстЗапросаСписка();
	бг_ДобавитьЭлементыВСписок();
	бг_ОтключитьПолнотекстовыйПоискСписка();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура бг_ИзменитьТекстЗапросаСписка()
	
	ТекстЗапроса = Список.ТекстЗапроса;
	ТекстЗапроса =  бг_ТекстЗапросаПриходныйОрдерНаТовары()
		+ ТекстЗапроса;
	
	ПозцияСлова = СтрНайти(ТекстЗапроса, СтрШаблон("%1%2%3", Символы.ПС, "ИЗ", Символы.ПС), НаправлениеПоиска.СКонца);
	Если ПозцияСлова > 0 Тогда
		
		ТекстЗапросаДоВставляемойСтроки = Лев(ТекстЗапроса, ПозцияСлова - 1);
		ТекстЗапросаПослеВставляемойСтроки = Сред(ТекстЗапроса, ПозцияСлова);
		ВставляемаяСтрока =
		",
		|ЕСТЬNULL(бг_ПриходныйОрдерНаТовары.Номер, """") КАК бг_ПриходныйОрдерНаТоварыНомер,
		|ЕСТЬNULL(бг_ПриходныйОрдерНаТовары.Ссылка,
		|	Значение(Документ.ПриходныйОрдерНаТовары.ПустаяСсылка)) КАК бг_ПриходныйОрдерНаТовары,
		|бг_РасширенныеСтатусыДокументовЕГАИС.Статус КАК бг_РасширенныйСтатусЕГАИС";
		
		ТекстЗапроса = СтрШаблон(
			"%1%2%3",
			ТекстЗапросаДоВставляемойСтроки,
			ВставляемаяСтрока, 
			ТекстЗапросаПослеВставляемойСтроки);
			
	КонецЕсли;
	
	ПозцияСлова = СтрНайти(ТекстЗапроса, СтрШаблон("%1%2%3", Символы.ПС, "ГДЕ", Символы.ПС), НаправлениеПоиска.СКонца);
	Если ПозцияСлова > 0 Тогда
		
		ТекстЗапросаДоВставляемойСтроки = Лев(ТекстЗапроса, ПозцияСлова - 1);
		ТекстЗапросаПослеВставляемойСтроки = Сред(ТекстЗапроса, ПозцияСлова);
		ВставляемаяСтрока =
		"ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бг_РасширенныеСтатусыДокументовЕГАИС КАК бг_РасширенныеСтатусыДокументовЕГАИС
		|ПО ДокументТТНВходящаяЕГАИС.Ссылка = бг_РасширенныеСтатусыДокументовЕГАИС.Документ
		|ЛЕВОЕ СОЕДИНЕНИЕ бг_ПриходныйОрдерНаТовары КАК бг_ПриходныйОрдерНаТовары
		|ПО ДокументТТНВходящаяЕГАИС.ДокументОснование = бг_ПриходныйОрдерНаТовары.Распоряжение";
		
		ТекстЗапроса = СтрШаблон(
			"%1%2%3",
			ТекстЗапросаДоВставляемойСтроки,
			ВставляемаяСтрока, 
			ТекстЗапросаПослеВставляемойСтроки);
			
	КонецЕсли;
	
	Список.ТекстЗапроса = ТекстЗапроса;	

КонецПроцедуры

&НаСервере
Процедура бг_ДобавитьЭлементыВСписок()

	КолонкаПроцентСборки = Элементы.Вставить(
		"бг_РасширенныйСтатусЕГАИС",
		Тип("ПолеФормы"),
		Элементы.Список,
		Элементы.СписокДальнейшееДействиеЕГАИС); 
	КолонкаПроцентСборки.Вид = ВидПоляФормы.ПолеНадписи;
	КолонкаПроцентСборки.Заголовок = НСтр("ru='Статус приемки'");
	КолонкаПроцентСборки.ПутьКДанным = "Список.бг_РасширенныйСтатусЕГАИС";

	КолонкаПриходныйОрдерНаТоварыНомер = Элементы.Вставить(
		"бг_ПриходныйОрдерНаТоварыНомер",
		Тип("ПолеФормы"),
		Элементы.Список,
		Элементы.СписокДальнейшееДействиеЕГАИС);
	КолонкаПриходныйОрдерНаТоварыНомер.Вид = ВидПоляФормы.ПолеНадписи;
	КолонкаПриходныйОрдерНаТоварыНомер.Заголовок = НСтр("ru='Номер приходного ордера'");
	КолонкаПриходныйОрдерНаТоварыНомер.ПутьКДанным = "Список.бг_ПриходныйОрдерНаТоварыНомер";

	КолонкаПриходныйОрдерНаТовары = Элементы.Вставить(
		"бг_ПриходныйОрдерНаТовары",
		Тип("ПолеФормы"),
		Элементы.Список,
		Элементы.СписокГруппаПоступление);
	КолонкаПриходныйОрдерНаТовары.Вид = ВидПоляФормы.ПолеНадписи;
	КолонкаПриходныйОрдерНаТовары.Заголовок = НСтр("ru='Приходный ордер'");
	КолонкаПриходныйОрдерНаТовары.ПутьКДанным = "Список.бг_ПриходныйОрдерНаТовары";

КонецПроцедуры

&НаСервере
Функция бг_ТекстЗапросаПриходныйОрдерНаТовары()

	Возврат "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(ПриходныйОрдерНаТовары.Ссылка) КАК Ссылка,
	|	ПриходныйОрдерНаТовары.Распоряжение КАК Распоряжение,
	|	МАКСИМУМ(ПриходныйОрдерНаТовары.Номер) КАК Номер
	|ПОМЕСТИТЬ бг_ПриходныйОрдерНаТовары
	|ИЗ
	|	Документ.ПриходныйОрдерНаТовары КАК ПриходныйОрдерНаТовары
	|ГДЕ
	|	ПриходныйОрдерНаТовары.Проведен
	|	И НЕ ПриходныйОрдерНаТовары.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	ПриходныйОрдерНаТовары.Распоряжение
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение
	|;";
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура бг_ОтключитьПолнотекстовыйПоискСписка()

 	бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ОтключитьПолнотекстовыйПоискСпискаФормы(
		Элементы.Список);
	
КонецПроцедуры

#КонецОбласти
