
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	бг_ДобавитьЭлементыОтраженияВРеглУчете();
	
	бг_ДобавитьКомандуФормированияСерийПриходуемогоТовара();
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.ВидДеятельностиНДС = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура бг_ТоварыНоменклатураСписаниеПриИзмененииПосле(Элемент)
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущиеДанные.НоменклатураОприходование = ТекущиеДанные.Номенклатура;
		ТоварыНоменклатураОприходованиеПриИзменении(Элементы.ТоварыНоменклатураОприходование);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура бг_СформироватьСерииПриходуемогоТовара(Команда)
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ТекстСообщения = НСтр("ru = 'Перед формированием серий необходимо записать документ'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	бг_СформироватьСерииПриходуемогоТовараНаСервере();
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура бг_ДобавитьЭлементыОтраженияВРеглУчете()
	
	бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_НеРегистрироватьКОтражениюВРеглУчете",
		Элементы.ГруппаИнформация,
		"Объект.бг_НеРегистрироватьКОтражениюВРеглУчете",
		"ПолеФормы",
		Элементы.ГруппаСостояниеЭДО,
		"ПолеФлажка");
	
КонецПроцедуры

&НаСервере
Процедура бг_ДобавитьКомандуФормированияСерийПриходуемогоТовара()
	ИмяКоманды       = "бг_СформироватьСерииПриходуемогоТовара";
	ЗаголовокКоманды = НСтр("ru = 'Сформировать серии приходуемого товара'");
	бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьКнопкуНаФорму(
		ЭтотОбъект,
		ИмяКоманды,
		Элементы.ТоварыКоманднаяПанель,
		ЗаголовокКоманды,
		ИмяКоманды,
		ИмяКоманды, ,
		ВидКнопкиФормы.КнопкаКоманднойПанели);
КонецПроцедуры

&НаСервере
Процедура бг_СформироватьСерииПриходуемогоТовараНаСервере()
	ДанныеСерийСписываемогоТовара = бг_ДанныеСерийСписываемогоТовара();
	ВидыПриходуемойНоменклатуры   = бг_ВидыПриходуемойНоменклатуры();
	
	КэшированныеЗначения = Неопределено;
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьСериюРассчитатьСтатус",
			Новый Структура("Склад, ПараметрыУказанияСерий", Объект.Склад, ПараметрыУказанияСерий));
			
	Для Каждого СтрокаТовар Из Объект.Товары Цикл
		Если СтрокаТовар.СтатусУказанияСерийОприходование = 0
			Или ЗначениеЗаполнено(СтрокаТовар.СерияОприходование) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаТовар.Серия) Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаТовар.Номенклатура <> СтрокаТовар.НоменклатураОприходование Тогда
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'В строке %1 списка ""Товары"" списываемая и приходуемая номенклатура должна совпадать'"),
				СтрокаТовар.НомерСтроки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
			Продолжить;
		КонецЕсли;
		
		ДанныеЗаполненияСерии = ДанныеСерийСписываемогоТовара[СтрокаТовар.Серия];
		ДанныеЗаполненияСерии.Вставить("бг_Номенклатура", СтрокаТовар.НоменклатураОприходование);
		ДанныеЗаполненияСерии.Вставить("бг_ДокументВыпуска", Объект.Ссылка);
		
		СтрокаТовар.СерияОприходование = бг_СерииНоменклатуры.СоздатьСерию(
						ВидыПриходуемойНоменклатуры[СтрокаТовар.НоменклатураОприходование], ДанныеЗаполненияСерии);
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТовар, СтруктураДействий, КэшированныеЗначения);
		
		бг_ЗарегистрироватьСоответствиеНоменклатурыЕГАИС(
						СтрокаТовар.НоменклатураОприходование,
						СтрокаТовар.СерияОприходование,
						ДанныеЗаполненияСерии.Справка2ЕГАИС,
						ДанныеЗаполненияСерии.АлкогольнаяПродукция);
		
		Если ЗначениеЗаполнено(ДанныеЗаполненияСерии.НомерГТД) Тогда
			СтрокаТовар.НомерГТД = ДанныеЗаполненияСерии.НомерГТД;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция бг_ДанныеСерийСписываемогоТовара()
	ДанныеСерий = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СерииНоменклатуры.Ссылка КАК Серия,
	|	СерииНоменклатуры.ДатаПроизводства КАК ДатаПроизводства,
	|	СерииНоменклатуры.бг_СтранаНазначения КАК бг_СтранаНазначения,
	|	СерииНоменклатуры.бг_Экспорт КАК бг_Экспорт,
	|	СерииНоменклатуры.ГоденДо КАК ГоденДо,
	|	СерииНоменклатуры.бг_УпаковкаПаллета КАК бг_УпаковкаПаллета,
	|	СерииНоменклатуры.ПроизводительЕГАИС КАК ПроизводительЕГАИС,
	|	СерииНоменклатуры.бг_ОрганизацияВладелец КАК бг_ОрганизацияВладелец,
	|	СерииНоменклатуры.бг_ОрганизацияЕГАИСВладелец КАК бг_ОрганизацияЕГАИСВладелец,
	|	СерииНоменклатуры.Справка2ЕГАИС КАК Справка2ЕГАИС,
	|	СерииНоменклатуры.бг_Сертификат КАК бг_Сертификат,
	|	СерииНоменклатуры.Справка2ЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СерииНоменклатуры.бг_Номенклатура.ВестиУчетПоГТД, ЛОЖЬ)
	|			ТОГДА ЕСТЬNULL(Справки1ЕГАИС.бг_НомерГТД, НЕОПРЕДЕЛЕНО)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НомерГТД
	|ИЗ
	|	Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Справки2ЕГАИС КАК Справки2ЕГАИС
	|		ПО СерииНоменклатуры.Справка2ЕГАИС = Справки2ЕГАИС.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Справки1ЕГАИС КАК Справки1ЕГАИС
	|		ПО (Справки2ЕГАИС.Справка1 = Справки1ЕГАИС.Ссылка)
	|ГДЕ
	|	СерииНоменклатуры.Ссылка В(&Серии)";
	Запрос.УстановитьПараметр("Серии", Объект.Товары.Выгрузить(, "Серия").ВыгрузитьКолонку("Серия"));
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеСерии = Новый Структура;
		Для Каждого КолонкаРезультат Из Результат.Колонки Цикл
			ДанныеСерии.Вставить(КолонкаРезультат.Имя, Выборка[КолонкаРезультат.Имя]);
		КонецЦикла;
		
		ДанныеСерий.Вставить(Выборка.Серия, ДанныеСерии);
	КонецЦикла;
	
	Возврат ДанныеСерий;
КонецФункции

&НаСервере
Функция бг_ВидыПриходуемойНоменклатуры()
	НоменклатураПриход =
		Объект.Товары.Выгрузить(, "НоменклатураОприходование").ВыгрузитьКолонку("НоменклатураОприходование");
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъектов(НоменклатураПриход, "ВидНоменклатуры");
КонецФункции

&НаСервере
Процедура бг_ЗарегистрироватьСоответствиеНоменклатурыЕГАИС(Номенклатура, Серия, Справка2, АлкогольнаяПродукция)
	СоответствиеНоменклатурыЕГАИС = РегистрыСведений.СоответствиеНоменклатурыЕГАИС.СоздатьМенеджерЗаписи();
	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция = АлкогольнаяПродукция;
	СоответствиеНоменклатурыЕГАИС.Номенклатура         = Номенклатура;
	СоответствиеНоменклатурыЕГАИС.Серия                = Серия;
	СоответствиеНоменклатурыЕГАИС.Справка2             = Справка2;
	СоответствиеНоменклатурыЕГАИС.Записать();
КонецПроцедуры

#КонецОбласти
