#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДействияПриОбменеЕГАИС

Функция СтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция) Экспорт
	
	Если Операция = Перечисления.ВидыДокументовЕГАИС.бг_ОтчетОПроизводствеЕГАИС Тогда
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусыКПередаче(
			ДокументСсылка,
			Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.КПередаче);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.бг_ЗапросНаОтменуПроведенияОтчетаОПроизводстве Тогда
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусыКПередаче(
			ДокументСсылка,
			Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ЗапросНаОтменуПроведенияКПередаче);
	
	Иначе
		ВызватьИсключение ИнтеграцияИС.ТекстИсключенияОбработкиСтатуса(ДокументСсылка, Операция);
	КонецЕсли;
	
	Возврат ПараметрыОбновления;
	
КонецФункции

Функция СтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки) Экспорт
	
	Если СтатусОбработки = Неопределено Тогда
		СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПереданоВУТМ;
	КонецЕсли;
	
	Если Операция = Перечисления.ВидыДокументовЕГАИС.бг_ОтчетОПроизводствеЕГАИС Тогда
		
		СтатусыБазовыйПроцесс = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
		
		СтатусыБазовыйПроцесс.Принят = Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ПереданВУТМ;
		СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПолученЕГАИС);
		СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПроведенЕГАИС);
		СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеУведомленияОРегистрацииДвижения);
		
		СтатусыБазовыйПроцесс.Ошибка = Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ОшибкаПередачи;
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПередайтеДанные);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, СтатусыБазовыйПроцесс);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.бг_ЗапросНаОтменуПроведенияОтчетаОПроизводстве Тогда
		
		СтатусыБазовыйПроцесс = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
		
		СтатусыБазовыйПроцесс.Принят = Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ЗапросНаОтменуПроведенияПереданВУТМ;
		СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПолученЕГАИС);
		СтатусыБазовыйПроцесс.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПроведенЕГАИС);
		
		СтатусыБазовыйПроцесс.Ошибка = Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ЗапросНаОтменуПроведенияОшибка;
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ЗапроситеОтменуПроведения);
		СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОтменитеОперацию);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, СтатусыБазовыйПроцесс);
		
	Иначе
		ВызватьИсключение ИнтеграцияИС.ТекстИсключенияОбработкиСтатуса(ДокументСсылка, Операция);
	КонецЕсли;
	
	Возврат ПараметрыОбновления;
	
КонецФункции

Функция СтатусПослеПолученияДанных(ДокументСсылка, Операция, ДополнительныеПараметры = Неопределено) Экспорт
	
	СтатусыБазовыйПроцесс = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
	СтатусыБазовыйПроцесс.Принят           = Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ПроведенЕГАИС;
	СтатусыБазовыйПроцесс.Обрабатывается   = Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ОбрабатываетсяЕГАИС;
	СтатусыБазовыйПроцесс.ОшибкаПроведения = Неопределено;
	СтатусыБазовыйПроцесс.Ошибка           = Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ОшибкаПередачи;
	СтатусыБазовыйПроцесс.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПередайтеДанные);
	СтатусыБазовыйПроцесс.УведомлениеОРегистрацииДвижения = Ложь;
	
	СтатусыЗапросНаОтменуПроведения = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
	СтатусыЗапросНаОтменуПроведения.Принят           = Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.Отменен;
	СтатусыЗапросНаОтменуПроведения.Обрабатывается   = Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ЗапросНаОтменуПроведенияОбрабатываетсяЕГАИС;
	СтатусыЗапросНаОтменуПроведения.ОтменаПроведения = Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.Отменен;
	СтатусыЗапросНаОтменуПроведения.ОшибкаПроведения = Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ЗапросНаОтменуПроведенияОшибка;
	СтатусыЗапросНаОтменуПроведения.Ошибка           = Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ЗапросНаОтменуПроведенияОшибка;
	СтатусыЗапросНаОтменуПроведения.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ЗапроситеОтменуПроведения);
	СтатусыЗапросНаОтменуПроведения.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОтменитеОперацию);
	СтатусыЗапросНаОтменуПроведения.УведомлениеОРегистрацииДвижения = Ложь;
	
	ВыполнитьРасчетТекущегоСостояния = Истина;
	Если ДополнительныеПараметры <> Неопределено
		И ДополнительныеПараметры.Свойство("ТекущееСостояние")
		И ДополнительныеПараметры.ТекущееСостояние <> Неопределено Тогда
		ВыполнитьРасчетТекущегоСостояния = ДополнительныеПараметры.ТекущееСостояние;
	КонецЕсли;
	
	Если Операция = Перечисления.ВидыДокументовЕГАИС.бг_УведомлениеОРегистрацииДвиженияОтчетаОбИмпортеПроизводстве Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовЕГАИС.СтруктураСтатусы();
		Статусы.Принят = Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.бг_Подтвержден;
		Статусы.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.НеТребуется);
		Статусы.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ЗапроситеОтменуПроведения);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусы(
		ДокументСсылка,
		Перечисления.СтатусыОбработкиСообщенийЕГАИС.ПринятИзЕГАИС,
		Статусы);
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияПолученЕГАИС Тогда
		
		Статусы = Неопределено;
		Если ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыДокументовЕГАИС.бг_ОтчетОПроизводствеЕГАИС Тогда
			
			Статусы = СтатусыБазовыйПроцесс;
			
		ИначеЕсли ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыДокументовЕГАИС.бг_ЗапросНаОтменуПроведенияОтчетаОПроизводстве Тогда
			
			Статусы = СтатусыЗапросНаОтменуПроведения;
			
		Иначе
			ВызватьИсключение ИнтеграцияИС.ТекстИсключенияОбработкиСтатуса(ДокументСсылка, Операция);
		КонецЕсли;
		
		Если Статусы <> Неопределено Тогда
			ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусыПриПолученииКвитанции(
			ДокументСсылка,
			"КвитанцияПолученЕГАИС", ДополнительныеПараметры.СтатусОбработки,
			Статусы, ВыполнитьРасчетТекущегоСостояния);
		КонецЕсли;
		
	ИначеЕсли Операция = Перечисления.ВидыДокументовЕГАИС.КвитанцияПроведенЕГАИС Тогда
		
		Статусы = Неопределено;
		Если ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыДокументовЕГАИС.бг_ОтчетОПроизводствеЕГАИС Тогда
			
			Статусы = СтатусыБазовыйПроцесс;
			
		ИначеЕсли ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыДокументовЕГАИС.бг_ЗапросНаОтменуПроведенияОтчетаОПроизводстве Тогда
			
			Статусы = СтатусыЗапросНаОтменуПроведения;
			
		Иначе
			ВызватьИсключение ИнтеграцияИС.ТекстИсключенияОбработкиСтатуса(ДокументСсылка, Операция);
		КонецЕсли;
		
		Если Статусы <> Неопределено Тогда
			СтатусОбработки = ДополнительныеПараметры.СтатусОбработки;
			
			Если ДополнительныеПараметры.ОперацияКвитанции = Перечисления.ВидыДокументовЕГАИС.бг_ЗапросНаОтменуПроведенияОтчетаОПроизводстве
				И ДополнительныеПараметры.СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийЕГАИС.ДокументПроведен Тогда
				СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийЕГАИС.ДокументОтменен;
			КонецЕсли;
			
			ПараметрыОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.РассчитатьСтатусыПриПолученииКвитанции(
			ДокументСсылка,
			"КвитанцияПолученЕГАИС", СтатусОбработки,
			Статусы, ВыполнитьРасчетТекущегоСостояния);
		КонецЕсли;
		
	Иначе
		ВызватьИсключение ИнтеграцияИС.ТекстИсключенияОбработкиСтатуса(ДокументСсылка, Операция);
	КонецЕсли;
	
	Возврат ПараметрыОбновления;
	
КонецФункции

Функция ОбновитьСтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыОбновления = СтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция);
	
	Если ПараметрыОбновления = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НовыйСтатусПослеОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.ОбновитьСтатус(
		ДокументСсылка,
		ПараметрыОбновления, ДополнительныеПараметры);
	
	Возврат НовыйСтатусПослеОбновления;
	
КонецФункции

Функция ОбновитьСтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыОбновления = СтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки);
	
	Если ПараметрыОбновления = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НовыйСтатусПослеОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.ОбновитьСтатус(
		ДокументСсылка,
		ПараметрыОбновления, ДополнительныеПараметры);
	
	Возврат НовыйСтатусПослеОбновления;
	
КонецФункции

Функция ОбновитьСтатусПослеПолученияДанных(ДокументСсылка, Операция, ДополнительныеПараметры = Неопределено) Экспорт
	ТекущийСтатус = РегистрыСведений.СтатусыДокументовЕГАИС.ТекущееСостояние(ДокументСсылка).Статус;
	ТекущийДокументПроведенВЕГАИС = ТекущийСтатус = Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.бг_Подтвержден;
	
	СписокДокументовПослеПроведенияЕГАИС = Новый Массив;
	СписокДокументовПослеПроведенияЕГАИС.Добавить(
		Перечисления.ВидыДокументовЕГАИС.бг_ЗапросНаОтменуПроведенияОтчетаОПроизводстве);
	
	Если ТекущийДокументПроведенВЕГАИС
		И СписокДокументовПослеПроведенияЕГАИС.Найти(Операция) = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыОбновления = СтатусПослеПолученияДанных(ДокументСсылка, Операция, ДополнительныеПараметры);
	
	Если ПараметрыОбновления = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НовыйСтатусПослеОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.ОбновитьСтатус(
		ДокументСсылка,
		ПараметрыОбновления, ДополнительныеПараметры);
	
	Возврат НовыйСтатусПослеОбновления;
	
КонецФункции

Функция ОбновитьСтатус(ДокументСсылка, ПараметрыОбновления, ДополнительныеПараметры) Экспорт
	
	НовыйСтатусПослеОбновления = РегистрыСведений.СтатусыДокументовЕГАИС.ОбновитьСтатус(
		ДокументСсылка,
		ПараметрыОбновления, ДополнительныеПараметры);
	
	Возврат НовыйСтатусПослеОбновления;
	
КонецФункции

Функция ПоследовательностьОпераций(ДокументСсылка) Экспорт
	
	Таблица = ИнтеграцияЕГАИС.ПустаяТаблицаПоследовательностьОпераций();
	
	Исходящий = Перечисления.ТипыЗапросовИС.Исходящий;
	Входящий  = Перечисления.ТипыЗапросовИС.Входящий;
	
	ИнтеграцияЕГАИС.ДобавитьОперациюВПоследовательность(Таблица,
		0,
		Исходящий,
		Перечисления.ВидыДокументовЕГАИС.бг_ОтчетОПроизводствеЕГАИС, ДокументСсылка);
	ИнтеграцияЕГАИС.ДобавитьОперациюВПоследовательность(Таблица,
		0,
		Входящий,
		Перечисления.ВидыДокументовЕГАИС.бг_УведомлениеОРегистрацииДвиженияОтчетаОбИмпортеПроизводстве);
	ИнтеграцияЕГАИС.ДобавитьОперациюВПоследовательность(Таблица,
		0,
		Исходящий,
		Перечисления.ВидыДокументовЕГАИС.бг_ЗапросНаОтменуПроведенияОтчетаОПроизводстве, ДокументСсылка);
	
	Возврат Таблица;
	
КонецФункции

Процедура ПриИзмененииСтатусаДокумента(ДокументСсылка, ПредыдущийСтатус, НовыйСтатус, ПараметрыОбновленияСтатуса) Экспорт
	
	ИнтеграцияЕГАИСПереопределяемый.ПриИзмененииСтатусаДокумента(ДокументСсылка, ПредыдущийСтатус, НовыйСтатус, ПараметрыОбновленияСтатуса);
	ПриИзмененииСтатусаДокументаОбработатьСправки1(ДокументСсылка, ПредыдущийСтатус, НовыйСтатус);
	
КонецПроцедуры

#КонецОбласти

#Область Статусы

Функция СтатусПоУмолчанию() Экспорт
	
	Возврат Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.КПередаче;
	
КонецФункции

// Возвращает статусы ошибок.
//
// Возвращаемое значение:
//  Массив - Статусы ошибок.
//
Функция СтатусыОшибок() Экспорт
	
	Статусы = Новый Массив;
	
	Статусы.Добавить(Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ОшибкаПередачи);
	Статусы.Добавить(Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ОшибкаПроведенияЕГАИС);
	Статусы.Добавить(Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ЗапросНаОтменуПроведенияОшибка);
	
	Возврат Статусы;
	
КонецФункции

// Возвращает конечные статусы.
//
// Возвращаемое значение:
//  Массив - Конечные статусы.
//
Функция КонечныеСтатусы(ТребуетсяПовторноеОформление = Истина) Экспорт
	
	Статусы = Новый Массив;
	
	Статусы.Добавить(Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.Отменен);
	Статусы.Добавить(Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.ОшибкаПроведенияЕГАИС);
	
	Возврат Статусы;
	
КонецФункции

// Возвращает дальнейшее действие по умолчанию.
// 
// Возвращаемое значение:
//  Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Дальнейшее действие по-умолчанию.
//
Функция ДальнейшееДействиеПоУмолчанию() Экспорт
	
	Возврат Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПередайтеДанные;
	
КонецФункции

Функция СтатусыДвижений()
	
	Результат = Новый Массив;
	Результат.Добавить(Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.бг_Подтвержден);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПанельОбменСЕГАИС

// Возвращает массив дальнейших действий с документом, требующих участия пользователя
// 
// Возвращаемое значение:
// 	Массив из ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - дальшейшие действия
//
Функция ВсеТребующиеДействия() Экспорт
	
	МассивДействий = Новый Массив;
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПередайтеДанные);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ЗапроситеОтменуПроведения);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ВыполнитеОбмен);
	
	Возврат МассивДействий;
	
КонецФункции

Функция ВсеТребующиеОжидания() Экспорт
	
	МассивДействий = Новый Массив;
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПередачуДанныхРегламентнымЗаданием);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПолученЕГАИС);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеКвитанцииПроведенЕГАИС);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОжидайтеПолучениеУведомленияОРегистрацииДвижения);
	
	Возврат МассивДействий;
	
КонецФункции

// Возвращает текст запроса для получения количества документов для отработки
// 
// Возвращаемое значение:
//  Строка - Текст запроса
//
Функция ТекстЗапросаПанельОбменСЕГАИСОформите() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(ЭтапПроизводства.Ссылка) КАК КоличествоДокументов
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.битОтчетОПроизводствеЕГАИС КАК битОтчетОПроизводствеЕГАИС
	|		ПО ЭтапПроизводства.Ссылка = битОтчетОПроизводствеЕГАИС.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК Классификатор
	|		ПО ЭтапПроизводства.Организация = Классификатор.Контрагент
	|ГДЕ
	|	битОтчетОПроизводствеЕГАИС.Ссылка ЕСТЬ NULL
	|	И ЭтапПроизводства.Проведен = ИСТИНА
	|	И (Классификатор.Ссылка В (&ОрганизацияЕГАИС)
	|			ИЛИ &БезОтбораПоОрганизацииЕГАИС)
	|	И ЭтапПроизводства.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)";
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса для получения количества документов для отработки
// 
// Возвращаемое значение:
//  Строка - Текст запроса
//
Функция ТекстЗапросаПанельОбменСЕГАИСОтработайте() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СтатусыДокументовЕГАИС.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.битОтчетОПроизводствеЕГАИС КАК ОтчетОПроизводствеЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ОтчетОПроизводствеЕГАИС.Ссылка
	|ГДЕ
	|	ОтчетОПроизводствеЕГАИС.Ссылка ЕСТЬ НЕ NULL 
	|	И НЕ ОтчетОПроизводствеЕГАИС.ПометкаУдаления
	|	И СтатусыДокументовЕГАИС.ДальнейшееДействие1 В(&ВсеТребующиеДействия)
	|	И (ОтчетОПроизводствеЕГАИС.ОрганизацияЕГАИС В (&ОрганизацияЕГАИС)
	|			ИЛИ &БезОтбораПоОрганизацииЕГАИС)
	|	И (ОтчетОПроизводствеЕГАИС.Ответственный = &Ответственный
	|			ИЛИ &Ответственный = НЕОПРЕДЕЛЕНО)";
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса для получения количества документов, находящихся в состоянии ожидания
// 
// Возвращаемое значение:
//  Строка - Текст запроса
//
Функция ТекстЗапросаПанельОбменСЕГАИСОжидайте() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СтатусыДокументовЕГАИС.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.битОтчетОПроизводствеЕГАИС КАК ОтчетОПроизводствеЕГАИС
	|		ПО СтатусыДокументовЕГАИС.Документ = ОтчетОПроизводствеЕГАИС.Ссылка
	|ГДЕ
	|	ОтчетОПроизводствеЕГАИС.Ссылка ЕСТЬ НЕ NULL 
	|	И НЕ ОтчетОПроизводствеЕГАИС.ПометкаУдаления
	|	И СтатусыДокументовЕГАИС.ДальнейшееДействие1 В(&ВсеТребующиеОжидания)
	|	И (ОтчетОПроизводствеЕГАИС.ОрганизацияЕГАИС В (&ОрганизацияЕГАИС)
	|			ИЛИ &БезОтбораПоОрганизацииЕГАИС)
	|	И (ОтчетОПроизводствеЕГАИС.Ответственный = &Ответственный
	|			ИЛИ &Ответственный = НЕОПРЕДЕЛЕНО)";
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Отчеты.бг_ДвижениеМарок.ДобавитьКомандуДвижениеМарокПоДокументу(КомандыОтчетов);
	
КонецПроцедуры

#Область СообщенияЕГАИС

// Сообщение к передаче XML
//
// Параметры:
//  ДокументСсылка - ДокументСсылка - Ссылка на документ.
//  ДальнейшееДействие - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюЕГАИС - Операция ЕГАИС.
// 
// Возвращаемое значение:
//  Строка - Текст сообщения XML
//
Функция СообщениеКПередачеXML(ДокументСсылка, ДальнейшееДействие, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПередайтеДанные Тогда
		
		Возврат СообщениеЗапросаОтчетОПроизводствеXML(ДокументСсылка);
		
	ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ЗапроситеОтменуПроведения Тогда
		
		Возврат ЗапросНаОтменуПроведенияXML(ДокументСсылка);
		
	КонецЕсли;
	
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Интерфейс для отложенной обработки
//
// Параметры:
//   Документ - ДокументСсылка.битОтчетОПроизводствеЕГАИС
//   ВариантОбработки - ПеречислениеСсылка.бг_ВариантыОтложеннойОбработкиОбъектов, Неопределено - вариант обработки
//   Отказ - Булево - отказ от обработки
//
Процедура бг_ОтложеннаяОбработкаОбъекта(Документ, ВариантОбработки, Отказ,
	ДополнительныеСведения = Неопределено) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ВариантОбработки = Перечисления.бг_ВариантыОтложеннойОбработкиОбъектов.ОтправитьЗапросыСправки1ЕГАИС Тогда
		бг_ИнтеграцияЕГАИС.ОтправитьЗапросыСправок1ВУТМ(Документ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаОстаткиАлкогольнойПродукцииЕГАИС(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДвижениеМарокЕГАИС(Запрос, ТекстыЗапроса, Регистры);
	
	ИнтеграцияЕГАИС.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеШапки.Дата КАК Период,
	|	ДанныеШапки.Ссылка КАК Ссылка,
	|	ДанныеШапки.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	СтатусыДокументовЕГАИС.Статус КАК СтатусОбработки,
	|	ДанныеШапки.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	Документ.битОтчетОПроизводствеЕГАИС КАК ДанныеШапки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО (СтатусыДокументовЕГАИС.Документ = ДанныеШапки.Ссылка)
	|ГДЕ
	|	ДанныеШапки.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",                  Реквизиты.Период);
	Запрос.УстановитьПараметр("Ссылка",                  Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование",       Реквизиты.ДокументОснование);
	Запрос.УстановитьПараметр("ОрганизацияЕГАИС",        Реквизиты.ОрганизацияЕГАИС);
	Запрос.УстановитьПараметр("ТекущийСтатус",           Реквизиты.СтатусОбработки);
	
	СтатусыДвиженияМарок = Перечисления.бг_СтатусыАкцизныхМарок.СтатусыПоОперации(Метаданные.Документы.битОтчетОПроизводствеЕГАИС.Имя);
	Запрос.УстановитьПараметр("СтатусПодтвержденОтчетЕГАИС", СтатусыДвиженияМарок.ПодтвержденОтчетЕГАИС);
	
	СтатусыДвиженияМарокЭтапПроизводства = Перечисления.бг_СтатусыАкцизныхМарок.СтатусыПоОперации(Метаданные.Документы.ЭтапПроизводства2_2.Имя);
	Запрос.УстановитьПараметр("СтатусЭтапПроизводстваЗавершен", СтатусыДвиженияМарокЭтапПроизводства.ЭтапПроизводстваЗавершен);
	
	Запрос.УстановитьПараметр("СтатусыДвижений", СтатусыДвижений());
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаОстаткиАлкогольнойПродукцииЕГАИС(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОстаткиАлкогольнойПродукцииЕГАИС";
	
	Если Не ИнтеграцияЕГАИС.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ИнтеграцияЕГАИС.ЕстьТаблицаЗапроса("ВТТовары", ТекстыЗапроса) Тогда
		ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ИнтеграцияЕГАИС.ЕстьТаблицаЗапроса("ВТСырьё", ТекстыЗапроса) Тогда
		ТекстЗапросаВТСырьё(Запрос, ТекстыЗапроса);
	КонецЕсли;

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ТаблицаТовары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТаблицаТовары.Справка2 КАК Справка2,
	|	ТаблицаТовары.Количество КАК СвободныйОстаток,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ВТТовары КАК ТаблицаТовары
	|ГДЕ
	|	&ТекущийСтатус В (&СтатусыДвижений)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Период,
	|	&ОрганизацияЕГАИС,
	|	ТаблицаСырьё.АлкогольнаяПродукция,
	|	ТаблицаСырьё.Справка2,
	|	ТаблицаСырьё.Количество,
	|	ТаблицаСырьё.Количество,
	|	ТаблицаСырьё.НомерСтроки
	|ИЗ
	|	ВТСырьё КАК ТаблицаСырьё
	|ГДЕ
	|	&ТекущийСтатус В (&СтатусыДвижений)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВТТовары";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка               КАК Ссылка,
	|	ТаблицаТовары.НомерСтроки          КАК НомерСтроки,
	|	ТаблицаТовары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТаблицаТовары.Количество           КАК Количество,
	|	ТаблицаТовары.Справка2             КАК Справка2
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ
	|	Документ.битОтчетОПроизводствеЕГАИС.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТСырьё(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВТСырьё";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаСырьё.Ссылка               КАК Ссылка,
	|	ТаблицаСырьё.НомерСтроки          КАК НомерСтроки,
	|	ТаблицаСырьё.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ТаблицаСырьё.Количество           КАК Количество,
	|	ТаблицаСырьё.Справка2             КАК Справка2
	|ПОМЕСТИТЬ ВТСырьё
	|ИЗ
	|	Документ.битОтчетОПроизводствеЕГАИС.Сырьё КАК ТаблицаСырьё
	|ГДЕ
	|	ТаблицаСырьё.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвижениеМарокЕГАИС(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "бг_ДвижениеМарок";
	
	Если НЕ ИнтеграцияЕГАИС.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	бг_ДвижениеМарок.ГУИДМарки КАК ГУИДМарки,
	|	бг_ДвижениеМарок.Серия КАК Серия,
	|	бг_ДвижениеМарок.КодУпаковки КАК КодУпаковки,
	|	&СтатусПодтвержденОтчетЕГАИС КАК СтатусМарки
	|ИЗ
	|	РегистрСведений.бг_ДвижениеМарок КАК бг_ДвижениеМарок
	|ГДЕ
	|	&ТекущийСтатус В (&СтатусыДвижений)
	|	И бг_ДвижениеМарок.Регистратор = &ДокументОснование
	|	И бг_ДвижениеМарок.СтатусМарки = &СтатусЭтапПроизводстваЗавершен";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область СообщенияЕГАИС

Функция СообщениеЗапросаОтчетОПроизводствеXML(ДокументСсылка)
	
	Операция = Перечисления.ВидыДокументовЕГАИС.бг_ОтчетОПроизводствеЕГАИС;

	ДанныеДляЗапросаОтчетОПроизводстве = ДанныеДляЗапросаОтчетОПроизводстве(ДокументСсылка);
	
	Если ДанныеДляЗапросаОтчетОПроизводстве.Организация = Неопределено Тогда
		Возврат 
			СообщениеЗапросаОтчетОПроизводствеXMLСОшибкой(
				Операция, 
				ДокументСсылка, 
				НСтр(
					"ru = 'Нет данных для выгрузки.';
					|en = 'Нет данных для выгрузки.'"));
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеДляЗапросаОтчетОПроизводстве.ОрганизацияЕГАИС) Тогда
		Возврат 
			СообщениеЗапросаОтчетОПроизводствеXMLСОшибкой(
				Операция, 
				ДокументСсылка, 
				НСтр(
					"ru = 'Не сопоставлена организация ЕГАИС.';
					|en = 'Не сопоставлена организация ЕГАИС.'"));
	КонецЕсли;
	
	ДанныеОрганизацииЕГАИС = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ДанныеДляЗапросаОтчетОПроизводстве.ОрганизацияЕГАИС,
		"Код");
		
	ИдентификаторФСРАР = ДанныеОрганизацииЕГАИС.Код;
	ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V5;
	
	ПространствоИмен = Перечисления.ВидыДокументовЕГАИС.ПространствоИмен(Операция, ФорматОбмена);
	ИмяТипа          = Перечисления.ВидыДокументовЕГАИС.ТипЕГАИС(Операция, ФорматОбмена);
	
	Если ПространствоИмен = Неопределено Или ИмяТипа = Неопределено Тогда
		Возврат 
			СообщениеЗапросаОтчетОПроизводствеXMLСОшибкой(
				Операция, 
				ДокументСсылка,
				СтрШаблон(
					НСтр(
						"ru = 'Операция не поддерживается в версии формата обмена: %1.';
						|en = 'Операция не поддерживается в версии формата обмена: %1.'"),
					ФорматОбмена));
	КонецЕсли;
	
	СообщениеXML = ИнтеграцияЕГАИС.СтруктураСообщенияXML();
	СообщениеXML.Описание = ИнтеграцияЕГАИС.ОписаниеОперацииПередачиДанных(Операция, ДокументСсылка); //НомерВерсии
	
	RepProduced = RepProduced(
			ПространствоИмен, 
			Строка(ДокументСсылка.УникальныйИдентификатор()),
			ДанныеДляЗапросаОтчетОПроизводстве,
			СообщениеXML);
		
	ТекстСообщенияXML = ИнтеграцияЕГАИС.ОбъектXDTOВXML(RepProduced, ИдентификаторФСРАР, ПространствоИмен, ИмяТипа);
	
	СообщениеXML.ТекстСообщенияXML = ТекстСообщенияXML;
	СообщениеXML.ТипСообщения      = Перечисления.ТипыЗапросовИС.Исходящий;
	СообщениеXML.ОрганизацияЕГАИС  = ДанныеДляЗапросаОтчетОПроизводстве.ОрганизацияЕГАИС;
	СообщениеXML.Операция          = Операция;
	СообщениеXML.ФорматОбмена      = ФорматОбмена;
	СообщениеXML.Документ          = ДокументСсылка;
	СообщениеXML.ДокументОснование = Неопределено;
	
	Возврат ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СообщениеXML);
	
КонецФункции

Функция ЗапросНаОтменуПроведенияXML(ДокументСсылка)
	
	СообщенияXML = Новый Массив;
	
	Операция = Перечисления.ВидыДокументовЕГАИС.бг_ЗапросНаОтменуПроведенияОтчетаОПроизводстве;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЕГАИСПрисоединенныеФайлы.Документ КАК Ссылка,
	|	КОЛИЧЕСТВО(ЕГАИСПрисоединенныеФайлы.Ссылка) КАК ПоследнийНомер
	|ПОМЕСТИТЬ Версии
	|ИЗ
	|	Справочник.ЕГАИСПрисоединенныеФайлы КАК ЕГАИСПрисоединенныеФайлы
	|ГДЕ
	|	ЕГАИСПрисоединенныеФайлы.Документ = &Ссылка
	|	И ЕГАИСПрисоединенныеФайлы.Операция = &Операция
	|	И ЕГАИСПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
	|СГРУППИРОВАТЬ ПО
	|	ЕГАИСПрисоединенныеФайлы.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Шапка.Номер                           КАК Номер,
	|	Шапка.Дата                            КАК Дата,
	|	ЕСТЬNULL(Версии.ПоследнийНомер, 0)    КАК ПоследнийНомерВерсии,
	|	Шапка.ДокументОснование               КАК ДокументОснование,
	|	
	|	Шапка.ИдентификаторЕГАИС              КАК ИдентификаторЕГАИС,
	|	Шапка.ОрганизацияЕГАИС                КАК ОрганизацияЕГАИС,
	|	Шапка.ОрганизацияЕГАИС.Код            КАК ИдентификаторФСРАР,
	|	Шапка.ОрганизацияЕГАИС.ФорматОбмена   КАК ФорматОбмена,
	|	Шапка.Ответственный                   КАК Ответственный
	|ИЗ
	|	Документ.битОтчетОПроизводствеЕГАИС КАК Шапка,
	|		ЛЕВОЕ СОЕДИНЕНИЕ Версии КАК Версии
	|		ПО Шапка.Ссылка = Версии.Ссылка
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка
	|");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Операция", Операция);
	
	Шапка = Запрос.Выполнить().Выбрать();
	
	Если Не Шапка.Следующий() Тогда
		
		СообщениеXML = ИнтеграцияЕГАИС.СтруктураСообщенияXML();
		СообщениеXML.Документ = ДокументСсылка;
		СообщениеXML.Описание = ИнтеграцияЕГАИС.ОписаниеОперацииПередачиДанных(Операция, ДокументСсылка); //НомерВерсии

		
		ИнтеграцияЕГАИСКлиентСервер.ДобавитьТекстОшибки(СообщениеXML, НСтр("ru = 'Нет данных для выгрузки.';
																			|en = 'Нет данных для выгрузки.'"));
		
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
		
	КонецЕсли;
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	ФорматОбмена = ИнтеграцияЕГАИСКлиентСервер.ФорматОбмена(Шапка.ФорматОбмена);
	
	СообщениеXML = ИнтеграцияЕГАИС.СтруктураСообщенияXML();
	СообщениеXML.Описание = ИнтеграцияЕГАИС.ОписаниеОперацииПередачиДанных(
		Операция, ДокументСсылка, НомерВерсии);
	
	ПространствоИмен = Перечисления.ВидыДокументовЕГАИС.ПространствоИмен(Операция, ФорматОбмена);
	ИмяТипа          = Перечисления.ВидыДокументовЕГАИС.ТипЕГАИС(Операция, ФорматОбмена);
	
	Если ПространствоИмен = Неопределено
		Или ИмяТипа = Неопределено Тогда
		ИнтеграцияЕГАИСКлиентСервер.ДобавитьТекстОшибки(
			СообщениеXML,
			СтрШаблон(НСтр("ru = 'Операция не поддерживается в версии формата обмена: %1.';
			               |en = 'Операция не поддерживается в версии формата обмена: %1.'"), ФорматОбмена));
		СообщенияXML.Добавить(СообщениеXML);
		Возврат СообщенияXML;
	КонецЕсли;
	
	ЗапросXDTO = ИнтеграцияЕГАИС.ОбъектXDTO(ПространствоИмен, "QueryRejectRepProduced");
	
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(ЗапросXDTO, "RegId",      Шапка.ИдентификаторЕГАИС, СообщениеXML);
	
	ТекстСообщенияXML = ИнтеграцияЕГАИС.ОбъектXDTOВXML(ЗапросXDTO, Шапка.ИдентификаторФСРАР, ПространствоИмен, ИмяТипа);
	
	СообщениеXML.ТекстСообщенияXML = ТекстСообщенияXML;
	СообщениеXML.ТипСообщения      = Перечисления.ТипыЗапросовИС.Исходящий;
	СообщениеXML.ОрганизацияЕГАИС  = Шапка.ОрганизацияЕГАИС;
	СообщениеXML.Операция          = Операция;
	СообщениеXML.ФорматОбмена      = ФорматОбмена;
	СообщениеXML.Документ          = ДокументСсылка;
	СообщениеXML.ДокументОснование = Шапка.ДокументОснование;
	СообщениеXML.Версия            = НомерВерсии;
	
	СообщенияXML.Добавить(СообщениеXML);
	
	Возврат СообщенияXML;
	
КонецФункции

Функция ДанныеДляЗапросаОтчетОПроизводстве(ДокументСсылка)
	
	ПоляЗаголовка = Новый Структура(
		"Номер, 
		|Дата, 
		|Организация, 
		|ОрганизацияЕГАИС,
		|ДатаПроизводства, 
		|ДокументОснование, 
		|Комментарий");
	
	ДанныеДляЗапросаОтчетОПроизводстве = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, ПоляЗаголовка);
	ДанныеДляЗапросаОтчетОПроизводстве.Номер       = СокрЛП(ДанныеДляЗапросаОтчетОПроизводстве.Номер);		
	ДанныеДляЗапросаОтчетОПроизводстве.Комментарий = СокрЛП(ДанныеДляЗапросаОтчетОПроизводстве.Комментарий);		
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеДляЗапросаОтчетОПроизводстве.ДокументОснование);
	Запрос.УстановитьПараметр("Дата", ДанныеДляЗапросаОтчетОПроизводстве.Дата);
	
	СтатусыДвиженияМарокЭтапПроизводства = Перечисления.бг_СтатусыАкцизныхМарок.СтатусыПоОперации(Метаданные.Документы.ЭтапПроизводства2_2.Имя);
	Запрос.УстановитьПараметр("СтатусЭтапПроизводстваЗавершен", СтатусыДвиженияМарокЭтапПроизводства.ЭтапПроизводстваЗавершен);
	
	Запрос.Текст = ТекстЗапросаДанныеДляЗапросаОтчетОПроизводстве();
	ПакетРезультатовЗапроса = Запрос.ВыполнитьПакет();
	
	ВерхнийИндексПакета = ПакетРезультатовЗапроса.ВГраница();
	
	ДанныеДляЗапросаОтчетОПроизводстве.Вставить("Содержимое", ПакетРезультатовЗапроса[0].Выгрузить());
	ДанныеДляЗапросаОтчетОПроизводстве.Вставить("Марки", ПакетРезультатовЗапроса[ВерхнийИндексПакета - 1].Выгрузить());
	ДанныеДляЗапросаОтчетОПроизводстве.Вставить("Сырьё", ПакетРезультатовЗапроса[ВерхнийИндексПакета].Выгрузить());
	
	Возврат ДанныеДляЗапросаОтчетОПроизводстве
	
КонецФункции

Функция ТекстЗапросаДанныеДляЗапросаОтчетОПроизводстве()

	ИспользоватьОптимизированныеЗапросы = бг_КонстантыПовтИсп.ЗначениеКонстанты(
		"ИспользоватьОптимизированныеЗапросыДвиженияМарок");
	
	Если ИспользоватьОптимизированныеЗапросы Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	битОтчетОПроизводствеЕГАИСТовары.НомерСтроки КАК НомерСтроки,
		|	битОтчетОПроизводствеЕГАИСТовары.Количество КАК Количество,
		|	битОтчетОПроизводствеЕГАИСТовары.Крепость КАК Крепость,
		|	битОтчетОПроизводствеЕГАИСТовары.КрепостьМин КАК КрепостьМин,
		|	битОтчетОПроизводствеЕГАИСТовары.КрепостьМакс КАК КрепостьМакс,
		|	битОтчетОПроизводствеЕГАИСТовары.НомерПартии КАК НомерПартии,
		|	КлассификаторАлкогольнойПродукцииЕГАИС.Код КАК Код,
		|	битОтчетОПроизводствеЕГАИСТовары.Серия КАК Серия,
		|	КлассификаторАлкогольнойПродукцииЕГАИС.ВидПродукции.Код КАК КодВидаПродукции
		|ИЗ
		|	Документ.битОтчетОПроизводствеЕГАИС.Товары КАК битОтчетОПроизводствеЕГАИСТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
		|		ПО битОтчетОПроизводствеЕГАИСТовары.АлкогольнаяПродукция = КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка
		|ГДЕ
		|	битОтчетОПроизводствеЕГАИСТовары.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	битОтчетОПроизводствеЕГАИСТовары.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	бг_ДвижениеМарок.ГУИДМарки КАК ГУИДМарки,
		|	бг_ДвижениеМарок.Серия КАК Серия
		|ПОМЕСТИТЬ втДвижениеМарок
		|ИЗ
		|	РегистрСведений.бг_ДвижениеМарок КАК бг_ДвижениеМарок
		|ГДЕ
		|	бг_ДвижениеМарок.Регистратор = &ДокументОснование
		|	И бг_ДвижениеМарок.СтатусМарки = &СтатусЭтапПроизводстваЗавершен
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ГУИДМарки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втДвижениеМарок.Серия КАК Серия,
		|	бг_ИдентификаторыМарок.ИдентификаторМарки КАК ИдентификаторМарки
		|ИЗ
		|	втДвижениеМарок КАК втДвижениеМарок
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.бг_ИдентификаторыМарок КАК бг_ИдентификаторыМарок
		|		ПО втДвижениеМарок.ГУИДМарки = бг_ИдентификаторыМарок.ГУИДМарки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	битОтчетОПроизводствеЕГАИССырьё.НомерСтроки КАК НомерСтроки,
		|	битОтчетОПроизводствеЕГАИССырьё.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	битОтчетОПроизводствеЕГАИССырьё.Крепость КАК Крепость,
		|	битОтчетОПроизводствеЕГАИССырьё.Количество КАК Количество,
		|	битОтчетОПроизводствеЕГАИССырьё.Производитель КАК Производитель,
		|	битОтчетОПроизводствеЕГАИССырьё.Справка2.РегистрационныйНомер КАК Справка2,
		|	ЕСТЬNULL(КлассификаторАлкогольнойПродукцииЕГАИС.ТипПродукции, НЕОПРЕДЕЛЕНО) КАК ТипПродукции,
		|	ЕСТЬNULL(КлассификаторАлкогольнойПродукцииЕГАИС.ВидПродукции.ВидЛицензии, НЕОПРЕДЕЛЕНО) КАК ВидЛицензии,
		|	ЕСТЬNULL(КлассификаторАлкогольнойПродукцииЕГАИС.НаименованиеПолное, """") КАК НаименованиеПолное,
		|	ЕСТЬNULL(КлассификаторАлкогольнойПродукцииЕГАИС.Наименование, """") КАК Наименование,
		|	ЕСТЬNULL(КлассификаторАлкогольнойПродукцииЕГАИС.Код, """") КАК Код,
		|	ЕСТЬNULL(КлассификаторАлкогольнойПродукцииЕГАИС.Объем, 0) КАК Объем,
		|	ЕСТЬNULL(КлассификаторАлкогольнойПродукцииЕГАИС.ВидПродукции.Код, """") КАК КодВидаПродукции,
		|	битОтчетОПроизводствеЕГАИССырьё.Производитель.ТипОрганизации КАК ТипОрганизацииПроизводителя
		|ИЗ
		|	Документ.битОтчетОПроизводствеЕГАИС.Сырьё КАК битОтчетОПроизводствеЕГАИССырьё
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
		|		ПО битОтчетОПроизводствеЕГАИССырьё.АлкогольнаяПродукция = КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка
		|ГДЕ
		|	битОтчетОПроизводствеЕГАИССырьё.Ссылка = &Ссылка";
	Иначе
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	битОтчетОПроизводствеЕГАИСТовары.НомерСтроки КАК НомерСтроки,
		|	битОтчетОПроизводствеЕГАИСТовары.Количество КАК Количество,
		|	битОтчетОПроизводствеЕГАИСТовары.Крепость КАК Крепость,
		|	битОтчетОПроизводствеЕГАИСТовары.КрепостьМин КАК КрепостьМин,
		|	битОтчетОПроизводствеЕГАИСТовары.КрепостьМакс КАК КрепостьМакс,
		|	битОтчетОПроизводствеЕГАИСТовары.НомерПартии КАК НомерПартии,
		|	КлассификаторАлкогольнойПродукцииЕГАИС.Код КАК Код,
		|	битОтчетОПроизводствеЕГАИСТовары.Серия КАК Серия,
		|	КлассификаторАлкогольнойПродукцииЕГАИС.ВидПродукции.Код КАК КодВидаПродукции
		|ИЗ
		|	Документ.битОтчетОПроизводствеЕГАИС.Товары КАК битОтчетОПроизводствеЕГАИСТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
		|		ПО битОтчетОПроизводствеЕГАИСТовары.АлкогольнаяПродукция = КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка
		|ГДЕ
		|	битОтчетОПроизводствеЕГАИСТовары.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	битОтчетОПроизводствеЕГАИСТовары.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	бг_ДвижениеМарокСрезПоследних.Серия КАК Серия,
		|	бг_ИдентификаторыМарок.ИдентификаторМарки КАК ИдентификаторМарки
		|ИЗ
		|	РегистрСведений.бг_ДвижениеМарок.СрезПоследних(&Дата, Регистратор = &ДокументОснование) КАК бг_ДвижениеМарокСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.бг_ИдентификаторыМарок КАК бг_ИдентификаторыМарок
		|		ПО бг_ДвижениеМарокСрезПоследних.ГУИДМарки = бг_ИдентификаторыМарок.ГУИДМарки
		|ГДЕ
		|	бг_ДвижениеМарокСрезПоследних.СтатусМарки = &СтатусЭтапПроизводстваЗавершен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	битОтчетОПроизводствеЕГАИССырьё.НомерСтроки КАК НомерСтроки,
		|	битОтчетОПроизводствеЕГАИССырьё.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
		|	битОтчетОПроизводствеЕГАИССырьё.Крепость КАК Крепость,
		|	битОтчетОПроизводствеЕГАИССырьё.Количество КАК Количество,
		|	битОтчетОПроизводствеЕГАИССырьё.Производитель КАК Производитель,
		|	битОтчетОПроизводствеЕГАИССырьё.Справка2.РегистрационныйНомер КАК Справка2,
		|	ЕСТЬNULL(КлассификаторАлкогольнойПродукцииЕГАИС.ТипПродукции, НЕОПРЕДЕЛЕНО) КАК ТипПродукции,
		|	ЕСТЬNULL(КлассификаторАлкогольнойПродукцииЕГАИС.ВидПродукции.ВидЛицензии, НЕОПРЕДЕЛЕНО) КАК ВидЛицензии,
		|	ЕСТЬNULL(КлассификаторАлкогольнойПродукцииЕГАИС.НаименованиеПолное, """") КАК НаименованиеПолное,
		|	ЕСТЬNULL(КлассификаторАлкогольнойПродукцииЕГАИС.Наименование, """") КАК Наименование,
		|	ЕСТЬNULL(КлассификаторАлкогольнойПродукцииЕГАИС.Код, """") КАК Код,
		|	ЕСТЬNULL(КлассификаторАлкогольнойПродукцииЕГАИС.Объем, 0) КАК Объем,
		|	ЕСТЬNULL(КлассификаторАлкогольнойПродукцииЕГАИС.ВидПродукции.Код, """") КАК КодВидаПродукции,
		|	битОтчетОПроизводствеЕГАИССырьё.Производитель.ТипОрганизации КАК ТипОрганизацииПроизводителя
		|ИЗ
		|	Документ.битОтчетОПроизводствеЕГАИС.Сырьё КАК битОтчетОПроизводствеЕГАИССырьё
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
		|		ПО битОтчетОПроизводствеЕГАИССырьё.АлкогольнаяПродукция = КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка
		|ГДЕ
		|	битОтчетОПроизводствеЕГАИССырьё.Ссылка = &Ссылка";	
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ОрганизацияЕГАИС(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Контрагент"              , Организация);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КлассификаторОрганизацийЕГАИС.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|ГДЕ
	|	НЕ КлассификаторОрганизацийЕГАИС.ПометкаУдаления
	|	И КлассификаторОрганизацийЕГАИС.Контрагент = &Контрагент
	|	И КлассификаторОрганизацийЕГАИС.СоответствуетОрганизации = Истина";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат РезультатЗапроса.Выгрузить()[0].Ссылка;

КонецФункции	

Функция СообщениеЗапросаОтчетОПроизводствеXMLСОшибкой(Операция, ДокументСсылка, ТекстОшибки)
	
	СообщениеXML = ИнтеграцияЕГАИС.СтруктураСообщенияXML();
	СообщениеXML.Документ = ДокументСсылка;
	СообщениеXML.Описание = ИнтеграцияЕГАИС.ОписаниеОперацииПередачиДанных(Операция, ДокументСсылка);
	
	ИнтеграцияЕГАИСКлиентСервер.ДобавитьТекстОшибки(СообщениеXML, ТекстОшибки);
	
	Возврат ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СообщениеXML);	
	
КонецФункции

Функция RepProduced(ПространствоИмен, Identity, ДанныеДляЗапросаОтчетОПроизводстве, СообщениеXML)
	
	RepProduced = ИнтеграцияЕГАИС.ОбъектXDTO(ПространствоИмен, "RepProducedType_v5");
	
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(RepProduced, "Identity", Identity, СообщениеXML);
	
	RepProduced.Header  = Header(ПространствоИмен, RepProduced, ДанныеДляЗапросаОтчетОПроизводстве, СообщениеXML);
	RepProduced.Content = Content(ПространствоИмен, RepProduced, ДанныеДляЗапросаОтчетОПроизводстве, СообщениеXML);
	
	Возврат RepProduced; 

КонецФункции

Функция Header(ПространствоИмен, RepProduced, ДанныеДляЗапросаОтчетОПроизводстве, СообщениеXML)
	
	Header = ИнтеграцияИС.ОбъектXDTOПоИмениСвойства(ПространствоИмен, "Header", RepProduced);
	
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Header, "Type",                 "OperProduction",  СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Header, "NUMBER",               ДанныеДляЗапросаОтчетОПроизводстве.Номер,             СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Header, "Date",                 ДанныеДляЗапросаОтчетОПроизводстве.Дата,              СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Header, "ProducedDate",         ДанныеДляЗапросаОтчетОПроизводстве.ДатаПроизводства,  СообщениеXML);
	
	Header.Producer = OrgInfoRus(ДанныеДляЗапросаОтчетОПроизводстве.ОрганизацияЕГАИС,  СообщениеXML);
	
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Header, "Note",                 ДанныеДляЗапросаОтчетОПроизводстве.Комментарий,        			 		СообщениеXML);

	Возврат Header;
	
КонецФункции

Функция OrgInfoRus(ОрганизацияЕГАИС, СообщениеXML)
	
	ПространствоИмен = "http://fsrar.ru/WEGAIS/ClientRef_v2";
	
	OrgInfoRus = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "OrgInfoRus_v2")); 
	
	РеквизитыОрганизацииЕГАИС = Новый Структура(
		"Код,
		|НаименованиеПолное,
		|Наименование,
		|ИНН,
		|КПП,
		|КодСтраны,
		|КодРегиона,
		|ПредставлениеАдреса");
	РеквизитыОрганизацииЕГАИС = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОрганизацияЕГАИС, РеквизитыОрганизацииЕГАИС); 
	
	OrgInfoRus.UL = UL(ПространствоИмен, РеквизитыОрганизацииЕГАИС, СообщениеXML);
	
	Возврат OrgInfoRus;
	
КонецФункции

Функция OrgInfo(ОрганизацияЕГАИС, СообщениеXML, ЭтоИмпортер = Ложь)
	
	ПространствоИмен = "http://fsrar.ru/WEGAIS/ClientRef_v2";
	
	OrgInfo = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "OrgInfo_v2")); 
	
	РеквизитыОрганизацииЕГАИС = Новый Структура(
		"Код,
		|НаименованиеПолное,
		|Наименование,
		|ИНН,
		|КПП,
		|КодСтраны,
		|КодРегиона,
		|ПредставлениеАдреса");
	РеквизитыОрганизацииЕГАИС = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОрганизацияЕГАИС, РеквизитыОрганизацииЕГАИС); 
	
	Если ЭтоИмпортер Тогда 
		OrgInfo.FO = FO(ПространствоИмен, РеквизитыОрганизацииЕГАИС, СообщениеXML);
	Иначе
		OrgInfo.UL = UL(ПространствоИмен, РеквизитыОрганизацииЕГАИС, СообщениеXML);
	КонецЕсли;

	Возврат OrgInfo;
	
КонецФункции

Функция UL(ПространствоИмен, РеквизитыОрганизацииЕГАИС, СообщениеXML)
	
	UL = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "ULType")); 
	
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(UL, "ClientRegId", РеквизитыОрганизацииЕГАИС.Код,                СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(UL, "FullName",    РеквизитыОрганизацииЕГАИС.НаименованиеПолное, СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(UL, "INN",         РеквизитыОрганизацииЕГАИС.ИНН,                СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(UL, "KPP",         РеквизитыОрганизацииЕГАИС.КПП,                СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(UL, "ShortName",         РеквизитыОрганизацииЕГАИС.Наименование, СообщениеXML);
	
	UL.address    = OrgAddress(ПространствоИмен, "OrgAddressTypeULFL",   РеквизитыОрганизацииЕГАИС, СообщениеXML);
	
	Возврат UL;
	
КонецФункции

Функция FO(ПространствоИмен, РеквизитыОрганизацииЕГАИС, СообщениеXML)
	
	FO = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "FOType")); 
	
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(FO, "ClientRegId", РеквизитыОрганизацииЕГАИС.Код,                СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(FO, "FullName",    РеквизитыОрганизацииЕГАИС.НаименованиеПолное, СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(FO, "ShortName",         РеквизитыОрганизацииЕГАИС.Наименование, СообщениеXML);
	
	FO.address    = OrgAddressFOTS(ПространствоИмен, "OrgAddressTypeFOTS",   РеквизитыОрганизацииЕГАИС, СообщениеXML);
	
	Возврат FO;
	
КонецФункции

Функция OrgAddressFOTS(ПространствоИмен, ИмяТипа, РеквизитыАдреса, СообщениеXML)
	
	OrgAddressFOTS = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, ИмяТипа)); 
	
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(OrgAddressFOTS, "Country",     Формат(РеквизитыАдреса.КодСтраны, "ЧЦ=3; ЧН=; ЧВН="), СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(OrgAddressFOTS, "description", СокрЛП(РеквизитыАдреса.ПредставлениеАдреса),          СообщениеXML);
	
	Возврат OrgAddressFOTS;
	
КонецФункции

Функция OrgAddress(ПространствоИмен, ИмяТипа, РеквизитыАдреса, СообщениеXML)
	
	OrgAddress = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, ИмяТипа)); 
	
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(OrgAddress, "Country",     Формат(РеквизитыАдреса.КодСтраны, "ЧЦ=3; ЧН=; ЧВН="), СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(OrgAddress, "RegionCode",  Формат(РеквизитыАдреса.КодРегиона, "ЧЦ=2; ЧВН="),     СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(OrgAddress, "description", СокрЛП(РеквизитыАдреса.ПредставлениеАдреса),          СообщениеXML);
	
	Возврат OrgAddress;
	
КонецФункции

Функция Content(ПространствоИмен, RepProduced, ДанныеДляЗапросаОтчетОПроизводстве, СообщениеXML)
	
	Content = ИнтеграцияИС.ОбъектXDTOПоИмениСвойства(ПространствоИмен, "Content", RepProduced);
	
	Для Каждого Позиция Из ДанныеДляЗапросаОтчетОПроизводстве.Содержимое Цикл
		
		Position = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(Content, "Position");
		
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position, "ProductCode",  	Позиция.Код,    		СообщениеXML);
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Quantity",  		Позиция.Количество,     СообщениеXML);
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position, "alcPercent", 		Позиция.Крепость, 		СообщениеXML); 
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position, "alcPercentMin",    Позиция.КрепостьМин,    СообщениеXML);
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position, "alcPercentMax",    Позиция.КрепостьМакс,   СообщениеXML);
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Identity",   		Позиция.НомерСтроки,    СообщениеXML);
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Party",    		Позиция.НомерПартии,    СообщениеXML);
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Comment1",    		"",                 	СообщениеXML);
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Comment2",    		"",                 	СообщениеXML);
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Position, "Comment3",    		"",                 	СообщениеXML);
		
		ОтборМарок = Новый Структура();
		ОтборМарок.Вставить("Серия", Позиция.Серия);
		НайденныеСтрокиМарок = ДанныеДляЗапросаОтчетОПроизводстве.Марки.НайтиСтроки(ОтборМарок);
		
		Если НайденныеСтрокиМарок.Количество() <> 0 Тогда
			MarkInfo = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(Position, "MarkInfo");
			
			Для Каждого Строка Из НайденныеСтрокиМарок Цикл
				ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(MarkInfo, "amc", Строка.ИдентификаторМарки, СообщениеXML);
			КонецЦикла;
			Position.MarkInfo = MarkInfo;
		КонецЕсли;
		
		Position.ContentResource = ContentResource(
			ПространствоИмен,
			Position,
			ДанныеДляЗапросаОтчетОПроизводстве,
			СообщениеXML,
			Позиция.НомерСтроки);
		
		Content.Position.Добавить(Position);
		
	КонецЦикла;
	
	Возврат Content;

КонецФункции

Функция ContentResource(ПространствоИмен, Position, ДанныеДляЗапросаОтчетОПроизводстве, СообщениеXML, ИдентификаторСтрокиСырья)
	
	ContentResource = ИнтеграцияИС.ОбъектXDTOПоИмениСвойства(ПространствоИмен, "ContentResource", Position);
	
	Для Каждого Позиция Из ДанныеДляЗапросаОтчетОПроизводстве.Сырьё Цикл
		
		Resource = ИнтеграцияИС.ОбъектXDTOПоИмениТипа(ContentResource, "Resource");
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Resource, "IdentityRes", Позиция.НомерСтроки, СообщениеXML);
		
		Resource.Product = Product(Позиция, СообщениеXML);
		
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Resource, "RegForm2", Позиция.Справка2, СообщениеXML);
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Resource, "Quantity", Позиция.Количество, СообщениеXML);
		
		ContentResource.Resource.Добавить(Resource);
		
	КонецЦикла;
	
	Возврат ContentResource;

КонецФункции

Функция Product(РеквизитыАдреса, СообщениеXML)
	
	ПеречислениеВидыЛицензий = Перечисления.ВидыЛицензийАлкогольнойПродукции;
	
	ТипыАП = Новый Соответствие;
	ТипыАП.Вставить(ПеречислениеВидыЛицензий.АлкогольнаяПродукция               , "АП");
	ТипыАП.Вставить(ПеречислениеВидыЛицензий.Пиво                               , "АП");
	ТипыАП.Вставить(ПеречислениеВидыЛицензий.Спирт                              , "ЭС");
	ТипыАП.Вставить(ПеречислениеВидыЛицензий.СпиртосодержащаяНеПищеваяПродукция , "ССНП");
	ТипыАП.Вставить(ПеречислениеВидыЛицензий.СпиртосодержащаяПищеваяПродукция   , "ССПП");
	
	ТипыПродукции = Новый Соответствие;
	ТипыПродукции.Вставить(Перечисления.ТипыПродукцииЕГАИС.ПустаяСсылка()   , "Packed");
	ТипыПродукции.Вставить(Перечисления.ТипыПродукцииЕГАИС.Упакованная      , "Packed");
	ТипыПродукции.Вставить(Перечисления.ТипыПродукцииЕГАИС.Неупакованная    , "Unpacked");

	ПространствоИмен = "http://fsrar.ru/WEGAIS/ProductRef_v2";
	
	Product = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "ProductInfo_v2")); 
	
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Product, "UnitType", ТипыПродукции[РеквизитыАдреса.ТипПродукции], СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Product, "Type", ТипыАП[РеквизитыАдреса.ВидЛицензии], СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Product, "FullName", РеквизитыАдреса.НаименованиеПолное, СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Product, "ShortName", РеквизитыАдреса.Наименование, СообщениеXML);
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Product, "AlcCode", РеквизитыАдреса.Код, СообщениеXML);
	Если РеквизитыАдреса.Объем > 0 Тогда
		ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Product, "Capacity", РеквизитыАдреса.Объем, СообщениеXML);
	КонецЕсли;
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Product, "AlcVolume", РеквизитыАдреса.Крепость, СообщениеXML);
	
	Product.Producer = OrgInfo(РеквизитыАдреса.Производитель,
		СообщениеXML,
		?(РеквизитыАдреса.ТипОрганизацииПроизводителя = Перечисления.ТипыОрганизацийЕГАИС.ЮридическоеЛицоРФ, Ложь, Истина));
	
	ИнтеграцияЕГАИС.ЗаполнитьСвойствоXDTO(Product, "ProductVCode", РеквизитыАдреса.КодВидаПродукции, СообщениеXML);
	
	Возврат Product;
	
КонецФункции

#КонецОбласти

Процедура ПриИзмененииСтатусаДокументаОбработатьСправки1(ДокументСсылка, ПредыдущийСтатус, НовыйСтатус)
	
	ТребуетсяОбрабатыватьСправки1 = ПредыдущийСтатус <> Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.бг_Подтвержден
		И НовыйСтатус = Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.бг_Подтвержден;
		
	Если ТребуетсяОбрабатыватьСправки1 Тогда
		бг_ИнтеграцияЕГАИС.ЗарегистрироватьДокументДляОтложенногоЗапросаСправок1(ДокументСсылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
