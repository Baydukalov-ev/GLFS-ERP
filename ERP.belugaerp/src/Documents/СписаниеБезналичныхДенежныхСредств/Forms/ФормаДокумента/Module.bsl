#Область ОбработчикиСобытийФормы

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
		
	бг_ДобавитьРеквизитыНаФорму();
	бг_ДобавитьЭлементыУправленияТЧРасшифровка();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура бг_ДоговорНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	
	СтандартнаяОбработка = Ложь;
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Организация", Объект.Организация);
	СтруктураОтбора.Вставить("ПометкаУдаления", Ложь);
	
	СтруктураОтбора.Вставить("ВалютаВзаиморасчетов", Объект.Валюта);
	
	СтруктураПараметровВыбора = Новый Структура;
	СтруктураПараметровВыбора.Вставить("РежимВыбора", Истина);
	
	СтруктураОтбора.Вставить("Контрагент", Объект.Контрагент);
	
	СтруктураПараметровВыбора.Вставить("Отбор",СтруктураОтбора);
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаВыбора",
	СтруктураПараметровВыбора,
	Элемент,
	Элемент,)

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура бг_ЗаполнитьСтатьюДДСВСтрокахРасшифровкиПлатежа(Команда)
	
	ВыделенныеСтроки = Элементы.РасшифровкаПлатежа.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ВыделенныеСтроки", ВыделенныеСтроки);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("бг_ВыборСтатьиДДСЗавершение", ЭтаФорма, ДополнительныеПараметры);
	
	Отбор = Новый Структура;
	Отбор.Вставить("ТолькоРасходДенежныхСредств");
	
	ПараметрыОтбора = Новый Структура("Отбор", Отбор);
	
	ОткрытьФорму("Справочник.СтатьиДвиженияДенежныхСредств.ФормаВыбора",
		ПараметрыОтбора,
		ЭтаФорма,
		ЭтаФорма,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура бг_ДобавитьРеквизитыНаФорму()
	
	бг_Договор = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_Договор",
		Элементы.ГруппаДополнительныеРеквизитыШапки,
		"Объект.Договор");
	бг_Договор.АвтоМаксимальнаяШирина = Истина;
	
	бг_Договор.УстановитьДействие("НачалоВыбора", "бг_ДоговорНачалоВыбора");

	НастроитьЗависимыеЭлементыФормыНаСервере();
КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("ДоступныеПорядкиРасчетов")
Функция бг_ДоступныеПорядкиРасчетов()
	СписокПорядков = Новый СписокЗначений;
	СписокПорядков.Добавить(ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоЗаказам"));
	СписокПорядков.Добавить(ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоНакладным"));
	СписокПорядков.Добавить(ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоЗаказамНакладным"));
	СписокПорядков.Добавить(ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоДоговорамНакладным"));
	Если Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту")
		ИЛИ Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию") Тогда
		СписокПорядков.Добавить(ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов"));
	КонецЕсли;
#Вставка
	Если Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.КонвертацияВалюты") Тогда
		СписокПорядков.Добавить(ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов"));
	КонецЕсли;
#КонецВставки
	Возврат СписокПорядков;
КонецФункции

&НаСервере
&После("НастроитьЗависимыеЭлементыФормыНаСервере")
Процедура бг_НастроитьЗависимыеЭлементыФормыНаСервере(ИзмененныйРеквизит)
	Если ЭтаФорма.Элементы.Найти("бг_Договор") <> Неопределено Тогда
		Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты Тогда
			ЭтаФорма.Элементы.бг_Договор.Видимость = Истина;
		Иначе
			ЭтаФорма.Элементы.бг_Договор.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура бг_ДобавитьЭлементыУправленияТЧРасшифровка()

	КнопкаЗаполнитьСтатьюДДС = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьКнопкуНаФорму(
		ЭтаФорма,
		"бг_ЗаполнитьСтатьюДДСВСтрокахРасшифровкиПлатежа",
		Элементы.РасшифровкаПлатежа.КоманднаяПанель,
		"Заполнить статью ДДС",
		"бг_ЗаполнитьСтатьюДДСВСтрокахРасшифровкиПлатежа",
		"бг_ЗаполнитьСтатьюДДСВСтрокахРасшифровкиПлатежа",
		,
		ВидКнопкиФормы.КнопкаКоманднойПанели);
	КнопкаЗаполнитьСтатьюДДС.ТолькоВоВсехДействиях = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ВыборСтатьиДДСЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТаблицы Из ДополнительныеПараметры.ВыделенныеСтроки Цикл
		ДанныеСтрокиТаблицы = Объект.РасшифровкаПлатежа.НайтиПоИдентификатору(СтрокаТаблицы);
		Если ДанныеСтрокиТаблицы <> Неопределено Тогда
			ДанныеСтрокиТаблицы.СтатьяДвиженияДенежныхСредств = Результат;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
