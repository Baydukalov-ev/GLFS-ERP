#Область ПрограммныйИнтерфейс

#Область ПроводкиРеглУчета

&Вместо("ТекстОтраженияВРеглУчете")
Функция бг_ТекстОтраженияВРеглУчете()
	ТекстЗапроса = ПродолжитьВызов();
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + 
	ТекстОтраженияНачисленияАкциза();
	
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти

// Возвращает признак наличия маркируемой алкогольной продукции в возврате (необходимы движения по маркам).
//
// Параметры:
//  ВозвратТоваровОтКлиента - ДокументСсылка.ВозвратТоваровОтКлиента - Ссылка на документ ВозвратТоваровОтКлиента.
//
// Возвращаемое значение:
//   Булево - признак наличия маркируемой алкогольной продукции.
//
Функция бг_ЕстьМаркируемаяАлкогольнаяПродукция(ВозвратТоваровОтКлиента) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВозвратТоваровОтКлиентаТовары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.Товары КАК ВозвратТоваровОтКлиентаТовары
	|ГДЕ
	|	ВозвратТоваровОтКлиентаТовары.Ссылка = &Ссылка
	|	И ВозвратТоваровОтКлиентаТовары.Номенклатура.ВидАлкогольнойПродукции.Маркируемый
	|	И ВозвратТоваровОтКлиентаТовары.Номенклатура.ВидАлкогольнойПродукции.ВидЛицензии = ЗНАЧЕНИЕ(Перечисление.ВидыЛицензийАлкогольнойПродукции.АлкогольнаяПродукция)
	|	И ВозвратТоваровОтКлиентаТовары.Номенклатура.ВидНоменклатуры.АлкогольнаяПродукция";
	
	Запрос.УстановитьПараметр("Ссылка", ВозвратТоваровОтКлиента);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

// Возвращает список связанных приходных ордеров.
//
// Параметры:
//  ВозвратТоваровОтКлиента - ДокументСсылка.ВозвратТоваровОтКлиента - Ссылка на документ ВозвратТоваровОтКлиента.
//
// Возвращаемое значение:
//   Массив
//
Функция бг_ПриходныеОрдера(ВозвратТоваровОтКлиента) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриходныйОрдерНаТовары.Ссылка КАК ПриходныйОрдерНаТовары
	|ИЗ
	|	Документ.ПриходныйОрдерНаТовары КАК ПриходныйОрдерНаТовары
	|ГДЕ
	|	НЕ ПриходныйОрдерНаТовары.ПометкаУдаления
	|	И ПриходныйОрдерНаТовары.Распоряжение = &Распоряжение";
	
	Запрос.УстановитьПараметр("Распоряжение", ВозвратТоваровОтКлиента);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ПриходныйОрдерНаТовары");
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

&После("ДобавитьКомандыПечати")
Процедура бг_ДобавитьКомандыПечати(КомандыПечати)
	
	// Универсальный корректировочный документ (УКД - основной)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "бг_УКД";
	КомандаПечати.Представление = НСтр("ru = 'Универсальный корректировочный документ (УКД - основной)';
										|en = 'Universal adjustment document (UAD - general)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("бг_ПереопределитьМакетУКД", Истина);
	КомандаПечати.ДополнительныеПараметры.Вставить("бг_ИспользоватьПечатныеФормыСФ2_5", Истина);
	КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВВалюте", Ложь);
	КомандаПечати.Порядок = 1;

КонецПроцедуры

#КонецОбласти

Функция ТекстОтраженияНачисленияАкциза()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	-СУММА(Строки.бг_СуммаАкциза) КАК Сумма,
	|	-СУММА(Строки.бг_СуммаАкциза) КАК СуммаУУ,
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Продажи_Акцизы) КАК СчетДт,
	|	Строки.Номенклатура.ГруппаФинансовогоУчета КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Акцизы) КАК СчетКт,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог) КАК СубконтоКт1,
	|	Операция.Организация.РегистрацияВНалоговомОргане КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Сторно начисленного акциза"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента КАК Операция
	|		ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|			И Операция.Дата >= Операция.Организация.бг_ДатаНачалаУчетаАкцизовПоПриобретеннымЦенностям
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.Товары КАК Строки
	|		ПО (Строки.Ссылка = Операция.Ссылка) И Строки.бг_СуммаАкциза <> 0 
	|
	|СГРУППИРОВАТЬ ПО
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	Операция.Организация,
	|	Операция.Подразделение,
	|	Строки.Номенклатура.ГруппаФинансовогоУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	СУММА(Строки.бг_СуммаАкциза) КАК Сумма,
	|	СУММА(Строки.бг_СуммаАкциза) КАК СуммаУУ,
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасходы) КАК СчетДт,
	|	НалогообложениеАкцизомОрганизаций.ВидОперацииНалогообложения.СтатьяРасхода КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	СУММА(ВЫБОР КОГДА НалогообложениеАкцизомОрганизаций.ВидОперацииНалогообложения.СтатьяРасхода <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|		 И НЕ НалогообложениеАкцизомОрганизаций.ВидОперацииНалогообложения.СтатьяРасхода.ПринятиеКНалоговомуУчету
	|		ТОГДА 0
	|		ИНАЧЕ Строки.бг_СуммаАкциза
	|	КОНЕЦ) КАК СуммаНУДт,
	|	СУММА(ВЫБОР КОГДА НалогообложениеАкцизомОрганизаций.ВидОперацииНалогообложения.СтатьяРасхода <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|		 И НЕ НалогообложениеАкцизомОрганизаций.ВидОперацииНалогообложения.СтатьяРасхода.ПринятиеКНалоговомуУчету
	|		ТОГДА Строки.бг_СуммаАкциза
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Акцизы) КАК СчетКт,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог) КАК СубконтоКт1,
	|	Операция.Организация.РегистрацияВНалоговомОргане КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Восстановление акциза при возврате"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента КАК Операция
	|		ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|			И Операция.Дата >= Операция.Организация.бг_ДатаНачалаУчетаАкцизовПоПриобретеннымЦенностям
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.Товары КАК Строки
	|		ПО (Строки.Ссылка = Операция.Ссылка) И Строки.бг_СуммаАкциза <> 0 
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.бг_НалогообложениеАкцизомОрганизаций КАК НалогообложениеАкцизомОрганизаций
	|		ПО (Операция.Организация = НалогообложениеАкцизомОрганизаций.Организация)
	|			И (НалогообложениеАкцизомОрганизаций.СтатусАкциза = ЗНАЧЕНИЕ(Перечисление.бг_СостоянияОплатыВходящегоАкциза.ПустаяСсылка))
	|			И (НалогообложениеАкцизомОрганизаций.СостояниеСырья = ЗНАЧЕНИЕ(Перечисление.бг_СостоянияПодакцизногоСырья.ВСписанныхМПЗПоНорме))
	|			И (НалогообложениеАкцизомОрганизаций.СтатусНачисленияАванса = ЗНАЧЕНИЕ(Перечисление.бг_СтатусыНачисленияАвансов.ПустаяСсылка))
	|			И (НЕ НалогообложениеАкцизомОрганизаций.ПродажаНаЭкспорт)
	|			И (НалогообложениеАкцизомОрганизаций.Номенклатура В (Строки.Номенклатура, Строки.Номенклатура.ВидНоменклатуры))
	|
	|СГРУППИРОВАТЬ ПО
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	Операция.Организация,
	|	Операция.Подразделение,
	|	НалогообложениеАкцизомОрганизаций.ВидОперацииНалогообложения.СтатьяРасхода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ //// Выручка от реализации собственного товара (сторно) (Дт 62.01 :: Кт 90.01.1)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Расчеты.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентами) КАК ВидСчетаДт,
	|	Расчеты.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Расчеты.Валюта КАК ВалютаДт,
	|	Расчеты.Подразделение КАК ПодразделениеДт,
	|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Расчеты.Контрагент КАК СубконтоДт1,
	|	Расчеты.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ВЫБОР КОГДА Операция.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыручкаОтПродажЕНВД)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыручкаОтПродаж)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	Строки.Номенклатура.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	Операция.Склад КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.Номенклатура.ГруппаФинансовогоУчета КАК СубконтоКт1,
	|	Строки.СтавкаНДС.ПеречислениеСтавкаНДС КАК СубконтоКт2,
	|	Строки.Номенклатура КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.бг_СуммаАкциза КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Выручка от реализации собственного товара (сторно)"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ВозвратТоваровОтКлиента КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|		И Операция.Дата >= Операция.Организация.бг_ДатаНачалаУчетаАкцизовПоПриобретеннымЦенностям
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		Документ.ВозвратТоваровОтКлиента.Товары КАК Строки
	|	ПО 
	|		Строки.Ссылка = Операция.Ссылка
	|		И Строки.бг_СуммаАкциза <> 0 
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВТРасчетыСКлиентами КАК Расчеты
	|	ПО
	|		Операция.Ссылка = Расчеты.Ссылка
	|		И Расчеты.Организация = Расчеты.ОрганизацияАналитики
	|
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(Операция.Дата, ГОД) = НАЧАЛОПЕРИОДА(ЕСТЬNULL(Строки.ДокументРеализации.Дата, Операция.Дата), ГОД)
	|	И Операция.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтРозничногоПокупателя))
	|";
	
	Возврат ТекстЗапроса;

КонецФункции 

#КонецОбласти
