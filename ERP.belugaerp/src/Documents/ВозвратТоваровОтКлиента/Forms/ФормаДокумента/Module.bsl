#Область ОбработчикиСобытийФормы

&НаСервере
Процедура бг_ПриЧтенииНаСервереПосле(ТекущийОбъект)
	
	// ОбщиеМеханизмы.ПриходныйОрдер
	бг_Склады.ДобавитьПолеПриходныйОрдерНаТовары(ЭтотОбъект);
	бг_ОбновитьОтображениеДекорацииПриходногоОрдераНаТовары();
	// Конец ОбщиеМеханизмы.ПриходныйОрдер
	
КонецПроцедуры

&НаСервере
Процедура бг_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)
	
	бг_ДобавитьРеквизитыТТНВходящаяЕГАИС();	
	бг_ЗаполнитьРеквизитыТТНВходящаяЕГАИС(Параметры);
	
КонецПроцедуры

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	бг_СоздатьЭлементыФормы();
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		// ОбщиеМеханизмы.ПриходныйОрдер
		бг_Склады.ДобавитьПолеПриходныйОрдерНаТовары(ЭтотОбъект);
		бг_ОбновитьОтображениеДекорацииПриходногоОрдераНаТовары();
		// Конец ОбщиеМеханизмы.ПриходныйОрдер
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура бг_ПослеЗаписиНаСервереПосле(ТекущийОбъект, ПараметрыЗаписи)
	
	// ОбщиеМеханизмы.ПриходныйОрдер
	бг_ОбновитьОтображениеДекорацииПриходногоОрдераНаТовары();
	// Конец ОбщиеМеханизмы.ПриходныйОрдер
	
КонецПроцедуры

&НаКлиенте
&Перед("ОбработкаНавигационнойСсылки")
Процедура бг_ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	// ОбщиеМеханизмы.ПриходныйОрдер
	Если НавигационнаяСсылкаФорматированнойСтроки = "бг_ОформитьПриходныйОрдерНаТовары" Тогда
		СтандартнаяОбработка = Ложь;
		бг_ОткрытьФормуНовогоПриходногоОрдераНаТовары();
	КонецЕсли;
	// Конец ОбщиеМеханизмы.ПриходныйОрдер
	
КонецПроцедуры

&НаКлиенте
&После("ОбработкаОповещения")
Процедура бг_ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ОбщиеМеханизмы.ПриходныйОрдер
	Если ИмяСобытия = "Запись_ПриходныйОрдерНаТовары" Тогда
		бг_ОбновитьОтображениеДекорацииПриходногоОрдераНаТовары();
	КонецЕсли;
	// Конец ОбщиеМеханизмы.ПриходныйОрдер
	
КонецПроцедуры

&НаСервере
Процедура бг_ПередЗаписьюНаСервереПеред(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтотОбъект, "бг_ТТНВходящаяЕГАИС")
		И ЗначениеЗаполнено(ЭтотОбъект["бг_ТТНВходящаяЕГАИС"]) Тогда
		
		ТекущийОбъект.ДополнительныеСвойства.Вставить("адаптер_ЭтоЗагрузкаДанных", Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура бг_ТоварыКоличествоУпаковокПриИзмененииПосле(Элемент)
	бг_ЗаполнитьСуммуАкцизаПоСтроке();
КонецПроцедуры

&НаКлиенте
Процедура бг_ТоварыНоменклатураПриИзмененииПосле(Элемент)
	бг_ЗаполнитьСуммуАкцизаПоСтроке();
КонецПроцедуры

&НаКлиенте
Процедура бг_ТоварыУпаковкаПриИзмененииПосле(Элемент)
	бг_ЗаполнитьСуммуАкцизаПоСтроке();
КонецПроцедуры

&НаКлиенте
Процедура бг_ТоварыДокументРеализацииПриИзмененииПосле(Элемент)
	бг_ЗаполнитьСуммуАкцизаПоСтроке();
КонецПроцедуры

&НаКлиенте
Процедура бг_ТоварыСерияПриИзмененииПосле(Элемент)
	бг_ЗаполнитьСуммуАкцизаПоСтроке();
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура бг_ПересчитатьСуммуАкциза(Команда)
	
	бг_ЗаполнитьСуммуАкциза();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура бг_СоздатьЭлементыФормы()
	
	бг_ДобавитьЭлементыАкциза();
	бг_ДобавитьПолеСертификатНоменклатуры();
	бг_ДобавитьПолеГрузоотправитель();
	
КонецПроцедуры

&НаСервере
Процедура бг_ДобавитьРеквизитыТТНВходящаяЕГАИС()
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтотОбъект, "бг_ТТНВходящаяЕГАИС") Тогда
		ОписаниеРеквизита = Новый РеквизитФормы( 
			"бг_ТТНВходящаяЕГАИС",
			Новый ОписаниеТипов("ДокументСсылка.ТТНВходящаяЕГАИС"));
		
		ДобавляемыеРеквизиты.Добавить(ОписаниеРеквизита);
	КонецЕсли;
	
	Если ДобавляемыеРеквизиты.Количество() <> 0 Тогда
		ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура бг_ЗаполнитьРеквизитыТТНВходящаяЕГАИС(Параметры)
	
	Если Параметры.Свойство("Основание") И ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.ТТНВходящаяЕГАИС") Тогда
		ЭтаФорма.бг_ТТНВходящаяЕГАИС = Параметры.Основание;
	КонецЕсли;
	
КонецПроцедуры

#Область Акциз

&НаСервере
Процедура бг_ДобавитьЭлементыАкциза()

	ИмяКоманды = "бг_ПересчитатьСуммуАкциза";
	ЗаголовокКоманды = НСтр("ru = 'Пересчитать сумму акциза';
							|en = 'Calculate the amount of excise'");
	
	бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьКнопкуНаФорму(
		ЭтотОбъект, 
		ИмяКоманды,
		Элементы.ТоварыГруппаЗаполнить, 
		ЗаголовокКоманды,
		ИмяКоманды, 
		ИмяКоманды, 
		, 
		ВидКнопкиФормы.КнопкаКоманднойПанели);
		
	бг_ТоварыСуммаАкциза = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
	  ЭтотОбъект,
	  "бг_ТоварыСуммаАкциза", 
	  Элементы.Товары, 
	  "Объект.Товары.бг_СуммаАкциза");

КонецПроцедуры

&НаСервере
&После("ОрганизацияПриИзмененииСервер")
Процедура бг_ОрганизацияПриИзмененииСервер()
	бг_УчетАкцизов.ЗаполнитьСуммуАкциза(Объект, Объект.Товары);
КонецПроцедуры

&НаСервере
&После("ОбработкаВыбораПодборНаСервере")
Процедура бг_ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение)
	бг_УчетАкцизов.ЗаполнитьСуммуАкциза(Объект, Объект.Товары);
КонецПроцедуры

&НаСервере
&После("ДатаПриИзмененииСервер")
Процедура бг_ДатаПриИзмененииСервер()
	бг_УчетАкцизов.ЗаполнитьСуммуАкциза(Объект, Объект.Товары);
КонецПроцедуры

&НаСервере
&После("НалогообложениеНДСПриИзмененииСервер")
Процедура бг_НалогообложениеНДСПриИзмененииСервер()
	бг_УчетАкцизов.ЗаполнитьСуммуАкциза(Объект, Объект.Товары);
КонецПроцедуры

&НаКлиенте
&После("ВыборСпособаОпределенияСебестоимостиЗавершение")
Процедура бг_ВыборСпособаОпределенияСебестоимостиЗавершение(РезультатВыбора, ДополнительныеПараметры)
	бг_ЗаполнитьСуммуАкцизаПоСтроке();
КонецПроцедуры

&НаСервере
Процедура бг_ЗаполнитьСуммуАкциза()
	бг_УчетАкцизов.ЗаполнитьСуммуАкциза(Объект, Объект.Товары);
КонецПроцедуры

&НаСервере
Процедура бг_ЗаполнитьСуммуАкцизаПоСтроке()
	ВыделенныеСтроки = Новый Массив;
	ВыделенныеСтроки.Добавить(Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока));
	бг_УчетАкцизов.ЗаполнитьСуммуАкциза(Объект, Объект.Товары, ВыделенныеСтроки);
КонецПроцедуры

#КонецОбласти

#Область ПриходныйОрдерНаТовары

// ОбщиеМеханизмы.ПриходныйОрдер
&НаСервере
Процедура бг_ОбновитьОтображениеДекорацииПриходногоОрдераНаТовары()
	
	бг_Склады.ОбновитьОтображениеДекорацииПриходногоОрдераНаТовары(
		ЭтотОбъект,
		Метаданные.Документы.ВозвратТоваровОтКлиента);
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ОткрытьФормуНовогоПриходногоОрдераНаТовары()
	
	Основание = Новый Структура;
	Основание.Вставить("Распоряжение",               Объект.Ссылка);
	Основание.Вставить("Отправитель",                Объект.Партнер);
	Основание.Вставить("Склад",                      Объект.Склад);
	Основание.Вставить("ДатаВходящегоДокумента",     Объект.ДатаВходящегоДокумента);
	Основание.Вставить("НомерВходящегоДокумента",    Объект.НомерВходящегоДокумента);
	Основание.Вставить("ДатаПоступления",            Объект.ДатаПоступления);
	Основание.Вставить("ХозяйственнаяОперация",      Объект.ХозяйственнаяОперация);
	
	ОткрытьФорму(
		"Документ.ПриходныйОрдерНаТовары.Форма.ФормаДокумента",
		Новый Структура("Основание", Основание));
	
КонецПроцедуры
// Конец ОбщиеМеханизмы.ПриходныйОрдер

#КонецОбласти

#Область СертификатНоменклатуры

&НаСервере
Процедура бг_ДобавитьПолеСертификатНоменклатуры()
	
	бг_Сертификат = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтаФорма,
		"бг_Сертификат",
		Элементы.Товары,
		"Объект.Товары.бг_Сертификат");
	
	бг_Сертификат.УстановитьДействие("НачалоВыбора", "бг_ТоварыСертификатНачалоВыбора");
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ТоварыСертификатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("бг_ТолькоДействующиеНаДату", Истина);
	ПараметрыФормы.Вставить("бг_Номенклатура", ТекущаяСтрока.Номенклатура);
	ПараметрыФормы.Вставить("бг_РежимВыбора", Истина);
	
	ОповещениеПоЗавершенииВыбора = Новый ОписаниеОповещения(
		"бг_ТоварыСертификатЗавершениеВыбора",
		ЭтотОбъект);
	
	ОткрытьФорму("Справочник.СертификатыНоменклатуры.Форма.ФормаСписка", ПараметрыФормы,,,,,ОповещениеПоЗавершенииВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ТоварыСертификатЗавершениеВыбора(Результат, ДополнительныеПараметры) Экспорт

	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	ТекущаяСтрока.бг_Сертификат = Результат;

КонецПроцедуры

#КонецОбласти

#Область Грузоотправитель

&НаСервере
Процедура бг_ДобавитьПолеГрузоотправитель()
	
	бг_Грузоотправитель = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтаФорма,
		"бг_Грузоотправитель",
		Элементы.ШапкаЛево,
		"Объект.бг_Грузоотправитель");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти // Конец СлужебныеПроцедурыИФункции
