#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Данные") Тогда
		ЗаполнитьФорму(Параметры.Данные);
	Иначе
		Отказ = Истина;
	КонецЕсли;
	Параметры.Свойство("Период", Период);
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
КонецПроцедуры
	
#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	Модифицированность = Ложь;
	ВыбранныеПозиции = Новый Массив;
	Для каждого СтрокаТовары Из Товары.НайтиСтроки(Новый Структура("ПереноситьВДокумент", Истина)) Цикл
		НоваяСтрока = Новый Структура("Номенклатура, Количество");
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовары);
		ВыбранныеПозиции.Добавить(НоваяСтрока);
	КонецЦикла;
	ЗапомнитьКратностьНаСервере();
	Закрыть(ПоместитьВХранилище(ВыбранныеПозиции));
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтметки(Команда)
	УстановитьОтметку(Истина);	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметки(Команда)
	УстановитьОтметку(Ложь);	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьКратность(Команда)
	ЗаполнитьКратносьРассчитатьКоличествоСервер();
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТоварыКратностьПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Кратность = 0 Тогда
		ТекущиеДанные.Кратность = 1; 
		ТекущиеДанные.ЗапоминатьКратность = Ложь;
	Иначе
		ТекущиеДанные.ЗапоминатьКратность = Истина;
	КонецЕсли;
	
	РассчитатьНовоеКоличество(ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекущиеДанные.ПереноситьВДокумент = Истина;
	ТекущиеДанные.ЗапоминатьКратность = Ложь;
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьНовоеКоличество(СтрокаТовары)
	Если СтрокаТовары.Кратность = 0 Тогда
		СтрокаТовары.Кратность = 1; 
	КонецЕсли;
	Если Цел(СтрокаТовары.КоличествоИсточник / СтрокаТовары.Кратность) 
		= СтрокаТовары.КоличествоИсточник / СтрокаТовары.Кратность Тогда
		СтрокаТовары.Количество = СтрокаТовары.КоличествоИсточник;
	Иначе
		СтрокаТовары.Количество = (Цел(СтрокаТовары.КоличествоИсточник / СтрокаТовары.Кратность) + 1)
				* СтрокаТовары.Кратность;
	КонецЕсли;
	СтрокаТовары.ПереноситьВДокумент = Истина;
КонецПроцедуры

&НаСервере
Функция ЗаполнитьКратносьРассчитатьКоличествоСервер()
	Номенклатура = Товары.Выгрузить(, "Номенклатура").ВыгрузитьКолонку("Номенклатура");
	ТаблицаКратности = РегистрыСведений.бг_КратностьНоменклатуры.КратностьНоменклатуры(Период, Номенклатура);
	Для каждого СтрокаТовары Из Товары Цикл
		СтрокаКратность = ТаблицаКратности.Найти(СтрокаТовары.Номенклатура, "Номенклатура");
		Если Не СтрокаКратность = Неопределено И Не СтрокаКратность.Кратность = 0 Тогда
			СтрокаТовары.Кратность = СтрокаКратность.Кратность;
			СтрокаТовары.ЗапоминатьКратность = Ложь;
			РассчитатьНовоеКоличество(СтрокаТовары);
		КонецЕсли;
	КонецЦикла;
КонецФункции

&НаСервере
Функция ПоместитьВХранилище(ВыбранныеПозиции)
	
	Возврат ПоместитьВоВременноеХранилище(ВыбранныеПозиции);
	
КонецФункции

&НаКлиенте
Процедура УстановитьОтметку(Отметка)
	Для каждого Идентификатор Из Элементы.Товары.ВыделенныеСтроки Цикл
		СтрокаТовары = Товары.НайтиПоИдентификатору(Идентификатор);
		СтрокаТовары.ПереноситьВДокумент = Отметка;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьФорму(ДанныеЗаполнения)
	Товары.Очистить();
	ТоварыДляЗаполнения = Товары.Выгрузить();
	Для каждого СтрокаДанные Из ДанныеЗаполнения Цикл
		НоваяСтрока = ТоварыДляЗаполнения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанные);
		НоваяСтрока.КоличествоИсточник = НоваяСтрока.Количество;
		НоваяСтрока.Кратность = 1;
	КонецЦикла;
	ТоварыДляЗаполнения.Свернуть("Номенклатура, Кратность", "Количество, КоличествоИсточник");
	Товары.Сортировать("Номенклатура");
	Товары.Загрузить(ТоварыДляЗаполнения);
КонецПроцедуры

&НаСервере
Процедура ЗапомнитьКратностьНаСервере()
	СтруктураОтбора = Новый Структура("ЗапоминатьКратность, ПереноситьВДокумент", Истина, Истина);
	ТаблицаКратности = Товары.Выгрузить(СтруктураОтбора, "Номенклатура, Кратность");
	РегистрыСведений.бг_КратностьНоменклатуры.ЗапомнитьКратность(Период, ТаблицаКратности);
КонецПроцедуры

#КонецОбласти 