#Область ПрограммныйИнтерфейс

// Возвращает марки документа по табличной части с диапазонами марок бг_ДанныеФСМ
//
// Параметры:
//   Ссылка - ДокументСсылка.ДвижениеПродукцииИМатериалов 
//
// Возвращаемое значение:
//   ТаблицаЗначений - данные о марках документа, содержит колонки:
//  	* Серия - СправочникСсылка.СерииНоменклатуры - Серия ФСМ
//  	* НомерМарки - Число - Номер марки
//
Функция бг_МаркиДокумента(Ссылка) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	ШаблонПолныйНомер = "%1%2%3";
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	Результат.Колонки.Добавить("НомерМарки", 
		ОбщегоНазначения.ОписаниеТипаЧисло(РегистрыСведений.бг_ИдентификаторыМарок.ДлинаНомераМарки(), 0));
	Результат.Колонки.Добавить("ОрганизацияЕГАИС", Новый ОписаниеТипов("СправочникСсылка.КлассификаторОрганизацийЕГАИС"));
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДвижениеПродукцииИМатериаловбг_ДанныеФСМ.Разряд КАК Разряд,
	|	ДвижениеПродукцииИМатериаловбг_ДанныеФСМ.НачалоДиапазона КАК НачалоДиапазона,
	|	ДвижениеПродукцииИМатериаловбг_ДанныеФСМ.КонецДиапазона КАК КонецДиапазона,
	|	ДвижениеПродукцииИМатериаловбг_ДанныеФСМ.Серия КАК Серия,
	|	СерииНоменклатуры.бг_Номенклатура.бг_ТипМарки.Код КАК ТипМарки,
	|	СерииНоменклатуры.бг_ОрганизацияЕГАИСВладелец КАК ОрганизацияЕГАИС
	|ИЗ
	|	Документ.ДвижениеПродукцииИМатериалов.бг_ДанныеФСМ КАК ДвижениеПродукцииИМатериаловбг_ДанныеФСМ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО ДвижениеПродукцииИМатериаловбг_ДанныеФСМ.Серия = СерииНоменклатуры.Ссылка
	|			И (ДвижениеПродукцииИМатериаловбг_ДанныеФСМ.Ссылка = &Ссылка)";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НачалоДиапазона = СтрШаблон(ШаблонПолныйНомер, Выборка.ТипМарки, Выборка.Разряд, Выборка.НачалоДиапазона);
		НачалоДиапазона = Число(НачалоДиапазона);
		КонецДиапазона = СтрШаблон(ШаблонПолныйНомер, Выборка.ТипМарки, Выборка.Разряд, Выборка.КонецДиапазона);
		КонецДиапазона = Число(КонецДиапазона);
		Для ТекущаяМарка = НачалоДиапазона По КонецДиапазона Цикл
			СтрокаМарка = Результат.Добавить();
			СтрокаМарка.Серия = Выборка.Серия;
			СтрокаМарка.НомерМарки = ТекущаяМарка;
			СтрокаМарка.ОрганизацияЕГАИС = Выборка.ОрганизацияЕГАИС;
		КонецЦикла;
	КонецЦикла;
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

&ИзменениеИКонтроль("СформироватьПечатнуюФормуНакладнойНаПеремещениеПолуфабрикатов")
Функция бг_СформироватьПечатнуюФормуНакладнойНаПеремещениеПолуфабрикатов(МассивОбъектов, ОбъектыПечати)

	УстановитьПривилегированныйРежим(Истина);

	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов();
	Колонка = КолонкаКодов.ИмяКолонки;
	ПредставлениеКолонкиКодов = КолонкаКодов.ПредставлениеКолонки;
	ВыводитьКоды = ЗначениеЗаполнено(Колонка);

	ИспользоватьУпаковкиНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры");

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПеремещениеПолуфабрикатов_Накладная";
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ДвижениеПродукцииИМатериалов.ПФ_MXL_НакладнаяНаПеремещение");

#Вставка
	бг_МакетИтогов = УправлениеПечатью.МакетПечатнойФормы(
		"Документ.ДвижениеПродукцииИМатериалов.бг_ПФ_MXL_НакладнаяНаПеремещениеСИтогами");
#КонецВставки

	ОбластьЗаголовкаПеремещение         = Макет.ПолучитьОбласть("ЗаголовокПеремещение");
	ОбластьЗаголовкаВнутренняяПередача  = Макет.ПолучитьОбласть("ЗаголовокВнутренняяПередача");

	Если ВыводитьКоды Тогда
		ОбластьКодовШапка  = Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаКодов");
		ОбластьКодовШапка.Параметры.ИмяКолонкиКодов = ПредставлениеКолонкиКодов;

		ОбластьКодовСтрока = Макет.ПолучитьОбласть("Строка|КолонкаКодов");
		ОбластьКодовПодвал = Макет.ПолучитьОбласть("Подвал|КолонкаКодов");
	Иначе
		ОбластьТовары = Макет.Область("Товар");
		ОбластьТовары.ШиринаКолонки = ОбластьТовары.ШиринаКолонки + Макет.Область("КолонкаКодов").ШиринаКолонки;
	КонецЕсли;

	Если ИспользоватьУпаковкиНоменклатуры Тогда
		ОбластьУпаковокШапка  =  Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаУпаковок");
		ОбластьУпаковокСтрока =  Макет.ПолучитьОбласть("Строка|КолонкаУпаковок");
		ОбластьУпаковокПодвал =  Макет.ПолучитьОбласть("Подвал|КолонкаУпаковок");
	Иначе
		ОбластьТовары = Макет.Область("Товар");
		ОбластьТовары.ШиринаКолонки = ОбластьТовары.ШиринаКолонки 
		+ Макет.Область("КолонкаУпаковокКоличество").ШиринаКолонки
		+ Макет.Область("КолонкаУпаковокПредставление").ШиринаКолонки;
	КонецЕсли;

	ОбластьНомераШапка = Макет.ПолучитьОбласть("ШапкаТаблицы|НомерСтроки");
	ОбластьДанныхШапка = Макет.ПолучитьОбласть("ШапкаТаблицы|Товар");
	ОбластьКонецСтрокиШапка = Макет.ПолучитьОбласть("ШапкаТаблицы|КонецСтроки");

	ОбластьНомераСтрока = Макет.ПолучитьОбласть("Строка|НомерСтроки");
	ОбластьДанныхСтрока = Макет.ПолучитьОбласть("Строка|Товар");
	ОбластьКонецСтрокиСтрока = Макет.ПолучитьОбласть("Строка|КонецСтроки");

	ОбластьНомераПодвал = Макет.ПолучитьОбласть("Подвал|НомерСтроки");
	ОбластьДанныхПодвал = Макет.ПолучитьОбласть("Подвал|Товар");
	ОбластьКонецСтрокиПодвал = Макет.ПолучитьОбласть("Подвал|КонецСтроки");

#Вставка
	бг_ОбластьНомераИтоги = бг_МакетИтогов.ПолучитьОбласть("Итоги|НомерСтроки");
	бг_ОбластьКодовИтоги = бг_МакетИтогов.ПолучитьОбласть("Итоги|КолонкаКодов");
	бг_ОбластьДанныхИтоги = бг_МакетИтогов.ПолучитьОбласть("Итоги|Товар");
	бг_ОбластьУпаковокИтоги = бг_МакетИтогов.ПолучитьОбласть("Итоги|КолонкаУпаковок");
	бг_ОбластьКонецСтрокиИтоги = бг_МакетИтогов.ПолучитьОбласть("Итоги|КонецСтроки");
#КонецВставки

	ОбластьПодписей = Макет.ПолучитьОбласть("Подписи");

	ЗапросПоШапке = Новый Запрос;
	ЗапросПоШапке.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                              КАК Ссылка,
	|	ТаблицаДокумента.Номер                               КАК Номер,
	|	ТаблицаДокумента.Дата                                КАК Дата,
	|	ТаблицаДокумента.ИсправляемыйДокумент.Номер          КАК НомерИсправляемогоДокумента,
	|	ТаблицаДокумента.ИсправляемыйДокумент.Дата           КАК ДатаИсправляемогоДокумента,
	|	ТаблицаДокумента.Организация.Префикс                 КАК Префикс,
	|	ТаблицаДокумента.ХозяйственнаяОперация               КАК ХозяйственнаяОперация,
	|	ТаблицаДокумента.Отправитель.Представление           КАК ОтправительПредставление,
	|	ТаблицаДокумента.Получатель.Представление            КАК ПолучательПредставление,
	|	ВЫБОР КОГДА ТаблицаДокумента.Организация.НаименованиеСокращенное = """" ТОГДА
	|		ТаблицаДокумента.Организация.Наименование
	|	ИНАЧЕ
	|		ТаблицаДокумента.Организация.НаименованиеСокращенное
	|	КОНЕЦ                                                  КАК ОрганизацияПредставление,
	|	ВЫБОР КОГДА ТаблицаДокумента.ОрганизацияПолучатель.НаименованиеСокращенное = """" ТОГДА
	|		ТаблицаДокумента.ОрганизацияПолучатель.Наименование
	|	ИНАЧЕ
	|		ТаблицаДокумента.ОрганизацияПолучатель.НаименованиеСокращенное
	|	КОНЕЦ                                                  КАК ОрганизацияПолучательПредставление,
	|	ВЫБОР КОГДА ТаблицаДокумента.Отправитель.ИспользоватьСерииНоменклатуры
	|	 ИЛИ ТаблицаДокумента.Получатель.ИспользоватьСерииНоменклатуры ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьСерииНоменклатуры
	|ИЗ
	|	Документ.ДвижениеПродукцииИМатериалов КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В(&МассивДокументов)
	|	И ТаблицаДокумента.ХозяйственнаяОперация В 
	|		(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеПолуфабрикатов),
	|		 ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеПолуфабрикатовМеждуФилиалами))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	Дата
	|ИТОГИ ПО
	|	ИспользоватьСерииНоменклатуры
	|";

	ЗапросПоШапке.УстановитьПараметр("МассивДокументов", МассивОбъектов);

	ДеревоОбъектов = ЗапросПоШапке.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);

	ПерваяСтрокаОбъектов = Истина;  // В дереве объектов может быть максимум 2 строки, эта переменная для 
	// определения того, что началась вторая строка для вывода разделителя границ
	ДопПараметрыПредставлениеНоменклатуры = НоменклатураКлиентСервер.ДополнительныеПараметрыПредставлениеНоменклатурыДляПечати();
	ДопПараметрыПредставлениеНоменклатуры.КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	Для Каждого ИспользованиеСерий Из ДеревоОбъектов.Строки Цикл

		ЗапросПоТоварам = Новый Запрос;
		ЗапросПоТоварам.УстановитьПараметр("МассивОбъектов", ИспользованиеСерий.Строки.ВыгрузитьКолонку("Ссылка"));

		Если ИспользованиеСерий.ИспользоватьСерииНоменклатуры Тогда

			ТекстЗапросаПоТоварам = 
			"ВЫБРАТЬ
			|	ТаблицаТовары.Ссылка КАК Ссылка,
			|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
			|	ТаблицаТовары.Номенклатура КАК Номенклатура,
			|	ТаблицаТовары.Характеристика КАК Характеристика,
			|	ВЫБОР
			|		КОГДА ТаблицаТовары.СтатусУказанияСерий В(10, 14)
			|			ТОГДА ТаблицаТовары.Серия
			|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
			|	КОНЕЦ КАК Серия,
			|	ТаблицаТовары.Упаковка КАК Упаковка,
			|	ТаблицаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
			|	ТаблицаТовары.Количество КАК Количество
			|ПОМЕСТИТЬ ТаблицаТоваров
			|ИЗ
			|	Документ.ДвижениеПродукцииИМатериалов.Товары КАК ТаблицаТовары
			|ГДЕ
			|	ТаблицаТовары.Ссылка В(&МассивОбъектов)
			|	И НЕ ТаблицаТовары.СтатусУказанияСерий В (2, 4, 6, 8)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТаблицаСерии.Ссылка,
			|	МИНИМУМ(ТаблицаТовары.НомерСтроки),
			|	ТаблицаСерии.Номенклатура,
			|	ТаблицаСерии.Характеристика,
			|	ТаблицаСерии.Серия,
			|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка),
			|	ТаблицаСерии.Количество,
			|	ТаблицаСерии.Количество
			|ИЗ
			|	Документ.ДвижениеПродукцииИМатериалов.Серии КАК ТаблицаСерии
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДвижениеПродукцииИМатериалов.Товары КАК ТаблицаТовары
			|		ПО ТаблицаСерии.Ссылка = ТаблицаТовары.Ссылка
			|			И ТаблицаСерии.Номенклатура = ТаблицаТовары.Номенклатура
			|			И ТаблицаСерии.Характеристика = ТаблицаТовары.Характеристика
			|ГДЕ
			|	ТаблицаСерии.Ссылка В(&МассивОбъектов)
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаСерии.Ссылка,
			|	ТаблицаСерии.Номенклатура,
			|	ТаблицаСерии.Характеристика,
			|	ТаблицаСерии.Серия,
			|	ТаблицаСерии.Количество,
			|	ТаблицаСерии.Количество
			|
			|ИМЕЮЩИЕ
			|	МАКСИМУМ(ТаблицаТовары.СтатусУказанияСерий) В (2, 4, 6, 8)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаТоваров.Ссылка,
			|	МИНИМУМ(ТаблицаТоваров.НомерСтроки) КАК НомерСтроки,
			|	ТаблицаТоваров.Номенклатура,
			|	ТаблицаТоваров.Характеристика,
			|	ВЫБОР
			|		КОГДА ТаблицаТоваров.Номенклатура.ВидНоменклатуры.НастройкаИспользованияСерий = ЗНАЧЕНИЕ(Перечисление.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара)
			|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
			|		ИНАЧЕ ТаблицаТоваров.Упаковка
			|	КОНЕЦ КАК Упаковка,
			|	СУММА(ТаблицаТоваров.КоличествоУпаковок) КАК КоличествоУпаковок,
			|	СУММА(ТаблицаТоваров.Количество) КАК Количество,
			|	ВЫБОР
			|		КОГДА ТаблицаТоваров.Номенклатура.ВидНоменклатуры.НастройкаИспользованияСерий = ЗНАЧЕНИЕ(Перечисление.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара)
			|			ТОГДА NULL
			|		ИНАЧЕ ТаблицаТоваров.Серия
			|	КОНЕЦ КАК Серия
			|ПОМЕСТИТЬ СуммированнаяТаблицаТоваров
			|ИЗ
			|	ТаблицаТоваров КАК ТаблицаТоваров
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаТоваров.Ссылка,
			|	ТаблицаТоваров.Номенклатура,
			|	ТаблицаТоваров.Характеристика,
			|	ВЫБОР
			|		КОГДА ТаблицаТоваров.Номенклатура.ВидНоменклатуры.НастройкаИспользованияСерий = ЗНАЧЕНИЕ(Перечисление.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара)
			|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
			|		ИНАЧЕ ТаблицаТоваров.Упаковка
			|	КОНЕЦ,
			|	ВЫБОР
			|		КОГДА ТаблицаТоваров.Номенклатура.ВидНоменклатуры.НастройкаИспользованияСерий = ЗНАЧЕНИЕ(Перечисление.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара)
			|			ТОГДА NULL
			|		ИНАЧЕ ТаблицаТоваров.Серия
			|	КОНЕЦ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаТовары.Ссылка КАК Ссылка,
			|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
			|	ТаблицаТовары.Номенклатура КАК Товар,
			|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
			|	ТаблицаТовары.Номенклатура.Код КАК Код,
			|	ТаблицаТовары.Номенклатура.Артикул КАК Артикул,
			|	ТаблицаТовары.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
			|	ТаблицаТовары.Характеристика КАК Характеристика,
			|	ТаблицаТовары.Серия КАК Серия,
			|	ВЫБОР
			|		КОГДА ТаблицаТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
			|			ТОГДА ПРЕДСТАВЛЕНИЕССЫЛКИ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения)
			|		ИНАЧЕ ПРЕДСТАВЛЕНИЕССЫЛКИ(ТаблицаТовары.Упаковка)
			|	КОНЕЦ КАК ПредставлениеЕдининицыИзмеренияУпаковки,
			|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
			|	ТаблицаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
			|	ТаблицаТовары.Количество КАК Количество,
			|	ТаблицаТовары.Номенклатура.ВидНоменклатуры.НастройкаИспользованияСерий КАК НастройкаИспользованияСерий
			|ИЗ
			|	СуммированнаяТаблицаТоваров КАК ТаблицаТовары
			|
			|УПОРЯДОЧИТЬ ПО
			|	Ссылка,
			|	НомерСтроки,
			|	Товар,
			|	Характеристика
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаТоваров.Ссылка КАК Ссылка,
			|	МИНИМУМ(ТаблицаТоваров.НомерСтроки) КАК НомерСтроки,
			|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
			|	ТаблицаТоваров.Характеристика КАК Характеристика,
			|	ТаблицаТоваров.Серия.Наименование КАК ПредставлениеСерии
			|ИЗ
			|	ТаблицаТоваров КАК ТаблицаТоваров
			|ГДЕ
			|	ТаблицаТоваров.Номенклатура.ВидНоменклатуры.НастройкаИспользованияСерий = ЗНАЧЕНИЕ(Перечисление.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара)
			|	И НЕ ТаблицаТоваров.Серия ЕСТЬ NULL 
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаТоваров.Ссылка,
			|	ТаблицаТоваров.Номенклатура,
			|	ТаблицаТоваров.Характеристика,
			|	ТаблицаТоваров.Серия.Наименование
			|
			|УПОРЯДОЧИТЬ ПО
			|	Ссылка,
			|	НомерСтроки,
			|	Номенклатура,
			|	Характеристика,
			|	ПредставлениеСерии";

			ЗапросПоТоварам.Текст = ТекстЗапросаПоТоварам;
			РезультатЗапроса		 = ЗапросПоТоварам.ВыполнитьПакет();
			ВыборкаПоТоварам		 = РезультатЗапроса[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			ВыборкаПоСериям 		 = РезультатЗапроса[3].Выбрать();

		Иначе

			ТекстЗапросаПоТоварам = 
			"ВЫБРАТЬ
			|	ТаблицаТовары.Ссылка КАК Ссылка,
			|	ТаблицаТовары.Номенклатура КАК Товар,
			|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
			|	ТаблицаТовары.Номенклатура.Код КАК Код,
			|	ТаблицаТовары.Номенклатура.Артикул КАК Артикул,
			|	ТаблицаТовары.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
			|	ТаблицаТовары.Характеристика КАК Характеристика,
			|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
			|	ВЫБОР
			|		КОГДА ТаблицаТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
			|			ТОГДА ПРЕДСТАВЛЕНИЕССЫЛКИ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения)
			|		ИНАЧЕ ПРЕДСТАВЛЕНИЕССЫЛКИ(ТаблицаТовары.Упаковка)
			|	КОНЕЦ КАК ПредставлениеЕдининицыИзмеренияУпаковки,
			|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
			|	ТаблицаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
			|	ТаблицаТовары.Количество КАК Количество,
			|	ЛОЖЬ КАК НастройкаИспользованияСерий,
			|	ТаблицаТовары.НомерСтроки
			|ИЗ
			|	Документ.ДвижениеПродукцииИМатериалов.Товары КАК ТаблицаТовары
			|ГДЕ
			|	ТаблицаТовары.Ссылка В(&МассивОбъектов)
			|
			|УПОРЯДОЧИТЬ ПО
			|	Ссылка,
			|	ТаблицаТовары.НомерСтроки,
			|	Товар,
			|	Характеристика";

			ЗапросПоТоварам.Текст = ТекстЗапросаПоТоварам;
			ВыборкаПоТоварам = ЗапросПоТоварам.Выполнить().Выбрать();

		КонецЕсли;

		ПервыйДокумент = Истина;

		Для Каждого Шапка Из ИспользованиеСерий.Строки Цикл

			ОписаниеДокумента = Новый Структура("Ссылка");
			ЗаполнитьЗначенияСвойств(ОписаниеДокумента, Шапка);

			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			Если Не ПервыйДокумент ИЛИ (ПервыйДокумент И НЕ ПерваяСтрокаОбъектов) Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			ПервыйДокумент = Ложь;

			// Выводим шапку накладной
			Если Шапка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеПолуфабрикатов Тогда

				ОбластьЗаголовка  = ОбластьЗаголовкаПеремещение;

			Иначе //Шапка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеПолуфабрикатовМеждуФилиалами

				ОбластьЗаголовка = ОбластьЗаголовкаВнутренняяПередача;

			КонецЕсли;

			ОбластьЗаголовка.Параметры.Заполнить(Шапка);

			ПараметрыОбласти = Новый Структура;
			ПараметрыОбласти.Вставить(
			"ТекстЗаголовка",
			ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(
			Шапка,
			НСтр("ru = 'Накладная на перемещение';
			|en = 'Transfer note'", ОбщегоНазначения.КодОсновногоЯзыка())));
			ОбластьЗаголовка.Параметры.Заполнить(ПараметрыОбласти);

			ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьЗаголовка, ОписаниеДокумента.Ссылка);
			ТабличныйДокумент.Вывести(ОбластьЗаголовка);

			ТабличныйДокумент.Вывести(ОбластьНомераШапка);

			Если ВыводитьКоды Тогда
				ТабличныйДокумент.Присоединить(ОбластьКодовШапка);
			КонецЕсли;

			ТабличныйДокумент.Присоединить(ОбластьДанныхШапка);

			Если ИспользоватьУпаковкиНоменклатуры Тогда
				ТабличныйДокумент.Присоединить(ОбластьУпаковокШапка);
			КонецЕсли;

			ТабличныйДокумент.Присоединить(ОбластьКонецСтрокиШапка);

#Вставка
			бг_Итоги = Новый ТаблицаЗначений;
			бг_Итоги.Колонки.Добавить("Количество", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
			бг_Итоги.Колонки.Добавить("ПредставлениеБазовойЕдиницыИзмерения",
				ОбщегоНазначения.ОписаниеТипаСтрока(50));
#КонецВставки

			// Выборка товаров

			НомерСтроки = 1;

			Пока ВыборкаПоТоварам.НайтиСледующий(Новый Структура("Ссылка", ОписаниеДокумента.Ссылка)) Цикл

				ПараметрыОбласти = Новый Структура;
				ПараметрыОбласти.Вставить("НомерСтроки", НомерСтроки);
				ОбластьНомераСтрока.Параметры.Заполнить(ПараметрыОбласти);

				ТабличныйДокумент.Вывести(ОбластьНомераСтрока);

				Если ВыводитьКоды Тогда
					ОбластьКодовСтрока.Параметры.Артикул = ВыборкаПоТоварам[Колонка];
					ТабличныйДокумент.Присоединить(ОбластьКодовСтрока);
				КонецЕсли;

				Если ВыборкаПоТоварам.НастройкаИспользованияСерий = Перечисления.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара Тогда

					СтруктураПоиска = Новый Структура;
					СтруктураПоиска.Вставить("Ссылка", ВыборкаПоТоварам.Ссылка);
					СтруктураПоиска.Вставить("НомерСтроки", ВыборкаПоТоварам.НомерСтроки);
					СтруктураПоиска.Вставить("Номенклатура", ВыборкаПоТоварам.Товар);
					СтруктураПоиска.Вставить("Характеристика", ВыборкаПоТоварам.Характеристика);

					СтрокаСерий = "";
					Пока ВыборкаПоСериям.НайтиСледующий(СтруктураПоиска) Цикл
						СтрокаСерий = СтрокаСерий + ВыборкаПоСериям.ПредставлениеСерии + ", ";
					КонецЦикла;

					Если СтрДлина(СтрокаСерий) <> 0 Тогда
						СтрокаСерий = Лев(СтрокаСерий, СтрДлина(СтрокаСерий) - 2);
					КонецЕсли;

				Иначе

					СтрокаСерий = ВыборкаПоТоварам.Серия;

				КонецЕсли;

				ОбластьДанныхСтрока.Параметры.Товар = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
				СокрЛП(ВыборкаПоТоварам.ТоварНаименование),
				СокрЛП(ВыборкаПоТоварам.ХарактеристикаНаименование),
				, // Упаковка
				СокрЛП(СтрокаСерий),
				ДопПараметрыПредставлениеНоменклатуры);

				ТабличныйДокумент.Присоединить(ОбластьДанныхСтрока);

				Если ИспользоватьУпаковкиНоменклатуры Тогда
					ОбластьУпаковокСтрока.Параметры.Заполнить(ВыборкаПоТоварам);
					ТабличныйДокумент.Присоединить(ОбластьУпаковокСтрока);
				КонецЕсли;

				ОбластьКонецСтрокиСтрока.Параметры.Заполнить(ВыборкаПоТоварам);
				ТабличныйДокумент.Присоединить(ОбластьКонецСтрокиСтрока);

				НомерСтроки = НомерСтроки + 1;

#Вставка
				ЗаполнитьЗначенияСвойств(бг_Итоги.Добавить(), ВыборкаПоТоварам);
#КонецВставки

			КонецЦикла;


			ТабличныйДокумент.Вывести(ОбластьНомераПодвал);

			Если ВыводитьКоды Тогда
				ТабличныйДокумент.Присоединить(ОбластьКодовПодвал);
			КонецЕсли;

			ТабличныйДокумент.Присоединить(ОбластьДанныхПодвал);

			Если ИспользоватьУпаковкиНоменклатуры Тогда
				ТабличныйДокумент.Присоединить(ОбластьУпаковокПодвал);
			КонецЕсли;

			ТабличныйДокумент.Присоединить(ОбластьКонецСтрокиПодвал);

#Вставка
			Если бг_Итоги.Количество() > 0 Тогда
				бг_Итоги.Свернуть("ПредставлениеБазовойЕдиницыИзмерения", "Количество");
				Для каждого бг_ИтогиПоЕдиницеИзмерения Из бг_Итоги Цикл
					ТабличныйДокумент.Вывести(бг_ОбластьНомераИтоги);

					Если ВыводитьКоды Тогда
						ТабличныйДокумент.Присоединить(бг_ОбластьКодовИтоги);
					КонецЕсли;

					ТабличныйДокумент.Присоединить(бг_ОбластьДанныхИтоги);

					Если ИспользоватьУпаковкиНоменклатуры Тогда
						ТабличныйДокумент.Присоединить(бг_ОбластьУпаковокИтоги);
					КонецЕсли;
					
					бг_ОбластьКонецСтрокиИтоги.Параметры.Заполнить(бг_ИтогиПоЕдиницеИзмерения);
					ТабличныйДокумент.Присоединить(бг_ОбластьКонецСтрокиИтоги);
				КонецЦикла;
			КонецЕсли;
#КонецВставки

			// Вывести подписи.
			ТабличныйДокумент.Вывести(ОбластьПодписей);

			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ОписаниеДокумента.Ссылка);

		КонецЦикла;

		ПерваяСтрокаОбъектов = Ложь;

	КонецЦикла;

	ТабличныйДокумент.АвтоМасштаб = Истина;

	Возврат ТабличныйДокумент;

КонецФункции

#КонецОбласти

#КонецОбласти
