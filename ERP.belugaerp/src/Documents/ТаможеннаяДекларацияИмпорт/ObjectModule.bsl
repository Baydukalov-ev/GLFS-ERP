#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

&После("ПриКопировании")
Процедура бг_ПриКопировании(ОбъектКопирования)
	
	Для каждого Строка Из Товары Цикл
		Строка.СуммаНДС =
			ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(
				Строка.ТаможеннаяСтоимость + Строка.СуммаПошлины + Строка.бг_СуммаАкциза,
				Строка.СтавкаНДС,
				Ложь);
	КонецЦикла;
	
КонецПроцедуры

&ИзменениеИКонтроль("ОбработкаПроверкиЗаполнения")
Процедура бг_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	НепроверяемыеРеквизиты = Новый Массив;
	// проверка реквизитов Объекта
	Если Перечисления.ХозяйственныеОперации.ОформлениеГТДСамостоятельно = ВариантОформления Тогда
		НепроверяемыеРеквизиты.Добавить("Соглашение");
	КонецЕсли;
#Удаление
Если Не ЗначениеЗаполнено(Соглашение) Или Не ОбщегоНазначенияУТ.ЗначениеРеквизитаОбъектаТипаБулево(Соглашение, "ИспользуютсяДоговорыКонтрагентов") Тогда
#КонецУдаления
#Вставка
	Если ЗначениеЗаполнено(Соглашение) И Не ОбщегоНазначенияУТ.ЗначениеРеквизитаОбъектаТипаБулево(Соглашение, "ИспользуютсяДоговорыКонтрагентов") Тогда
#КонецВставки
		НепроверяемыеРеквизиты.Добавить("Договор");
	КонецЕсли;
	Если ТаможенныйШтраф > 0 Тогда
		ПроверяемыеРеквизиты.Добавить("СтатьяРасходовШтраф");
	КонецЕсли;
	Если ТаможенныйСбор > 0 Тогда
		ПроверяемыеРеквизиты.Добавить("СтатьяРасходовСбор");
	КонецЕсли;
	
	// проверка табчасти Разделы
	Если ИспользоватьРазделы Тогда
		Шаблон = НСтр("ru = '""%СинонимПоля%"" в строке %НомерСтроки% списка ""%ИмяТабчасти%"" необходимо заполнить.';
						|en = 'Fill out ""%СинонимПоля%"" in row %НомерСтроки% of the ""%ИмяТабчасти%"" list.'");
		Для Каждого Раздел Из Разделы Цикл
			ПроверитьИСообщитьОшибку(Раздел.СтавкаПошлины<>0. И Раздел.СуммаПошлины=0.,
					Отказ, Шаблон, "СуммаПошлины", "Сумма пошлины", "Разделы", Раздел.НомерСтроки);
			ЗаполнитьНДС = ЗначениеЗаполнено(Раздел.СтавкаНДС)
						И Раздел.СтавкаНДС <> Справочники.СтавкиНДС.БезНДС
						И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Раздел.СтавкаНДС, "Ставка") > 0;
			ПроверитьИСообщитьОшибку(ЗаполнитьНДС И Раздел.СуммаНДС=0.,
					Отказ, Шаблон, "СуммаНДС", "Сумма НДС", "Разделы", Раздел.НомерСтроки);
		КонецЦикла;
	Иначе
		НепроверяемыеРеквизиты.Добавить("Разделы");
		НепроверяемыеРеквизиты.Добавить("Товары.НомерРаздела");
	КонецЕсли;
		
	// проверки и исключения, зависимые от статуса декларации
	Если Перечисления.СтатусыТаможенныхДеклараций.ТаможенноеОформление = Статус Тогда
		НепроверяемыеРеквизиты.Добавить("Товары");
		НепроверяемыеРеквизиты.Добавить("Товары.Склад");
		НепроверяемыеРеквизиты.Добавить("Товары.НомерРаздела");
		НепроверяемыеРеквизиты.Добавить("Товары.ТаможеннаяСтоимость");
		НепроверяемыеРеквизиты.Добавить("Товары.НомерДляСФ");
	ИначеЕсли Перечисления.СтатусыТаможенныхДеклараций.ВыпущеноСТаможни = Статус Тогда
		
		// проверка соответствия товаров и разделов
		КешРазделов = Новый Соответствие;
		Для Каждого Раздел Из Разделы Цикл
			
			Значение = НоваяСтруктураПараметров();
			Значение.Раздел = Раздел;
			
			КешРазделов.Вставить(Раздел.НомерРаздела, Значение);
		КонецЦикла;
		Для Каждого Товар Из Товары Цикл
			ЭлементКеша = КешРазделов[Товар.НомерРаздела];
			Если Неопределено <> ЭлементКеша Тогда
				ЭлементКеша.ТаможеннаяСтоимость = ЭлементКеша.ТаможеннаяСтоимость + Товар.ТаможеннаяСтоимость;
				ЭлементКеша.СуммаПошлины = ЭлементКеша.СуммаПошлины + Товар.СуммаПошлины;
				ЭлементКеша.СуммаНДС = ЭлементКеша.СуммаНДС + Товар.СуммаНДС;
			КонецЕсли;
		КонецЦикла;
		Шаблон = НСтр("ru = '""%СинонимПоля%"" в строке %НомерСтроки% списка ""%ИмяТабчасти%"" не соответствует итогу по товарам раздела.';
						|en = '""%СинонимПоля%"" in line %НомерСтроки% of ""%ИмяТабчасти%"" list does not match the total number of goods of the section.'");
		Для Каждого ЭлементКеша Из КешРазделов Цикл
			Значение = ЭлементКеша.Значение; // см. НоваяСтруктураПараметров
			ПроверитьИСообщитьОшибку(Значение.Раздел.ТаможеннаяСтоимость<>Значение.ТаможеннаяСтоимость,
					Отказ, Шаблон, "ТаможеннаяСтоимость", "Таможенная стоимость", "Разделы", Значение.Раздел.НомерСтроки);
			ПроверитьИСообщитьОшибку(Значение.Раздел.СуммаПошлины<>Значение.СуммаПошлины,
					Отказ, Шаблон, "СуммаПошлины", "Сумма пошлины", "Разделы", Значение.Раздел.НомерСтроки);
			ПроверитьИСообщитьОшибку(Значение.Раздел.СуммаНДС<>Значение.СуммаНДС,
					Отказ, Шаблон, "СуммаНДС", "Сумма НДС", "Разделы", Значение.Раздел.НомерСтроки);
		КонецЦикла;
	КонецЕсли;
	// дополнительные проверки
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, НепроверяемыеРеквизиты, Отказ);
	НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект,
												НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ТаможеннаяДекларацияИмпорт),
												Отказ,
												НепроверяемыеРеквизиты);
	НоменклатураСервер.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ);
	
	НепроверяемыеРеквизиты.Добавить("Товары.КоличествоПоРНПТ");
	
	ПараметрыДоступностиСтатей = Новый Структура("ТаможенныйСбор, ТаможенныйШтраф, ОбработкаПроверкиЗаполнения", ТаможенныйСбор, ТаможенныйШтраф, Истина);
	ПараметрыВыбораСтатейИАналитик = Документы.ТаможеннаяДекларацияИмпорт.ПараметрыВыбораСтатейИАналитик(ПараметрыДоступностиСтатей);
	ДоходыИРасходыСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, ПараметрыВыбораСтатейИАналитик);
	
	УчетПрослеживаемыхТоваровЛокализация.ПроверитьКорректностьНастроекТоваровРНПТ(ЭтотОбъект, Товары, Дата);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
	ТаможеннаяДекларацияИмпортЛокализация.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры

&После("ОбработкаЗаполнения")
Процедура бг_ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Для Каждого СтрокаТовары из Товары Цикл
		бг_РассчитатьСуммуАкцизаДляСтрокиТоваров(СтрокаТовары);
	КонецЦикла;
	
	Для Каждого СтрокаРаздела Из Разделы Цикл
		бг_РассчитатьСуммуАкцизаДляСтрокиРаздела(СтрокаРаздела);	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура бг_РассчитатьСуммуАкцизаДляСтрокиТоваров(СтрокаТовары)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Номенклатура.ВидНоменклатуры.бг_ВидОперацииНалогообложенияАкцизом КАК ВидОперацииНалогообложенияАкцизом
	|ПОМЕСТИТЬ ВидыОперацииНалогообложенияАкцизом
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка = &Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КодыВидовОперацийСрезПоследних.ВидОперацииНалогообложенияАкцизом КАК ВидОперацииНалогообложенияАкцизом,
	|	КодыВидовОперацийСрезПоследних.ВидПодакцизныхТоваров КАК ВидПодакцизныхТоваров
	|ПОМЕСТИТЬ ВидыПодакцизныхТоваров
	|ИЗ
	|	РегистрСведений.бг_КодыВидовОперацийНалогообложенияАкцизом.СрезПоследних(
	|			&ДатаДокумента,
	|			ВидОперацииНалогообложенияАкцизом В
	|				(ВЫБРАТЬ
	|					ВидыОперацииНалогообложенияАкцизом.ВидОперацииНалогообложенияАкцизом КАК ВидОперацииНалогообложенияАкцизом
	|				ИЗ
	|					ВидыОперацииНалогообложенияАкцизом КАК ВидыОперацииНалогообложенияАкцизом)) КАК КодыВидовОперацийСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыОперацииНалогообложенияАкцизом.ВидОперацииНалогообложенияАкцизом КАК ВидОперацииНалогообложенияАкцизом,
	|	ВидыПодакцизныхТоваров.ВидПодакцизныхТоваров.НалоговаяСтавка КАК СтавкаАкциза
	|ИЗ
	|	ВидыОперацииНалогообложенияАкцизом КАК ВидыОперацииНалогообложенияАкцизом
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВидыПодакцизныхТоваров КАК ВидыПодакцизныхТоваров
	|		ПО ВидыОперацииНалогообложенияАкцизом.ВидОперацииНалогообложенияАкцизом = ВидыПодакцизныхТоваров.ВидОперацииНалогообложенияАкцизом";
	Запрос.УстановитьПараметр("Номенклатура", СтрокаТовары.Номенклатура);
	Запрос.УстановитьПараметр("ДатаДокумента", Дата);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		СтрокаТовары.бг_ВидОперацииАкциза = Выборка.ВидОперацииНалогообложенияАкцизом;
		СтрокаТовары.бг_СуммаАкциза = Выборка.СтавкаАкциза * СтрокаТовары.Количество * 10;
	КонецЕсли;
	
КонецПроцедуры

Процедура бг_РассчитатьСуммуАкцизаДляСтрокиРаздела(СтрокаРаздела)
	
	СтрокаРаздела.бг_СуммаАкциза = 0;
	
	НайденныеСтрокиРаздела = Товары.НайтиСтроки(Новый Структура("НомерРаздела", СтрокаРаздела.НомерРаздела));
	
	Для Каждого НайденнаяСтрока из НайденныеСтрокиРаздела Цикл
		СтрокаРаздела.бг_СуммаАкциза = СтрокаРаздела.бг_СуммаАкциза + НайденнаяСтрока.бг_СуммаАкциза;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
