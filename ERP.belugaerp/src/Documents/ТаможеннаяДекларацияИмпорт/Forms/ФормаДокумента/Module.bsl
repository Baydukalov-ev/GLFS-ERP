
#Область ОбработчикиСобытий

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	бг_ДобавитьПоляАкцизаНаСервере();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура бг_ТоварыНоменклатураПриИзмененииПосле(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	бг_ТоварыНоменклатураПриИзмененииНаСервереПосле(ТекущаяСтрока.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаСервере
Процедура бг_ТоварыНоменклатураПриИзмененииНаСервереПосле(ИдентификаторСтроки)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(ИдентификаторСтроки);
	ТекущиеДанные.бг_ВидОперацииАкциза = бг_ПолучитьВидОперацииАкциза(ТекущиеДанные.Номенклатура);
	бг_ПересчитатьПоляАкциза(ИдентификаторСтроки);
	бг_РассчитатьСуммуАкцизаДляСтрокиРаздела(ТекущиеДанные.НомерРаздела);
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ТоварыКоличествоУпаковокПриИзмененииПосле(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	бг_ТоварыКоличествоУпаковокПриИзмененииНаСервереПосле(ТекущаяСтрока.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаСервере
Процедура бг_ТоварыКоличествоУпаковокПриИзмененииНаСервереПосле(ИдентификаторСтроки)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(ИдентификаторСтроки);
	бг_ПересчитатьПоляАкциза(ИдентификаторСтроки);
	бг_РассчитатьСуммуАкцизаДляСтрокиРаздела(ТекущиеДанные.НомерРаздела);
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ТоварыСуммаАкцизаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	ТекущаяСтрока.СуммаНДС =
		ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(
		ТекущаяСтрока.ТаможеннаяСтоимость + ТекущаяСтрока.СуммаПошлины + ТекущаяСтрока.бг_СуммаАкциза,
		ТекущаяСтрока.СтавкаНДС,
		Ложь);
	
	бг_РассчитатьСуммуАкцизаДляСтрокиРаздела(ТекущаяСтрока.НомерРаздела);
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ТоварыВидОперацииАкцизаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	бг_ТоварыВидОперацииАкцизаПриИзмененииНаСервере(ТекущаяСтрока.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаСервере
Процедура бг_ТоварыВидОперацииАкцизаПриИзмененииНаСервере(ИдентификаторСтроки)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(ИдентификаторСтроки);
	бг_ПересчитатьПоляАкциза(ИдентификаторСтроки);
	бг_РассчитатьСуммуАкцизаДляСтрокиРаздела(ТекущиеДанные.НомерРаздела)
	
КонецПроцедуры

&НаКлиенте
Процедура бг_РазделыСуммаАкцизаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Разделы.ТекущиеДанные;
	ПересчитатьПоля(ТекущаяСтрока, "СуммаНДС", ТекущаяСтрока, Объект.ИспользоватьРазделы);
	ЗаполнитьЗначенияСвойств(ЭтаФорма, РазностиИтогов(Объект.Разделы, Объект.Товары));
	РаспределитьНаТоварыСервер(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаКомандФормыНаСервере

&НаСервере
&После("РаспределитьНаТоварыСервер")
Процедура бг_РаспределитьНаТоварыСервер(РаспределятьТаможеннуюСтоимость)
	
	Отбор = Новый Структура("НомерРаздела");
	Если РаспределятьТаможеннуюСтоимость Тогда
		СписокСтрокРазделов = Элементы.Разделы.ВыделенныеСтроки;
	Иначе
		СписокСтрокРазделов = Новый Массив();
		СписокСтрокРазделов.Добавить(Элементы.Разделы.ТекущаяСтрока);
	КонецЕсли;
	Разделы = РаботаСТабличнымиЧастямиКлиентСервер.ЭлементыКоллекции(Объект.Разделы, СписокСтрокРазделов);
	Для Каждого Раздел Из Разделы Цикл
		Отбор.НомерРаздела = Раздел.НомерРаздела;
		ТоварыРаздела = Объект.Товары.Выгрузить(Отбор);
		
		Если ТоварыРаздела.Количество() > 0 Тогда
			Коэффициенты = ТоварыРаздела.ВыгрузитьКолонку("ТаможеннаяСтоимость");
			НовыеСуммы = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(Раздел.бг_СуммаАкциза, Коэффициенты);
			Если НовыеСуммы <> Неопределено Тогда
				ТоварыРаздела.ЗагрузитьКолонку(НовыеСуммы, "бг_СуммаАкциза");
			КонецЕсли;
		КонецЕсли;
		
		Для каждого Товар Из ТоварыРаздела Цикл
			Объект.Товары[Товар.НомерСтроки-1].бг_СуммаАкциза = ?(Раздел.бг_СуммаАкциза <> 0., Товар.бг_СуммаАкциза, 0.);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("РассчитатьПоТоварамСервер")
Процедура бг_РассчитатьПоТоварамСервер()
	Отбор = Новый Структура("НомерРаздела");
	Разделы = РаботаСТабличнымиЧастямиКлиентСервер.ЭлементыКоллекции(Объект.Разделы, Элементы.Разделы.ВыделенныеСтроки);
	Для Каждого Раздел Из Разделы Цикл
		Отбор.НомерРаздела = Раздел.НомерРаздела;
		ТоварыРаздела = Объект.Товары.НайтиСтроки(Отбор);
		Если ТоварыРаздела.Количество() > 0 Тогда
#Вставка
			Раздел.бг_СуммаАкциза = 0;
#КонецВставки
			Раздел.ТаможеннаяСтоимость = 0.;
			ПересчитатьПоля(Раздел, "СуммаПошлины, СуммаНДС", Раздел, Объект.ИспользоватьРазделы);
			Для Каждого Товар Из ТоварыРаздела Цикл
#Вставка
				СтавкаАкциза = бг_ПолучитьСтавкуАкциза(Товар.бг_ВидОперацииАкциза, Объект.Дата);
				Товар.бг_СуммаАкциза = СтавкаАкциза * Товар.Количество * 10;
				Раздел.бг_СуммаАкциза = Раздел.бг_СуммаАкциза + Товар.бг_СуммаАкциза;
#КонецВставки
				Раздел.ТаможеннаяСтоимость = Раздел.ТаможеннаяСтоимость + Товар.ТаможеннаяСтоимость;
				Раздел.СуммаПошлины = Раздел.СуммаПошлины + Товар.СуммаПошлины;
				Раздел.СуммаНДС = Раздел.СуммаНДС + Товар.СуммаНДС;
			КонецЦикла;
			ПересчитатьПоля(Раздел, "СтавкаПошлины", Раздел, Объект.ИспользоватьРазделы);
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, РазностиИтогов(Объект.Разделы, Объект.Товары));
КонецПроцедуры

#КонецОбласти

#Область ПоддержкаТаблицФормы
	
&НаКлиентеНаСервереБезКонтекста
&ИзменениеИКонтроль("ПересчитатьПоля")
Процедура бг_ПересчитатьПоля(ТекущиеДанные, Знач ПоляПересчета, Знач ДанныеРаздела, Знач ИспользоватьРазделы)
	
	ПоляПересчета = НРег(ПоляПересчета);
	Если СтрНайти(ПоляПересчета, "суммапошлины")>0 Тогда
		Если ИспользоватьРазделы Тогда
			ТекущиеДанные.СтавкаПошлины = ДанныеРаздела.СтавкаПошлины;
		КонецЕсли;
		ТекущиеДанные.СуммаПошлины = ТекущиеДанные.ТаможеннаяСтоимость * ТекущиеДанные.СтавкаПошлины / 100.;
	ИначеЕсли СтрНайти(ПоляПересчета, "ставкапошлины")>0 Тогда
		ТекущиеДанные.СтавкаПошлины =
		100. * ?(ТекущиеДанные.ТаможеннаяСтоимость=0., 0., ТекущиеДанные.СуммаПошлины / ТекущиеДанные.ТаможеннаяСтоимость);
	КонецЕсли;
	Если СтрНайти(ПоляПересчета, "суммандс")>0 Тогда
		Если ИспользоватьРазделы Тогда
			ТекущиеДанные.СтавкаНДС = ДанныеРаздела.СтавкаНДС;
		КонецЕсли;
#Удаление
		ТекущиеДанные.СуммаНДС =
		ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(
		ТекущиеДанные.ТаможеннаяСтоимость + ТекущиеДанные.СуммаПошлины,
		ТекущиеДанные.СтавкаНДС,
		Ложь);
#КонецУдаления
#Вставка
		ТекущиеДанные.СуммаНДС =
		ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(
		ТекущиеДанные.ТаможеннаяСтоимость + ТекущиеДанные.СуммаПошлины + ТекущиеДанные.бг_СуммаАкциза,
		ТекущиеДанные.СтавкаНДС,
		Ложь);
#КонецВставки
	КонецЕсли;
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("АктуализироватьСтавкуНДСТовары")
Процедура бг_АктуализироватьСтавкуНДСТовары()

	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДС", ОбработкаТабличнойЧастиКлиентСервер.ПараметрыЗаполненияСтавкиНДС(Объект));
	ОбработкаТабличнойЧастиСервер.ОбработатьТЧ(Объект.Товары, СтруктураДействий, Неопределено);

	Для Каждого Строка Из Объект.Товары Цикл
#Удаление
		Строка.СуммаНДС =
		ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(
		Строка.ТаможеннаяСтоимость + Строка.СуммаПошлины,
		Строка.СтавкаНДС,
		Ложь);
#КонецУдаления
#Вставка
		Строка.СуммаНДС =
		ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(
		Строка.ТаможеннаяСтоимость + Строка.СуммаПошлины + Строка.бг_СуммаАкциза,
		Строка.СтавкаНДС,
		Ложь);
#КонецВставки
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура бг_ДобавитьПоляАкцизаНаСервере()
	
	Перем бг_МодификацияИнтерфейсовОбщегоНазначенияСервер;
		бг_МодификацияИнтерфейсовОбщегоНазначенияСервер = ОбщегоНазначения.ОбщийМодуль("бг_МодификацияИнтерфейсовОбщегоНазначенияСервер");
	
	бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект, "ТоварыСуммаАкциза", Элементы.Товары, 
		"Объект.Товары.бг_СуммаАкциза", , Элементы.ТоварыСтранаПроисхождения);
	
	Элементы.ТоварыСуммаАкциза.УстановитьДействие("ПриИзменении", "бг_ТоварыСуммаАкцизаПриИзменении");
	
	бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект, "ТоварыВидОперацииАкциза", Элементы.Товары, 
		"Объект.Товары.бг_ВидОперацииАкциза", , Элементы.ТоварыСтранаПроисхождения);

	Элементы.ТоварыВидОперацииАкциза.УстановитьДействие("ПриИзменении", "бг_ТоварыВидОперацииАкцизаПриИзменении");
	
	бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект, "РазделыСуммаАкциза", Элементы.Разделы, 
		"Объект.Разделы.бг_СуммаАкциза", , Элементы.РазделыСтранаПроисхождения);

	Элементы.РазделыСуммаАкциза.УстановитьДействие("ПриИзменении", "бг_РазделыСуммаАкцизаПриИзменении");
	
КонецПроцедуры

&НаСервере
Процедура бг_ПересчитатьПоляАкциза(ИдентификаторСтроки)
	
	ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	СтавкаАкциза = бг_ПолучитьСтавкуАкциза(ТекущиеДанные.бг_ВидОперацииАкциза, Объект.Дата);
	
	Если СтавкаАкциза <> Неопределено Тогда
		ТекущиеДанные.бг_СуммаАкциза = СтавкаАкциза * ТекущиеДанные.Количество * 10;
		ТекущиеДанные.СуммаНДС =
			ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(
			ТекущиеДанные.ТаможеннаяСтоимость + ТекущиеДанные.СуммаПошлины + ТекущиеДанные.бг_СуммаАкциза,
			ТекущиеДанные.СтавкаНДС,
			Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция бг_ПолучитьСтавкуАкциза(ВидОперацииАкциза, Дата)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	бг_КодыВидовОперацийНалогообложенияАкцизомСрезПоследних.ВидПодакцизныхТоваров.НалоговаяСтавка КАК СтавкаАкциза
	|ИЗ
	|	РегистрСведений.бг_КодыВидовОперацийНалогообложенияАкцизом.СрезПоследних(&ДатаДокумента, ВидОперацииНалогообложенияАкцизом = &ВидОперацииАкциза) КАК бг_КодыВидовОперацийНалогообложенияАкцизомСрезПоследних";
	Запрос.УстановитьПараметр("ВидОперацииАкциза", ВидОперацииАкциза);
	Запрос.УстановитьПараметр("ДатаДокумента", Дата);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.СтавкаАкциза;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервереБезКонтекста
Функция бг_ПолучитьВидОперацииАкциза(Номенклатура)
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Номенклатура.ВидНоменклатуры.бг_ВидОперацииНалогообложенияАкцизом КАК ВидОперацииНалогообложенияАкцизом
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка = &Номенклатура";
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Результат = Запрос.Выполнить();
	Выборка   = Результат.Выбрать();
	
	Если Выборка.Следующий()
		И ЗначениеЗаполнено(Выборка.ВидОперацииНалогообложенияАкцизом) Тогда
		Возврат Выборка.ВидОперацииНалогообложенияАкцизом;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

&НаСервере
Процедура бг_РассчитатьСуммуАкцизаДляСтрокиРаздела(НомерРаздела)
	
	СтрокиРаздела = Объект.Разделы.НайтиСтроки(Новый Структура("НомерРаздела", НомерРаздела));
	Если СтрокиРаздела.Количество() > 0 Тогда
		СтрокаРаздела = СтрокиРаздела[0];
		
		СтрокаРаздела.бг_СуммаАкциза = 0;
		СтрокаРаздела.СуммаНДС = 0;
		
		НайденныеСтрокиРаздела = Объект.Товары.НайтиСтроки(Новый Структура("НомерРаздела", НомерРаздела));
		
		Для Каждого НайденнаяСтрока из НайденныеСтрокиРаздела Цикл
			СтрокаРаздела.бг_СуммаАкциза = СтрокаРаздела.бг_СуммаАкциза + НайденнаяСтрока.бг_СуммаАкциза;
			СтрокаРаздела.СуммаНДС = СтрокаРаздела.СуммаНДС + НайденнаяСтрока.СуммаНДС;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
