#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ЗначениеЗаполнено(ПроизводственнаяКладовая) Тогда
		ЗаполнитьОстаткиГотовойПродукции();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПроизводственнаяКладоваяПриИзменении(Элемент)
	ЗаполнитьОстаткиГотовойПродукции();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	Закрыть(АдресРезультатаПодбораПродукции());
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьОстаткиГотовойПродукции();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтметки(Команда)
	УстановитьОтметку(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметки(Команда)
	УстановитьОтметку(Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьОстаткиГотовойПродукции()
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Склад КАК Склад,
	|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладахОстатки.Характеристика КАК Характеристика,
	|	ТоварыНаСкладахОстатки.Серия КАК Серия,
	|	ТоварыНаСкладахОстатки.ВНаличииОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|			,
	|			Склад = &Склад
	|				И Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|				И Номенклатура.ВидНоменклатуры = &ГотоваяПродукция) КАК ТоварыНаСкладахОстатки";
	Запрос.УстановитьПараметр("Склад", ПроизводственнаяКладовая);
	Запрос.УстановитьПараметр("ГотоваяПродукция", бг_КонстантыПовтИсп.ЗначениеКонстанты("ВидНоменклатурыАлкогольнаяПродукция"));
	Результат = Запрос.Выполнить();
	
	Продукция.Загрузить(Результат.Выгрузить());
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтметку(Значение)
	Для Каждого СтрокаПродукция Из Продукция Цикл
		Если СтрокаПродукция.Количество = 0 Тогда
			СтрокаПродукция.ПеренестиВДокумент = Ложь;
		Иначе
			СтрокаПродукция.ПеренестиВДокумент = Значение;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция АдресРезультатаПодбораПродукции()
	Отбор = Новый Структура("ПеренестиВДокумент", Истина);
	ПродукцияПеренестиВДокумент = Продукция.Выгрузить(Отбор);
	
	Возврат ПоместитьВоВременноеХранилище(ПродукцияПеренестиВДокумент);
КонецФункции

#КонецОбласти
