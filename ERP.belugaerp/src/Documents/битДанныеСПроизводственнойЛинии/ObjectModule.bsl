#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

&После("ПередЗаписью")
Процедура бг_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПропуститьЗаписьДвижений = ДополнительныеСвойства.Свойство("ПропуститьЗаписьДвижений")
									И ДополнительныеСвойства.ПропуститьЗаписьДвижений = Истина;
	
	Если Не ПропуститьЗаписьДвижений Тогда
		Движения.бг_ДвижениеМарок.Записывать = Истина;
		Движения.бг_СоставУпаковок.Записывать = Истина;
		
		АктивностьДвижений = (Статус = Перечисления.бг_СтатусыЗагрузкиДанныхСПроизводственнойЛинии.Загружен
															И РежимЗаписи = РежимЗаписиДокумента.Проведение);
		УстановитьАктивностьДвижений(АктивностьДвижений);
		
		КоличествоМарок = Движения.бг_ДвижениеМарок.Количество();
	КонецЕсли;
КонецПроцедуры

&После("ОбработкаПроверкиЗаполнения")
Процедура бг_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Статус = Перечисления.бг_СтатусыЗагрузкиДанныхСПроизводственнойЛинии.ЗагруженСОшибками Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КодПаллеты");
		МассивНепроверяемыхРеквизитов.Добавить("Организация");
		МассивНепроверяемыхРеквизитов.Добавить("Номенклатура");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаРозлива");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	Если Не ДополнительныеСвойства.Свойство("ПропуститьПроверкуЗагруженныхМарок") Тогда
		Документы.битДанныеСПроизводственнойЛинии.ВыполнитьПроверкуЗагруженныхМарок(ЭтотОбъект, Истина, Отказ);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьАктивностьДвижений(ФлагАктивности)
	
	Для Каждого Движение Из Движения Цикл
		
		Движение.УстановитьАктивность(ФлагАктивности);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
