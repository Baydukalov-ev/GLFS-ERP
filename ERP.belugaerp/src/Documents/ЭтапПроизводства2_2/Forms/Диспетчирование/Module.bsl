#Область ПрограммныйИнтерфейс

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, "Этапы.Статус КАК Статус,", "Этапы.Статус КАК Статус,
		|	isnull(бг_ЭтапыВыходныеИзделия.Произведено, Ложь) КАК бг_Произведено,");

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Документ.ЭтапПроизводства2_2 КАК Этапы", "Документ.ЭтапПроизводства2_2 КАК Этапы
		|	
		|	Левое соединение (Выбрать ВыходныеИзделия.Ссылка КАК Ссылка, 
		|					  Максимум(ВыходныеИзделия.Произведено) КАК Произведено 
		|					  ИЗ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ВыходныеИзделия
		|					  Где НЕ ВыходныеИзделия.Ссылка.ПометкаУдаления
		|					  Сгруппировать По ВыходныеИзделия.Ссылка) КАК бг_ЭтапыВыходныеИзделия
		|		По Этапы.Ссылка = бг_ЭтапыВыходныеИзделия.Ссылка");
	
	Список.ТекстЗапроса = ТекстЗапроса;
	
	ПолеПроизведено = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
			ЭтотОбъект, 
			"Списокбг_Произведено", Элементы.Список, "Список.бг_Произведено", , 
			Элементы.СписокЭтаповГруппаСостояние);
	ПолеПроизведено.Заголовок = НСтр("ru='Произведено'");
	
	бг_ВывестиПоляДанныеЕГАИС();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура бг_СписокВыборПосле(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если Поле.Имя = "бг_НомерОтчетаОПроизводствеЕГАИС"
		И ТекущиеДанные <> Неопределено
		И ЗначениеЗаполнено(ТекущиеДанные.бг_ОтчетОПроизводствеЕГАИС) Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, ТекущиеДанные.бг_ОтчетОПроизводствеЕГАИС);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура бг_ВывестиПоляДанныеЕГАИС()
	Если Не ПравоДоступа("Просмотр", Метаданные.Документы.битОтчетОПроизводствеЕГАИС) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаЗаменить = "Документ.ЭтапПроизводства2_2 КАК Этапы";
	ТекстЗапросаЗамена   = "Документ.ЭтапПроизводства2_2 КАК Этапы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.битОтчетОПроизводствеЕГАИС КАК ОтчетОПроизводствеЕГАИС
	|		ПО Этапы.Ссылка = ОтчетОПроизводствеЕГАИС.ДокументОснование
	|			И ОтчетОПроизводствеЕГАИС.Проведен
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО (ОтчетОПроизводствеЕГАИС.Ссылка = СтатусыДокументовЕГАИС.Документ)";
	Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, ТекстЗапросаЗаменить, ТекстЗапросаЗамена);
	
	ТекстЗапросаЗаменить = "Этапы.Ссылка КАК Ссылка";
	ТекстЗапросаЗамена   = "Этапы.Ссылка КАК Ссылка,
	|	ОтчетОПроизводствеЕГАИС.Ссылка КАК бг_ОтчетОПроизводствеЕГАИС,
	|	ОтчетОПроизводствеЕГАИС.Номер КАК бг_НомерОтчетаОПроизводствеЕГАИС,
	|	СтатусыДокументовЕГАИС.Статус КАК бг_СтатусЕГАИС"; 
	Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, ТекстЗапросаЗаменить, ТекстЗапросаЗамена);
	
	ПолеОтчетЕГАИС = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
			ЭтотОбъект,
			"бг_НомерОтчетаОПроизводствеЕГАИС", Элементы.Список, "Список.бг_НомерОтчетаОПроизводствеЕГАИС");
	ПолеОтчетЕГАИС.Заголовок = НСтр("ru='Отчет ЕГАИС'");
	ПолеОтчетЕГАИС.ГиперссылкаЯчейки = Истина;
	
	ПолеСтатусЕГАИС = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
			ЭтотОбъект,
			"бг_СтатусЕГАИС", Элементы.Список, "Список.бг_СтатусЕГАИС");
	ПолеСтатусЕГАИС.Заголовок = НСтр("ru='Статус ЕГАИС'");
	
	Список.УстановитьОбязательноеИспользование("бг_ОтчетОПроизводствеЕГАИС", Истина);
КонецПроцедуры

#КонецОбласти
