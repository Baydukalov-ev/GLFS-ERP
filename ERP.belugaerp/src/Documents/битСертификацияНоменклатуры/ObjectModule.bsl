#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

&После("ОбработкаЗаполнения")
Процедура бг_ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	ДатаСертификата = ТекущаяДатаСеанса();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		
		Если ЗначениеЗаполнено(Номенклатура) Тогда
			ЗаполнитьПоНоменклатуре();
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Серия) Тогда
			ЗаполнитьПроизводственнуюЛинию();
		КонецЕсли;
	КонецЕсли;
	
	ИнициализироватьДокумент();
КонецПроцедуры

&После("ПриКопировании")
Процедура бг_ПриКопировании(ОбъектКопирования)
	ИнициализироватьДокумент();
КонецПроцедуры

&После("ОбработкаПроведения")
Процедура бг_ОбработкаПроведения(Отказ, РежимПроведения)
	Если РезультатСертификации = Перечисления.бг_РезультатыСертификации.Сертификат Тогда
		ЗаполнитьНомерСертифицированнойПартии();
	Иначе
		ОчиститьНомерСертифицированнойПартии();
	КонецЕсли;
КонецПроцедуры

&После("ОбработкаУдаленияПроведения")
Процедура бг_ОбработкаУдаленияПроведения(Отказ)
	ОчиститьНомерСертифицированнойПартии();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоНоменклатуре()
	РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, "бг_АнализыДляУдостоверенияОКачестве, бг_НормативныйДокумент");
	
	ТиповойАнализ = РеквизитыНоменклатуры.бг_АнализыДляУдостоверенияОКачестве;
	НормативныйДокумент = РеквизитыНоменклатуры.бг_НормативныйДокумент;
	
	ТаблицаАнализов = Документы.битСертификацияНоменклатуры.ТаблицаАнализовДляСертификации(ТиповойАнализ);
	Анализы.Загрузить(ТаблицаАнализов);
КонецПроцедуры

Процедура ЗаполнитьПроизводственнуюЛинию()
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 2
	|	ЭтапВыходныеИзделия.бг_ПроизводственнаяЛиния КАК ПроизводственнаяЛиния
	|ИЗ
	|	Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ЭтапВыходныеИзделия
	|		ПО (СерииНоменклатуры.Ссылка = &Серия)
	|			И СерииНоменклатуры.бг_ДокументВыпуска = ЭтапВыходныеИзделия.Ссылка";
	Запрос.УстановитьПараметр("Серия", Серия);
	Результат = Запрос.Выполнить();
	Выборка   = Результат.Выбрать();
	
	Если Выборка.Количество() = 1
		И Выборка.Следующий() Тогда
		ПроизводственнаяЛиния = Выборка.ПроизводственнаяЛиния;
	КонецЕсли;
КонецПроцедуры

Процедура ИнициализироватьДокумент()
	Ответственный   = Пользователи.ТекущийПользователь();
	РезультатСертификации = Перечисления.бг_РезультатыСертификации.ОтказаноВСертификации;
КонецПроцедуры

Процедура ЗаполнитьНомерСертифицированнойПартии()
	Если ПустаяСтрока(НомерПартии) Тогда
		Возврат;
	КонецЕсли;
	
	СерияОбъект = Серия.ПолучитьОбъект();
	Если СерияОбъект.бг_НомерСертифицированнойПартии <> НомерПартии Тогда
		СерияОбъект.Заблокировать();
		СерияОбъект.бг_НомерСертифицированнойПартии = НомерПартии;
		СерияОбъект.Записать();
	КонецЕсли;
КонецПроцедуры

Процедура ОчиститьНомерСертифицированнойПартии()
	СерияОбъект = Серия.ПолучитьОбъект();
	Если Не ПустаяСтрока(СерияОбъект.бг_НомерСертифицированнойПартии) Тогда
		СерияОбъект.Заблокировать();
		СерияОбъект.бг_НомерСертифицированнойПартии = "";
		СерияОбъект.Записать();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли
