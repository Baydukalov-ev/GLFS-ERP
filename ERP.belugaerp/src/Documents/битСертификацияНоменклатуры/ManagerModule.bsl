#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ТаблицаАнализовДляСертификации(ТиповойАнализ) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТиповыеАнализыПоказатели.Ссылка КАК ТиповойАнализ,
	|	ТиповыеАнализыПоказатели.ПоказательАнализа КАК Показатель,
	|	ПоказателиАнализовНоменклатуры.МинимальноеЗначение КАК МинимальноеНормативноеЗначение,
	|	ПоказателиАнализовНоменклатуры.МаксимальноеЗначение КАК МаксимальноеНормативноеЗначение,
	|	ПоказателиАнализовНоменклатуры.ВидРезультата КАК ВидРезультата,
	|	ЕСТЬNULL(ДопустимыеЗначения.ЗначениеПоказателя, 0) КАК Значение,
	|	ВЫБОР
	|		КОГДА НЕ ДопустимыеЗначения.ЗначениеПоказателя ЕСТЬ NULL
	|				ИЛИ ПоказателиАнализовНоменклатуры.МинимальноеЗначение = 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СоответствуетНормативу
	|ИЗ
	|	Справочник.бг_ТиповыеАнализыНоменклатуры.ПоказателиАнализа КАК ТиповыеАнализыПоказатели
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.бг_ПоказателиАнализовНоменклатуры КАК ПоказателиАнализовНоменклатуры
	|		ПО (ТиповыеАнализыПоказатели.Ссылка = &ТиповойАнализ)
	|			И ТиповыеАнализыПоказатели.ПоказательАнализа = ПоказателиАнализовНоменклатуры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.бг_ПоказателиАнализовНоменклатуры.ДопустимыеЗначенияПоказателей КАК ДопустимыеЗначения
	|		ПО ТиповыеАнализыПоказатели.ПоказательАнализа = ДопустимыеЗначения.Ссылка
	|			И (ДопустимыеЗначения.Ссылка.ВидРезультата = ЗНАЧЕНИЕ(Перечисление.бг_ВидыРезультатовЛабораторныхАнализов.ЗначениеИзСписка))
	|			И (ДопустимыеЗначения.НомерСтроки = 1)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПоказателиАнализовНоменклатуры.Родитель.Наименование,
	|	ПоказателиАнализовНоменклатуры.Наименование";
	Запрос.УстановитьПараметр("ТиповойАнализ", ТиповойАнализ);
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить();
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Если ПравоДоступа("Чтение", Метаданные.Документы.битСертификацияНоменклатуры) Тогда
		
		// Качественное удостоверение
		Обработки.бг_ПечатьКачественногоУдостоверения.ДобавитьКомандуПечати(КомандыПечати);
		
	КонецЕсли;
		
КонецПроцедуры

Функция бг_ДанныеДляПечатнойФормыКачественногоУдостоверения(ПараметрыПечати, МассивОбъектов) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СертификацияНоменклатуры.Ссылка КАК Документ,
	|	СертификацияНоменклатуры.Организация КАК Организация,
	|	СертификацияНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	СертификацияНоменклатуры.Серия КАК Серия,
	|	СертификацияНоменклатуры.Ссылка КАК ДокументСертификации,
	|	СертификацияНоменклатуры.Номер КАК НомерСертификата,
	|	СертификацияНоменклатуры.НормативныйДокумент КАК ГОСТ,
	|	СертификацияНоменклатуры.ДатаСертификата КАК ДатаСертификата,
	|	СертификацияНоменклатуры.Ответственный КАК Автор
	|ПОМЕСТИТЬ НоменклатураСертификации
	|ИЗ
	|	Документ.битСертификацияНоменклатуры КАК СертификацияНоменклатуры
	|ГДЕ
	|	СертификацияНоменклатуры.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Лицензии.Владелец КАК Организация,
	|	Лицензии.Ссылка КАК Ссылка,
	|	Лицензии.бг_Серия КАК ЛицензияНаПроизводствоСерия,
	|	Лицензии.бг_Номер КАК ЛицензияНаПроизводствоНомер,
	|	Лицензии.бг_ДатаВыдачи КАК ЛицензияНаПроизводствоДатаВыдачи,
	|	Лицензии.ДатаНачала КАК ЛицензияНаПроизводствоДатаНачалаСрокаДействия,
	|	Лицензии.ДатаОкончания КАК ЛицензияНаПроизводствоДатаОкончанияСрокаДействия,
	|	Лицензии.КемВыдана КАК ЛицензияНаПроизводствоОрганВыдачи,
	|	Лицензии.бг_ВидЛицензируемойДеятельности КАК ВидЛицензируемойДеятельности,
	|	Лицензии.бг_ВидЛицензируемойДеятельности.Примечание КАК ВидЛицензируемойДеятельностиПримечание,
	|	Лицензии.бг_ВидЛицензируемойДеятельности.Наименование КАК ВидЛицензируемойДеятельностиНаименование
	|ПОМЕСТИТЬ ЛицензииОрганизаций
	|ИЗ
	|	Справочник.ЛицензииПоставщиковАлкогольнойПродукции КАК Лицензии
	|ГДЕ
	|	Лицензии.Владелец В
	|			(ВЫБРАТЬ
	|				НоменклатураСертификации.Организация
	|			ИЗ
	|				НоменклатураСертификации
	|			ГДЕ
	|				НоменклатураСертификации.Документ.Дата < Лицензии.бг_ДатаПрекращенияДействия)
	|	И Лицензии.бг_ТипЛицензии = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЛицензийПоставщиковАлкогольнойПродукции.Производство)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтветственныеЛица.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ОтветственныеЛица.СсылкаОтветственноеЛицо КАК СсылкаОтветственноеЛицо
	|ПОМЕСТИТЬ ДоверенностиОтветственныхЛиц
	|ИЗ
	|	(ВЫБРАТЬ
	|		Пользователи.ФизическоеЛицо КАК ФизическоеЛицо,
	|		МАКСИМУМ(ОтветственныеЛицаОрганизаций.Ссылка) КАК СсылкаОтветственноеЛицо
	|	ИЗ
	|		НоменклатураСертификации КАК НоменклатураСертификации
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОтветственныеЛицаОрганизаций КАК ОтветственныеЛицаОрганизаций
	|				ПО Пользователи.ФизическоеЛицо = ОтветственныеЛицаОрганизаций.ФизическоеЛицо
	|			ПО НоменклатураСертификации.Автор = Пользователи.Ссылка
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Пользователи.ФизическоеЛицо) КАК ОтветственныеЛица
	|ГДЕ
	|	ОтветственныеЛица.ФизическоеЛицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвижениеПродукцииИМатериаловТовары.Серия КАК Серия,
	|	СУММА(ДвижениеПродукцииИМатериаловТовары.Количество) КАК Количество,
	|	СУММА(ВЫБОР
	|			КОГДА СерииНоменклатуры.бг_УпаковкаПаллета.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Паллета)
	|					И СерииНоменклатуры.бг_УпаковкаПаллета.Родитель.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Коробка)
	|				ТОГДА ВЫБОР
	|						КОГДА ЕСТЬNULL(СерииНоменклатуры.бг_УпаковкаПаллета.Родитель.КоличествоУпаковок, 0) <> 0
	|							ТОГДА ДвижениеПродукцииИМатериаловТовары.Количество / СерииНоменклатуры.бг_УпаковкаПаллета.Родитель.КоличествоУпаковок
	|					КОНЕЦ
	|			КОГДА СерииНоменклатуры.бг_УпаковкаПаллета.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Коробка)
	|					И СерииНоменклатуры.бг_УпаковкаПаллета.Родитель.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Бутылка)
	|				ТОГДА ВЫБОР
	|						КОГДА ЕСТЬNULL(СерииНоменклатуры.бг_УпаковкаПаллета.КоличествоУпаковок, 0) <> 0
	|							ТОГДА ДвижениеПродукцииИМатериаловТовары.Количество / СерииНоменклатуры.бг_УпаковкаПаллета.КоличествоУпаковок
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КолМест
	|ПОМЕСТИТЬ ПроизведеннаяПродукция
	|ИЗ
	|	Документ.ДвижениеПродукцииИМатериалов.Товары КАК ДвижениеПродукцииИМатериаловТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДвижениеПродукцииИМатериалов КАК ДвижениеПродукцииИМатериалов
	|		ПО ДвижениеПродукцииИМатериаловТовары.Ссылка = ДвижениеПродукцииИМатериалов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО ДвижениеПродукцииИМатериаловТовары.Серия = СерииНоменклатуры.Ссылка
	|ГДЕ
	|	ДвижениеПродукцииИМатериалов.Проведен
	|	И ДвижениеПродукцииИМатериаловТовары.Серия В
	|			(ВЫБРАТЬ
	|				НоменклатураСертификации.Серия
	|			ИЗ
	|				НоменклатураСертификации КАК НоменклатураСертификации)
	|	И ДвижениеПродукцииИМатериалов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвижениеПродукцииИМатериаловТовары.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураСертификации.Документ КАК Документ,
	|	НоменклатураСертификации.Документ.Дата КАК ДатаДокумента,
	|	НоменклатураСертификации.Документ.Номер КАК НомерДокумента,
	|	НоменклатураСертификации.Организация КАК Организация,
	|	НоменклатураСертификации.Номенклатура КАК Номенклатура,
	|	НоменклатураСертификации.Номенклатура.НаименованиеПолное КАК НаименованиеТовара,
	|	НоменклатураСертификации.Характеристика КАК ХарактеристикаНоменклатуры,
	|	СерииНоменклатуры.Ссылка КАК Серия,
	|	СпрНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ПРЕДСТАВЛЕНИЕ(СпрНоменклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияНаименование,
	|	ВидыНоменклатуры.бг_ЕдиницаИзмеренияМест КАК ЕдиницаИзмеренияМест,
	|	СпрНоменклатура.бг_КонтрагентПроизводитель КАК Производитель,
	|	СпрНоменклатура.бг_ОписаниеСоставаПродукта КАК СоставПродукта,
	|	СпрНоменклатура.бг_УсловияТранспортирования КАК УсловияТранспортирования,
	|	ЕСТЬNULL(ПроизведеннаяПродукция.Количество, 1) КАК Количество,
	|	ЕСТЬNULL(ПроизведеннаяПродукция.КолМест, 1) КАК КолМест,
	|	СпрНоменклатура.бг_АнализыДляУдостоверенияОКачестве КАК АнализыДляУдостоверенияОКачестве,
	|	СпрНоменклатура.ВидАлкогольнойПродукции.Код КАК КодАлкогольнойПродукции,
	|	ВЫБОР
	|		КОГДА СерииНоменклатуры.бг_Сертификат.ТипСертификата = &СертификатСоответствия
	|			ТОГДА СерииНоменклатуры.бг_Сертификат
	|	КОНЕЦ КАК СертификатСоответствия,
	|	СерииНоменклатуры.бг_Сертификат.ТипСертификата КАК ТипСертификата,
	|	ВЫБОР
	|		КОГДА СертификатыНоменклатуры.ТипСертификата = &СертификатСоответствия
	|			ТОГДА СертификатыНоменклатуры.Номер
	|	КОНЕЦ КАК НомерСертификатаСоответствия,
	|	ВЫБОР
	|		КОГДА СертификатыНоменклатуры.ТипСертификата = &СертификатСоответствия
	|			ТОГДА СертификатыНоменклатуры.ОрганВыдавшийДокумент
	|	КОНЕЦ КАК ОрганВыдачиСертификатаСоответствия,
	|	ВЫБОР
	|		КОГДА СертификатыНоменклатуры.ТипСертификата = &СертификатСоответствия
	|			ТОГДА СертификатыНоменклатуры.ДатаНачалаСрокаДействия
	|	КОНЕЦ КАК ДатаВыдачиСертификатСоответствия,
	|	ВЫБОР
	|		КОГДА СертификатыНоменклатуры.ТипСертификата = &СертификатСоответствия
	|			ТОГДА СертификатыНоменклатуры.ДатаОкончанияСрокаДействия
	|	КОНЕЦ КАК ДатаОкончанияСрокаДействияСертификата,
	|	ВЫБОР
	|		КОГДА СертификатыНоменклатуры.ТипСертификата = &СертификатСоответствия
	|			ТОГДА ВЫБОР
	|					КОГДА СертификатыНоменклатуры.Бессрочный
	|						ТОГДА 0
	|					ИНАЧЕ РАЗНОСТЬДАТ(СертификатыНоменклатуры.ДатаНачалаСрокаДействия, СертификатыНоменклатуры.ДатаОкончанияСрокаДействия, МЕСЯЦ)
	|				КОНЕЦ
	|	КОНЕЦ КАК СрокДействияСертификата,
	|	ВЫБОР
	|		КОГДА СертификатыНоменклатуры.ТипСертификата = &ДекларацияОСоответствии
	|			ТОГДА СертификатыНоменклатуры.Ссылка
	|	КОНЕЦ КАК ДекларацияОСоответствии,
	|	ВЫБОР
	|		КОГДА СертификатыНоменклатуры.ТипСертификата = &ДекларацияОСоответствии
	|			ТОГДА СертификатыНоменклатуры.Номер
	|	КОНЕЦ КАК НомерДекларацииОСоответствии,
	|	ВЫБОР
	|		КОГДА СертификатыНоменклатуры.ТипСертификата = &ДекларацияОСоответствии
	|			ТОГДА СертификатыНоменклатуры.ОрганВыдавшийДокумент
	|	КОНЕЦ КАК ОрганВыдачиДекларацииОСоответствии,
	|	ВЫБОР
	|		КОГДА СертификатыНоменклатуры.ТипСертификата = &ДекларацияОСоответствии
	|			ТОГДА СертификатыНоменклатуры.ДатаНачалаСрокаДействия
	|	КОНЕЦ КАК ДатаВыдачиДекларацииОСоответствии,
	|	ВЫБОР
	|		КОГДА СертификатыНоменклатуры.ТипСертификата = &ДекларацияОСоответствии
	|			ТОГДА СертификатыНоменклатуры.ДатаОкончанияСрокаДействия
	|	КОНЕЦ КАК ДатаОкончанияСрокаДействияДекларации,
	|	ВЫБОР
	|		КОГДА СертификатыНоменклатуры.ТипСертификата = &ДекларацияОСоответствии
	|			ТОГДА ВЫБОР
	|					КОГДА СертификатыНоменклатуры.Бессрочный
	|						ТОГДА 0
	|					ИНАЧЕ РАЗНОСТЬДАТ(СертификатыНоменклатуры.ДатаНачалаСрокаДействия, СертификатыНоменклатуры.ДатаОкончанияСрокаДействия, МЕСЯЦ)
	|				КОНЕЦ
	|	КОНЕЦ КАК СрокДействияДекларацииОСоответствии,
	|	ЛицензииОрганизаций.Ссылка КАК ЛицензияНаПроизводство,
	|	ЛицензииОрганизаций.ЛицензияНаПроизводствоСерия КАК ЛицензияНаПроизводствоСерия,
	|	ЛицензииОрганизаций.ЛицензияНаПроизводствоНомер КАК ЛицензияНаПроизводствоНомер,
	|	ЛицензииОрганизаций.ЛицензияНаПроизводствоДатаВыдачи КАК ЛицензияНаПроизводствоДатаВыдачи,
	|	ЛицензииОрганизаций.ЛицензияНаПроизводствоДатаНачалаСрокаДействия КАК ЛицензияНаПроизводствоДатаНачалаСрокаДействия,
	|	ЛицензииОрганизаций.ЛицензияНаПроизводствоДатаОкончанияСрокаДействия КАК ЛицензияНаПроизводствоДатаОкончанияСрокаДействия,
	|	ЛицензииОрганизаций.ЛицензияНаПроизводствоОрганВыдачи КАК ЛицензияНаПроизводствоОрганВыдачи,
	|	ЛицензииОрганизаций.ВидЛицензируемойДеятельности КАК ВидЛицензируемойДеятельности,
	|	ЛицензииОрганизаций.ВидЛицензируемойДеятельностиПримечание КАК ВидЛицензируемойДеятельностиПримечание,
	|	ЛицензииОрганизаций.ВидЛицензируемойДеятельностиНаименование КАК ВидЛицензируемойДеятельностиНаименование,
	|	НоменклатураСертификации.ДокументСертификации КАК ДокументСертификации,
	|	НоменклатураСертификации.ГОСТ КАК ГОСТ,
	|	НоменклатураСертификации.ДатаСертификата КАК ДатаСертификата,
	|	НоменклатураСертификации.НомерСертификата КАК НомерСертификата,
	|	СерииНоменклатуры.ДатаПроизводства КАК ДатаРозлива,
	|	ФизическиеЛица.Ссылка КАК Ответственный,
	|	ФизическиеЛица.бг_Должность КАК Должность,
	|	ОтветственныеЛицаОрганизаций.НомерДокументаПраваПодписи КАК НомерОснования,
	|	ОтветственныеЛицаОрганизаций.ДатаДокументаПраваПодписи КАК ДатаОснования,
	|	ОтветственныеЛицаОрганизаций.Ссылка КАК ОтветственноеЛицо,
	|	ЕСТЬNULL(ОтветственныеЛицаОрганизаций.ДокументПраваПодписи, НЕОПРЕДЕЛЕНО) КАК ДокументПраваПодписи,
	|	НоменклатураСертификации.Организация КАК Контрагент,
	|	СпрНоменклатура.ОбъемДАЛ * 10 КАК Емкость,
	|	ВЫРАЗИТЬ(СерииНоменклатуры.ПроизводительЕГАИС.Контрагент КАК Справочник.Организации) КАК ПроизводительЕГАИСОрганизация
	|ИЗ
	|	НоменклатураСертификации КАК НоменклатураСертификации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СертификатыНоменклатуры КАК СертификатыНоменклатуры
	|			ПО (СерииНоменклатуры.бг_Сертификат = СертификатыНоменклатуры.Ссылка)
	|		ПО (НоменклатураСертификации.Серия = СерииНоменклатуры.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|			ПО (СпрНоменклатура.ВидНоменклатуры = ВидыНоменклатуры.Ссылка)
	|		ПО (НоменклатураСертификации.Номенклатура = СпрНоменклатура.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЛицензииОрганизаций КАК ЛицензииОрганизаций
	|		ПО (НоменклатураСертификации.Организация = ЛицензииОрганизаций.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	|				ЛЕВОЕ СОЕДИНЕНИЕ ДоверенностиОтветственныхЛиц КАК ДоверенностиОтветственныхЛиц
	|					ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОтветственныеЛицаОрганизаций КАК ОтветственныеЛицаОрганизаций
	|					ПО (ДоверенностиОтветственныхЛиц.СсылкаОтветственноеЛицо = ОтветственныеЛицаОрганизаций.Ссылка)
	|				ПО (ФизическиеЛица.Ссылка = ДоверенностиОтветственныхЛиц.ФизическоеЛицо)
	|			ПО (Пользователи.ФизическоеЛицо = ФизическиеЛица.Ссылка)
	|		ПО (НоменклатураСертификации.Автор = Пользователи.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПроизведеннаяПродукция КАК ПроизведеннаяПродукция
	|		ПО (НоменклатураСертификации.Серия = ПроизведеннаяПродукция.Серия)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ
	|ИТОГИ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураСертификации.Номенклатура КАК Номенклатура,
	|	СпрНоменклатура.бг_ФайлПиктограммы КАК ПрисоединенныйФайл
	|ИЗ
	|	НоменклатураСертификации КАК НоменклатураСертификации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО НоменклатураСертификации.Номенклатура = СпрНоменклатура.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураСертификации.Номенклатура,
	|	СпрНоменклатура.бг_ФайлПиктограммы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураСертификации.Документ КАК Документ,
	|	СертификацияНоменклатурыАнализы.Ссылка КАК ДокументСертификации,
	|	НоменклатураСертификации.Номенклатура КАК Номенклатура,
	|	НоменклатураСертификации.Серия КАК Серия,
	|	СертификацияНоменклатурыАнализы.ТиповойАнализ КАК ТиповойАнализ,
	|	СертификацияНоменклатурыАнализы.Показатель КАК Показатель,
	|	СертификацияНоменклатурыАнализы.ВидРезультата КАК ВидРезультата,
	|	СертификацияНоменклатурыАнализы.Значение КАК Значение,
	|	СертификацияНоменклатурыАнализы.СоответствуетНормативу КАК СоответствуетНормативу,
	|	СертификацияНоменклатурыАнализы.МинимальноеНормативноеЗначение КАК МинимальноеНормативноеЗначение,
	|	СертификацияНоменклатурыАнализы.МаксимальноеНормативноеЗначение КАК МаксимальноеНормативноеЗначение
	|ИЗ
	|	НоменклатураСертификации КАК НоменклатураСертификации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.битСертификацияНоменклатуры.Анализы КАК СертификацияНоменклатурыАнализы
	|		ПО НоменклатураСертификации.Документ = СертификацияНоменклатурыАнализы.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НоменклатураСертификации.Документ,
	|	СертификацияНоменклатурыАнализы.НомерСтроки";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("СертификатСоответствия", "Сертификат соответствия");
	Запрос.УстановитьПараметр("ДекларацияОСоответствии", "Декларация о соответствии");
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	
	ДанныеДляПечати = Новый Структура;
	
	ДанныеДляПечати.Вставить("СведенияПоДокументам", 	ПакетРезультатов[4]);
	ДанныеДляПечати.Вставить("ПиктограммыНоменклатуры", ПакетРезультатов[5]);
	ДанныеДляПечати.Вставить("РезультатыАнализа", 		ПакетРезультатов[6]);
	
	Возврат ДанныеДляПечати; 
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли
