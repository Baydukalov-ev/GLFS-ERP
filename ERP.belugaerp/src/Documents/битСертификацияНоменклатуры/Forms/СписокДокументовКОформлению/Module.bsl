#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьПериод();
	
	УстановитьПараметрыДинамическихСписков();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	УстановитьПараметрыДинамическихСписков();
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураОтборДокументыКОформлениюПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокДокументыКОформлению, "Номенклатура", НоменклатураОтбор,
											ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(НоменклатураОтбор));
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОтборДокументыКОформлениюПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокДокументыКОформлению, "Организация", ОрганизацияОтбор,
											ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(ОрганизацияОтбор));
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокДокументыКОформлениюВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если Поле.Имя = "СписокДокументыКОформлениюНомерРаспоряжения" Тогда
		ТекущиеДанные = Элементы.СписокДокументыКОформлению.ТекущиеДанные;
		
		Если ТекущиеДанные <> Неопределено Тогда
			ПоказатьЗначение(, ТекущиеДанные.Распоряжение);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьСписокДокументов(Команда)
	ОткрытьФорму("Документ.битСертификацияНоменклатуры.Форма.СписокДокументов", ПараметрыЖурнала());
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументСертификации(Команда)
	ТекущиеДанные = Элементы.СписокДокументыКОформлению.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Номенклатура", ТекущиеДанные.Номенклатура);
	ПараметрыЗаполнения.Вставить("Серия", ТекущиеДанные.Серия);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОформлениеДокументаСертификацииЗавершение", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ПараметрыЗаполнения);
	ОткрытьФорму("Документ.битСертификацияНоменклатуры.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект, , , , ОписаниеОповещения);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПериод()
	ТекущаяДата = ТекущаяДатаСеанса();
	
	// Период - последний месяц
	Период.ДатаОкончания = КонецДня(ТекущаяДата);
	Период.ДатаНачала    = ДобавитьМесяц(НачалоДня(ТекущаяДата), - 1);
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыДинамическихСписков()
	СписокДокументыКОформлению.Параметры.УстановитьЗначениеПараметра("НачалоПериода", Период.ДатаНачала);
	СписокДокументыКОформлению.Параметры.УстановитьЗначениеПараметра("КонецПериода", Период.ДатаОкончания);
КонецПроцедуры

&НаКлиенте
Функция ПараметрыЖурнала()
	
	СтруктураБыстрогоОтбора = Новый Структура;
	СтруктураБыстрогоОтбора.Вставить("Организация",  ОрганизацияОтбор);
	СтруктураБыстрогоОтбора.Вставить("Номенклатура", НоменклатураОтбор);
	
	ПараметрыЖурнала = Новый Структура;
	ПараметрыЖурнала.Вставить("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора);
	
	Возврат ПараметрыЖурнала;
	
КонецФункции

&НаКлиенте
Процедура ОформлениеДокументаСертификацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Элементы.СписокДокументыКОформлению.Обновить();
КонецПроцедуры

#КонецОбласти
