#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ДобавитьИзменитьКомандыПечати(КомандыПечати) Экспорт
	
	УдаляемыеКоманды = Новый Массив;
	УдаляемыеКоманды.Добавить("КомплектДокументов");
	УдаляемыеКоманды.Добавить("КомплектДокументовСНастройкой");
	
	Для Каждого УдаляемаяКоманда Из УдаляемыеКоманды Цикл
		
		КомандыКУдалению = КомандыПечати.НайтиСтроки(Новый Структура("Идентификатор", УдаляемаяКоманда));
		Для Каждого КомандаКУдалению Из КомандыКУдалению Цикл
			КомандыПечати.Удалить(КомандаКУдалению);
		КонецЦикла;
		
	КонецЦикла;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "бг_УправлениеПечатьюКлиент.ПакетнаяПечатьДокументов";
	КомандаПечати.МенеджерПечати = "";
	КомандаПечати.Идентификатор = "КомплектДокументов";
	КомандаПечати.СразуНаПринтер = Истина;
	КомандаПечати.Представление = НСтр("ru = 'Комплект документов на принтер (Белуга)';
										|en = 'Printable document set (Beluga)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 1;

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "бг_УправлениеПечатьюКлиент.ПакетнаяПечатьДокументовСНастройкой";
	КомандаПечати.МенеджерПечати = "";
	КомандаПечати.Идентификатор = "ПакетнаяПечатьДокументовСНастройкой";
	КомандаПечати.Представление = НСтр("ru = 'Комплект документов с настройкой... (Белуга)';
										|en = 'Printable document set with setting...'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 2;
	
КонецПроцедуры

Функция НастройкиПечатныхФорм(ТипОбъекта, Объекты) Экспорт
	
	УсловиеОтбораПоОбъектам = Справочники.бг_УсловияПечатиКомплектов.ПодходящиеУсловияОтбораПоОбъектам(
		ТипОбъекта, Объекты);
		
	Настройки = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УсловияПоОбъектам.Объект КАК Объект,
	|	УсловияПоОбъектам.УсловиеОтбора КАК УсловиеОтбора,
	|	УсловияПоОбъектам.Приоритет КАК Приоритет
	|ПОМЕСТИТЬ ВсеУсловияПоОбъектам
	|ИЗ
	|	&УсловияПоОбъектам КАК УсловияПоОбъектам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеУсловияПоОбъектам.Объект КАК Объект,
	|	ВсеУсловияПоОбъектам.УсловиеОтбора КАК УсловиеОтбора,
	|	ВсеУсловияПоОбъектам.Приоритет КАК Приоритет
	|ПОМЕСТИТЬ УсловияОтбораСНастройками
	|ИЗ
	|	ВсеУсловияПоОбъектам КАК ВсеУсловияПоОбъектам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.бг_НастройкиПечатиКомплектов КАК бг_НастройкиПечатиКомплектов
	|		ПО ВсеУсловияПоОбъектам.УсловиеОтбора = бг_НастройкиПечатиКомплектов.УсловияОтбора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Объект КАК Объект,
	|	МИНИМУМ(Т.Приоритет) КАК Приоритет
	|ПОМЕСТИТЬ МинимальныйПриоритетПоОбъектам
	|ИЗ
	|	УсловияОтбораСНастройками КАК Т
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УсловияОтбораСНастройками.Объект КАК Объект,
	|	МАКСИМУМ(УсловияОтбораСНастройками.УсловиеОтбора) КАК УсловиеОтбора
	|ПОМЕСТИТЬ ДействующееУсловиеОтбора
	|ИЗ
	|	МинимальныйПриоритетПоОбъектам КАК МинимальныйПриоритетПоОбъектам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ УсловияОтбораСНастройками КАК УсловияОтбораСНастройками
	|		ПО МинимальныйПриоритетПоОбъектам.Объект = УсловияОтбораСНастройками.Объект
	|			И МинимальныйПриоритетПоОбъектам.Приоритет = УсловияОтбораСНастройками.Приоритет
	|
	|СГРУППИРОВАТЬ ПО
	|	УсловияОтбораСНастройками.Объект
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	бг_НастройкиПечатиКомплектов.Настройки КАК Настройки,
	|	ДействующееУсловиеОтбора.Объект КАК Объект,
	|	бг_НастройкиПечатиКомплектов.УсловияОтбора КАК УсловияОтбора
	|ИЗ
	|	ДействующееУсловиеОтбора КАК ДействующееУсловиеОтбора
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.бг_НастройкиПечатиКомплектов КАК бг_НастройкиПечатиКомплектов
	|		ПО ДействующееУсловиеОтбора.УсловиеОтбора = бг_НастройкиПечатиКомплектов.УсловияОтбора
	|ИТОГИ ПО
	|	УсловияОтбора";
	
	Запрос.УстановитьПараметр("УсловияПоОбъектам", УсловиеОтбораПоОбъектам);
	
	Результат = Запрос.Выполнить();
	ВыборкаУсловийОтборов = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаУсловийОтборов.Следующий() Цикл
	
		АдресПечатныхФорм = Неопределено;
		МассивОбъектов = Новый Массив;
		
		ВыборкаДетальная = ВыборкаУсловийОтборов.Выбрать();
		Пока ВыборкаДетальная.Следующий() Цикл
			
			Если АдресПечатныхФорм = Неопределено Тогда
				АдресПечатныхФорм = ПоместитьВоВременноеХранилище(ВыборкаДетальная.Настройки.Получить());
			КонецЕсли;
			
			МассивОбъектов.Добавить(ВыборкаДетальная.Объект);
			
		КонецЦикла;
		
		Настройки.Вставить(АдресПечатныхФорм, МассивОбъектов);
	
	КонецЦикла;
	
	Возврат Настройки;
	
КонецФункции

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаЗаписи" Тогда
		
		СтандартнаяОбработка = Ложь;
		ВыбраннаяФорма = "РегистрСведений.бг_НастройкиПечатиКомплектов.Форма.ФормаЗаписиПроизвольнойНастройки";
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти