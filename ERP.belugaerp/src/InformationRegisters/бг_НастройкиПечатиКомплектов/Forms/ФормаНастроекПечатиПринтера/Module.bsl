
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПараметрыПрограммыПечати = бг_УправлениеНастройкамиДляПечатиФайлов.ПроверитьУстановитьПрограммуПечати();
	Если ПараметрыПрограммыПечати.ОшибкаПодключения Тогда
		
		Элементы.НастроитьОдностороннююПечать.Доступность = Ложь;
		Элементы.НастроитьДвустороннююПечать.Доступность = Ложь;
		Возврат;
		
	КонецЕсли;
	
	НастройкиПринтера = бг_УправлениеНастройкамиДляПечатиФайлов.НастройкиПринтера(ПараметрыПрограммыПечати);
	
	УстановитьНадписиКнопок();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастроитьОдностороннююПечать(Команда)
	
	WshShell = Новый COMОбъект("WScript.Shell");
	WshShell.Run("""" + ПараметрыПрограммыПечати.ПолныйПуть + """"
		+ " -savedevmode """ + НастройкиПринтера.ОдносторонняяПечать + """", 0, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьДвустороннююПечать(Команда)
	
	WshShell = Новый COMОбъект("WScript.Shell");
	WshShell.Run("""" + ПараметрыПрограммыПечати.ПолныйПуть + """"
		+ " -savedevmode """ + НастройкиПринтера.ДвусторонняяПечать + """", 0, Истина);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьНадписиКнопок()
	
	Если бг_УправлениеНастройкамиДляПечатиФайлов.НастройкаВыполнена(НастройкиПринтера.ОдносторонняяПечать) Тогда
		Элементы.НастроитьОдностороннююПечать.Заголовок = НСтр("ru = 'Настройка односторонней печати выполнена, изменить'",
			ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
	Иначе
		Элементы.НастроитьОдностороннююПечать.Заголовок = НСтр("ru = 'Выполнить настройку односторонней печати'",
			ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
	КонецЕсли;
		
	Если бг_УправлениеНастройкамиДляПечатиФайлов.НастройкаВыполнена(НастройкиПринтера.ДвусторонняяПечать) Тогда
		Элементы.НастроитьДвустороннююПечать.Заголовок = НСтр("ru = 'Настройка двусторонней печати выполнена, изменить'",
			ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
	Иначе
		Элементы.НастроитьДвустороннююПечать.Заголовок = НСтр("ru = 'Выполнить настройку двусторонней печати'",
			ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
