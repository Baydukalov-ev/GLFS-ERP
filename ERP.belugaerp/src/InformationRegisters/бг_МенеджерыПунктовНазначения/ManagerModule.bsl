
#Область ПрограммныйИнтерфейс

// Возвращает структуру с данными записи регистра Менеджеры пунктов назначения
//
// Параметры:
//  ПунктНазначения - Справочник.битПунктыНазначения
//  Менеджер - Справочник.ФизическиеЛица
//
// Возвращаемое значение:
//  Структура
//
Функция НайтиЗаписьМенеджераПунктаНазначения(ПунктНазначения, Менеджер = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеЗаписи = ДанныеЗаписиРегистра();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПунктНазначения", ПунктНазначения);
	Запрос.УстановитьПараметр("Менеджер", Менеджер);
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	бг_МенеджерыПунктовНазначенияСрезПоследних.Период КАК Период,
	|	бг_МенеджерыПунктовНазначенияСрезПоследних.ПунктНазначения КАК ПунктНазначения,
	|	бг_МенеджерыПунктовНазначенияСрезПоследних.Менеджер КАК Менеджер,
	|	бг_МенеджерыПунктовНазначенияСрезПоследних.МаршрутТорговогоПредставителя КАК МаршрутТорговогоПредставителя,
	|	бг_МенеджерыПунктовНазначенияСрезПоследних.ПризнакЭТК КАК ПризнакЭТК
	|ИЗ
	|	РегистрСведений.бг_МенеджерыПунктовНазначения.СрезПоследних(, ) КАК бг_МенеджерыПунктовНазначенияСрезПоследних
	|ГДЕ
	|	бг_МенеджерыПунктовНазначенияСрезПоследних.ПунктНазначения = &ПунктНазначения
	|	И &УсловиеПоМенеджеру";
	
	
	Если Менеджер = Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоМенеджеру", "ИСТИНА");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоМенеджеру", "бг_МенеджерыПунктовНазначенияСрезПоследних.Менеджер = &Менеджер");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(ДанныеЗаписи, Выборка);
	КонецЕсли;
	
	Возврат ДанныеЗаписи;
	
КонецФункции

// Создает запись в регистре Менеджеры пунктов назначения
//
// Параметры:
//  ПунктНазначения - Справочник.битПунктыНазначения
//  Менеджер - Справочник.ФизическиеЛица
//  МаршрутТорговогоПредставителя - Справочник.бг_МаршрутыТорговыхПредставителей
//  ПризнакЭТК - Булево
//  ДатаНачалаДействия - Дата
//
Процедура СоздатьЗаписьМенеджераПунктаНазначения(ПунктНазначения, Менеджер, 
			МаршрутТорговогоПредставителя = Неопределено, ПризнакЭТК = Ложь, ДатаНачалаДействия = Неопределено) Экспорт
			
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = РегистрыСведений.бг_МенеджерыПунктовНазначения.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.ПунктНазначения = ПунктНазначения;
	МенеджерЗаписи.Менеджер = Менеджер;
	МенеджерЗаписи.Период = ?(ДатаНачалаДействия = Неопределено, ТекущаяДатаСеанса(), ДатаНачалаДействия);
	МенеджерЗаписи.МаршрутТорговогоПредставителя = МаршрутТорговогоПредставителя;
	МенеджерЗаписи.ПризнакЭТК = ПризнакЭТК;
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

// Возвращает менеджера пункта назначения на указанную дату
//
// Параметры:
//  ПунктНазначения - СправочникСсылка.битПунктыНазначения
//  ДатаСреза - Дата, не обязательный, по умолчанию текущая дата сеанса
// 
// Возвращаемое значение:
//  Менеджер - СправочникСсылка.ФизическиеЛица
//
Функция МенеджерПунктаНазначения(ПунктНазначения, ДатаСреза = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ДатаСреза) Тогда
		ДатаСреза = КонецДня(ТекущаяДатаСеанса());
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	бг_МенеджерыПунктовНазначенияСрезПоследних.Менеджер КАК Менеджер
	|ИЗ
	|	РегистрСведений.бг_МенеджерыПунктовНазначения.СрезПоследних(&ДатаСреза, ПунктНазначения = &ПунктНазначения) КАК бг_МенеджерыПунктовНазначенияСрезПоследних";
	
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	Запрос.УстановитьПараметр("ПунктНазначения", ПунктНазначения);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Менеджер;
	КонецЕсли;
	
	Возврат ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка");
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеЗаписиРегистра()
	
	Возврат Новый Структура("ПунктНазначения, Менеджер, МаршрутТорговогоПредставителя, ПризнакЭТК");
	
КонецФункции

#КонецОбласти