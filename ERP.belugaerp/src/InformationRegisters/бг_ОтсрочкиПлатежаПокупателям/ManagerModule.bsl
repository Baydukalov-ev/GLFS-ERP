#Область ПрограммныйИнтерфейс

// Возвращает установленная отсрочка платежа контрагента
// 
// Параметры:
//  Контрагент - Контрагент
//  Договор - Договор
//  Соглашение - Соглашение
//  ПунктНазначения - ПунктНазначения
//
// Возвращаемое значение:
//  Число - число дней отсрочки 
//
Функция ОтсрочкаПлатежаПоКонтрагенту(Контрагент, Договор, Соглашение, ПунктНазначения) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = ТестЗапросаОтсрочкиПлатежа();
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Договор", Договор);
	Запрос.УстановитьПараметр("ПунктНазначения", ПунктНазначения);
	Запрос.УстановитьПараметр("Соглашение", Соглашение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.КоличествоДнейОтсрочки;
	Иначе
		Возврат 0;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции
	
// Возвращает текст запроса для получения отсрочки платежа
// Используется, в т.ч. при автосогласовании
//
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТестЗапросаОтсрочкиПлатежа() Экспорт
	
	Возврат 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтсрочкиПлатежаПокупателям.Количество КАК КоличествоДнейОтсрочки
	|ПОМЕСТИТЬ ОтсрочкиПлатежаЛогистическая
	|ИЗ
	|	РегистрСведений.бг_ОтсрочкиПлатежаПокупателям КАК ОтсрочкиПлатежаПокупателям
	|ГДЕ
	|	ОтсрочкиПлатежаПокупателям.ТипОтсрочки = ЗНАЧЕНИЕ(Перечисление.бг_ВидыОтсрочкиПлатежаПокупателям.Логистическая)
	|	И ОтсрочкиПлатежаПокупателям.Контрагент = &Контрагент
	|	И ОтсрочкиПлатежаПокупателям.ПунктНазначения = &ПунктНазначения
	|	И ОтсрочкиПлатежаПокупателям.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтсрочкиПлатежаПокупателям.Количество КАК КоличествоДнейОтсрочки
	|ПОМЕСТИТЬ ОтсрочкиПлатежаУправленческая
	|ИЗ
	|	РегистрСведений.бг_ОтсрочкиПлатежаПокупателям КАК ОтсрочкиПлатежаПокупателям
	|ГДЕ
	|	ОтсрочкиПлатежаПокупателям.Контрагент = &Контрагент
	|	И ОтсрочкиПлатежаПокупателям.ТипОтсрочки = ЗНАЧЕНИЕ(Перечисление.бг_ВидыОтсрочкиПлатежаПокупателям.Управленческая)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтсрочкиПлатежаПокупателям.Количество КАК КоличествоДнейОтсрочки,
	|	0 КАК Приоритет
	|ПОМЕСТИТЬ ОтсрочкиПлатежаПоДоговоруПредварительно
	|ИЗ
	|	РегистрСведений.бг_ОтсрочкиПлатежаПокупателям КАК ОтсрочкиПлатежаПокупателям
	|ГДЕ
	|	ОтсрочкиПлатежаПокупателям.ТипОтсрочки = ЗНАЧЕНИЕ(Перечисление.бг_ВидыОтсрочкиПлатежаПокупателям.ПоДоговору)
	|	И ОтсрочкиПлатежаПокупателям.Контрагент = &Контрагент
	|	И ОтсрочкиПлатежаПокупателям.Договор = &Договор
	|	И ОтсрочкиПлатежаПокупателям.ПунктНазначения = &ПунктНазначения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтсрочкиПлатежаПокупателям.Количество КАК КоличествоДнейОтсрочки,
	|	1 КАК Приоритет
	|ИЗ
	|	РегистрСведений.бг_ОтсрочкиПлатежаПокупателям КАК ОтсрочкиПлатежаПокупателям
	|ГДЕ
	|	ОтсрочкиПлатежаПокупателям.ТипОтсрочки = ЗНАЧЕНИЕ(Перечисление.бг_ВидыОтсрочкиПлатежаПокупателям.ПоДоговору)
	|	И ОтсрочкиПлатежаПокупателям.Контрагент = &Контрагент
	|	И ОтсрочкиПлатежаПокупателям.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|	И ОтсрочкиПлатежаПокупателям.ПунктНазначения = &ПунктНазначения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтсрочкиПлатежаПокупателям.Количество КАК КоличествоДнейОтсрочки,
	|	10 КАК Приоритет
	|ИЗ
	|	РегистрСведений.бг_ОтсрочкиПлатежаПокупателям КАК ОтсрочкиПлатежаПокупателям
	|ГДЕ
	|	ОтсрочкиПлатежаПокупателям.ТипОтсрочки = ЗНАЧЕНИЕ(Перечисление.бг_ВидыОтсрочкиПлатежаПокупателям.ПоДоговору)
	|	И ОтсрочкиПлатежаПокупателям.Контрагент = &Контрагент
	|	И ОтсрочкиПлатежаПокупателям.Договор = &Договор
	|	И ОтсрочкиПлатежаПокупателям.ПунктНазначения = ЗНАЧЕНИЕ(Справочник.битПунктыНазначения.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтсрочкиПлатежаПокупателям.Количество КАК КоличествоДнейОтсрочки,
	|	11 КАК Приоритет
	|ИЗ
	|	РегистрСведений.бг_ОтсрочкиПлатежаПокупателям КАК ОтсрочкиПлатежаПокупателям
	|ГДЕ
	|	ОтсрочкиПлатежаПокупателям.ТипОтсрочки = ЗНАЧЕНИЕ(Перечисление.бг_ВидыОтсрочкиПлатежаПокупателям.ПоДоговору)
	|	И ОтсрочкиПлатежаПокупателям.Контрагент = &Контрагент
	|	И ОтсрочкиПлатежаПокупателям.Договор = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|	И ОтсрочкиПлатежаПокупателям.ПунктНазначения = ЗНАЧЕНИЕ(Справочник.битПунктыНазначения.ПустаяСсылка)
	|;
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтсрочкиПлатежаПоДоговоруПредварительно.КоличествоДнейОтсрочки КАК КоличествоДнейОтсрочки
	|ПОМЕСТИТЬ ОтсрочкиПлатежаПоДоговору
	|ИЗ
	|	ОтсрочкиПлатежаПоДоговоруПредварительно КАК ОтсрочкиПлатежаПоДоговоруПредварительно
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Логистическая.КоличествоДнейОтсрочки КАК КоличествоДнейОтсрочки
	|ПОМЕСТИТЬ ОтсрочкиПлатежаОбщая
	|ИЗ
	|	ОтсрочкиПлатежаЛогистическая КАК Логистическая
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Управленческая.КоличествоДнейОтсрочки
	|ИЗ
	|	ОтсрочкиПлатежаУправленческая КАК Управленческая
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПоДоговору.КоличествоДнейОтсрочки
	|ИЗ
	|	ОтсрочкиПлатежаПоДоговору КАК ПоДоговору
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	0 КАК Приоритет,
	|	СУММА(ОтсрочкиПлатежаОбщая.КоличествоДнейОтсрочки) КАК КоличествоДнейОтсрочки
	|ПОМЕСТИТЬ ОтсрочкаПлатежа
	|ИЗ
	|	ОтсрочкиПлатежаОбщая КАК ОтсрочкиПлатежаОбщая
	|
	|ИМЕЮЩИЕ
	|	СУММА(ОтсрочкиПлатежаОбщая.КоличествоДнейОтсрочки) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СоглашенияСКлиентамиЭтапыГрафикаОплаты.НомерСтроки КАК Приоритет,
	|	СоглашенияСКлиентамиЭтапыГрафикаОплаты.Сдвиг КАК КоличествоДнейОтсрочки
	|ИЗ
	|	Справочник.СоглашенияСКлиентами.ЭтапыГрафикаОплаты КАК СоглашенияСКлиентамиЭтапыГрафикаОплаты
	|ГДЕ
	|	СоглашенияСКлиентамиЭтапыГрафикаОплаты.Ссылка = &Соглашение;
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОтсрочкаПлатежа.КоличествоДнейОтсрочки КАК КоличествоДнейОтсрочки
	|ИЗ
	|	ОтсрочкаПлатежа КАК ОтсрочкаПлатежа
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет";
	
КонецФункции

#КонецОбласти

