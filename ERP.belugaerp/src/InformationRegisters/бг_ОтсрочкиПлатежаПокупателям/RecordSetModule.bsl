#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	ПроверитьВозможностьЗаписи(Отказ, Отбор);	
КонецПроцедуры

Процедура ПроверитьВозможностьЗаписи(Отказ, Отбор)
	
	Если Отбор.ТипОтсрочки.Значение = Перечисления.бг_ВидыОтсрочкиПлатежаПокупателям.Логистическая Тогда
		Если Не ЗначениеЗаполнено(Отбор.ПунктНазначения.Значение) Или ЗначениеЗаполнено(Отбор.Договор.Значение) Тогда
			Отказ = Истина;
			ТекстОшибки = НСтр("ru = 'Не удалось выполнить запись. 
								|Для логистической отсрочки должен быть заполнен пункт назначения'");
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			Возврат;				 
		КонецЕсли;				
	ИначеЕсли Отбор.ТипОтсрочки.Значение = Перечисления.бг_ВидыОтсрочкиПлатежаПокупателям.Управленческая 
		Или Отбор.ТипОтсрочки.Значение = Перечисления.бг_ВидыОтсрочкиПлатежаПокупателям.ПоДоговору Тогда
		ПроверитьНаличиеЗаписей(Отказ, Отбор);
	КонецЕсли;		
		
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции  

Процедура ПроверитьНаличиеЗаписей(Отказ, Отбор)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОтсрочкиПлатежаПокупателям.ТипОтсрочки КАК ТипОтсрочки,
	|	ОтсрочкиПлатежаПокупателям.Контрагент КАК Контрагент,
	|	ОтсрочкиПлатежаПокупателям.Договор КАК Договор,
	|	ОтсрочкиПлатежаПокупателям.ПунктНазначения КАК ПунктНазначения
	|ИЗ
	|	РегистрСведений.бг_ОтсрочкиПлатежаПокупателям КАК ОтсрочкиПлатежаПокупателям
	|ГДЕ
	|	ОтсрочкиПлатежаПокупателям.Контрагент = &Контрагент
	|	И ОтсрочкиПлатежаПокупателям.ПунктНазначения <> ЗНАЧЕНИЕ(Справочник.битПунктыНазначения.ПустаяСсылка)
	|	И ОтсрочкиПлатежаПокупателям.ТипОтсрочки = &ТипОтсрочки";
	
	Запрос.УстановитьПараметр("ТипОтсрочки", Отбор.ТипОтсрочки.Значение);
	Запрос.УстановитьПараметр("Контрагент", Отбор.Контрагент.Значение);
	Если ЗначениеЗаполнено(Отбор.ПунктНазначения.Значение) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"ПунктНазначения <> ЗНАЧЕНИЕ(Справочник.битПунктыНазначения.ПустаяСсылка)",
			"ПунктНазначения = ЗНАЧЕНИЕ(Справочник.битПунктыНазначения.ПустаяСсылка)");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
		
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();		
		Если Выборка.Следующий() Тогда
		
			ТекстОшибки = СтрШаблон(НСтр("ru = 'В регистре уже имеются записи 
				| по виду отсрочки %1 для контрагента %2
				| не может быть установлена отсрочка в разрезе пункта назначения,
				| если в регистре существуют записи с незаполненным пунктом назначения
				| и наоборот если в регистре существуют записи с установкой отсрочки 
				| в разрезе пункта назначения, тогда нельзя добавить отсрочку не в разрезе пункта назначения.
				| Вы пытаетесь записать следующие значения:
				| Пункт назначения: %3, значение в регистре: %4'"),
				Выборка.ТипОтсрочки,
				Выборка.Контрагент,
				?(ЗначениеЗаполнено(Отбор.ПунктНазначения.Значение), 
					Отбор.ПунктНазначения.Значение, "Значение не заполнено"),
				?(ЗначениеЗаполнено(Выборка.ПунктНазначения), 
					Выборка.ПунктНазначения, "Значение не заполнено"));					
			
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,,, Отказ);	
		
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти 