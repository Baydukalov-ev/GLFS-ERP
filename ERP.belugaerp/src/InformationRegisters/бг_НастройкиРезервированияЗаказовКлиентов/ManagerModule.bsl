#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает параметры резервирования по настройкам на текущую дату
// для переданной даты отгрузки.
//
//  Параметры:
//   Организация - СправочникСсылка.Организации - ссылка на организацию
//   КаналПродаж - СправочникСсылка.битКаналыПродаж - ссылка на канал продаж
//   Партнер - СправочникСсылка.Партнеры - ссылка на партнера
//   ДатаОтгрузки - Дата - дата отгрузки
//
//  Возвращаемое значение:
//   ПараметрыРезервирования - Структура - параметры резервирования по настройкам.
//
Функция ПараметрыРезервирования(Организация, КаналПродаж, Партнер, ДатаОтгрузки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	ПараметрыРезервирования = Новый Структура;
	ПараметрыРезервирования.Вставить("РазрешеноРезервированиеПоДатеОтгрузки", Истина);
	ПараметрыРезервирования.Вставить("ДнейДоОтгрузкиДляРезервирования", 0);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("КаналПродаж", КаналПродаж);
	Запрос.УстановитьПараметр("Партнер", Партнер);
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("ДатаОтгрузки", ДатаОтгрузки);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиРезервирования.ДнейДоОтгрузкиДляРезервирования КАК ДнейДоОтгрузкиДляРезервирования,
	|	1 КАК Приоритет
	|ПОМЕСТИТЬ НастройкиРезервирования
	|ИЗ
	|	РегистрСведений.бг_НастройкиРезервированияЗаказовКлиентов КАК НастройкиРезервирования
	|ГДЕ
	|	НастройкиРезервирования.Организация = &Организация
	|	И НастройкиРезервирования.КаналПродаж = &КаналПродаж
	|	И НастройкиРезервирования.Партнер = &Партнер
	|	И НастройкиРезервирования.ДнейДоОтгрузкиДляРезервирования > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НастройкиРезервирования.ДнейДоОтгрузкиДляРезервирования,
	|	2
	|ИЗ
	|	РегистрСведений.бг_НастройкиРезервированияЗаказовКлиентов КАК НастройкиРезервирования
	|ГДЕ
	|	НастройкиРезервирования.Организация = &Организация
	|	И НастройкиРезервирования.КаналПродаж = ЗНАЧЕНИЕ(Справочник.битКаналыПродаж.ПустаяСсылка)
	|	И НастройкиРезервирования.Партнер = &Партнер
	|	И НастройкиРезервирования.ДнейДоОтгрузкиДляРезервирования > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НастройкиРезервирования.ДнейДоОтгрузкиДляРезервирования,
	|	3
	|ИЗ
	|	РегистрСведений.бг_НастройкиРезервированияЗаказовКлиентов КАК НастройкиРезервирования
	|ГДЕ
	|	НастройкиРезервирования.Организация = &Организация
	|	И НастройкиРезервирования.КаналПродаж = &КаналПродаж
	|	И НастройкиРезервирования.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|	И НастройкиРезервирования.ДнейДоОтгрузкиДляРезервирования > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НастройкиРезервирования.ДнейДоОтгрузкиДляРезервирования,
	|	4
	|ИЗ
	|	РегистрСведений.бг_НастройкиРезервированияЗаказовКлиентов КАК НастройкиРезервирования
	|ГДЕ
	|	НастройкиРезервирования.Организация = &Организация
	|	И НастройкиРезервирования.КаналПродаж = ЗНАЧЕНИЕ(Справочник.битКаналыПродаж.ПустаяСсылка)
	|	И НастройкиРезервирования.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|	И НастройкиРезервирования.ДнейДоОтгрузкиДляРезервирования > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиРезервирования.ДнейДоОтгрузкиДляРезервирования КАК ДнейДоОтгрузкиДляРезервирования,
	|	ВЫБОР
	|		КОГДА НастройкиРезервирования.ДнейДоОтгрузкиДляРезервирования > 0
	|				И РАЗНОСТЬДАТ(&ТекущаяДата, &ДатаОтгрузки, ДЕНЬ) > НастройкиРезервирования.ДнейДоОтгрузкиДляРезервирования
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК РазрешеноРезервированиеПоДатеОтгрузки,
	|	НастройкиРезервирования.Приоритет КАК Приоритет
	|ИЗ
	|	НастройкиРезервирования КАК НастройкиРезервирования
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(ПараметрыРезервирования, Выборка);	
	КонецЕсли;	
	
	Возврат ПараметрыРезервирования;
	
КонецФункции	

// Возвращает текст запроса поля количества дней до отгрузки для резервирования.
//
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаДнейДоОтгрузкиДляРезервирования() Экспорт
	
	Возврат 
		"ВЫБОР
		|		КОГДА НЕ НастройкиРезервированияПоКаналуИПартнеру.ДнейДоОтгрузкиДляРезервирования ЕСТЬ NULL
		|			ТОГДА НастройкиРезервированияПоКаналуИПартнеру.ДнейДоОтгрузкиДляРезервирования
		|		КОГДА НЕ НастройкиРезервированияПоПартнеру.ДнейДоОтгрузкиДляРезервирования ЕСТЬ NULL
		|			ТОГДА НастройкиРезервированияПоПартнеру.ДнейДоОтгрузкиДляРезервирования
		|		КОГДА НЕ НастройкиРезервированияПоКаналу.ДнейДоОтгрузкиДляРезервирования ЕСТЬ NULL
		|			ТОГДА НастройкиРезервированияПоКаналу.ДнейДоОтгрузкиДляРезервирования
		|		ИНАЧЕ НастройкиРезервированияОбщиеПоОрганизации.ДнейДоОтгрузкиДляРезервирования
		|	КОНЕЦ";
	
КонецФункции

// Возвращает текст запроса соединения таблицы заказов с регистром "Настройки резервирования заказов клиентов".
//
// Параметры:
//  ИсточникЗаказа - Строка - Таблица с заказами. Например, 
//                            "ВЫРАЗИТЬ(ЗаказыКлиентовНеОтгруженные.ЗаказКлиента КАК Документ.ЗаказКлиента)" 
//                            или "СписокЗаказов.ЗаказКлиента"
// 
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаСоединенияСНастройкамиРезервирования(ИсточникЗаказа) Экспорт
	
	ТекстЗапроса = 
	"		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бг_НастройкиРезервированияЗаказовКлиентов КАК НастройкиРезервированияПоКаналуИПартнеру
	|		ПО (СписокЗаказов.ЗаказКлиента.Организация = НастройкиРезервированияПоКаналуИПартнеру.Организация)
	|			И (СписокЗаказов.ЗаказКлиента.бг_КаналПродаж = НастройкиРезервированияПоКаналуИПартнеру.КаналПродаж)
	|			И (СписокЗаказов.ЗаказКлиента.Партнер = НастройкиРезервированияПоКаналуИПартнеру.Партнер)
	|			И (НастройкиРезервированияПоКаналуИПартнеру.ДнейДоОтгрузкиДляРезервирования > 0)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бг_НастройкиРезервированияЗаказовКлиентов КАК НастройкиРезервированияПоПартнеру
	|		ПО (СписокЗаказов.ЗаказКлиента.Организация = НастройкиРезервированияПоПартнеру.Организация)
	|			И (НастройкиРезервированияПоПартнеру.КаналПродаж = ЗНАЧЕНИЕ(Справочник.битКаналыПродаж.ПустаяСсылка))
	|			И (СписокЗаказов.ЗаказКлиента.Партнер = НастройкиРезервированияПоПартнеру.Партнер)
	|			И (НастройкиРезервированияПоПартнеру.ДнейДоОтгрузкиДляРезервирования > 0)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бг_НастройкиРезервированияЗаказовКлиентов КАК НастройкиРезервированияПоКаналу
	|		ПО (СписокЗаказов.ЗаказКлиента.Организация = НастройкиРезервированияПоКаналу.Организация)
	|			И (СписокЗаказов.ЗаказКлиента.бг_КаналПродаж = НастройкиРезервированияПоКаналу.КаналПродаж)
	|			И (НастройкиРезервированияПоКаналу.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка))
	|			И (НастройкиРезервированияПоКаналу.ДнейДоОтгрузкиДляРезервирования > 0)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бг_НастройкиРезервированияЗаказовКлиентов КАК НастройкиРезервированияОбщиеПоОрганизации
	|		ПО (СписокЗаказов.ЗаказКлиента.Организация = НастройкиРезервированияОбщиеПоОрганизации.Организация)
	|			И (НастройкиРезервированияОбщиеПоОрганизации.КаналПродаж = ЗНАЧЕНИЕ(Справочник.битКаналыПродаж.ПустаяСсылка))
	|			И (НастройкиРезервированияОбщиеПоОрганизации.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка))
	|			И (НастройкиРезервированияОбщиеПоОрганизации.ДнейДоОтгрузкиДляРезервирования > 0)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СписокЗаказов.ЗаказКлиента", ИсточникЗаказа);
	
	Возврат ТекстЗапроса;	 
	
КонецФункции	

#КонецОбласти 

#КонецЕсли
