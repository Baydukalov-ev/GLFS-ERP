#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ОбновитьСтатус(Документ, НовыйСтатус) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.бг_РасширенныеСтатусыДокументовЕГАИС;	
	
	Если Не ЗначениеЗаполнено(МетаданныеРегистра.Измерения.Документ.Тип.ПривестиЗначение(Документ)) Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(Документ);
	
	Запись = НаборЗаписей.Добавить();
	Запись.Документ = Документ;
	Запись.Статус = НовыйСтатус;
	
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

Функция ТекущийСтатус(Документ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	бг_РасширенныеСтатусыДокументовЕГАИС.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.бг_РасширенныеСтатусыДокументовЕГАИС КАК бг_РасширенныеСтатусыДокументовЕГАИС
	|ГДЕ
	|	бг_РасширенныеСтатусыДокументовЕГАИС.Документ = &Документ";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Статус;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли
