#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Выполняет запись в регистр.
//
// Параметры:
//  ЗаявкаКлиента      - ДокументСсылка.битЗаявкаКлиента  - Ссылка на заявку клиента, которую надо добавить.
//  ТаблицаРасхождений - ТаблицаЗначений - Таблица, содержащая выявленные расхождения данных
//                                         по товарам зарезервированным по предзаказам из Hybris 
//                                         и товарам в консолидированного заказе из SAP.                              
//
Процедура ДобавитьЗаявкуКлиентаВПротоколРасхожденияДанных(ЗаявкаКлиента, ТаблицаРасхождений) Экспорт
		
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ЗаявкаКлиента) Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		НаборЗаписей = РегистрыСведений.бг_РасхожденияДанныхКонсолидированныхЗаказовSAPиHybris.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ЗаявкаКлиента.Установить(ЗаявкаКлиента);  
		
		НаборЗаписей.Загрузить(ТаблицаРасхождений);
		НаборЗаписей.Записать(Истина);
	Исключение
		ШаблонСообщения = НСтр("ru = 'Не удалось добавить в регистр ""Расхождения данных консолидированных заказов SAP и Hybris"" заявку клиента %1.
									 |По причине: %2'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСообщения, 
				ЗаявкаКлиента,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;
	
КонецПроцедуры

// Удаляет заявку клиента из протокола расхождений.
//
// Параметры:
//  ЗаявкаКлиента  - ДокументСсылка.битЗаявкаКлиента  - Ссылка на заявку клиента, записи которой надо удалить.
//
Процедура УдалитьЗаявкуКлиентаИзПротоколаРасхожденияДанных(ЗаявкаКлиента) Экспорт
		
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ЗаявкаКлиента) Тогда
		Возврат;
	КонецЕсли;

	Попытка	
		НаборЗаписей = РегистрыСведений.бг_РасхожденияДанныхКонсолидированныхЗаказовSAPиHybris.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ЗаявкаКлиента.Установить(ЗаявкаКлиента);  
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() > 0 Тогда
		    НаборЗаписей.Очистить();
			НаборЗаписей.Записать(Истина);
		КонецЕсли;
	Исключение
		ШаблонСообщения = НСтр("ru = 'Не удалось удалить из регистра ""Расхождения данных консолидированных заказов SAP и Hybris"" заявку клиента %1.
									 |По причине: %2'");
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСообщения, 
				ЗаявкаКлиента,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли
