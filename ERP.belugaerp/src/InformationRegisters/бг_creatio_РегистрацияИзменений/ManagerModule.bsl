
#Область ПрограммныйИнтерфейс

// Возвращает признак регистрации объекта
// Параметры:
//  СсылкаНаОбъект - СправочникСсылка, ДокументСсылка
//
// Тип возвращаемого значения - Булево
//
Функция ОбъектЗарегистрирован(СсылкаНаОбъект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РегистрацияИзменений.Данные КАК Данные
	|ИЗ
	|	РегистрСведений.бг_creatio_РегистрацияИзменений.СрезПоследних(, Данные = &Ссылка) КАК РегистрацияИзменений
	|ГДЕ
	|	РегистрацияИзменений.ИдентификаторПакета = """"";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

// Регистрирует изменения объекта для обмена с Creatio
// Параметры:
//  СсылкаНаОбъект - СправочникСсылка, ДокументСсылка
//
Процедура ЗарегистрироватьИзменениеОбъекта(СсылкаНаОбъект) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.бг_creatio_РегистрацияИзменений.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период = ТекущаяДатаСеанса();
	МенеджерЗаписи.Данные = СсылкаНаОбъект;
	
	ТипДанных = ТипЗнч(СсылкаНаОбъект);
	
	Если ТипДанных = Тип("СправочникСсылка.Контрагенты") Тогда
		МенеджерЗаписи.Клиент = СсылкаНаОбъект;
		ЗарегистрироватьИзменениеКонтактнойИнформации(СсылкаНаОбъект);
	ИначеЕсли ТипДанных = Тип("СправочникСсылка.БанковскиеСчетаКонтрагентов")
		Или ТипДанных = Тип("СправочникСсылка.ЛицензииПоставщиковАлкогольнойПродукции") Тогда
		МенеджерЗаписи.Клиент = СсылкаНаОбъект.Владелец;
	ИначеЕсли ТипДанных = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		МенеджерЗаписи.Клиент = СсылкаНаОбъект.Контрагент;
	ИначеЕсли ТипДанных = Тип("СправочникСсылка.битПунктыНазначения") Тогда
		МенеджерЗаписи.Клиент = СсылкаНаОбъект.Клиент;
	ИначеЕсли ТипДанных = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		МенеджерЗаписи.Клиент = СсылкаНаОбъект.Контрагент;
	КонецЕсли;
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

// Регистрирует выгрузку объекта в Creatio
// Параметры:
//  СсылкаНаОбъект - СправочникСсылка, ДокументСсылка
//  Клиент - СправочникСсылка.Контрагенты
//  ВидКонтаткнойИнформации - СправочникСсылка.ВидыКонтактнойИнформации
//  ИдентификаторПакета - Строка
//  Счетчик - Число
//
Функция ЗарегистрироватьВыгрузкуОбъекта(СсылкаНаОбъект, Клиент, ВидКонтактнойИнформации, 
			ИдентификаторПакета, Счетчик) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.бг_creatio_РегистрацияИзменений.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период = ТекущаяДатаСеанса();
	МенеджерЗаписи.Данные = СсылкаНаОбъект;
	МенеджерЗаписи.Клиент = Клиент;
	МенеджерЗаписи.ВидАдреса = ВидКонтактнойИнформации;
	МенеджерЗаписи.ИдентификаторПакета = ИдентификаторПакета;
	МенеджерЗаписи.НомерЗаписиВПакете = Счетчик;
	
	МенеджерЗаписи.Записать();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗарегистрироватьИзменениеКонтактнойИнформации(Клиент)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Клиент);
	Запрос.УстановитьПараметр("ВидыКИ", ВидыКонтактнойИнформацииДляОбмена());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КонтрагентыКонтактнаяИнформация.Вид КАК ВидКонтактнойИнформации
	|ИЗ
	|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
	|ГДЕ
	|	КонтрагентыКонтактнаяИнформация.Вид В(&ВидыКИ)
	|	И КонтрагентыКонтактнаяИнформация.Ссылка = &Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		МенеджерЗаписи = РегистрыСведений.бг_creatio_РегистрацияИзменений.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Период = ТекущаяДатаСеанса();
		МенеджерЗаписи.Данные = Справочники.Контрагенты.ПустаяСсылка();
		МенеджерЗаписи.Клиент = Клиент;
		МенеджерЗаписи.ВидАдреса = Выборка.ВидКонтактнойИнформации;
		МенеджерЗаписи.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВидыКонтактнойИнформацииДляОбмена()
	
	ВидыКонтактнойИнформации = Новый Массив;
	
	ВидыКонтактнойИнформации.Добавить(Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента);
	ВидыКонтактнойИнформации.Добавить(Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента);
	
	Возврат ВидыКонтактнойИнформации;
	
КонецФункции

#КонецОбласти