#Область ПрограмммныйИнтерфейс

// Записывает значения кратности для номенклатуры
//
// Параметры:
//  Период - Дата - Дата записи
//  Данные - ТаблицаЗначений - данные для записи в регистр:
//			* Номенклатура - Справочник.Номенклатура
//			* Кратность - Число
//
Процедура ЗапомнитьКратность(Период, Данные) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.Кратность КАК Кратность
	|ПОМЕСТИТЬ втДанные
	|ИЗ
	|	&Данные КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанные.Номенклатура КАК Номенклатура,
	|	втДанные.Кратность КАК Кратность
	|ИЗ
	|	втДанные КАК втДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бг_КратностьНоменклатуры.СрезПоследних(&Период, ) КАК бг_КратностьНоменклатурыСрезПоследних
	|		ПО втДанные.Номенклатура = бг_КратностьНоменклатурыСрезПоследних.Номенклатура
	|			И втДанные.Кратность = бг_КратностьНоменклатурыСрезПоследних.Кратность
	|ГДЕ
	|	бг_КратностьНоменклатурыСрезПоследних.Кратность ЕСТЬ NULL";
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Данные", Данные);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаписатьНовуюКратность(Период, Выборка.Номенклатура, Выборка.Кратность);
	КонецЦикла;
КонецПроцедуры

// Возвращает данные о кратности номенклатуры на дату
//
// Параметры:
//  Период - Дата - Дата данных
//  Номенклатура - Массив из СправочникСсылка.Номенклатура - Номенклатура, для которой получаем кратность
//
// Возвращаемое значение:
//  ТаблицаЗначений: 
//		* Номенклатура - Справочник.Номенклатура
//		* Кратность - Число
//
Функция КратностьНоменклатуры(Период, Номенклатура) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СправочникНоменклатура.Ссылка КАК Номенклатура,
	|	ЕСТЬNULL(бг_КратностьНоменклатурыСрезПоследних.Кратность, 0) КАК Кратность
	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бг_КратностьНоменклатуры.СрезПоследних(&Период, ) КАК бг_КратностьНоменклатурыСрезПоследних
	|		ПО СправочникНоменклатура.Ссылка = бг_КратностьНоменклатурыСрезПоследних.Номенклатура
	|ГДЕ
	|	СправочникНоменклатура.Ссылка В(&Номенклатура)";
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Результат = Запрос.Выполнить().Выгрузить();
	Возврат Результат;
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура ЗаписатьНовуюКратность(Период, Номенклатура, Кратность)
	МенеджерЗаписи = РегистрыСведений.бг_КратностьНоменклатуры.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Номенклатура = Номенклатура;
	МенеджерЗаписи.Кратность = Кратность;
	МенеджерЗаписи.Период = Период;
	МенеджерЗаписи.Записать(Истина);
КонецПроцедуры

#КонецОбласти 