#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает текст запроса поля количества часов до отгрузки.
//
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаКоличествоЧасовДоОтгрузки() Экспорт
	
	Возврат 
	"ВЫБОР
	|		КОГДА НЕ НастройкиПередачиКаналИсточникПартнер.КоличествоЧасовДоОтгрузки ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиКаналИсточникПартнер.КоличествоЧасовДоОтгрузки
	|		КОГДА НЕ НастройкиПередачиКаналИсточник.КоличествоЧасовДоОтгрузки ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиКаналИсточник.КоличествоЧасовДоОтгрузки
	|		КОГДА НЕ НастройкиПередачиКаналПартнер.КоличествоЧасовДоОтгрузки ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиКаналПартнер.КоличествоЧасовДоОтгрузки
	|		КОГДА НЕ НастройкиПередачиКанал.КоличествоЧасовДоОтгрузки ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиКанал.КоличествоЧасовДоОтгрузки
	|		КОГДА НЕ НастройкиПередачиИсточникПартнер.КоличествоЧасовДоОтгрузки ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиИсточникПартнер.КоличествоЧасовДоОтгрузки
	|		КОГДА НЕ НастройкиПередачиИсточник.КоличествоЧасовДоОтгрузки ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиИсточник.КоличествоЧасовДоОтгрузки
	|		КОГДА НЕ НастройкиПередачиПартнер.КоличествоЧасовДоОтгрузки ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиПартнер.КоличествоЧасовДоОтгрузки
	|		ИНАЧЕ НастройкиПередачиОбщие.КоличествоЧасовДоОтгрузки
	|	КОНЕЦ";
	
КонецФункции

// Возвращает текст запроса поля актуализировать обеспечение.
//
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаАктуализироватьОбеспечение() Экспорт
	
	Возврат 
	"ВЫБОР
	|		КОГДА НЕ НастройкиПередачиКаналИсточникПартнер.АктуализироватьОбеспечение ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиКаналИсточникПартнер.АктуализироватьОбеспечение
	|		КОГДА НЕ НастройкиПередачиКаналИсточник.АктуализироватьОбеспечение ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиКаналИсточник.АктуализироватьОбеспечение
	|		КОГДА НЕ НастройкиПередачиКаналПартнер.АктуализироватьОбеспечение ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиКаналПартнер.АктуализироватьОбеспечение
	|		КОГДА НЕ НастройкиПередачиКанал.АктуализироватьОбеспечение ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиКанал.АктуализироватьОбеспечение
	|		КОГДА НЕ НастройкиПередачиИсточникПартнер.АктуализироватьОбеспечение ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиИсточникПартнер.АктуализироватьОбеспечение
	|		КОГДА НЕ НастройкиПередачиИсточник.АктуализироватьОбеспечение ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиИсточник.АктуализироватьОбеспечение
	|		КОГДА НЕ НастройкиПередачиПартнер.АктуализироватьОбеспечение ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиПартнер.АктуализироватьОбеспечение
	|		ИНАЧЕ НастройкиПередачиОбщие.АктуализироватьОбеспечение
	|	КОНЕЦ";
	
КонецФункции	

// Возвращает текст запроса поля отменять необеспеченные позиции.
//
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаОтменятьНеОбеспеченныеПозиции() Экспорт
	
	Возврат 
	"ВЫБОР
	|		КОГДА НЕ НастройкиПередачиКаналИсточникПартнер.ОтменятьНеОбеспеченныеПозиции ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиКаналИсточникПартнер.ОтменятьНеОбеспеченныеПозиции
	|		КОГДА НЕ НастройкиПередачиКаналИсточник.ОтменятьНеОбеспеченныеПозиции ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиКаналИсточник.ОтменятьНеОбеспеченныеПозиции
	|		КОГДА НЕ НастройкиПередачиКаналПартнер.ОтменятьНеОбеспеченныеПозиции ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиКаналПартнер.ОтменятьНеОбеспеченныеПозиции
	|		КОГДА НЕ НастройкиПередачиКанал.ОтменятьНеОбеспеченныеПозиции ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиКанал.ОтменятьНеОбеспеченныеПозиции
	|		КОГДА НЕ НастройкиПередачиИсточникПартнер.ОтменятьНеОбеспеченныеПозиции ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиИсточникПартнер.ОтменятьНеОбеспеченныеПозиции
	|		КОГДА НЕ НастройкиПередачиИсточник.ОтменятьНеОбеспеченныеПозиции ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиИсточник.ОтменятьНеОбеспеченныеПозиции
	|		КОГДА НЕ НастройкиПередачиПартнер.ОтменятьНеОбеспеченныеПозиции ЕСТЬ NULL
	|			ТОГДА НастройкиПередачиПартнер.ОтменятьНеОбеспеченныеПозиции
	|		ИНАЧЕ НастройкиПередачиОбщие.ОтменятьНеОбеспеченныеПозиции
	|	КОНЕЦ";
	
КонецФункции	

// Возвращает текст запроса соединения таблицы заказов с регистром "Настройки автоматической передачи заказов клиентов на сборку".
//
// Параметры:
//  ИсточникЗаказа - Строка - Таблица с упаковкой. Например, 
//                            "ВЫРАЗИТЬ(ЗаказыКлиентовНеОтгруженные.ЗаказКлиента КАК Документ.ЗаказКлиента)" 
//                            или "СписокЗаказов.ЗаказКлиента"
// 
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТекстЗапросаСоединенияСНастройкамиПередачиНаСборку(ИсточникЗаказа) Экспорт
	
	// В регистре настроек могут быть заданы: "Организация", "КаналПродаж", "ИсточникЗаказа", "Партнер".
	// В псевдонимах таблиц будем сокращенно уазывать заполненные поля, по которым соединяемся с заказами клиентов.
	// Поле Организация в регистре настроек обязательное для заполнения, поэтому "Организация" в
	// псевдонимах таблиц писать не будем (она всегда заполнена).
	//
	// Приоритет получения настроек при незаполненных измерениях регистра следующий:
	//  - Приоритет отдается заполненному каналу продаж.
	//  - Далее, канал продаж может быть уточнен заполненным источником заказа и/или партнером.
	//  - Далее, источник заказа может быть уточнен заполненным партнером.
	//
	// Матрица приоритизации получения настроек при незаполненных измерениях регистра.
	// ----------------------------------------------------------------------------------------------
	// | Приоритет | КаналПродаж | ИсточникЗаказа | Партнер |     Псевдоним соединяемой таблицы     |
	// ----------------------------------------------------------------------------------------------
	// |    1      |      +      |       +        |    +    | НастройкиПередачиКаналИсточникПартнер |
	// |    2      |      +      |       +        |    -    | НастройкиПередачиКаналИсточник        | 
	// |    3      |      +      |       -        |    +    | НастройкиПередачиКаналПартнер         |
	// |    4      |      +      |       -        |    -    | НастройкиПередачиКанал                |
	// |    5      |      -      |       +        |    +    | НастройкиПередачиИсточникПартнер      |
	// |    6      |      -      |       +        |    -    | НастройкиПередачиИсточник             |
	// |    7      |      -      |       -        |    +    | НастройкиПередачиПартнер              |
	// |    8      |      -      |       -        |    -    | НастройкиПередачиОбщие                |
	// ----------------------------------------------------------------------------------------------
	   
	ТекстЗапроса = 
	"		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бг_НастройкиАвтоматическойПередачиЗаказовКлиентовНаСборку КАК НастройкиПередачиКаналИсточникПартнер
	|		ПО (СписокЗаказов.ЗаказКлиента.Организация = НастройкиПередачиКаналИсточникПартнер.Организация)
	|			И (СписокЗаказов.ЗаказКлиента.бг_КаналПродаж = НастройкиПередачиКаналИсточникПартнер.КаналПродаж)
	|			И (СписокЗаказов.ЗаказКлиента.бг_ИсточникЗаказа = НастройкиПередачиКаналИсточникПартнер.ИсточникЗаказа)
	|			И (СписокЗаказов.ЗаказКлиента.Партнер = НастройкиПередачиКаналИсточникПартнер.Партнер)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бг_НастройкиАвтоматическойПередачиЗаказовКлиентовНаСборку КАК НастройкиПередачиКаналИсточник
	|		ПО (СписокЗаказов.ЗаказКлиента.Организация = НастройкиПередачиКаналИсточник.Организация)
	|			И (СписокЗаказов.ЗаказКлиента.бг_КаналПродаж = НастройкиПередачиКаналИсточник.КаналПродаж)
	|			И (СписокЗаказов.ЗаказКлиента.бг_ИсточникЗаказа = НастройкиПередачиКаналИсточник.ИсточникЗаказа)
	|			И (НастройкиПередачиКаналИсточник.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бг_НастройкиАвтоматическойПередачиЗаказовКлиентовНаСборку КАК НастройкиПередачиКаналПартнер
	|		ПО (СписокЗаказов.ЗаказКлиента.Организация = НастройкиПередачиКаналПартнер.Организация)
	|			И (СписокЗаказов.ЗаказКлиента.бг_КаналПродаж = НастройкиПередачиКаналПартнер.КаналПродаж)
	|			И (НастройкиПередачиКаналПартнер.ИсточникЗаказа = ЗНАЧЕНИЕ(Перечисление.бг_ИсточникиЗагрузкиЗаказовКлиентов.ПустаяСсылка))
	|			И (СписокЗаказов.ЗаказКлиента.Партнер = НастройкиПередачиКаналПартнер.Партнер)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бг_НастройкиАвтоматическойПередачиЗаказовКлиентовНаСборку КАК НастройкиПередачиКанал
	|		ПО (СписокЗаказов.ЗаказКлиента.Организация = НастройкиПередачиКанал.Организация)
	|			И (СписокЗаказов.ЗаказКлиента.бг_КаналПродаж = НастройкиПередачиКанал.КаналПродаж)
	|			И (НастройкиПередачиКанал.ИсточникЗаказа = ЗНАЧЕНИЕ(Перечисление.бг_ИсточникиЗагрузкиЗаказовКлиентов.ПустаяСсылка))
	|			И (НастройкиПередачиКанал.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бг_НастройкиАвтоматическойПередачиЗаказовКлиентовНаСборку КАК НастройкиПередачиИсточникПартнер
	|		ПО (СписокЗаказов.ЗаказКлиента.Организация = НастройкиПередачиИсточникПартнер.Организация)
	|			И (НастройкиПередачиИсточникПартнер.КаналПродаж = ЗНАЧЕНИЕ(Справочник.битКаналыПродаж.ПустаяСсылка))
	|			И (СписокЗаказов.ЗаказКлиента.бг_ИсточникЗаказа = НастройкиПередачиИсточникПартнер.ИсточникЗаказа)
	|			И (СписокЗаказов.ЗаказКлиента.Партнер = НастройкиПередачиИсточникПартнер.Партнер)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бг_НастройкиАвтоматическойПередачиЗаказовКлиентовНаСборку КАК НастройкиПередачиИсточник
	|		ПО (СписокЗаказов.ЗаказКлиента.Организация = НастройкиПередачиИсточник.Организация)
	|			И (НастройкиПередачиИсточник.КаналПродаж = ЗНАЧЕНИЕ(Справочник.битКаналыПродаж.ПустаяСсылка))
	|			И (СписокЗаказов.ЗаказКлиента.бг_ИсточникЗаказа = НастройкиПередачиИсточник.ИсточникЗаказа)
	|			И (НастройкиПередачиИсточник.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бг_НастройкиАвтоматическойПередачиЗаказовКлиентовНаСборку КАК НастройкиПередачиПартнер
	|		ПО (СписокЗаказов.ЗаказКлиента.Организация = НастройкиПередачиПартнер.Организация)
	|			И (НастройкиПередачиПартнер.КаналПродаж = ЗНАЧЕНИЕ(Справочник.битКаналыПродаж.ПустаяСсылка))
	|			И (НастройкиПередачиПартнер.ИсточникЗаказа = ЗНАЧЕНИЕ(Перечисление.бг_ИсточникиЗагрузкиЗаказовКлиентов.ПустаяСсылка))
	|			И (СписокЗаказов.ЗаказКлиента.Партнер = НастройкиПередачиПартнер.Партнер)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бг_НастройкиАвтоматическойПередачиЗаказовКлиентовНаСборку КАК НастройкиПередачиОбщие
	|		ПО (СписокЗаказов.ЗаказКлиента.Организация = НастройкиПередачиОбщие.Организация)
	|			И (НастройкиПередачиОбщие.КаналПродаж = ЗНАЧЕНИЕ(Справочник.битКаналыПродаж.ПустаяСсылка))
	|			И (НастройкиПередачиОбщие.ИсточникЗаказа = ЗНАЧЕНИЕ(Перечисление.бг_ИсточникиЗагрузкиЗаказовКлиентов.ПустаяСсылка))
	|			И (НастройкиПередачиОбщие.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка))";
	   
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СписокЗаказов.ЗаказКлиента", ИсточникЗаказа);
	
	Возврат ТекстЗапроса; 
	
КонецФункции

#КонецОбласти 

#КонецЕсли

