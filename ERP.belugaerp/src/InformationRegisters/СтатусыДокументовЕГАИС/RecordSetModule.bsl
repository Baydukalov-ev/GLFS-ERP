#Область ОбработчикиСобытий

&После("ПриЗаписи")
Процедура бг_ПриЗаписи(Отказ, Замещение)
	Для Каждого МенеджерЗаписи Из ЭтотОбъект Цикл
		Если ТипЗнч(МенеджерЗаписи.Документ) = Тип("ДокументСсылка.АктСписанияЕГАИС")
			И МенеджерЗаписи.Статус = Перечисления.СтатусыОбработкиАктаСписанияЕГАИС.Отменен
			И ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(МенеджерЗаписи.Документ, "ДокументОснование")) Тогда
			ДокументОбъект = МенеджерЗаписи.Документ.ПолучитьОбъект();
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		КонецЕсли;
		
		Если ТипЗнч(МенеджерЗаписи.Документ) = Тип("ДокументСсылка.ТТНВходящаяЕГАИС") Тогда
			Документы.ТТНВходящаяЕГАИС.бг_ОбновитьРасширенныйСтатусЕГАИС(МенеджерЗаписи.Документ);
		КонецЕсли;
		
		Если ТипЗнч(МенеджерЗаписи.Документ) = Тип("ДокументСсылка.ТТНИсходящаяЕГАИС") Тогда
			
			бг_ЗарегистрироватьЗаказКлиентаB2BКВыгрузке(МенеджерЗаписи);
			бг_ЗарегистрироватьРеализациюТоваровУслугКВыгрузке(МенеджерЗаписи);
			
			Если МенеджерЗаписи.Статус = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОтмененКлиентом Тогда			
				бг_ОбщегоНазначенияСервер.ЗарегистрироватьИсходящееСообщениеПриЗаписи(МенеджерЗаписи.Документ);			
			КонецЕсли;
			
		КонецЕсли;

		Если ТипЗнч(МенеджерЗаписи.Документ) = Тип("ДокументСсылка.ТранспортнаяНакладнаяЕГАИС")
			И МенеджерЗаписи.Статус = Перечисления.СтатусыОбработкиТранспортныхНакладныхЕГАИС.ПроведенЕГАИС Тогда
			бг_ОбщегоНазначенияСервер.ЗарегистрироватьИсходящееСообщениеПриЗаписи(МенеджерЗаписи.Документ);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура бг_ЗарегистрироватьЗаказКлиентаB2BКВыгрузке(МенеджерЗаписи)

	Если Документы.ТТНИсходящаяЕГАИС.бг_СтатусЯвляетсяПодтвержденнымB2B(МенеджерЗаписи.Статус) Тогда
						
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ 
		|	ВЫРАЗИТЬ(ТТНИсходящаяЕГАИС.ДокументОснование.ЗаказКлиента КАК Документ.ЗаказКлиента) КАК ЗаказКлиента
		|ИЗ
		|	Документ.ТТНИсходящаяЕГАИС КАК ТТНИсходящаяЕГАИС
		|ГДЕ
		|	ТТНИсходящаяЕГАИС.Ссылка = &Ссылка 
		|	И ТТНИсходящаяЕГАИС.ДокументОснование ССЫЛКА Документ.РеализацияТоваровУслуг
		|	И ТТНИсходящаяЕГАИС.ДокументОснование.ЗаказКлиента ССЫЛКА Документ.ЗаказКлиента
		|	И НЕ ТТНИсходящаяЕГАИС.ДокументОснование.ЗаказКлиента.бг_ГУИДЗаказаСПортала = """"";
		
		Запрос.УстановитьПараметр("Ссылка", МенеджерЗаписи.Документ);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда
		
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				бг_ОбщегоНазначенияСервер.ЗарегистрироватьИсходящееСообщениеПриЗаписи(ВыборкаДетальныеЗаписи.ЗаказКлиента);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
	
Процедура бг_ЗарегистрироватьРеализациюТоваровУслугКВыгрузке(МенеджерЗаписи)
	
	Документ = МенеджерЗаписи.Документ;
	Статус = МенеджерЗаписи.Статус;
	СтатусыОтменыЕГАИС = Документы.ТТНИсходящаяЕГАИС.бг_СтатусыОтменыЕГАИС();
	Если СтатусыОтменыЕГАИС.Найти(Статус) = Неопределено Тогда
		 ЭтоСтатусОтмены = Ложь;
	Иначе
		 ЭтоСтатусОтмены = Истина;
	КонецЕсли; 
	
	Если Статус = Перечисления.СтатусыОбработкиТТНИсходящейЕГАИС.ОбрабатываетсяКлиентом 
		Или ЭтоСтатусОтмены Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ТТНИсходящаяЕГАИС.ДокументОснование КАК Документ.РеализацияТоваровУслуг) КАК РеализацияТоваровУслуг
		|ИЗ
		|	Документ.ТТНИсходящаяЕГАИС КАК ТТНИсходящаяЕГАИС
		|ГДЕ
		|	ТТНИсходящаяЕГАИС.Ссылка = &Ссылка
		|	И ТИПЗНАЧЕНИЯ(ТТНИсходящаяЕГАИС.ДокументОснование) = ТИП(Документ.РеализацияТоваровУслуг)
		|	И НЕ ТТНИсходящаяЕГАИС.ДокументОснование = ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)";
		
		Запрос.УстановитьПараметр("Ссылка", Документ);
		
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			Возврат;
		КонецЕсли;
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Если ЭтоСтатусОтмены Тогда
			Документы.РеализацияТоваровУслуг.бг_УдалитьДатуОтправкиDESADVвSAP(
				Выборка.РеализацияТоваровУслуг,
				Документ);
		Иначе
			Документы.РеализацияТоваровУслуг.бг_ЗарегистрироватьКВыгрузкеDESADV(
				Выборка.РеализацияТоваровУслуг,
				Ложь,
				Документ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

