
#Область ПрограммныйИнтерфейс

&ИзменениеИКонтроль("ОбновитьСтатус")
Функция бг_ОбновитьСтатус(Документ, ПараметрыОбновления, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ПараметрыОбновления = Неопределено Тогда
		
		МенеджерОбъекта = ИнтеграцияИС.МенеджерОбъектаПоСсылке(Документ);
		
		ДальнейшиеДействия = Новый Массив;
		ДальнейшиеДействия.Добавить(МенеджерОбъекта.ДальнейшееДействиеПоУмолчанию());
		
		Возврат ОбновитьСтатус(
			Документ,
			РегистрыСведений.СтатусыДокументовЕГАИС.ВозвращаемоеЗначениеДальнейшиеДействияСтатус(
				МенеджерОбъекта.СтатусПоУмолчанию(), ДальнейшиеДействия, Истина),
			ИнтеграцияЕГАИС.ПараметрыОбновленияСтатуса());
		
	КонецЕсли;
	
	Если ПараметрыОбновления.НовыйСтатус = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка изменения статуса';
								|en = 'Ошибка изменения статуса'");
	КонецЕсли;
	
	ЭтоНовый = Ложь;
	НаборЗаписей = НаборЗаписей(Документ, ЭтоНовый);
	ЗаписатьНабор = НаборЗаписей.Модифицированность();
	
	Результат = ОбработатьНаборЗаписей(НаборЗаписей, ПараметрыОбновления, ЭтоНовый);
	Если Результат.ЗаписатьНабор <> Неопределено Тогда
		ЗаписатьНабор = Результат.ЗаписатьНабор;
	КонецЕсли;
	НовыйСтатус  = Результат.НовыйСтатус;
	СтарыйСтатус = Результат.СтарыйСтатус;
#Вставка
	бг_ОбработатьЗапретныйДаунгрейдСтатуса(Документ, СтарыйСтатус, НовыйСтатус, ЗаписатьНабор, ДополнительныеПараметры);
	бг_ОтразитьДополнительныеСведенияЗаказаКлиента(Документ, ЗаписатьНабор);
	бг_ОбновитьСтатусДоверительнаяПриемка(Документ, НаборЗаписей, НовыйСтатус);
#КонецВставки
	
	Если ЗаписатьНабор Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		НаборЗаписей.Записать();
		
		Если СтарыйСтатус <> НовыйСтатус Тогда
			
			ПолноеИмя = Документ.Метаданные().ПолноеИмя();
			МенеджерОбъекта = ИнтеграцияИС.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
			
			ПараметрыОбновленияСтатуса = ИнтеграцияЕГАИС.ДополнитьПараметрыОбновленияСтатуса(ДополнительныеПараметры);
			
			МенеджерОбъекта.ПриИзмененииСтатусаДокумента(
				Документ,
				СтарыйСтатус, НовыйСтатус,
				ПараметрыОбновленияСтатуса);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат НовыйСтатус;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура бг_ОбработатьЗапретныйДаунгрейдСтатуса(Документ, СтарыйСтатус, НовыйСтатус, ЗаписатьНабор, ДополнительныеПараметры)
	
	ТипДокумента = ТипЗнч(Документ);
	
	Если ТипДокумента = Тип("ДокументСсылка.АктПостановкиНаБалансЕГАИС")
		Или ТипДокумента = Тип("ДокументСсылка.ТТНВходящаяЕГАИС")
		Или ТипДокумента = Тип("ДокументСсылка.ТТНИсходящаяЕГАИС") Тогда
		
		МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Документ);
		
		Если МенеджерДокумента.бг_ЭтоЗапретныйДаунгрейдСтатуса(СтарыйСтатус, НовыйСтатус) Тогда
			
			Если ДополнительныеПараметры = Неопределено Тогда
				ДополнительныеПараметры = Новый Структура;
			КонецЕсли;
			
			ДополнительныеПараметры.Вставить("бг_ОтменитьОбработку", Истина);
			
			ЗаписатьНабор = Ложь;
			НовыйСтатус = СтарыйСтатус;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура бг_ОтразитьДополнительныеСведенияЗаказаКлиента(Документ, ЗаписатьНабор)
	
	ТипДокумента = ТипЗнч(Документ);
	
	Если ТипДокумента = Тип("ДокументСсылка.ТТНИсходящаяЕГАИС") И ЗаписатьНабор Тогда
		
		ДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "ДокументОснование");
		
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			
			ЗаказКлиента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "ЗаказКлиента");
			
			Если ЗначениеЗаполнено(ЗаказКлиента) Тогда
				РегистрыСведений.бг_ОбъектыДляОтложеннойОбработки.ДобавитьОбъект(
					ЗаказКлиента,
					Перечисления.бг_ВариантыОтложеннойОбработкиОбъектов.ОтразитьДополнительныеСведенияЗаказаКлиента,,
					Новый ХранилищеЗначения(
						РегистрыСведений.бг_ДополнительныеСведенияПоЗаказамКлиентов.ПоказателиПоТипуДокумента(Документ)));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура бг_ОбновитьСтатусДоверительнаяПриемка(Документ, НаборЗаписей, НовыйСтатус)
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьНабора = НаборЗаписей[0];
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ТТНВходящаяЕГАИС") Тогда
		
		Если бг_ЭтоТТНВходящаяПоДоверительнойПриемке(Документ)
			И ЗаписьНабора.Статус = Перечисления.СтатусыОбработкиТТНВходящейЕГАИС.ПринятИзЕГАИС
			И ЗаписьНабора.ДальнейшееДействие1 = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ВыполнитеПроверку Тогда
			
			ЗаписьНабора.ДальнейшееДействие1 = Перечисления.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ВыполнитеПроверку;
			
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

Функция бг_ЭтоТТНВходящаяПоДоверительнойПриемке(Документ)
	
	ДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Документ,
		"Проведен, ЕстьРасхождения, СтатусПроверкиИПодбора, бг_ДоверительнаяПриемка");
	
	Возврат ДанныеДокумента.Проведен
		И Не ДанныеДокумента.ЕстьРасхождения
		И ДанныеДокумента.СтатусПроверкиИПодбора = Перечисления.СтатусыПроверкиИПодбораИС.Завершено
		И ДанныеДокумента.бг_ДоверительнаяПриемка;
	
КонецФункции

#КонецОбласти
