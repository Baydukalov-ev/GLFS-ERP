#Область ПрограммныйИнтерфейс

// Возвращает ссылку на действующую лицензию пункта назначения на дату запроса
// Параметры:
//	ПунктНазначения - СправочникСсылка.битПунктыНазначения;
//	ДатаЗапроса - Дата;
//
Функция ТекущаяЛицензияПунктаНазначения(ПунктНазначения, ДатаЗапроса = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПунктНазначения", ПунктНазначения);
	Запрос.УстановитьПараметр("Период", ?(ЗначениеЗаполнено(ДатаЗапроса), ДатаЗапроса, ТекущаяДатаСеанса()));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	бг_ЛицензииПунктовНазначенияСрезПоследних.Лицензия КАК Ссылка
	|ИЗ
	|	РегистрСведений.бг_ЛицензииПунктовНазначения.СрезПоследних(&Период, ПунктНазначения = &ПунктНазначения) КАК бг_ЛицензииПунктовНазначенияСрезПоследних";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Возвращает массив, который содержит ссылки на пункты назначения, 
// в которых действует лицензия
// Параметры:
//	ПунктНазначения - СправочникСсылка.битПунктыНазначения;
//	ДатаЗапроса - Дата;
//
Функция ПунктыНазначенияЛицензии(Лицензия, ДатаЗапроса = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПунктыНазначения = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Лицензия", Лицензия);
	Запрос.УстановитьПараметр("Период", ?(ЗначениеЗаполнено(ДатаЗапроса), ДатаЗапроса, ТекущаяДатаСеанса()));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	бг_ЛицензииПунктовНазначенияСрезПоследних.ПунктНазначения КАК Ссылка
	|ИЗ
	|	РегистрСведений.бг_ЛицензииПунктовНазначения.СрезПоследних(&Период, Лицензия = &Лицензия) КАК бг_ЛицензииПунктовНазначенияСрезПоследних";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ПунктыНазначения.Добавить(Выборка.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ПунктыНазначения;
	
КонецФункции

// Возвращает массив, ссылок на лицензии пункта назначения
// Параметры:
//  ПунктНазначения - СправочникСсылка.битПунктыНазначения;
//  ДатаЗапроса - Дата - дата для отбора лицензий по дате прекращения действия
//  ТолькоДействующие - Булево - Если Истина - в результат попадут только действующие лицензии
// 
// Возвращаемое значение:
//  Массив - лицензии привязанные к пункту назначения
//
Функция ЛицензииПунктаНазначения(ПунктНазначения, ДатаЗапроса = Неопределено, ТолькоДействующие = Истина) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Лицензии = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	бг_ЛицензииПунктовНазначения.Лицензия КАК Лицензия
		|ИЗ
		|	РегистрСведений.бг_ЛицензииПунктовНазначения КАК бг_ЛицензииПунктовНазначения
		|ГДЕ
		|	бг_ЛицензииПунктовНазначения.ПунктНазначения = &ПунктНазначения
		|	И (НЕ &ТолькоДействующие
		|			ИЛИ бг_ЛицензииПунктовНазначения.Лицензия.бг_ДатаПрекращенияДействия >= &бг_ДатаПрекращенияДействия)
		|
		|УПОРЯДОЧИТЬ ПО
		|	бг_ЛицензииПунктовНазначения.Лицензия.ДатаНачала,
		|	бг_ЛицензииПунктовНазначения.Лицензия.ДатаОкончания,
		|	бг_ЛицензииПунктовНазначения.Лицензия.бг_ДатаПрекращенияДействия,
		|	бг_ЛицензииПунктовНазначения.Лицензия.Наименование";
	
	Запрос.УстановитьПараметр("бг_ДатаПрекращенияДействия", ?(ЗначениеЗаполнено(ДатаЗапроса), ДатаЗапроса, ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("ПунктНазначения", ПунктНазначения);
	Запрос.УстановитьПараметр("ТолькоДействующие", ТолькоДействующие);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Лицензии.Добавить(ВыборкаДетальныеЗаписи.Лицензия);
	КонецЦикла;
	
	Возврат Лицензии;
	
КонецФункции
	
// Устанавливает действие лицензии для пункта назначения с указанной даты
// Параметры:
//	ПунктНазначения - СправочникСсылка.битПунктыНазначения;
//	Лицензия - СправочникСсылка.ЛицензииПоставщиковАлкогольнойПродукции;
//	ДатаДействия - Дата;
//
Процедура УстановитьДействиеЛицензииВПунктеНазначения(ПунктНазначения, Лицензия, ДатаДействия = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекущаяЛицензия = ТекущаяЛицензияПунктаНазначения(ПунктНазначения);
	Если ТекущаяЛицензия = Лицензия Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.бг_ЛицензииПунктовНазначения.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ПунктНазначения = ПунктНазначения;
	МенеджерЗаписи.Лицензия = Лицензия;
	МенеджерЗаписи.Период = ?(ЗначениеЗаполнено(ДатаДействия), ДатаДействия, ТекущаяДатаСеанса());
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

// Отменяет действие лицензии в пункте назначения
// Параметры:
//	ПунктНазначения - СправочникСсылка.битПунктыНазначения;
//	Лицензия - СправочникСсылка.ЛицензииПоставщиковАлкогольнойПродукции;
//
Процедура ОтменитьДействиеЛицензииВПунктеНазначения(ПунктНазначения, Лицензия) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.бг_ЛицензииПунктовНазначения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПунктНазначения.Установить(ПунктНазначения);
	НаборЗаписей.Прочитать();
	
	КоличествоЗаписей = НаборЗаписей.Количество();
	Если КоличествоЗаписей = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Индекс = КоличествоЗаписей - 1;
	Пока Индекс >= 0 Цикл
		Запись = НаборЗаписей[Индекс];
		Если Запись.Лицензия = Лицензия Тогда
			НаборЗаписей.Удалить(Запись);
		КонецЕсли;
		Индекс = Индекс - 1;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти