
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Очищает очередь по документу по версии равной и ранее зарегистрированным.
// Параметры:
//	Документ - ДокументСсылка
//	Версия - Строка - версия документа.
// 
Процедура ОчиститьОчередьПоДокументу(Документ, Версия) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОчередьСинхронизации.Документ КАК Документ,
	|	ОчередьСинхронизации.Версия КАК Версия,
	|	ОчередьСинхронизации.Период КАК Период
	|ИЗ
	|	РегистрСведений.бг_ОчередьСинхронизацииСвязанныхДокументов КАК ОбработаннаяОчередь
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.бг_ОчередьСинхронизацииСвязанныхДокументов КАК ОчередьСинхронизации
	|		ПО ОбработаннаяОчередь.Документ = ОчередьСинхронизации.Документ
	|			И ОбработаннаяОчередь.Период >= ОчередьСинхронизации.Период
	|ГДЕ
	|	ОбработаннаяОчередь.Документ = &Документ
	|	И ОбработаннаяОчередь.Версия = &Версия";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.УстановитьПараметр("Версия", Версия);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
		НаборЗаписей.Отбор.Документ.Установить(Выборка.Документ);
		НаборЗаписей.Отбор.Версия.Установить(Выборка.Версия);
		
		НаборЗаписей.Записать();
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
