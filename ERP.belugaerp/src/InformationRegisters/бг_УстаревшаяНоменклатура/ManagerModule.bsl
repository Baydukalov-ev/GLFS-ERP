#Область ПрограммныйИнтерфейс

// Возвращает флаг наличия устаревшей номенклатуры в таблице
//
// Параметры:
//  ТаблицаНоменклатуры - ТаблицаЗначений - содержит как минимум 1 колонку:
//  	* ИмяКолонкиНоменклатура - СправочникСсылка.Номенклатура - проверяемая номенклатура      
//  ИмяКолонкиНоменклатура - Строка - имя колонки таблицы значений с проверяемой номенклатурой
//  ДатаПроверки - Дата, Неопределено - дата на которую проверяем (если не заполнено - текущая дата)
//
// Возвращаемое значение:
//   Булево - наличие устаревшей номенклатуры в таблице
//
Функция ЕстьУстаревшаяНоменклатураВТаблице(ТаблицаНоменклатуры, ИмяКолонкиНоменклатура = "Номенклатура", ДатаПроверки = Неопределено) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Результат = Ложь;
	Если ЗначениеЗаполнено(ДатаПроверки) Тогда
		ДатаПроверкиПараметр = НачалоДня(ДатаПроверки);
	Иначе
		ДатаПроверкиПараметр = НачалоДня(ТекущаяДатаСеанса());
	КонецЕсли;                                    
	НоменклатураКПроверке = ТаблицаНоменклатуры.ВыгрузитьКолонку(ИмяКолонкиНоменклатура);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаЗапретаИспользования", ДатаПроверкиПараметр);
	Запрос.УстановитьПараметр("Номенклатура", НоменклатураКПроверке);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	бг_УстаревшаяНоменклатура.Номенклатура КАК Номенклатура,
	|	бг_УстаревшаяНоменклатура.ДатаЗапретаИспользования КАК ДатаЗапрета,
	|	бг_УстаревшаяНоменклатура.НоваяНоменклатура КАК НоваяНоменклатура
	|ИЗ
	|	РегистрСведений.бг_УстаревшаяНоменклатура КАК бг_УстаревшаяНоменклатура
	|ГДЕ
	|	бг_УстаревшаяНоменклатура.Номенклатура В(&Номенклатура)
	|	И бг_УстаревшаяНоменклатура.ДатаЗапретаИспользования <= &ДатаЗапретаИспользования";
	Выборка = Запрос.Выполнить().Выбрать();
	ШаблонБезНовой = НСтр("ru='Номенклатуру %1 запрещено использовать начиная с даты %2'");
	ШаблонСНовой = НСтр("ru='Начиная с даты %1 вместо номенклатуры %2 следует использовать номенклатуру %3'");
	Пока Выборка.Следующий() Цикл
		Результат = Истина;
		Если ЗначениеЗаполнено(Выборка.НоваяНоменклатура) Тогда
			ТекстОшибки = СтрШаблон(ШаблонСНовой, Формат(Выборка.ДатаЗапрета, "ДЛФ=D"), Выборка.Номенклатура, Выборка.НоваяНоменклатура);
		Иначе 
			ТекстОшибки = СтрШаблон(ШаблонБезНовой, Выборка.Номенклатура, Формат(Выборка.ДатаЗапрета, "ДЛФ=D"));
		КонецЕсли;
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
	КонецЦикла; 
	Возврат Результат;
КонецФункции	

#КонецОбласти