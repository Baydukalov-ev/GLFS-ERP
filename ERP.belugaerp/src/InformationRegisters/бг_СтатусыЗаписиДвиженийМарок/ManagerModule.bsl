
#Область ПрограммныйИнтерфейс

// Процедура - Установить статус записи движений марок
//
// Параметры:
//  Документ - ДокументСсылка.ТТНВходящаяЕГАИС - Ссылка на документ, для которого нужно установить статус
//  Статус	 - ПеречислениеСсылка.бг_СтатусыЗаписиДвиженийМарок - устанавливаемый статус
//
Процедура УстановитьСтатус(Документ, Статус) Экспорт
	
	НаборЗаписей = РегистрыСведений.бг_СтатусыЗаписиДвиженийМарок.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(Документ);
	
	Запись = НаборЗаписей.Добавить();
	Запись.Документ = Документ;
	Запись.Статус = Статус;
	Запись.ДатаОбновленияСтатуса = ТекущаяДата();
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

Функция ДвиженияЗаписываютсяВДанныйМомент(Документ) Экспорт

	Возврат ТекущийСтатус(Документ) = Перечисления.бг_СтатусыЗаписиДвиженийМарок.ДвиженияЗаписываются;
	
КонецФункции

Процедура УстановитьБлокировкуИзмененияСтатуса(Документ) Экспорт

	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.бг_СтатусыЗаписиДвиженийМарок");
	ЭлементБлокировки.УстановитьЗначение("Документ", Документ);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекущийСтатус(Документ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	бг_СтатусыЗаписиДвиженийМарок.Статус КАК Статус
		|ИЗ
		|	РегистрСведений.бг_СтатусыЗаписиДвиженийМарок КАК бг_СтатусыЗаписиДвиженийМарок
		|ГДЕ
		|	бг_СтатусыЗаписиДвиженийМарок.Документ = &Документ";
	Запрос.УстановитьПараметр("Документ", Документ);
	РезультатЗапроса = Запрос.Выполнить();
	
	ТекущийСтатус = Неопределено;
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		
		ТекущийСтатус = ВыборкаДетальныеЗаписи.Статус;
	КонецЕсли;
	
	Возврат ТекущийСтатус;
	
КонецФункции

#КонецОбласти
