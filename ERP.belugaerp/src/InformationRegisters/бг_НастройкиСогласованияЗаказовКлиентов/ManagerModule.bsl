#Область ПрограммныйИнтерфейс

// Возвращает настройки согласования заказа
//
// Параметры:
//   ЗаказКлиента - ссылка или объект Заказ клиента
//   
// Возвращаемое значение:
//  Структура - содержит имена (ключи) и значения настроек.
Функция НастройкиСогласования(ЗаказКлиента) Экспорт

	Если ТипЗнч(ЗаказКлиента) = Тип("ДокументОбъект.ЗаказКлиента") Тогда
		РеквизитыЗаказа = Новый Структура(
			"Дата, ДатаОтгрузки, Организация, Контрагент, Партнер,  
			|бг_ПунктНазначения, бг_КаналПродаж, бг_Магазин, бг_ПунктНазначенияТерритория");
		ЗаполнитьЗначенияСвойств(РеквизитыЗаказа, ЗаказКлиента);
		РеквизитыЗаказа.бг_ПунктНазначенияТерритория = 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаказКлиента.бг_ПунктНазначения, "Территория");
	Иначе
		РеквизитыЗаказа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗаказКлиента,
			"Дата, ДатаОтгрузки, Организация, Контрагент, Партнер,
			|бг_ПунктНазначения, бг_КаналПродаж, бг_Магазин, бг_ПунктНазначения.Территория");
	КонецЕсли;
	ВозвращаемыеДанные = Новый Структура("ТребуетсяВизаКК, ТребуетсяВизаФК, ТребуетсяВизаСБ", Ложь, Ложь, Ложь);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НастройкиСогласования.ТребуетсяВизаКК,
	|	НастройкиСогласования.ТребуетсяВизаФК,
	|	НастройкиСогласования.ТребуетсяВизаСБ
	|ИЗ
	|	НастройкиСогласования";
	
	МассивТекстов = Новый Массив;
	МассивТекстов.Добавить(ТестЗапросаНастройкиСогласования());
	МассивТекстов.Добавить(ТекстЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(МассивТекстов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Запрос.УстановитьПараметр("ДатаОтгрузки", РеквизитыЗаказа.ДатаОтгрузки);
	Запрос.УстановитьПараметр("Организация", РеквизитыЗаказа.Организация);
	Запрос.УстановитьПараметр("Контрагент", РеквизитыЗаказа.Контрагент);
	Запрос.УстановитьПараметр("Партнер", РеквизитыЗаказа.Партнер);
	Запрос.УстановитьПараметр("ПунктНазначения", РеквизитыЗаказа.бг_ПунктНазначения);
	Запрос.УстановитьПараметр("Территория", РеквизитыЗаказа.бг_ПунктНазначенияТерритория);
	Запрос.УстановитьПараметр("КаналПродаж", РеквизитыЗаказа.бг_КаналПродаж);
	Запрос.УстановитьПараметр("ЧерезМагазин", ЗначениеЗаполнено(РеквизитыЗаказа.бг_Магазин));
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ВозвращаемыеДанные, Выборка);
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ВозвращаемыеДанные;
	
КонецФункции

// Возвращает текст запроса настроек согласования по заказу
// Используется, в т.ч. при автосогласовании
//
// Возвращаемое значение:
//  Строка - текст запроса
//
Функция ТестЗапросаНастройкиСогласования() Экспорт
	
	Возврат 
	"ВЫБРАТЬ
	|	0 КАК Приоритет,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяВизаКК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяВизаФК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяВизаСБ,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.АвтоСогласованиеФК КАК АвтоСогласованиеФК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.АвтоСогласованиеКК КАК АвтоСогласованиеКК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.АвтоСогласованиеСБ КАК АвтоСогласованиеСБ,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.КонтролироватьПодтверждениеВ_ЕГАИС КАК КонтролироватьПодтверждениеВ_ЕГАИС,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяКредитныйЛимит КАК ТребуетсяКредитныйЛимит,
	|	ВЫБОР
	|		КОГДА &ЧерезМагазин = ИСТИНА
	|			ТОГДА НастройкиСогласованияЗаказовКлиентовСрезПоследних.ДопустимыеДниПДЗ_ДляАвтосогласованияЧерезМагазин
	|		ИНАЧЕ НастройкиСогласованияЗаказовКлиентовСрезПоследних.ДопустимыеДниПДЗ_ДляАвтосогласованияПрямаяОтгрузка
	|	КОНЕЦ КАК ДопустимыеДниПДЗ
	|ПОМЕСТИТЬ НастройкиСогласованияПредварительно
	|ИЗ
	|	РегистрСведений.бг_НастройкиСогласованияЗаказовКлиентов.СрезПоследних(
	|			&ДатаОтгрузки,
	|			Организация = &Организация
	|				И ДопАналитика = &ПунктНазначения) КАК НастройкиСогласованияЗаказовКлиентовСрезПоследних
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	1 КАК Приоритет,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяВизаКК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяВизаФК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяВизаСБ,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.АвтоСогласованиеФК КАК АвтоСогласованиеФК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.АвтоСогласованиеКК КАК АвтоСогласованиеКК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.АвтоСогласованиеСБ КАК АвтоСогласованиеСБ,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.КонтролироватьПодтверждениеВ_ЕГАИС КАК КонтролироватьПодтверждениеВ_ЕГАИС,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяКредитныйЛимит КАК ТребуетсяКредитныйЛимит,
	|	ВЫБОР
	|		КОГДА &ЧерезМагазин = ИСТИНА
	|			ТОГДА НастройкиСогласованияЗаказовКлиентовСрезПоследних.ДопустимыеДниПДЗ_ДляАвтосогласованияЧерезМагазин
	|		ИНАЧЕ НастройкиСогласованияЗаказовКлиентовСрезПоследних.ДопустимыеДниПДЗ_ДляАвтосогласованияПрямаяОтгрузка
	|	КОНЕЦ КАК ДопустимыеДниПДЗ
	|ИЗ
	|	РегистрСведений.бг_НастройкиСогласованияЗаказовКлиентов.СрезПоследних(
	|			&ДатаОтгрузки,
	|			Организация = &Организация
	|				И ДопАналитика = &Контрагент) КАК НастройкиСогласованияЗаказовКлиентовСрезПоследних
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	2 КАК Приоритет,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяВизаКК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяВизаФК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяВизаСБ,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.АвтоСогласованиеФК КАК АвтоСогласованиеФК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.АвтоСогласованиеКК КАК АвтоСогласованиеКК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.АвтоСогласованиеСБ КАК АвтоСогласованиеСБ,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.КонтролироватьПодтверждениеВ_ЕГАИС КАК КонтролироватьПодтверждениеВ_ЕГАИС,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяКредитныйЛимит КАК ТребуетсяКредитныйЛимит,
	|	ВЫБОР
	|		КОГДА &ЧерезМагазин = ИСТИНА
	|			ТОГДА НастройкиСогласованияЗаказовКлиентовСрезПоследних.ДопустимыеДниПДЗ_ДляАвтосогласованияЧерезМагазин
	|		ИНАЧЕ НастройкиСогласованияЗаказовКлиентовСрезПоследних.ДопустимыеДниПДЗ_ДляАвтосогласованияПрямаяОтгрузка
	|	КОНЕЦ КАК ДопустимыеДниПДЗ
	|ИЗ
	|	РегистрСведений.бг_НастройкиСогласованияЗаказовКлиентов.СрезПоследних(
	|			&ДатаОтгрузки,
	|			Организация = &Организация
	|				И ДопАналитика В (ВЫБРАТЬ ПартнерыСегмента.Сегмент ИЗ РегистрСведений.ПартнерыСегмента КАК ПартнерыСегмента ГДЕ ПартнерыСегмента.Партнер = &Партнер)) КАК НастройкиСогласованияЗаказовКлиентовСрезПоследних
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	3,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяВизаКК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяВизаФК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяВизаСБ,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.АвтоСогласованиеФК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.АвтоСогласованиеКК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.АвтоСогласованиеСБ,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.КонтролироватьПодтверждениеВ_ЕГАИС,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяКредитныйЛимит,
	|	ВЫБОР
	|		КОГДА &ЧерезМагазин = ИСТИНА
	|			ТОГДА НастройкиСогласованияЗаказовКлиентовСрезПоследних.ДопустимыеДниПДЗ_ДляАвтосогласованияЧерезМагазин
	|		ИНАЧЕ НастройкиСогласованияЗаказовКлиентовСрезПоследних.ДопустимыеДниПДЗ_ДляАвтосогласованияПрямаяОтгрузка
	|	КОНЕЦ
	|ИЗ
	|	РегистрСведений.бг_НастройкиСогласованияЗаказовКлиентов.СрезПоследних(
	|			&ДатаОтгрузки,
	|			Организация = &Организация
	|				И Территория = &Территория
	|				И КаналПродаж = &КаналПродаж) КАК НастройкиСогласованияЗаказовКлиентовСрезПоследних
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	4,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяВизаКК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяВизаФК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяВизаСБ,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.АвтоСогласованиеФК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.АвтоСогласованиеКК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.АвтоСогласованиеСБ,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.КонтролироватьПодтверждениеВ_ЕГАИС,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяКредитныйЛимит,
	|	ВЫБОР
	|		КОГДА &ЧерезМагазин = ИСТИНА
	|			ТОГДА НастройкиСогласованияЗаказовКлиентовСрезПоследних.ДопустимыеДниПДЗ_ДляАвтосогласованияЧерезМагазин
	|		ИНАЧЕ НастройкиСогласованияЗаказовКлиентовСрезПоследних.ДопустимыеДниПДЗ_ДляАвтосогласованияПрямаяОтгрузка
	|	КОНЕЦ
	|ИЗ
	|	РегистрСведений.бг_НастройкиСогласованияЗаказовКлиентов.СрезПоследних(
	|			&ДатаОтгрузки,
	|			Организация = &Организация
	|				И Территория = ЗНАЧЕНИЕ(Справочник.битТерриторииПунктовНазначения.ПустаяСсылка)
	|				И КаналПродаж = &КаналПродаж) КАК НастройкиСогласованияЗаказовКлиентовСрезПоследних
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	100,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяВизаКК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяВизаФК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяВизаСБ,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.АвтоСогласованиеФК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.АвтоСогласованиеКК,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.АвтоСогласованиеСБ,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.КонтролироватьПодтверждениеВ_ЕГАИС,
	|	НастройкиСогласованияЗаказовКлиентовСрезПоследних.ТребуетсяКредитныйЛимит,
	|	ВЫБОР
	|		КОГДА &ЧерезМагазин = ИСТИНА
	|			ТОГДА НастройкиСогласованияЗаказовКлиентовСрезПоследних.ДопустимыеДниПДЗ_ДляАвтосогласованияЧерезМагазин
	|		ИНАЧЕ НастройкиСогласованияЗаказовКлиентовСрезПоследних.ДопустимыеДниПДЗ_ДляАвтосогласованияПрямаяОтгрузка
	|	КОНЕЦ
	|ИЗ
	|	РегистрСведений.бг_НастройкиСогласованияЗаказовКлиентов.СрезПоследних(
	|			&ДатаОтгрузки,
	|			Организация = &Организация
	|				И КаналПродаж = ЗНАЧЕНИЕ(Справочник.битКаналыПродаж.ПустаяСсылка)
	|				И Территория = ЗНАЧЕНИЕ(Справочник.битТерриторииПунктовНазначения.ПустаяСсылка)
	|				И ДопАналитика = НЕОПРЕДЕЛЕНО) КАК НастройкиСогласованияЗаказовКлиентовСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиСогласованияПредварительно.ТребуетсяВизаКК,
	|	НастройкиСогласованияПредварительно.ТребуетсяВизаФК,
	|	НастройкиСогласованияПредварительно.ТребуетсяВизаСБ,
	|	НастройкиСогласованияПредварительно.АвтоСогласованиеФК КАК АвтоСогласованиеФК,
	|	НастройкиСогласованияПредварительно.АвтоСогласованиеКК КАК АвтоСогласованиеКК,
	|	НастройкиСогласованияПредварительно.АвтоСогласованиеСБ КАК АвтоСогласованиеСБ,
	|	НастройкиСогласованияПредварительно.КонтролироватьПодтверждениеВ_ЕГАИС КАК КонтролироватьПодтверждениеВ_ЕГАИС,
	|	НастройкиСогласованияПредварительно.ТребуетсяКредитныйЛимит КАК ТребуетсяКредитныйЛимит,
	|	НастройкиСогласованияПредварительно.ДопустимыеДниПДЗ КАК ДопустимыеДниПДЗ
	|ПОМЕСТИТЬ НастройкиСогласования
	|ИЗ
	|	НастройкиСогласованияПредварительно КАК НастройкиСогласованияПредварительно
	|
	|УПОРЯДОЧИТЬ ПО
	|	НастройкиСогласованияПредварительно.Приоритет";
	
КонецФункции

#КонецОбласти 