
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не ЗначениеЗаполнено(Запись.Сотрудник) И Параметры.Свойство("Сотрудник") Тогда
		Запись.Сотрудник = Параметры.Сотрудник;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Запись.ТС) И Параметры.Свойство("ТС") Тогда
		Запись.ТС = Параметры.ТС;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если Запись.ЧленЭкипажа = Перечисления.бг_ЧленыЭкипажа.ОсновнойВодитель
		Или Запись.ЧленЭкипажа = Перечисления.бг_ЧленыЭкипажа.ОсновноеСопроводительноеЛицо Тогда
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("ТС",          Запись.ТС);
		Запрос.УстановитьПараметр("ЧленЭкипажа", Запись.ЧленЭкипажа);
		Запрос.УстановитьПараметр("Сотрудник",   Запись.Сотрудник);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	бг_ЭкипажТС.ТС КАК ТС
		|ИЗ
		|	РегистрСведений.бг_ЭкипажТС КАК бг_ЭкипажТС
		|ГДЕ
		|	бг_ЭкипажТС.ТС = &ТС
		|	И бг_ЭкипажТС.ЧленЭкипажа = &ЧленЭкипажа
		|	И НЕ бг_ЭкипажТС.Сотрудник = &Сотрудник";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			ТекстОшибки = НСтр("ru='Для ТС ""%1"" член экипажа ""%2"" уже был выбран.'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, Запись.ТС, Запись.ЧленЭкипажа);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , , , Отказ);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
