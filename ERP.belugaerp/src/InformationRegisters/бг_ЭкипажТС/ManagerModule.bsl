#Область ПрограммныйИнтерфейс

// Возвращает транспортые средства, в экипаж которых входит сотрудник
//
// Параметры:
//  Сотрудник - СправочникСсылка.ФизическиеЛица - сотрудник, по которорму осуществляется поиск ТС
//  РольСотрудника - Перечисление.бг_ЧленыЭкипажа, Массив, Неопределено - роль сотрудника в экипаже ТС, 
//  	если Неопределено, то все роли
//
// Возвращаемое значение:
//   Массив - СправочникСсылка.ТранспортныеСредства - ТС, в экипаже которых найден сотрудник 
//
Функция ТранспортныеСредстваСотрудника(Сотрудник, РольСотрудника = Неопределено) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	бг_ЭкипажТС.ТС КАК ТС
	|ИЗ
	|	РегистрСведений.бг_ЭкипажТС КАК бг_ЭкипажТС
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ОтборПоЧленуЭкипажа
	|				ТОГДА бг_ЭкипажТС.ЧленЭкипажа в(&ЧленЭкипажа)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И бг_ЭкипажТС.Сотрудник = &Сотрудник";
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Запрос.УстановитьПараметр("ОтборПоЧленуЭкипажа", ЗначениеЗаполнено(РольСотрудника));
	Если ТипЗнч(РольСотрудника) = Тип("Массив") Тогда
		Запрос.УстановитьПараметр("ЧленЭкипажа", РольСотрудника);
	Иначе
		Запрос.УстановитьПараметр("ЧленЭкипажа", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РольСотрудника));
	КонецЕсли;
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ТС");
КонецФункции

#КонецОбласти