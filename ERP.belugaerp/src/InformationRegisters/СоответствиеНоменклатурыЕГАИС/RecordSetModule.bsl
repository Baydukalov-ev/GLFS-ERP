
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

&После("ОбработкаПроверкиЗаполнения")
Процедура бг_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Отключаем проверку заполнения для роли
	Если Пользователи.РолиДоступны("бг_КонтролерЕГАИС") Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запись = Получить(0);
	
	// По номенклатуре и алкогольной продукции отработают типовые проверки на выдачу ошибки, дальнейшие проверки не нужны.
	Если Не ЗначениеЗаполнено(Запись.Номенклатура) Или Не ЗначениеЗаполнено(Запись.АлкогольнаяПродукция) Тогда
		Возврат;
	КонецЕсли;
	
	ВидНоменклатурыАлкогольнаяПродукция = бг_КонстантыПовтИсп.ЗначениеКонстанты("ВидНоменклатурыАлкогольнаяПродукция");
	ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.Номенклатура, "ВидНоменклатуры");
	Если ВидНоменклатуры <> ВидНоменклатурыАлкогольнаяПродукция Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Запись.Серия) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Не заполнена серия'"),,,, Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Запись.Справка2) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Не заполнена справка 2'"),,,, Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		// Если есть незаполненные основные поля, остальные проверки выполнять нет смысла.
		Возврат;
	КонецЕсли;
	
	ДанныеДляПроверки = бг_ДанныеДляПроверки(Запись);
	
	Если ДанныеДляПроверки.НоменклатураКрепость <> ДанныеДляПроверки.АлкогольнаяПродукцияКрепость Тогда
		ТекстОшибки = НСтр("ru='Крепость номенклатуры не соответствует крепости алкогольной продукции'"); 
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,,, Отказ);
	КонецЕсли;
	
	Если ДанныеДляПроверки.Справка1ДатаРозлива = Неопределено Тогда
		ТекстОшибки = НСтр("ru='Отсутствует справка1'"); 
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,,, Отказ);
	ИначеЕсли ДанныеДляПроверки.СерияДатаПроизводства <> ДанныеДляПроверки.Справка1ДатаРозлива Тогда
		ТекстОшибки = НСтр("ru='Дата производства серии не соответствует дате розлива справки 1'"); 
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,,, Отказ);
	КонецЕсли;
            
	Если ДанныеДляПроверки.ДругаяСправка2 <> Неопределено Тогда
		ТекстОшибки = СтрШаблон(
			НСтр("ru='Данная серия уже соответствует другой справке 2 ""%1""'"),
			ДанныеДляПроверки.ДругаяСправка2); 
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,,, Отказ);
	КонецЕсли;	
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция бг_ДанныеДляПроверки(Запись)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Крепость КАК НоменклатураКрепость
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка = &Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КлассификаторАлкогольнойПродукцииЕГАИС.Крепость КАК АлкогольнаяПродукцияКрепость
	|ИЗ
	|	Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
	|ГДЕ
	|	КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка = &АлкогольнаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СерииНоменклатуры.ДатаПроизводства КАК СерияДатаПроизводства
	|ИЗ
	|	Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|ГДЕ
	|	СерииНоменклатуры.Ссылка = &Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Справки1ЕГАИС.Ссылка КАК Справка1,
	|	Справки1ЕГАИС.ДатаРозлива КАК Справка1ДатаРозлива
	|ИЗ
	|	Справочник.Справки1ЕГАИС КАК Справки1ЕГАИС
	|ГДЕ
	|	Справки1ЕГАИС.Ссылка В
	|			(ВЫБРАТЬ
	|				Справки2ЕГАИС.Справка1 КАК Справка1
	|			ИЗ
	|				Справочник.Справки2ЕГАИС КАК Справки2ЕГАИС
	|			ГДЕ
	|				Справки2ЕГАИС.Ссылка = &Справка2)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоответствиеНоменклатурыЕГАИС.Серия КАК Серия,
	|	СоответствиеНоменклатурыЕГАИС.Справка2 КАК ДругаяСправка2
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|ГДЕ
	|	СоответствиеНоменклатурыЕГАИС.Серия = &Серия
	|	И СоответствиеНоменклатурыЕГАИС.Справка2 <> &Справка2
	|	И СоответствиеНоменклатурыЕГАИС.Справка2 <> ЗНАЧЕНИЕ(Справочник.Справки2ЕГАИС.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("Номенклатура", Запись.Номенклатура);
	Запрос.УстановитьПараметр("АлкогольнаяПродукция", Запись.АлкогольнаяПродукция);
	Запрос.УстановитьПараметр("Серия", Запись.Серия);
	Запрос.УстановитьПараметр("Справка2", Запись.Справка2);
	
	ПакетЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаНоменклатураКрепость = ПакетЗапроса[0].Выбрать();
	ВыборкаАлкогольнаяПродукцияКрепость = ПакетЗапроса[1].Выбрать();
	ВыборкаСерияДатаПроизводства = ПакетЗапроса[2].Выбрать();
	ВыборкаСправка1ДатаРозлива = ПакетЗапроса[3].Выбрать();
	ВыборкаДругаяСправка2 = ПакетЗапроса[4].Выбрать();
	
	ДанныеДляПроверки = Новый Структура("НоменклатураКрепость, АлкогольнаяПродукцияКрепость,
	| СерияДатаПроизводства, Справка1ДатаРозлива, ДругаяСправка2");
	
	Если ВыборкаНоменклатураКрепость.Следующий() Тогда
		ДанныеДляПроверки.НоменклатураКрепость = ВыборкаНоменклатураКрепость.НоменклатураКрепость;
	КонецЕсли;
	
	Если ВыборкаАлкогольнаяПродукцияКрепость.Следующий() Тогда
		ДанныеДляПроверки.АлкогольнаяПродукцияКрепость = ВыборкаАлкогольнаяПродукцияКрепость.АлкогольнаяПродукцияКрепость;
	КонецЕсли;
	
	Если ВыборкаСерияДатаПроизводства.Следующий() Тогда
		ДанныеДляПроверки.СерияДатаПроизводства = ВыборкаСерияДатаПроизводства.СерияДатаПроизводства;
	КонецЕсли;
	
	Если ВыборкаСправка1ДатаРозлива.Следующий() Тогда
		ДанныеДляПроверки.Справка1ДатаРозлива = ВыборкаСправка1ДатаРозлива.Справка1ДатаРозлива;
	КонецЕсли;
	
	Если ВыборкаДругаяСправка2.Следующий() Тогда
		ДанныеДляПроверки.ДругаяСправка2 = ВыборкаДругаяСправка2.ДругаяСправка2;
	КонецЕсли;
	
	Возврат ДанныеДляПроверки;
	
КонецФункции

#КонецОбласти

#КонецЕсли