#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

&После("ПередЗаписью")
Процедура бг_ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Отбор.Пользователь.Использование Тогда

		Запрос = бг_ЗапросИсторияИзмененийЗапретаРедактирования();
		Выборка = Запрос.Выполнить().Выбрать();
		
		РегистрыСведений.бг_ИсторияИзмененийЗапретаРедактирования.ЗаписатьДанные(Выборка);
		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция бг_ЗапросИсторияИзмененийЗапретаРедактирования()

	Пользователь = Отбор.Пользователь.Значение;
	Раздел = Отбор.Раздел.Значение;
	Объект = Отбор.Объект.Значение;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДатыЗапретаИзменения.Раздел КАК Раздел,
	|	ДатыЗапретаИзменения.Объект КАК Объект,
	|	ДатыЗапретаИзменения.Пользователь КАК Пользователь,
	|	ДатыЗапретаИзменения.ДатаЗапрета КАК ДатаЗапрета,
	|	ДатыЗапретаИзменения.ОписаниеДатыЗапрета КАК ОписаниеДатыЗапрета,
	|	ДатыЗапретаИзменения.Комментарий КАК Комментарий
	|ПОМЕСТИТЬ ДатыЗапретаИзменения
	|ИЗ
	|	&ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыЗапретаИзменения.Раздел КАК Раздел,
	|	ДатыЗапретаИзменения.Объект КАК Объект,
	|	ДатыЗапретаИзменения.Пользователь КАК ГруппаПользователей,
	|	СоставыГруппПользователей.Пользователь КАК Пользователь,
	|	ДатыЗапретаИзменения.ДатаЗапрета КАК ДатаЗапретаДо,
	|	ДатыЗапретаИзменения.ОписаниеДатыЗапрета КАК ОписаниеДатыЗапретаДо,
	|	НЕОПРЕДЕЛЕНО КАК ДатаЗапретаПосле,
	|	НЕОПРЕДЕЛЕНО КАК ОписаниеДатыЗапретаПосле,
	|	NULL КАК Комментарий
	|ПОМЕСТИТЬ ДатыЗапретаИзмененияДоПосле
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставыГруппПользователей КАК СоставыГруппПользователей
	|		ПО ДатыЗапретаИзменения.Пользователь = СоставыГруппПользователей.ГруппаПользователей
	|			И (СоставыГруппПользователей.Используется)
	|ГДЕ
	|	ДатыЗапретаИзменения.Пользователь = &Пользователь
	|	И ДатыЗапретаИзменения.Раздел = &Раздел
	|	И ДатыЗапретаИзменения.Объект = &Объект
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДатыЗапретаИзменения.Раздел,
	|	ДатыЗапретаИзменения.Объект,
	|	ДатыЗапретаИзменения.Пользователь,
	|	СоставыГруппПользователей.Пользователь,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ДатыЗапретаИзменения.ДатаЗапрета,
	|	ДатыЗапретаИзменения.ОписаниеДатыЗапрета,
	|	ДатыЗапретаИзменения.Комментарий
	|ИЗ
	|	ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставыГруппПользователей КАК СоставыГруппПользователей
	|		ПО ДатыЗапретаИзменения.Пользователь = СоставыГруппПользователей.ГруппаПользователей
	|			И (СоставыГруппПользователей.Используется)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыЗапретаИзменения.Раздел КАК Раздел,
	|	ДатыЗапретаИзменения.Объект КАК Объект,
	|	&Пользователь КАК Пользователь,
	|	ДатыЗапретаИзменения.Пользователь КАК ПользовательКонечный,
	|	МАКСИМУМ(ДатыЗапретаИзменения.ДатаЗапретаДо) КАК ДатаЗапретаДо,
	|	МАКСИМУМ(ДатыЗапретаИзменения.ОписаниеДатыЗапретаДо) КАК ОписаниеДатыЗапретаДо,
	|	МАКСИМУМ(ДатыЗапретаИзменения.ДатаЗапретаПосле) КАК ДатаЗапретаПосле,
	|	МАКСИМУМ(ДатыЗапретаИзменения.ОписаниеДатыЗапретаПосле) КАК ОписаниеДатыЗапретаПосле,
	|	МАКСИМУМ(ДатыЗапретаИзменения.Комментарий) КАК Комментарий,
	|	МАКСИМУМ(ДатыЗапретаИзменения.ГруппаПользователей) КАК ГруппаПользователей
	|ИЗ
	|	ДатыЗапретаИзмененияДоПосле КАК ДатыЗапретаИзменения
	|
	|СГРУППИРОВАТЬ ПО
	|	ДатыЗапретаИзменения.Раздел,
	|	ДатыЗапретаИзменения.Объект,
	|	ДатыЗапретаИзменения.Пользователь
	|
	|ИМЕЮЩИЕ
	|	(МАКСИМУМ(ДатыЗапретаИзменения.ДатаЗапретаДо) <> МАКСИМУМ(ДатыЗапретаИзменения.ДатаЗапретаПосле)
	|		ИЛИ МАКСИМУМ(ДатыЗапретаИзменения.ОписаниеДатыЗапретаДо) <> МАКСИМУМ(ДатыЗапретаИзменения.ОписаниеДатыЗапретаПосле))";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Если Отбор.Раздел.Использование Тогда
		Запрос.УстановитьПараметр("Раздел", Раздел);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ДатыЗапретаИзменения.Раздел = &Раздел", "");
	КонецЕсли;
	Если Отбор.Объект.Использование Тогда
		Запрос.УстановитьПараметр("Объект", Объект);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ДатыЗапретаИзменения.Объект = &Объект", "");
	КонецЕсли;
	Запрос.УстановитьПараметр("ДатыЗапретаИзменения", ЭтотОбъект.Выгрузить());

	Возврат Запрос;
	
КонецФункции

#КонецОбласти

#КонецЕсли
