
#Область ПрограммныйИнтерфейс

Функция ВариантПоУмолчанию() Экспорт
	
	Возврат ПоДатеДокумента;
	
КонецФункции

Функция ВариантОпределенияДатыДействия(ВхПараметрыЗапроса) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ВЫБОР ТИПЗНАЧЕНИЯ(Р.Получатель)
		|		КОГДА ТИП(Справочник.Партнеры) ТОГДА 1
		|		КОГДА ТИП(Справочник.Контрагенты) ТОГДА 2
		|		КОГДА ТИП(Справочник.ДоговорыКонтрагентов) ТОГДА 3
		|		КОГДА ТИП(Справочник.битПунктыНазначения) ТОГДА 4
		|	КОНЕЦ Приоритет,
		|	Получатель
		|ПОМЕСТИТЬ ВТ_ПриоритетыНастроек
		|ИЗ
		|	РегистрСведений.бг_НастройкиОпределенияДатыДействияСкидокНаценок Р
		|ГДЕ
		|	Р.Организация = &Организация
		|	И Р.Получатель В (&Партнер, &Контрагент, &Договор, &бг_ПунктНазначения)
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ
		|	0 Приоритет,
		|	НЕОПРЕДЕЛЕНО
		|ИЗ
		|	РегистрСведений.бг_НастройкиОпределенияДатыДействияСкидокНаценок Р
		|ГДЕ
		|	Р.Организация = &Организация
		|	И Р.Получатель = НЕОПРЕДЕЛЕНО
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Р.ВариантОпределенияДатыДействия
		|ИЗ
		|	РегистрСведений.бг_НастройкиОпределенияДатыДействияСкидокНаценок Р
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПриоритетыНастроек Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ МАКСИМУМ(Приоритет) ИЗ ВТ_ПриоритетыНастроек) М
		|		ПО Т.Приоритет = М.Приоритет
		|	ПО Р.Организация = &Организация
		|		И Р.Получатель = Т.Получатель"
	);
	
	ПараметрыЗапроса = Запрос.НайтиПараметры();
	Для каждого ПараметрЗапроса из ПараметрыЗапроса Цикл
		ЗначениеПараметра = Неопределено;
		Если ВхПараметрыЗапроса.Свойство(ПараметрЗапроса.Имя, ЗначениеПараметра) Тогда
			Запрос.УстановитьПараметр(ПараметрЗапроса.Имя, ЗначениеПараметра);
		КонецЕсли;
	КонецЦикла;
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат ВариантПоУмолчанию();
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка[0];
	КонецЕсли;
	
КонецФункции

#КонецОбласти
