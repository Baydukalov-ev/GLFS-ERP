
&Вместо("ТребуетсяОформление")
Функция бг_ТребуетсяОформление(ДокументОснование, Реквизиты, КоличествоСтрокДокументовОснования, ДополнительныеПараметры = Неопределено)
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПеремещениеТоваров") И Реквизиты.Проведен Тогда
		
		Возврат бг_ТребуетсяОформлениеДляПеремещенияТоваров(Реквизиты);
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") И Реквизиты.Проведен Тогда
		
		ЭтоХозОперацияПоИмпорту = (ДокументОснование.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту
			Или ДокументОснование.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС);
				
		Если КоличествоСтрокДокументовОснования[ДокументОснование] = 0 Тогда
			Возврат Ложь;
		ИначеЕсли ТипЗнч(Реквизиты.Документ) = Тип("ДокументСсылка.битОтчетОбИмпортеПродукцииЕГАИС") Тогда
			Возврат ЭтоХозОперацияПоИмпорту;
		ИначеЕсли ТипЗнч(Реквизиты.Документ) = Тип("ДокументСсылка.ТТНВходящаяЕГАИС") Тогда
			Возврат НЕ ЭтоХозОперацияПоИмпорту;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПрочееОприходованиеТоваров") И Реквизиты.Проведен Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Результат = ПродолжитьВызов(ДокументОснование, Реквизиты, КоличествоСтрокДокументовОснования, ДополнительныеПараметры);
	
	Возврат Результат;
	
КонецФункции

&Вместо("ОпределитьСтатусыОформленияДокументов")
Функция бг_ОпределитьСтатусыОформленияДокументов(МассивДокументов, МетаданныеДокументаЕГАИС, МенеджерВТ)
	
	Результат = ПродолжитьВызов(МассивДокументов, МетаданныеДокументаЕГАИС, МенеджерВТ);
	
	Если  МетаданныеДокументаЕГАИС = Метаданные.Документы.битОтчетОбИмпортеПродукцииЕГАИС Тогда
		Для Каждого Элемент Из Результат Цикл
			Если Элемент.Значение.СтатусОформления = Перечисления.СтатусыОформленияДокументовЕГАИС.ОформленоЧастично Тогда
				Элемент.Значение.СтатусОформления = Перечисления.СтатусыОформленияДокументовЕГАИС.Оформлено;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция бг_ТребуетсяОформлениеДляПеремещенияТоваров(Реквизиты)
	
	РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Реквизиты.ДокументОснование, "Организация, СкладОтправитель, СкладПолучатель");
	
	ОрганизацияЕГАИСОтправитель = Справочники.КлассификаторОрганизацийЕГАИС.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(
		РеквизитыОснования.Организация,
		РеквизитыОснования.СкладОтправитель,
		Истина, // СоответствуетОрганизации
		Ложь);  // ТолькоСопоставленные
	
	ОрганизацияЕГАИСПолучатель = Справочники.КлассификаторОрганизацийЕГАИС.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(
		РеквизитыОснования.Организация,
		РеквизитыОснования.СкладПолучатель,
		Истина, // СоответствуетОрганизации
		Ложь);  // ТолькоСопоставленные
	
	Если ЗначениеЗаполнено(ОрганизацияЕГАИСПолучатель) И ЗначениеЗаполнено(ОрганизацияЕГАИСОтправитель) Тогда
		Возврат Истина
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции
