#Область ПрограммныйИнтерфейс

Процедура ЗаписатьЗагружаемыйОбъект(ЗагружаемыйОбъект, СтандартнаяОбработка) Экспорт

	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(ЗагружаемыйОбъект.Ссылка) И ЗагружаемыйОбъект.Проведен Тогда
		ЗагружаемыйОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
	КонецЕсли;
		                                                                
	ЗагружаемыйОбъект.Проведен = Ложь;
	ЗагружаемыйОбъект.Записать(РежимЗаписиДокумента.Запись);
	
	Если бг_КонстантыПовтИсп.ЗначениеКонстанты("ПроводитьДокументыОтWMSОтложенно") Тогда
		РегистрыСведений.бг_ОбъектыДляОтложеннойОбработки.ДобавитьОбъект(
			ЗагружаемыйОбъект.Ссылка,
			Перечисления.бг_ВариантыОтложеннойОбработкиОбъектов.ОбработатьДокументыWMS);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьЗагружаемыйОбъект(ЗагружаемыйОбъект, СтруктураОбъекта, СписокСвойств = Неопределено, ИсключаяСвойства = Неопределено, СтандартнаяОбработка) Экспорт
	
	Перем адаптер_ОбработчикиСобытийСтандартный;
	адаптер_ОбработчикиСобытийСтандартный = ОбщегоНазначения.ОбщийМодуль("адаптер_ОбработчикиСобытийСтандартный");
	
	СтандартнаяОбработка = Ложь;
	адаптер_ОбработчикиСобытийСтандартный.ЗаполнитьЗагружаемыйОбъект(ЗагружаемыйОбъект, СтруктураОбъекта,, "Номер");
	
КонецПроцедуры

Процедура ПослеЗаписиДанныхОбъектаВИБ(ЗагружаемыйОбъект, РезультатОбработки) Экспорт

	Если Не РезультатОбработки.Успешно Тогда
		Возврат;
	КонецЕсли;
	
	Если бг_КонстантыПовтИсп.ЗначениеКонстанты("ПроводитьДокументыОтWMSОтложенно") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ЗагружаемыйОбъект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатОбработки.Успешно Тогда
		
		Попытка
			
			ЗагружаемыйОбъект.Заблокировать();
			ЗагружаемыйОбъект.Записать(РежимЗаписиДокумента.Проведение);
			
		Исключение
			
			ШапкаОшибки = ОписаниеОшибки();
			
			ТелоОшибки = "";
			Для каждого СообщениеПользователю Из ПолучитьСообщенияПользователю(Истина) Цикл
				ТелоОшибки = ТелоОшибки + ?(ПустаяСтрока(ТелоОшибки), "", Символы.ПС) + СообщениеПользователю.Текст;
			КонецЦикла;
			
			ТекстОшибки = ШапкаОшибки + Символы.ПС + ТелоОшибки;
			
			РегистрыСведений.бг_ДокументыИнтеграцииСоСкладскимиСистемами.ОбновитьСтатусДокумента(
				ЗагружаемыйОбъект.Ссылка,
				ТекущаяДатаСеанса(),
				Перечисления.бг_СтатусыДокументовИнтеграцииСоСкладскимиСистемами.ЗагруженБезОбработки,
				0,
				ТекстОшибки);
			
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // Конец ПрограммныйИнтерфейс
