#Область ПрограммныйИнтерфейс

#Область Паллетизация_РаботаСДокументами

// Возвращает признак необходимости отображения реквизитов паллетизации (в товарах и в подвале) на форме документа.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения.
//
Функция ОтображатьПаллетизацию(Форма) Экспорт

	ЕстьРеквизитОтображатьПаллетизацию = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(
		Форма,
		"бг_ОтображатьПаллетизацию");
	
	Если ЕстьРеквизитОтображатьПаллетизацию Тогда
		Возврат Форма.бг_ОтображатьПаллетизацию;
	Иначе
		Возврат Истина;
	КонецЕсли;

КонецФункции

// Очищает показатели паллетизации в строке товаров на форме документа.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения.
//  СтрокаТовары - Строка табличной части Товары.
//
Процедура ОчиститьПоказателиПаллетизацииВСтрокеТовары(Форма, СтрокаТовары) Экспорт

	СтрокаТовары.бг_УпаковкаПаллета = Неопределено;
	СтрокаТовары.бг_ТипЕдиницыИзмерения = Неопределено;
	
	СтрокаТовары.бг_КоличествоКоробок = 0;
	СтрокаТовары.бг_КоличествоПаллет = 0;
	СтрокаТовары.бг_КоэффициентКоробки = 0;
	СтрокаТовары.бг_КоэффициентПаллеты = 0;
	
	СтрокаТовары.бг_ВесУпаковки = 0;
	СтрокаТовары.бг_ОбъемУпаковки = 0;
	СтрокаТовары.бг_ОбъемДАЛЕд = 0;
	СтрокаТовары.бг_ВесУпаковкиБрутто = 0;
	СтрокаТовары.бг_ОбъемУпаковкиБрутто = 0;
	СтрокаТовары.бг_ОбъемДАЛ = 0;
	
КонецПроцедуры

// Рассчитывает показатели паллетизации в строке товаров на форме документа.
//
// Параметры:
//  СтрокаТовары - Строка табличной части Товары.
//
Процедура РассчитатьПоказателиПаллетизацииВСтрокеТовары(СтрокаТовары) Экспорт
	
	РассчитатьКоличествоКоробокВСтрокеТовары(СтрокаТовары);
	РассчитатьКоличествоПаллетВСтрокеТовары(СтрокаТовары);
	РассчитатьОбъемВДекалитрахВСтрокеТовары(СтрокаТовары);
	
КонецПроцедуры

// Рассчитывает количество упаковок и показатели паллетизации в строке товаров на форме документа.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения.
//  СтрокаТовары - Строка табличной части Товары.
//
Процедура РассчитатьКоличествоУпаковокИПаллетизациюВСтрокеТовары(Форма, СтрокаТовары) Экспорт
	
	Если СтрокаТовары.бг_ТипЕдиницыИзмерения = Форма.бг_ТипЕдиницыИзмеренияПаллета Тогда
		
		СтрокаТовары.КоличествоУпаковок = СтрокаТовары.бг_КоличествоПаллет * СтрокаТовары.бг_КоэффициентПаллеты;
		
		РассчитатьКоличествоКоробокВСтрокеТовары(СтрокаТовары);
		
	ИначеЕсли СтрокаТовары.бг_ТипЕдиницыИзмерения = Форма.бг_ТипЕдиницыИзмеренияКоробка Тогда
		
		СтрокаТовары.КоличествоУпаковок = СтрокаТовары.бг_КоличествоКоробок * СтрокаТовары.бг_КоэффициентКоробки;
		
		РассчитатьКоличествоПаллетВСтрокеТовары(СтрокаТовары);
		
	Иначе
		// NOP
	КонецЕсли;
	
КонецПроцедуры

// Рассчитывает итоговые показатели паллетизации документа.
//
// Параметры:
//  КонтекстЗаполнения - Форма, Структура.
//   Необходимые свойства:
//    Объект - ДокументОбъект или ДанныеФормыСтруктура.
//    бг_ТипЕдиницыИзмеренияКоробка - ПеречислениеСсылка.бг_ТипыЕдиницИзмерения.
//    бг_ТипЕдиницыИзмеренияПаллета - ПеречислениеСсылка.бг_ТипыЕдиницИзмерения.
//
Процедура РассчитатьИтоговыеПоказателиПаллетизации(КонтекстЗаполнения, Товары = Неопределено) Экспорт
	
	Объект = КонтекстЗаполнения.Объект;
	
	Объект.бг_КоличествоБутылок = 0;
	Объект.бг_КоличествоПаллет = 0;
	Объект.бг_КоличествоКоробок = 0;
	Объект.бг_Вес = 0;
	Объект.бг_ВесБрутто = 0;
	Объект.бг_Объем = 0;
	Объект.бг_ОбъемБрутто = 0;
	Объект.бг_ОбъемДАЛ = 0;
	
	КоллекцияТовары = ?(Товары = Неопределено, КонтекстЗаполнения.Объект.Товары, Товары);
	
	Если КоллекцияТовары.Количество() > 0 Тогда
		ЕстьРеквизитОтменено = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(
			КоллекцияТовары[0],
			"Отменено");
	Иначе
		ЕстьРеквизитОтменено = Ложь;
	КонецЕсли;
	
	Для каждого СтрокаТовары Из КоллекцияТовары Цикл
		
		Если ЕстьРеквизитОтменено И СтрокаТовары.Отменено Тогда
			Продолжить;
		КонецЕсли;
		
		ТипЕдиницыИзмерения = СтрокаТовары.бг_ТипЕдиницыИзмерения;
		
		Если ЗначениеЗаполнено(ТипЕдиницыИзмерения) Тогда
			Объект.бг_КоличествоБутылок = Объект.бг_КоличествоБутылок + СтрокаТовары.Количество;
		КонецЕсли;
		
		Объект.бг_КоличествоПаллет = Объект.бг_КоличествоПаллет + СтрокаТовары.бг_КоличествоПаллет;
		Объект.бг_КоличествоКоробок = Объект.бг_КоличествоКоробок + СтрокаТовары.бг_КоличествоКоробок;
		
		Объект.бг_Вес = Объект.бг_Вес + СтрокаТовары.бг_ВесУпаковки * СтрокаТовары.Количество;
		Объект.бг_Объем = Объект.бг_Объем + СтрокаТовары.бг_ОбъемУпаковки * СтрокаТовары.Количество;
		
		Если ТипЕдиницыИзмерения = КонтекстЗаполнения.бг_ТипЕдиницыИзмеренияПаллета Тогда
			КоличествоУпаковок = СтрокаТовары.бг_КоличествоПаллет;
		ИначеЕсли ТипЕдиницыИзмерения = КонтекстЗаполнения.бг_ТипЕдиницыИзмеренияКоробка Тогда
			КоличествоУпаковок = СтрокаТовары.бг_КоличествоКоробок;
		Иначе
			КоличествоУпаковок = 0;
		КонецЕсли;
		
		Объект.бг_ВесБрутто = Объект.бг_ВесБрутто + СтрокаТовары.бг_ВесУпаковкиБрутто * КоличествоУпаковок;
		Объект.бг_ОбъемБрутто = Объект.бг_ОбъемБрутто + СтрокаТовары.бг_ОбъемУпаковкиБрутто * КоличествоУпаковок;
		
		Объект.бг_ОбъемДАЛ = Объект.бг_ОбъемДАЛ + СтрокаТовары.бг_ОбъемДАЛ;
		
	КонецЦикла;
	
КонецПроцедуры

// Сообщает пользователю (вызывает исключение - нужно для автотестов именно так) ошибку по выбранной паллете.
//
Процедура СообщитьОНеверноВыбраннойУпаковкеПаллете() Экспорт

	ВызватьИсключение НСтр("ru = 'Выберите коробку или паллету'");

КонецПроцедуры

#КонецОбласти // Конец Паллетизация_РаботаСДокументами

#КонецОбласти // Конец ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

#Область Паллетизация_РаботаСДокументами

Процедура РассчитатьКоличествоКоробокВСтрокеТовары(СтрокаТовары)

	Если СтрокаТовары.бг_КоэффициентКоробки > 0 Тогда
		СтрокаТовары.бг_КоличествоКоробок = СтрокаТовары.КоличествоУпаковок / СтрокаТовары.бг_КоэффициентКоробки;
	Иначе
		СтрокаТовары.бг_КоличествоКоробок = 0;
	КонецЕсли;

КонецПроцедуры

Процедура РассчитатьКоличествоПаллетВСтрокеТовары(СтрокаТовары)

	Если СтрокаТовары.бг_КоэффициентПаллеты > 0 Тогда
		СтрокаТовары.бг_КоличествоПаллет = СтрокаТовары.КоличествоУпаковок / СтрокаТовары.бг_КоэффициентПаллеты;
	Иначе
		СтрокаТовары.бг_КоличествоПаллет = 0;
	КонецЕсли;

КонецПроцедуры

Процедура РассчитатьОбъемВДекалитрахВСтрокеТовары(СтрокаТовары)
	
	СтрокаТовары.бг_ОбъемДАЛ = СтрокаТовары.бг_ОбъемДАЛЕд * СтрокаТовары.Количество;
	
КонецПроцедуры

#КонецОбласти // Конец Паллетизация_РаботаСДокументами

#КонецОбласти // Конец СлужебныеПроцедурыИФункции
