#Область ПрограммныйИнтерфейс

// Процедура - Запускает изменение статусов у элементов справочников:
//	1. СертификатыНоменклатуры - по окончанию срока действия меняется статус на "Недействующий"
//	2. ДоговорыКонтрагентов - по окончанию срока действия меняется статус на "Закрыт"
//
Процедура ЗакрытьДоговораДекларацииСоответствия() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	ДанныеДляЗакрытия = ДанныеДляЗакрытия();
	
	ЗакрытьИстекшиеДоговораКонтрагентов(ДанныеДляЗакрытия.ДоговорыКонтрагентов);
	ЗакрытьИстекшиеДекларацииСоответствия(ДанныеДляЗакрытия.ДекларацииСоответствия);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляЗакрытия()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК ДоговорКонтрагента
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ДатаОкончанияДействия <> ДАТАВРЕМЯ(1, 1, 1)
	|	И &ТекущаяДата > КОНЕЦПЕРИОДА(ДоговорыКонтрагентов.ДатаОкончанияДействия, ДЕНЬ)
	|	И ДоговорыКонтрагентов.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Закрыт)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДоговорыКонтрагентов.ДатаОкончанияДействия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СертификатыНоменклатуры.Ссылка КАК ДекларацияСоответствия
	|ИЗ
	|	Справочник.СертификатыНоменклатуры КАК СертификатыНоменклатуры
	|ГДЕ
	|	СертификатыНоменклатуры.ДатаОкончанияСрокаДействия <> ДАТАВРЕМЯ(1, 1, 1)
	|	И &ТекущаяДата > КОНЕЦПЕРИОДА(СертификатыНоменклатуры.ДатаОкончанияСрокаДействия, ДЕНЬ)
	|	И СертификатыНоменклатуры.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыСертификатовНоменклатуры.Недействующий)
	|	И СертификатыНоменклатуры.ТипСертификата = &ТипСертификата
	|	И НЕ СертификатыНоменклатуры.Бессрочный
	|
	|УПОРЯДОЧИТЬ ПО
	|	СертификатыНоменклатуры.ДатаОкончанияСрокаДействия";
	
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ТипСертификата", "Декларация о соответствии");
	
	Результаты = Запрос.ВыполнитьПакет();
	
	ДанныеДляЗакрытия = Новый Структура;
	
	ДанныеДляЗакрытия.Вставить(
		"ДоговорыКонтрагентов", 
		Результаты[0].Выгрузить().ВыгрузитьКолонку("ДоговорКонтрагента"));
		
	ДанныеДляЗакрытия.Вставить(
		"ДекларацииСоответствия", 
		Результаты[1].Выгрузить().ВыгрузитьКолонку("ДекларацияСоответствия"));

	Возврат ДанныеДляЗакрытия;
	
КонецФункции

Процедура ЗакрытьИстекшиеДоговораКонтрагентов(ДоговорыКЗакрытию)
	
	ШаблонОшибки = НСтр("ru='Закрытие договоров контрагентов: %ОписаниеОшибки%'");
	
	Для Каждого ДоговорКонтрагента Из ДоговорыКЗакрытию Цикл
		
		Попытка
			ДоговорКонтрагентаОбъект = ДоговорКонтрагента.ПолучитьОбъект();
			ДоговорКонтрагентаОбъект.Статус = Перечисления.СтатусыДоговоровКонтрагентов.Закрыт;
		
			ДоговорКонтрагентаОбъект.Записать();
		Исключение
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,
				ДоговорКонтрагента,
				СтрЗаменить(ШаблонОшибки, "%ОписаниеОшибки%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
				
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры 

Процедура ЗакрытьИстекшиеДекларацииСоответствия(ДекларацииКЗакрытию)
	
	ШаблонОшибки = НСтр("ru='Закрытие деклараций о соответствии: %ОписаниеОшибки%'");
	
	Для Каждого ДекларацияСоответствия Из ДекларацииКЗакрытию Цикл
		
		Попытка
			ДекларацияСоответствияОбъект = ДекларацияСоответствия.ПолучитьОбъект();
			ДекларацияСоответствияОбъект.Статус = Перечисления.СтатусыСертификатовНоменклатуры.Недействующий;
			
			ДекларацияСоответствияОбъект.Записать();
		Исключение
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,
				ДекларацияСоответствия,
				СтрЗаменить(ШаблонОшибки, "%ОписаниеОшибки%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти