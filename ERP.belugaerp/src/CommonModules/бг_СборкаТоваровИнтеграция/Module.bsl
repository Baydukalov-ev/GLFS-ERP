
#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	адаптер_НастройкиОбмена.ОставитьРеквизиты(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		ВыгружаемыеРеквизиты());
		
	ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства);
	ДобавитьКлючевыеПоляКВыгрузке(РеквизитыИСвойства);
		
КонецПроцедуры

Процедура ЗаполнитьТекстыЗапросовУсловиями(ТекстЗапроса, ТекстЗапросаТаблицаКлючей, ПараметрыЗапроса, НастройкаВыгрузки, Объект, СтандартнаяОбработка) Экспорт

	ТекстЗапроса = бг_ОбщегоНазначенияСервер.ТекстЗапросаБезОбращенияЧерезТочкуКNull(ТекстЗапроса);
	ТекстЗапросаТаблицаКлючей = бг_ОбщегоНазначенияСервер.ТекстЗапросаБезОбращенияЧерезТочкуКNull(ТекстЗапросаТаблицаКлючей);
	
КонецПроцедуры

Функция ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения) Экспорт
	
	Перем адаптер_РаботаСДаннымиИБ;
	адаптер_РаботаСДаннымиИБ = ОбщегоНазначения.ОбщийМодуль("адаптер_РаботаСДаннымиИБ");
	
	ПараметрыВыполненияЗапросов = адаптер_РаботаСДаннымиИБ.ПолучитьПараметрыВыполненияЗапросов(Объект, ДанныеСообщения);	
	
	ДанныеВыгружаемогоОбъекта = Новый Структура;
	ДанныеВыгружаемогоОбъекта.Вставить("ПолноеИмя", ПараметрыВыполненияЗапросов.ПолноеИмя);
	ДанныеВыгружаемогоОбъекта.Вставить("Реквизиты", Новый Массив);
	
	ВыгружаемыеДанные = ВыгружаемыеДанные(Объект);
	
	Если ВыгружаемыеДанные.РезультатПоШапке.Пустой() Тогда
		Возврат ДанныеВыгружаемогоОбъекта;	
	КонецЕсли;
	
	Реквизиты = адаптер_РаботаСДаннымиИБ.РезультатЗапросаВСтруктуруРеквизитов(
		ВыгружаемыеДанные.РезультатПоШапке,
		ПараметрыВыполненияЗапросов.ТаблицаКлючей,
		ДанныеСообщения);
		
	Если Реквизиты.Количество() > 0 Тогда
			
		Товары = адаптер_РаботаСДаннымиИБ.РезультатЗапросаВСтруктуруРеквизитов(
			ВыгружаемыеДанные.РезультатПоТоварам,
			ПараметрыВыполненияЗапросов.ТаблицаКлючей,
			ДанныеСообщения);
			
		Реквизиты[0].Вставить("Товары", Товары);	
	КонецЕсли;
	
	ДанныеВыгружаемогоОбъекта.Вставить("Реквизиты", Реквизиты);
	
	Возврат ДанныеВыгружаемогоОбъекта;
	
КонецФункции

#КонецОбласти // Конец ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ВыгружаемыеРеквизиты()

	ВыгружаемыеРеквизиты = Новый Массив;
	
	// Шапка
	ВыгружаемыеРеквизиты.Добавить("Номер");
	ВыгружаемыеРеквизиты.Добавить("Дата");
	ВыгружаемыеРеквизиты.Добавить("ПометкаУдаления");
	ВыгружаемыеРеквизиты.Добавить("Проведен");
	ВыгружаемыеРеквизиты.Добавить("Организация");
	ВыгружаемыеРеквизиты.Добавить("Склад");
	ВыгружаемыеРеквизиты.Добавить("Статус");
	ВыгружаемыеРеквизиты.Добавить("ХозяйственнаяОперация");
	ВыгружаемыеРеквизиты.Добавить("Номенклатура");
	ВыгружаемыеРеквизиты.Добавить("Серия");
	ВыгружаемыеРеквизиты.Добавить("Упаковка");
	ВыгружаемыеРеквизиты.Добавить("Количество");
	ВыгружаемыеРеквизиты.Добавить("Комментарий");
	ВыгружаемыеРеквизиты.Добавить("Ответственный");
	ВыгружаемыеРеквизиты.Добавить("Подразделение");
	
	// Товары
	ВыгружаемыеРеквизиты.Добавить("Товары.Номенклатура");
	ВыгружаемыеРеквизиты.Добавить("Товары.Серия");
	ВыгружаемыеРеквизиты.Добавить("Товары.Упаковка");
	ВыгружаемыеРеквизиты.Добавить("Товары.Количество");
	
	ВыгружаемыеРеквизиты = СтрСоединить(ВыгружаемыеРеквизиты, ",");
	
	Возврат ВыгружаемыеРеквизиты;

КонецФункции

Процедура ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства)
	
    Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	// Реквизиты шапки
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		, // ИмяРеквизита
		"Представление",
		Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(100)));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		, // ИмяРеквизита
		"ПунктРазгрузки",
		Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		, // ИмяРеквизита
		"КодКатегорииСкладаSolvo",
		ОбщегоНазначения.ОписаниеТипаСтрока(2));

	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		, // ИмяРеквизита
		"ОрганизацияСклада",
		Новый ОписаниеТипов("СправочникСсылка.Организации"));
		
	// Реквизиты ТЧ "Товары"
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		, // ИмяРеквизита
		"Товары.НомерСтроки",
		ОбщегоНазначения.ОписаниеТипаЧисло(15));

КонецПроцедуры

Процедура ДобавитьКлючевыеПоляКВыгрузке(РеквизитыИСвойства)

	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	// Описанные ниже реквизиты не являются ключевыми, по ним не производится поиск в приемнике.
	// Но приемниками этого сообщения являются WMS системы, которым очевиднее видеть свойства данных реквзитов
	// внутри вложенной структуры, а не в корне сообщения.
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.СерииНоменклатуры,
		"Наименование");

	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.СерииНоменклатуры,
		"КодВидаМарки");
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Номенклатура,
		"Код");
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Номенклатура,
		"КодЕКНоменклатуры");
						
КонецПроцедуры

Функция ВыгружаемыеДанные(Ссылка)

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаВыгружаемыхДанных();
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	ВыгружаемыеДанные = Новый Структура;
	ВыгружаемыеДанные.Вставить("РезультатПоШапке", РезультатыЗапросов[2]);
	ВыгружаемыеДанные.Вставить("РезультатПоТоварам", РезультатыЗапросов[3]);
	
	Возврат ВыгружаемыеДанные;

КонецФункции

Функция ТекстЗапросаВыгружаемыхДанных()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СборкаТоваровТовары.Номенклатура КАК Номенклатура,
	|	СборкаТоваровТовары.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ СборкаТоваров
	|ИЗ
	|	Документ.СборкаТоваров.Товары КАК СборкаТоваровТовары
	|ГДЕ
	|	СборкаТоваровТовары.Ссылка = &Ссылка
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ
	|	СборкаТоваров.Номенклатура,
	|	СборкаТоваров.Ссылка
	|ИЗ
	|	Документ.СборкаТоваров КАК СборкаТоваров
	|ГДЕ
	|	СборкаТоваров.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СборкаТоваров.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(УпаковкиЕдиницыИзмерения.Ссылка) КАК БазоваяУпаковка,
	|	СборкаТоваров.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ БазовыеУпаковки
	|ИЗ
	|	СборкаТоваров КАК СборкаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
	|		ПО (СборкаТоваров.Ссылка = &Ссылка)
	|			И СборкаТоваров.Номенклатура = УпаковкиЕдиницыИзмерения.Владелец
	|			И СборкаТоваров.Номенклатура.ЕдиницаИзмерения = УпаковкиЕдиницыИзмерения.ЕдиницаИзмерения
	|			И (НЕ УпаковкиЕдиницыИзмерения.ПометкаУдаления)
	|			И (УпаковкиЕдиницыИзмерения.Числитель = 1)
	|			И (УпаковкиЕдиницыИзмерения.Знаменатель = 1)
	|СГРУППИРОВАТЬ ПО
	|	СборкаТоваров.Номенклатура,
	|	СборкаТоваров.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СборкаТоваров.Ссылка КАК Идентификатор,
	|	СборкаТоваров.Номер КАК Номер,
	|	СборкаТоваров.Дата КАК Дата,
	|	СборкаТоваров.ПометкаУдаления КАК ПометкаУдаления,
	|	СборкаТоваров.Проведен КАК Проведен,
	|	СборкаТоваров.Организация КАК Организация_ЗначениеРеквизитаИдентификатор,
	|	СборкаТоваров.Организация КАК Организация_ЗначениеРеквизитаТаблицаКлючей,
	|	СборкаТоваров.Склад КАК Склад_ЗначениеРеквизитаИдентификатор,
	|	СборкаТоваров.Склад КАК Склад_ЗначениеРеквизитаТаблицаКлючей,
	|	СборкаТоваров.Склад.бг_Организация КАК ОрганизацияСклада_ЗначениеРеквизитаИдентификатор,
	|	СборкаТоваров.Склад.бг_КодКатегорииСкладаSolvo КАК КодКатегорииСкладаSolvo,
	|	СборкаТоваров.Склад.бг_ПунктРазгрузки КАК ПунктРазгрузки_ЗначениеРеквизитаИдентификатор,
	|	СборкаТоваров.Статус КАК Статус_ЗначениеРеквизитаЗначениеПеречисления,
	|	СборкаТоваров.ХозяйственнаяОперация КАК ХозяйственнаяОперация_ЗначениеРеквизитаЗначениеПеречисления,
	|	СборкаТоваров.Номенклатура КАК Номенклатура_ЗначениеРеквизитаИдентификатор,
	|	СборкаТоваров.Номенклатура КАК Номенклатура_ЗначениеРеквизитаТаблицаКлючей,
	|	СборкаТоваров.Номенклатура.Код КАК Номенклатура_ЗначениеРеквизитаКод,
	|	ЕСТЬNULL(СборкаТоваров.Серия.бг_УпаковкаПаллета.бг_КодЕК_Номенклатуры, """") КАК Номенклатура_ЗначениеРеквизитаКодЕКНоменклатуры,
	|	СборкаТоваров.Серия КАК Серия_ЗначениеРеквизитаТаблицаКлючей,
	|	СборкаТоваров.Серия КАК Серия_ЗначениеРеквизитаИдентификатор,
	|	СборкаТоваров.Серия.Наименование КАК Серия_ЗначениеРеквизитаНаименование,
	|	СборкаТоваров.Серия.бг_ВидМарки.Код КАК Серия_ЗначениеРеквизитаКодВидаМарки,
	|	ЕСТЬNULL(БазовыеУпаковки.БазоваяУпаковка, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК Упаковка_ЗначениеРеквизитаИдентификатор,
	|	ЕСТЬNULL(БазовыеУпаковки.БазоваяУпаковка, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК Упаковка_ЗначениеРеквизитаТаблицаКлючей,
	|	СборкаТоваров.Количество КАК Количество,
	|	СборкаТоваров.Представление КАК Представление,
	|	СборкаТоваров.Комментарий КАК Комментарий,
	|	СборкаТоваров.Ответственный КАК Ответственный_ЗначениеРеквизитаИдентификатор,
	|	СборкаТоваров.Ответственный КАК Ответственный_ЗначениеРеквизитаТаблицаКлючей,
	|	СборкаТоваров.Подразделение КАК Подразделение_ЗначениеРеквизитаИдентификатор,
	|	СборкаТоваров.Подразделение КАК Подразделение_ЗначениеРеквизитаТаблицаКлючей
	|ИЗ
	|	Документ.СборкаТоваров КАК СборкаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ БазовыеУпаковки КАК БазовыеУпаковки
	|		ПО СборкаТоваров.Номенклатура = БазовыеУпаковки.Номенклатура
	|ГДЕ
	|	СборкаТоваров.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СборкаТоваровТовары.НомерСтроки КАК НомерСтроки,
	|	СборкаТоваровТовары.Номенклатура КАК Номенклатура_ЗначениеРеквизитаИдентификатор,
	|	СборкаТоваровТовары.Номенклатура КАК Номенклатура_ЗначениеРеквизитаТаблицаКлючей,
	|	СборкаТоваровТовары.Номенклатура.Код КАК Номенклатура_ЗначениеРеквизитаКод,
	|	ЕСТЬNULL(СборкаТоваровТовары.Серия.бг_УпаковкаПаллета.бг_КодЕК_Номенклатуры, """") КАК Номенклатура_ЗначениеРеквизитаКодЕКНоменклатуры,
	|	СборкаТоваровТовары.Серия КАК Серия_ЗначениеРеквизитаИдентификатор,
	|	СборкаТоваровТовары.Серия КАК Серия_ЗначениеРеквизитаТаблицаКлючей,
	|	СборкаТоваровТовары.Серия.Наименование КАК Серия_ЗначениеРеквизитаНаименование,
	|	СборкаТоваровТовары.Серия.бг_ВидМарки.Код КАК Серия_ЗначениеРеквизитаКодВидаМарки,
	|	ЕСТЬNULL(БазовыеУпаковки.БазоваяУпаковка, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК Упаковка_ЗначениеРеквизитаИдентификатор,
	|	ЕСТЬNULL(БазовыеУпаковки.БазоваяУпаковка, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК Упаковка_ЗначениеРеквизитаТаблицаКлючей,
	|	СборкаТоваровТовары.Количество КАК Количество
	|ИЗ
	|	Документ.СборкаТоваров.Товары КАК СборкаТоваровТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ БазовыеУпаковки КАК БазовыеУпаковки
	|		ПО СборкаТоваровТовары.Номенклатура = БазовыеУпаковки.Номенклатура
	|ГДЕ
	|	СборкаТоваровТовары.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти // Конец СлужебныеПроцедурыИФункции
