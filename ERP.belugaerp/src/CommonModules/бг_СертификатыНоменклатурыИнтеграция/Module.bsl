#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	адаптер_НастройкиОбмена.ОставитьРеквизиты(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		ВыгружаемыеРеквизиты());
		
	ДобавитьКлючевыеПоляКВыгрузке(РеквизитыИСвойства);	
	
КонецПроцедуры

Процедура ЗаполнитьТекстыЗапросовУсловиями(ТекстЗапроса, ТекстЗапросаТаблицаКлючей, ПараметрыЗапроса, НастройкаВыгрузки, Объект, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ПараметрыЗапроса.Вставить("ВидДокументаДекларацияСоответствия", бг_КонстантыПовтИсп.ЗначениеКонстанты("ВидДокументаДекларацияСоответствия"));
	ТекстПоиска = "
		|ИЗ
		|	Справочник.СертификатыНоменклатуры КАК ВыгружаемыйОбъект";
	ТекстЗамены = "
		|ИЗ
		|	Справочник.СертификатыНоменклатуры КАК ВыгружаемыйОбъект
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.бг_СопроводительныеДокументы КАК ДекларацияСоответствия
		|		ПО ВыгружаемыйОбъект.Ссылка = ДекларацияСоответствия.Владелец
		|			И ДекларацияСоответствия.Вид = &ВидДокументаДекларацияСоответствия";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстПоиска, ТекстЗамены);

	ТекстПоиска = "&УсловиеЗапроса";
	ТекстЗамены = "ВыгружаемыйОбъект.Ссылка = &Ссылка
	|	И &УсловиеЗапроса";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстПоиска, ТекстЗамены);
		
	ТекстПоиска = "NULL КАК бг_АдресФайлаСертификата";
	ТекстЗамены = "ЕСТЬNULL(ДекларацияСоответствия.ПутьКФайлу, """") КАК бг_АдресФайлаСертификата";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстПоиска, ТекстЗамены);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВыгружаемыеРеквизиты()
	
	ВыгружаемыеРеквизиты = Новый Массив;
	
	ВыгружаемыеРеквизиты.Добавить("Бессрочный");	
	ВыгружаемыеРеквизиты.Добавить("ДатаНачалаСрокаДействия");
	ВыгружаемыеРеквизиты.Добавить("ДатаОкончанияСрокаДействия");
	ВыгружаемыеРеквизиты.Добавить("Наименование");
	ВыгружаемыеРеквизиты.Добавить("Номер");
	ВыгружаемыеРеквизиты.Добавить("ТипСертификата");
	ВыгружаемыеРеквизиты.Добавить("бг_ВладелецСертификата");
	ВыгружаемыеРеквизиты.Добавить("ОрганВыдавшийДокумент");
	
	ВыгружаемыеРеквизиты = СтрСоединить(ВыгружаемыеРеквизиты, ",");
	
	Возврат ВыгружаемыеРеквизиты;
	
КонецФункции

Процедура ДобавитьКлючевыеПоляКВыгрузке(РеквизитыИСвойства)

	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Организации,
		"ИНН");
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Организации,
		"КПП");
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		, // ИмяРеквизита
		"бг_АдресФайлаСертификата",
		ОбщегоНазначения.ОписаниеТипаСтрока(1024));
				
КонецПроцедуры

#КонецОбласти
