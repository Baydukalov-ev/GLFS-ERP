#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	адаптер_НастройкиОбмена.УстановитьРеквизиты(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта, 
		ВыгружаемыеРеквизиты());
	
КонецПроцедуры

Процедура ЗаполнитьЗагружаемыйОбъект(ЗагружаемыйОбъект, СтруктураОбъекта, СписокСвойств, ИсключаяСвойства, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ЗаполнитьИсполнительныйЛист(ЗагружаемыйОбъект, СтруктураОбъекта);
	
КонецПроцедуры

#КонецОбласти // Конец ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ВыгружаемыеРеквизиты()
	
	ВыгружаемыеРеквизиты = Новый Массив;	
	ВыгружаемыеРеквизиты.Добавить("Номер");
	ВыгружаемыеРеквизиты.Добавить("Дата");
	ВыгружаемыеРеквизиты.Добавить("ПометкаУдаления");
	ВыгружаемыеРеквизиты.Добавить("Проведен");
	ВыгружаемыеРеквизиты.Добавить("ФизическоеЛицо");
	ВыгружаемыеРеквизиты.Добавить("Организация");
	ВыгружаемыеРеквизиты.Добавить("ВидИсполнительногоДокумента");
	ВыгружаемыеРеквизиты.Добавить("РеквизитыИсполнительногоДокумента");
	ВыгружаемыеРеквизиты.Добавить("Получатель");
	ВыгружаемыеРеквизиты.Добавить("ДатаДействия");
	ВыгружаемыеРеквизиты.Добавить("ДатаОкончания");
	ВыгружаемыеРеквизиты.Добавить("СпособРасчета");
	ВыгружаемыеРеквизиты.Добавить("ВидБазы");
	ВыгружаемыеРеквизиты.Добавить("Процент");
	ВыгружаемыеРеквизиты.Добавить("Сумма");
	ВыгружаемыеРеквизиты.Добавить("Предел");
	ВыгружаемыеРеквизиты.Добавить("ПрекратитьПоДостижениюПредела");
	ВыгружаемыеРеквизиты.Добавить("УчитыватьБольничныеЛисты");
	ВыгружаемыеРеквизиты.Добавить("Ответственный");
	ВыгружаемыеРеквизиты.Добавить("Комментарий");
	
	Возврат ВыгружаемыеРеквизиты;
	
КонецФункции

Процедура ЗаполнитьИсполнительныйЛист(ЗагружаемыйОбъект, СтруктураОбъекта)
	Перем адаптер_ОбработчикиСобытийСтандартный;
	адаптер_ОбработчикиСобытийСтандартный = ОбщегоНазначения.ОбщийМодуль("адаптер_ОбработчикиСобытийСтандартный");
	
	СпособРасчетаУдержания = СпособРасчетаУдержания(СтруктураОбъекта);
	
	ИсключаемыеСвойства = "Номер";
	адаптер_ОбработчикиСобытийСтандартный.ЗаполнитьЗагружаемыйОбъект(
		ЗагружаемыйОбъект,
		СтруктураОбъекта,
		Неопределено,
		ИсключаемыеСвойства);
		
	ЗагружаемыйОбъект.ФизическоеЛицо = СтруктураОбъекта.Физлицо;	
	Если СтруктураОбъекта.Свойство("Предел") И ЗначениеЗаполнено(СтруктураОбъекта.Предел) Тогда
		ЗагружаемыйОбъект.ПрекратитьПоДостижениюПредела = Истина;
	КонецЕсли;
	ЗагружаемыйОбъект.ВидИсполнительногоДокумента = Перечисления.ВидыИсполнительныхДокументов.ИсполнительныйЛист;
	ЗагружаемыйОбъект.Удержание = ПланыВидовРасчета.Удержания.бг_УдержаниеПоИсполнительномуДокументу();

	ЗаполнитьИсполнительныйЛистПоСпособуРасчета(ЗагружаемыйОбъект, СтруктураОбъекта, СпособРасчетаУдержания);
	
КонецПроцедуры

Функция СпособРасчетаУдержания(СтруктураОбъекта)
	
	СпособРасчетаУдержания = Неопределено;
	
	Если СтруктураОбъекта.Свойство("СпособРасчетаУдержания") И ЗначениеЗаполнено(СтруктураОбъекта.СпособРасчетаУдержания) Тогда
		СпособРасчетаУдержания = СтруктураОбъекта.СпособРасчетаУдержания.ЗначениеПеречисления;
	КонецЕсли;
	
	Возврат СпособРасчетаУдержания;

КонецФункции

Процедура ЗаполнитьИсполнительныйЛистПоСпособуРасчета(ЗагружаемыйОбъект, СтруктураОбъекта, СпособРасчетаУдержания)
	
	СпособРасчетаФиксированнойСуммой = Перечисления.СпособыРасчетаУдержанияПоИсполнительномуДокументу.ФиксированнойСуммой;
	СпособРасчетаПроцентом = Перечисления.СпособыРасчетаУдержанияПоИсполнительномуДокументу.Процентом;
	ВидБазыЗаработок = Перечисления.ВидыБазыУдержанияПоИсполнительномуДокументу.Заработок;
	
	Если СпособРасчетаУдержания = "ФиксированнойСуммой" Тогда
		ЗагружаемыйОбъект.СпособРасчета = СпособРасчетаФиксированнойСуммой;
		ЗагружаемыйОбъект.Сумма = СтруктураОбъекта.Размер;
	ИначеЕсли СпособРасчетаУдержания = "ПроцентомОтЗаработка" Тогда
		ЗагружаемыйОбъект.СпособРасчета = СпособРасчетаПроцентом;
		ЗагружаемыйОбъект.Процент = СтруктураОбъекта.Размер;
		ЗагружаемыйОбъект.ВидБазы = ВидБазыЗаработок;
	ИначеЕсли СпособРасчетаУдержания = "ПроцентомОтЗаработкаСУчетомБольничныхЛистов" Тогда
		ЗагружаемыйОбъект.СпособРасчета = СпособРасчетаПроцентом;
		ЗагружаемыйОбъект.Процент = СтруктураОбъекта.Размер;
		ЗагружаемыйОбъект.ВидБазы = ВидБазыЗаработок;
		ЗагружаемыйОбъект.УчитыватьБольничныеЛисты = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // Конец СлужебныеПроцедурыИФункции
