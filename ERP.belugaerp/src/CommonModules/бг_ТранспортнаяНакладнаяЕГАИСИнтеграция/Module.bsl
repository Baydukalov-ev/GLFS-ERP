#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт
	СтрокиСРеквизитом = РеквизитыИСвойства.Реквизиты.НайтиСтроки(Новый Структура("ИмяСвойстваXDTO", "Идентификатор"));
	Для каждого СтрокаРеквизита Из СтрокиСРеквизитом Цикл
		СтрокаРеквизита.ИмяСвойстваXDTO = "";
	КонецЦикла;
	ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства, ФорматСообщения);
КонецПроцедуры

Процедура ЗаполнитьТекстыЗапросовУсловиями(ТекстЗапроса, ТекстЗапросаТаблицаКлючей, ПараметрыЗапроса, 
				НастройкаВыгрузки, Объект, СтандартнаяОбработка) Экспорт
	
	ТекстПоиска = 	"NULL КАК бг_Идентификатор,";
	ТекстЗамены =	"ВыгружаемыйОбъект.Идентификатор КАК бг_Идентификатор,";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстПоиска, ТекстЗамены);

	ТекстПоиска = 	"ВыгружаемыйОбъект.ТипТранспорта КАК ТипТранспорта_ЗначениеРеквизитаЗначениеПеречисления,";
	ТекстЗамены =	
	"ВЫБОР 
	|	КОГДА ВыгружаемыйОбъект.ТипТранспорта = ЗНАЧЕНИЕ(Перечисление.ТипыТранспортаЕГАИС.ПустаяСсылка) 
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыТранспортаЕГАИС.Автомобиль) 
	|	ИНАЧЕ ВыгружаемыйОбъект.ТипТранспорта 
	|КОНЕЦ КАК ТипТранспорта_ЗначениеРеквизитаЗначениеПеречисления,";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстПоиска, ТекстЗамены);

	ТекстПоиска = 	"&УсловиеЗапроса";
	ТекстЗамены =	
	"&УсловиеЗапроса
	| И НЕ ВыгружаемыйОбъект.ИдентификаторЕГАИС = """"";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстПоиска, ТекстЗамены);
КонецПроцедуры

Функция ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения) Экспорт
	
	Перем адаптер_обработчикиСобытийСтандартный;
	адаптер_обработчикиСобытийСтандартный = ОбщегоНазначения.ОбщийМодуль("адаптер_обработчикиСобытийСтандартный");
	
	ДанныеОбъекта = адаптер_обработчикиСобытийСтандартный.ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения);
	
	Если ДанныеОбъекта.Реквизиты.Количество() > 0 Тогда
		
		Операции = Новый Массив;
		Операции.Добавить(Перечисления.ВидыДокументовЕГАИС.ТранспортнаяНакладная);
		
		РеквизитыОбъекта = ДанныеОбъекта.Реквизиты[0];
		РеквизитыОбъекта.Вставить("ИдентификаторЗапросаАкта", бг_ТТНВходящаяЕГАИСИнтеграция.ИдентификаторЗапросаЕГАИС(Объект, Операции));
	КонецЕсли;
	
	Возврат ДанныеОбъекта;
	
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства, ФорматСообщения)
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, Метаданные.Документы.ТранспортнаяНакладнаяЕГАИС, ,
		"ИдентификаторЗапросаАкта", ОбщегоНазначения.ОписаниеТипаСтрока(36));
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, Метаданные.Документы.ТранспортнаяНакладнаяЕГАИС, ,
		"бг_Идентификатор", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	
КонецПроцедуры

#КонецОбласти
