#Область ПрограммныйИнтерфейс

#Область Проведение

&После("ЗарегистрироватьУчетныеМеханизмы")
Процедура бг_ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента)
	
	МеханизмыДокумента.Добавить("бг_УчетБанковскихГарантий");
	
КонецПроцедуры

#КонецОбласти

// Проводки
Функция бг_ТекстОтраженияНачисленияАкциза() Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	СУММА(СтрокиРасхождения.бг_СуммаАкциза) КАК Сумма,
	|	СУММА(СтрокиРасхождения.бг_СуммаАкциза) КАК СуммаУУ,
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Продажи_Акцизы) КАК СчетДт,
	|	СтрокиРасхождения.Номенклатура.ГруппаФинансовогоУчета КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Акцизы) КАК СчетКт,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог) КАК СубконтоКт1,
	|	Операция.Организация.РегистрацияВНалоговомОргане КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Начисление акциза"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК Операция
	|		ПО ДокументыКОтражению.Ссылка = Операция.Ссылка 
	|			И Операция.Дата >= Операция.Организация.бг_ДатаНачалаУчетаАкцизовПоПриобретеннымЦенностям
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Расхождения КАК СтрокиРасхождения
	|		ПО (СтрокиРасхождения.Ссылка = Операция.Ссылка) 
	|			И СтрокиРасхождения.бг_СуммаАкциза <> 0 
	|
	|СГРУППИРОВАТЬ ПО
	|	Операция.Ссылка,
	|	Операция.Дата,
	|	Операция.Организация,
	|	СтрокиРасхождения.Номенклатура.ГруппаФинансовогоУчета
	|";
	Возврат ТекстЗапроса;

КонецФункции 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

&После("ДополнитьТекстыЗапросовПроведения")
Процедура бг_ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры)
	
	бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностям(Запрос, ТекстыЗапроса, Регистры);
	бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностямПродукция(Запрос, ТекстыЗапроса, Регистры);
	
КонецПроцедуры

Процедура бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностям(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "бг_АкцизПоПриобретеннымЦенностям";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностям(), ИмяРегистра);
	
КонецПроцедуры

Процедура бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностямПродукция(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "бг_АкцизПоПриобретеннымЦенностямВыпускПродукции";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностямПродукция(), ИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#Область Печать

&Перед("ДобавитьКомандыПечати")
Процедура бг_ДобавитьКомандыПечати(КомандыПечати)
	
	ЭтоПартнер = ПраваПользователяПовтИсп.ЭтоПартнер();	
	
	Если Не ЭтоПартнер Тогда
			
		// Счет-фактура Общий для сетей
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "бг_СчетФактура";
		КомандаПечати.Представление = НСтр("ru = 'Счет-фактура (основной)';
											|en = 'Tax Invoice (General)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("бг_ПереопределитьМакетСчетФактуры", Истина);
		КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВВалюте", Ложь);
		КомандаПечати.Порядок = 5;

		// Универсальный корректировочный документ (УКД - основной)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "бг_УКД";
		КомандаПечати.Представление = НСтр("ru = 'Универсальный корректировочный документ (УКД - основной)';
											|en = 'Universal adjustment document (UAD - general)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("бг_ПереопределитьМакетУКД", Истина);
		КомандаПечати.ДополнительныеПараметры.Вставить("бг_ИспользоватьПечатныеФормыСФ2_5", Истина);
		КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВВалюте", Ложь);
		КомандаПечати.Порядок = 5;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&ИзменениеИКонтроль("ТекстВыручкаОтРеализацииСобственногоКомиссионногоТовара")
Функция бг_ТекстВыручкаОтРеализацииСобственногоКомиссионногоТовара()

	ТекстВыручкаОтРеализацииСобственногоКомиссионногоТовара = "
	|ВЫБРАТЬ //// Выручка от реализации собственного/комиссионного товара (Дт 62.01 :: Кт 90.01.1)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл, Строки.СуммаСНДС) КАК Сумма,
	|	ЕСТЬNULL(Суммы.СуммаБезНДСУпр + Суммы.СуммаНДСУпр, Строки.СуммаСНДС / КурсВалютыУпрУчета.Курс) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентами) КАК ВидСчетаДт,
	|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Операция.ВалютаВзаиморасчетов КАК ВалютаДт,
	|	Расчеты.Подразделение КАК ПодразделениеДт,
	|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Расчеты.Контрагент КАК СубконтоДт1,
	|	Расчеты.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	Строки.СуммаВзаиморасчетов КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|	ЕСТЬNULL(Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл, Строки.СуммаСНДС) КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ВЫБОР 
	|		КОГДА Строки.НаДоходыРасходы ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы)
	|		КОГДА Операция.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД) ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыручкаОтПродажЕНВД)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыручкаОтПродаж)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	
	|	ВЫБОР
	|		КОГДА Строки.НаДоходыРасходы ТОГДА
	|			Операция.СтатьяДоходов
	|		ИНАЧЕ
	|			ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|				Строки.ВидЗапасов.ГруппаФинансовогоУчета
	|			ИНАЧЕ
	|				Строки.АналитикаУчетаНоменклатуры.Номенклатура.ГруппаФинансовогоУчета
	|			КОНЕЦ
	|	КОНЕЦ КАК АналитикаУчетаКт,
	|
	|	ВЫБОР
	|		КОГДА Строки.НаДоходыРасходы ТОГДА
	|			Операция.ПодразделениеДоходы
	|		ИНАЧЕ
	|			Строки.АналитикаУчетаНоменклатуры.МестоХранения
	|	КОНЕЦ КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ВЫБОР
	|		КОГДА Строки.НаДоходыРасходы ТОГДА
	|			Операция.ПодразделениеДоходы
	|		ИНАЧЕ
	|			Операция.Подразделение
	|	КОНЕЦ КАК ПодразделениеКт,
	|	ВЫБОР КОГДА Строки.НаДоходыРасходы
	|		ТОГДА ЕСТЬNULL(Расчеты.НаправлениеДеятельности, Операция.НаправлениеДеятельности)
	|		ИНАЧЕ Операция.НаправлениеДеятельности КОНЕЦ КАК НаправлениеДеятельностиКт,
	|	
	|	ВЫБОР
	|		КОГДА Строки.НаДоходыРасходы ТОГДА
	|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) 
	|	КОНЕЦ КАК СчетКт,
	|
	|	ВЫБОР
	|		КОГДА Строки.НаДоходыРасходы ТОГДА
	|			Операция.СтатьяДоходов
	|		ИНАЧЕ
	|			ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|				Строки.ВидЗапасов.ГруппаФинансовогоУчета
	|			ИНАЧЕ
	|				Строки.АналитикаУчетаНоменклатуры.Номенклатура.ГруппаФинансовогоУчета
	|			КОНЕЦ
	|	КОНЕЦ КАК СубконтоКт1,
	|	Строки.СтавкаНДС.ПеречислениеСтавкаНДС КАК СубконтоКт2,
	|	Строки.АналитикаУчетаНоменклатуры.Номенклатура КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	(ВЫБОР КОГДА Строки.НаДоходыРасходы И НЕ Операция.СтатьяДоходов.ПринятиеКНалоговомуУчету
	|		ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСРегл, Строки.СуммаСНДС - Строки.СуммаНДС)
#Вставка
	|	- ISNULL(СтрокиРасхождения.бг_СуммаАкциза, 0)
#КонецВставки
	|	КОНЕЦ) СуммаНУКт,
	|	(ВЫБОР КОГДА Строки.НаДоходыРасходы И НЕ Операция.СтатьяДоходов.ПринятиеКНалоговомуУчету
	|		ТОГДА ЕСТЬNULL(Суммы.СуммаБезНДСРегл, Строки.СуммаСНДС - Строки.СуммаНДС) 
#Вставка
	|	- ISNULL(СтрокиРасхождения.бг_СуммаАкциза, 0)
#КонецВставки
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	ВЫБОР
	|		КОГДА Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|		ТОГДА ""Выручка от реализации собственного товара""
	|		ИНАЧЕ ""Выручка от реализации комиссионного товара""
	|	КОНЕЦ КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаРеализации КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВТДанныеДокументаОснования КАК ДанныеДокументаОснования
	|	ПО
	|		Операция.Ссылка = ДанныеДокументаОснования.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|	
#Вставка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Расхождения КАК СтрокиРасхождения
	|		ПО (СтрокиРасхождения.Ссылка = Операция.Ссылка) 
	|		И СтрокиРасхождения.АналитикаУчетаНоменклатуры = Строки.АналитикаУчетаНоменклатуры
	|		И СтрокиРасхождения.бг_СуммаАкциза <> 0
	|	
#КонецВставки
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО
	|		Строки.Ссылка = Суммы.Регистратор
	|		И Строки.ИдентификаторСтроки = Суммы.ИдентификаторСтроки
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВТРасчетыСКлиентами КАК Расчеты
	|	ПО 
	|		Операция.Ссылка = Расчеты.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
	|	
	|ГДЕ
	|	НЕ ДанныеДокументаОснования.ПередачаНаКомиссию
	|	И НЕ ДанныеДокументаОснования.КорректировкаПрошлыхПериодов
	|	И (Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) ИЛИ Строки.НаДоходыРасходы
	|		И Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар))
	|	И (НЕ ДанныеДокументаОснования.ВернутьМногооборотнуюТару
	|		ИЛИ Строки.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|";

	Возврат ТекстВыручкаОтРеализацииСобственногоКомиссионногоТовара;

КонецФункции

&ИзменениеИКонтроль("ТекстВыручкаОтРеализацииСобственногоКомиссионногоТовараПрошлыеПериоды")
Функция бг_ТекстВыручкаОтРеализацииСобственногоКомиссионногоТовараПрошлыеПериоды()
	
	ТекстыЗапроса = Новый Массив;
	
	#Область ТекстОтнесениеНаДоходыОтРеализацииСобственногоКомиссионногоТовара
	ТекстЗапроса = "
	|ВЫБРАТЬ //// Отнесение на доходы от реализации собственного / комиссионного товара прошлых периодов (Дт 62.01 :: Кт 91.01)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл, Строки.СуммаСНДС) КАК Сумма,
	|	ЕСТЬNULL(Суммы.СуммаБезНДСУпр + Суммы.СуммаНДСУпр, Строки.СуммаСНДС / КурсВалютыУпрУчета.Курс) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентами) КАК ВидСчетаДт,
	|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Операция.ВалютаВзаиморасчетов КАК ВалютаДт,
	|	Расчеты.Подразделение КАК ПодразделениеДт,
	|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	Расчеты.Контрагент КАК СубконтоДт1,
	|	Расчеты.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	Строки.СуммаВзаиморасчетов КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ФинансовыеРезультатыКорректировокПрошлогоГода.РезультатНУ, 0) <=0
	|			ТОГДА ЕСТЬNULL(Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл, Строки.СуммаСНДС) 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	
	|	Операция.СтатьяДоходов КАК АналитикаУчетаКт,
	|
	|	Операция.ПодразделениеДоходы КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.ПодразделениеДоходы КАК ПодразделениеКт,
	|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	Операция.СтатьяДоходов КАК СубконтоКт1,
	|	Строки.СтавкаНДС.ПеречислениеСтавкаНДС КАК СубконтоКт2,
	|	Строки.АналитикаУчетаНоменклатуры.Номенклатура КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	(ВЫБОР КОГДА НЕ Операция.СтатьяДоходов.ПринятиеКНалоговомуУчету
	|		ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(ФинансовыеРезультатыКорректировокПрошлогоГода.РезультатНУ, 0) <= 0
	|				ТОГДА ЕСТЬNULL(Суммы.СуммаБезНДСРегл, Строки.СуммаСНДС - Строки.СуммаНДС)
#Вставка
	|	- ISNULL(СтрокиРасхождения.бг_СуммаАкциза, 0)
#КонецВставки
	|				ИНАЧЕ 0
	|			КОНЕЦ  
	|	КОНЕЦ) КАК СуммаНУКт,
	|	(ВЫБОР КОГДА НЕ Операция.СтатьяДоходов.ПринятиеКНалоговомуУчету И ЕСТЬNULL(ФинансовыеРезультатыКорректировокПрошлогоГода.РезультатНУ, 0) <= 0
	|			ИЛИ ЕСТЬNULL(ФинансовыеРезультатыКорректировокПрошлогоГода.РезультатНУ, 0) > 0
	|			ТОГДА ЕСТЬNULL(Суммы.СуммаБезНДСРегл, Строки.СуммаСНДС - Строки.СуммаНДС)
#Вставка
	|	- ISNULL(СтрокиРасхождения.бг_СуммаАкциза, 0)
#КонецВставки
	|			ИНАЧЕ 0
	| 	КОНЕЦ)  СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	ВЫБОР
	|		КОГДА Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|		ТОГДА ""Выручка от реализации собственного товара прошлых периодов""
	|		ИНАЧЕ ""Выручка от реализации комиссионного товара прошлых периодов""
	|	КОНЕЦ КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаРеализации КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВТДанныеДокументаОснования КАК ДанныеДокументаОснования
	|	ПО
	|		Операция.Ссылка = ДанныеДокументаОснования.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
#Вставка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Расхождения КАК СтрокиРасхождения
	|		ПО (СтрокиРасхождения.Ссылка = Операция.Ссылка) 
	|		И СтрокиРасхождения.АналитикаУчетаНоменклатуры = Строки.АналитикаУчетаНоменклатуры
	|		И СтрокиРасхождения.бг_СуммаАкциза <> 0
	|	
#КонецВставки
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО
	|		Строки.Ссылка = Суммы.Регистратор
	|		И Строки.ИдентификаторСтроки = Суммы.ИдентификаторСтроки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ФинансовыеРезультатыКорректировокПрошлогоГода КАК ФинансовыеРезультатыКорректировокПрошлогоГода
	|	ПО
	|		Операция.Ссылка = ФинансовыеРезультатыКорректировокПрошлогоГода.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВТРасчетыСКлиентами КАК Расчеты
	|	ПО 
	|		Операция.Ссылка = Расчеты.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
	|	
	|ГДЕ
	|	НЕ ДанныеДокументаОснования.ПередачаНаКомиссию
	|	И ДанныеДокументаОснования.КорректировкаПрошлыхПериодов
	|	И Строки.ВидЗапасов.ТипЗапасов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар))
	|	И (НЕ ДанныеДокументаОснования.ВернутьМногооборотнуюТару
	|		ИЛИ Строки.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И Строки.НаДоходыРасходы = ИСТИНА
	|
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ТекстОтнесениеНаПрочиеДоходыОтРеализацииСобственногоТовара
	ТекстЗапроса = "
	|ВЫБРАТЬ //// Отнесение на прочие доходы от реализации собственного товара прошлых периодов (Дт 62.01 :: Кт 91.01)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл, Строки.СуммаСНДС) КАК Сумма,
	|	ЕСТЬNULL(Суммы.СуммаБезНДСУпр + Суммы.СуммаНДСУпр, Строки.СуммаСНДС / КурсВалютыУпрУчета.Курс) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентами) КАК ВидСчетаДт,
	|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Операция.ВалютаВзаиморасчетов КАК ВалютаДт,
	|	Расчеты.Подразделение КАК ПодразделениеДт,
	|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	Расчеты.Контрагент КАК СубконтоДт1,
	|	Расчеты.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	Строки.СуммаВзаиморасчетов КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ФинансовыеРезультатыКорректировокПрошлогоГода.РезультатНУ, 0) <=0
	|			ТОГДА ЕСТЬNULL(Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл, Строки.СуммаСНДС) 
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеДоходы) КАК СчетКт,
	|
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПрибыльУбытокПрошлыхЛет) КАК СубконтоКт1,
	|	Строки.СтавкаНДС.ПеречислениеСтавкаНДС КАК СубконтоКт2,
	|	Строки.АналитикаУчетаНоменклатуры.Номенклатура КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ФинансовыеРезультатыКорректировокПрошлогоГода.РезультатНУ, 0) <= 0 
	|			ТОГДА ЕСТЬNULL(Суммы.СуммаБезНДСРегл, Строки.СуммаСНДС - Строки.СуммаНДС)
	|		ИНАЧЕ 0
	|	КОНЕЦ СуммаНУКт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ФинансовыеРезультатыКорректировокПрошлогоГода.РезультатНУ, 0) > 0 
	|			ТОГДА ЕСТЬNULL(Суммы.СуммаБезНДСРегл, Строки.СуммаСНДС - Строки.СуммаНДС)
	|		ИНАЧЕ 0
	|	КОНЕЦ СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Выручка от реализации собственного товара прошлых периодов"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаРеализации КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВТДанныеДокументаОснования КАК ДанныеДокументаОснования
	|	ПО
	|		Операция.Ссылка = ДанныеДокументаОснования.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО
	|		Строки.Ссылка = Суммы.Регистратор
	|		И Строки.ИдентификаторСтроки = Суммы.ИдентификаторСтроки
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ФинансовыеРезультатыКорректировокПрошлогоГода КАК ФинансовыеРезультатыКорректировокПрошлогоГода
	|	ПО
	|		Операция.Ссылка = ФинансовыеРезультатыКорректировокПрошлогоГода.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВТРасчетыСКлиентами КАК Расчеты
	|	ПО 
	|		Операция.Ссылка = Расчеты.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
	|	
	|ГДЕ
	|	НЕ ДанныеДокументаОснования.ПередачаНаКомиссию
	|	И ДанныеДокументаОснования.КорректировкаПрошлыхПериодов
	|	И Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|	И (НЕ ДанныеДокументаОснования.ВернутьМногооборотнуюТару
	|		ИЛИ Строки.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И Строки.НаДоходыРасходы = ЛОЖЬ
	|
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ТекстВыручкаОтРеализацииСобственногоКомиссионногоТовараНалоговыйУчет
	ТекстЗапроса = "
	|ВЫБРАТЬ //// Увеличение выручки прошлых нал. периодов (Дт 76.К :: Кт 90.01.1)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	КОНЕЦПЕРИОДА(ДанныеДокументаОснования.ДатаОтгрузки, МЕСЯЦ) КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Операция.Валюта КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкаРасчетовПрошлогоПериода) КАК СчетДт,
	|	Операция.Контрагент КАК СубконтоДт1,
	|	Операция.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	Строки.СуммаВзаиморасчетов КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|	ЕСТЬNULL(Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл, Строки.СуммаСНДС) КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ВЫБОР КОГДА Строки.НаДоходыРасходы ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы)
	|	КОГДА Операция.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыручкаОтПродажЕНВД)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыручкаОтПродаж)
	|	КОНЕЦ КАК ВидСчетаКт,
	|
	|	ВЫБОР КОГДА Строки.НаДоходыРасходы ТОГДА
	|		Операция.СтатьяДоходов
	|	ИНАЧЕ
	|		ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|			Строки.ВидЗапасов.ГруппаФинансовогоУчета
	|		ИНАЧЕ
	|			Строки.АналитикаУчетаНоменклатуры.Номенклатура.ГруппаФинансовогоУчета
	|		КОНЕЦ
	|	КОНЕЦ КАК АналитикаУчетаКт,
	|
	|	ВЫБОР КОГДА Строки.НаДоходыРасходы ТОГДА
	|		Операция.ПодразделениеДоходы
	|	ИНАЧЕ
	|		Строки.АналитикаУчетаНоменклатуры.МестоХранения
	|	КОНЕЦ КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ВЫБОР КОГДА Строки.НаДоходыРасходы ТОГДА
	|		Операция.ПодразделениеДоходы
	|	ИНАЧЕ
	|		Операция.Подразделение
	|	КОНЕЦ КАК ПодразделениеКт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	ВЫБОР КОГДА Строки.НаДоходыРасходы ТОГДА
	|		Операция.СтатьяДоходов
	|	ИНАЧЕ
	|		ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|			Строки.ВидЗапасов.ГруппаФинансовогоУчета
	|		ИНАЧЕ
	|			Строки.АналитикаУчетаНоменклатуры.Номенклатура.ГруппаФинансовогоУчета
	|		КОНЕЦ
	|	КОНЕЦ КАК СубконтоКт1,
	|	Строки.СтавкаНДС.ПеречислениеСтавкаНДС КАК СубконтоКт2,
	|	Строки.АналитикаУчетаНоменклатуры.Номенклатура КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	(ВЫБОР КОГДА Строки.НаДоходыРасходы И НЕ Операция.СтатьяДоходов.ПринятиеКНалоговомуУчету
	|			ТОГДА 0
	|			ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСРегл, Строки.СуммаСНДС -  Строки.СуммаНДС)
	|	КОНЕЦ) КАК СуммаНУКт,
	|	(ВЫБОР КОГДА Строки.НаДоходыРасходы И НЕ Операция.СтатьяДоходов.ПринятиеКНалоговомуУчету
	|			ТОГДА ЕСТЬNULL(Суммы.СуммаБезНДСРегл, Строки.СуммаСНДС -  Строки.СуммаНДС)
	|			ИНАЧЕ 0
	|	КОНЕЦ) КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	ВЫБОР
	|		КОГДА Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|		ТОГДА ""Выручка от реализации собственного товара прошлых периодов (НУ)""
	|		ИНАЧЕ ""Выручка от реализации комиссионного товара прошлых периодов (НУ)""
	|	КОНЕЦ КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаРеализации КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВТДанныеДокументаОснования КАК ДанныеДокументаОснования
	|	ПО
	|		Операция.Ссылка = ДанныеДокументаОснования.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО
	|		Строки.Ссылка = Суммы.Регистратор
	|		И Строки.ИдентификаторСтроки = Суммы.ИдентификаторСтроки
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ФинансовыеРезультатыКорректировокПрошлогоГода КАК ФинансовыеРезультатыКорректировокПрошлогоГода
	|	ПО
	|		Операция.Ссылка = ФинансовыеРезультатыКорректировокПрошлогоГода.Ссылка
	|		И ФинансовыеРезультатыКорректировокПрошлогоГода.РезультатНУ > 0
	|	
	|ГДЕ
	|	НЕ ДанныеДокументаОснования.ПередачаНаКомиссию
	|	И ДанныеДокументаОснования.КорректировкаПрошлыхПериодов
	|	И (Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) ИЛИ Строки.НаДоходыРасходы
	|		И Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар))
	|	И (НЕ ДанныеДокументаОснования.ВернутьМногооборотнуюТару
	|		ИЛИ Строки.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	Возврат СтрСоединить(ТекстыЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

&ИзменениеИКонтроль("ТекстСторноВыручкиОтРеализацииСобственногоКомиссионногоТовара")
Функция бг_ТекстСторноВыручкиОтРеализацииСобственногоКомиссионногоТовара()

	ТекстыЗапроса = Новый Массив;

	#Область ТекстСторноВыручкиОтРеализацииСобственногоТовара
	ТекстЗапроса = "
	|ВЫБРАТЬ //// Сторно выручки от реализации собственного товара Дт 62.01 :: Кт 90.01.1)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(СуммыДокументовВВалютахУчета.СуммаБезНДСРегл + СуммыДокументовВВалютахУчета.СуммаНДСРегл, -Строки.СуммаСНДС) КАК Сумма,
	|	ЕСТЬNULL(СуммыДокументовВВалютахУчета.СуммаБезНДСУпр + СуммыДокументовВВалютахУчета.СуммаНДСУпр, -Строки.СуммаСНДС / КурсВалютыУпрУчета.Курс) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентами) КАК ВидСчетаДт,
	|	Расчеты.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Операция.ВалютаВзаиморасчетов КАК ВалютаДт,
	|	Расчеты.Подразделение КАК ПодразделениеДт,
	|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	Расчеты.Контрагент КАК СубконтоДт1,
	|	Расчеты.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	-Строки.СуммаВзаиморасчетов КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ЕСТЬNULL(СуммыДокументовВВалютахУчета.СуммаБезНДСРегл + СуммыДокументовВВалютахУчета.СуммаНДСРегл, -Строки.СуммаСНДС) КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ВЫБОР КОГДА Операция.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыручкаОтПродажЕНВД)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыручкаОтПродаж)
	|	КОНЕЦ КАК ВидСчетаКт,
	|
	|	ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|		Строки.ВидЗапасов.ГруппаФинансовогоУчета
	|	ИНАЧЕ
	|		Строки.АналитикаУчетаНоменклатуры.Номенклатура.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК АналитикаУчетаКт,
	|
	|	Строки.АналитикаУчетаНоменклатуры.МестоХранения КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|		Строки.ВидЗапасов.ГруппаФинансовогоУчета
	|	ИНАЧЕ
	|		Строки.АналитикаУчетаНоменклатуры.Номенклатура.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК СубконтоКт1,
	|	Строки.СтавкаНДС.ПеречислениеСтавкаНДС КАК СубконтоКт2,
	|	Строки.АналитикаУчетаНоменклатуры.Номенклатура КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	-Строки.Количество КАК КоличествоКт,
#Вставка
	|	- ISNULL(СтрокиРасхождения.бг_СуммаАкциза, 0) + 
#КонецВставки
	|	ЕСТЬNULL(СуммыДокументовВВалютахУчета.СуммаБезНДСРегл, -(Строки.СуммаСНДС - Строки.СуммаНДС)) КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Сторно выручки от реализации собственного товара"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаРеализации КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВТДанныеДокументаОснования КАК ДанныеДокументаОснования
	|	ПО
	|		Операция.Ссылка = ДанныеДокументаОснования.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК Строки
	|	ПО 
	|		Строки.Ссылка = Операция.Ссылка
	|
#Вставка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Расхождения КАК СтрокиРасхождения
	|		ПО (СтрокиРасхождения.Ссылка = Операция.Ссылка) 
	|		И СтрокиРасхождения.АналитикаУчетаНоменклатуры = Строки.АналитикаУчетаНоменклатуры
	|		И СтрокиРасхождения.бг_СуммаАкциза <> 0
	|	
#КонецВставки
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|	ПО 
	|		Строки.Ссылка = СуммыДокументовВВалютахУчета.Регистратор
	|		И Строки.ИдентификаторСтроки = СуммыДокументовВВалютахУчета.ИдентификаторСтроки
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВТРасчетыСКлиентами КАК Расчеты
	|	ПО 
	|		Операция.Ссылка = Расчеты.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
	|
	|ГДЕ
	|	НЕ Строки.НаДоходыРасходы
	|	И НЕ ДанныеДокументаОснования.КорректировкаПрошлыхПериодов
	|	И НЕ ДанныеДокументаОснования.ПередачаНаКомиссию
	|	И Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|	И (НЕ ДанныеДокументаОснования.ВернутьМногооборотнуюТару
	|		ИЛИ Строки.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти

	#Область ТекстСторноВыручкиОтРеализацииСобственногоКомиссионногоТовараСписатьНаРасходы
	ТекстЗапроса = "
	|ВЫБРАТЬ //// Уменьшение реализации собственного/комиссионного товара на расходы (Дт 91.02 :: Кт 62.01)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(-(СуммыДокументовВВалютахУчета.СуммаБезНДСРегл + СуммыДокументовВВалютахУчета.СуммаНДСРегл), Строки.СуммаСНДС) КАК Сумма,
	|	ЕСТЬNULL(-(СуммыДокументовВВалютахУчета.СуммаБезНДСУпр + СуммыДокументовВВалютахУчета.СуммаНДСУпр), Строки.СуммаСНДС / КурсВалютыУпрУчета.Курс) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Операция.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Операция.ПодразделениеРасходы КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.ПодразделениеРасходы КАК ПодразделениеДт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Операция.СтатьяРасходов КАК СубконтоДт1,
	|	Операция.АналитикаРасходов КАК СубконтоДт2,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА
	|			ВЫБОР КОГДА ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка) 
	|					ТОГДА ОбъектыСтроительства.СпособСтроительства
	|				ИНАЧЕ 
	|					ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный) 
	|			КОНЕЦ
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	(ВЫБОР КОГДА НЕ Операция.СтатьяРасходов.ПринятиеКНалоговомуУчету
	|		ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(-СуммыДокументовВВалютахУчета.СуммаБезНДСРегл, Строки.СуммаСНДС - Строки.СуммаНДС) 
#Вставка
	|	+ ISNULL(СтрокиРасхождения.бг_СуммаАкциза, 0)
#КонецВставки
	|	КОНЕЦ) КАК СуммаНУДт,
	|	(ВЫБОР КОГДА НЕ Операция.СтатьяРасходов.ПринятиеКНалоговомуУчету
	|		ТОГДА ЕСТЬNULL(-СуммыДокументовВВалютахУчета.СуммаБезНДСРегл, Строки.СуммаСНДС - Строки.СуммаНДС)
#Вставка
	|	+ ISNULL(СтрокиРасхождения.бг_СуммаАкциза, 0)
#КонецВставки
	|		ИНАЧЕ 0 
	|	КОНЕЦ) КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентами) КАК ВидСчетаКт,
	|	Расчеты.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Операция.ВалютаВзаиморасчетов КАК ВалютаКт,
	|	Расчеты.Подразделение КАК ПодразделениеКт,
	|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	Расчеты.Контрагент КАК СубконтоКт1,
	|	Расчеты.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Строки.СуммаВзаиморасчетов КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ЕСТЬNULL(-(СуммыДокументовВВалютахУчета.СуммаБезНДСРегл + СуммыДокументовВВалютахУчета.СуммаНДСРегл), Строки.СуммаСНДС) КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	ВЫБОР
	|		КОГДА Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|		ТОГДА ""Уменьшение реализации собственного товара на расходы""
	|		ИНАЧЕ ""Уменьшение реализации комиссионного товара на расходы""
	|	КОНЕЦ КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаРеализации КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВТДанныеДокументаОснования КАК ДанныеДокументаОснования
	|	ПО
	|		Операция.Ссылка = ДанныеДокументаОснования.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК Строки
	|	ПО 
	|		Строки.Ссылка = Операция.Ссылка
#Вставка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Расхождения КАК СтрокиРасхождения
	|		ПО (СтрокиРасхождения.Ссылка = Операция.Ссылка) 
	|		И СтрокиРасхождения.АналитикаУчетаНоменклатуры = Строки.АналитикаУчетаНоменклатуры
	|		И СтрокиРасхождения.бг_СуммаАкциза <> 0
	|	
#КонецВставки
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|	ПО 
	|		Строки.Ссылка = СуммыДокументовВВалютахУчета.Регистратор
	|		И Строки.ИдентификаторСтроки = СуммыДокументовВВалютахУчета.ИдентификаторСтроки
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		Операция.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВТРасчетыСКлиентами КАК Расчеты
	|	ПО 
	|		Операция.Ссылка = Расчеты.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		КурсыВалют КАК КурсВалютыУпрУчета
	|	ПО
	|		КурсВалютыУпрУчета.Валюта = &ВалютаУпрУчета
	|		И КурсВалютыУпрУчета.Дата = НАЧАЛОПЕРИОДА(Операция.Дата, День)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО 
	|		ОбъектыСтроительства.Ссылка = Операция.АналитикаРасходов
	|ГДЕ
	|	Строки.НаДоходыРасходы
	|	И НЕ ДанныеДокументаОснования.КорректировкаПрошлыхПериодов
	|	И НЕ ДанныеДокументаОснования.ПередачаНаКомиссию
	|	И Строки.ВидЗапасов.ТипЗапасов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар))
	|	И (НЕ ДанныеДокументаОснования.ВернутьМногооборотнуюТару
	|		ИЛИ Строки.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	#КонецОбласти

	Возврат СтрСоединить(ТекстыЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());

КонецФункции

#КонецОбласти

#КонецОбласти

#Область АкцизыПоПриобретеннымЦенностям

Функция бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностям() Экспорт
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	РеализацияТоваровУслугТовары.Серия КАК Серия,
	|	РеализацияТоваровУслугТовары.Количество КАК КоличествоИсходное,
	|	0 КАК КоличествоФактическое
	|ПОМЕСТИТЬ ДанныеКорректировки
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|		ПО (КорректировкаРеализации.Ссылка = &Ссылка)
	|			И КорректировкаРеализации.ДокументОснование = РеализацияТоваровУслугТовары.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализацииТовары.Номенклатура,
	|	КорректировкаРеализацииТовары.Серия,
	|	0,
	|	КорректировкаРеализацииТовары.Количество
	|ИЗ
	|	Документ.КорректировкаРеализации.Товары КАК КорректировкаРеализацииТовары
	|ГДЕ
	|	КорректировкаРеализацииТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеКорректировки.Номенклатура КАК Номенклатура,
	|	ДанныеКорректировки.Серия КАК Серия,
	|	ВЫБОР
	|		КОГДА СУММА(ДанныеКорректировки.КоличествоИсходное) = 0
	|			ТОГДА 0
	|		ИНАЧЕ СУММА(ДанныеКорректировки.КоличествоФактическое) / СУММА(ДанныеКорректировки.КоличествоИсходное)
	|	КОНЕЦ КАК КоэффициентКорректировки
	|ПОМЕСТИТЬ КоэффициентыКорректировки
	|ИЗ
	|	ДанныеКорректировки КАК ДанныеКорректировки
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеКорректировки.Номенклатура,
	|	ДанныеКорректировки.Серия
	|
	|ИМЕЮЩИЕ
	|	СУММА(ДанныеКорректировки.КоличествоИсходное) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Акциз.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	Акциз.Организация КАК Организация,
	|	Акциз.Номенклатура КАК Номенклатура,
	|	Акциз.СерияНоменклатуры КАК СерияНоменклатуры,
	|	Акциз.Сделка КАК Сделка,
	|	Акциз.СостояниеСырья КАК СостояниеСырья,
	|	Акциз.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	Акциз.СтатусАкциза КАК СтатусАкциза,
	|	Акциз.Продукция КАК Продукция,
	|	Акциз.СерияПродукции КАК СерияПродукции,
	|	Акциз.ДатаРеализации КАК ДатаРеализации,
	|	-Акциз.Количество * (1 - КоэффициентыКорректировки.КоэффициентКорректировки) КАК Количество
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям КАК Акциз
	|		ПО (КорректировкаРеализации.Ссылка = &Ссылка)
	|			И КорректировкаРеализации.ДокументОснование = Акциз.Регистратор
	|			И (Акциз.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КоэффициентыКорректировки КАК КоэффициентыКорректировки
	|		ПО (Акциз.Продукция = КоэффициентыКорректировки.Номенклатура)
	|			И (Акциз.СерияПродукции = КоэффициентыКорректировки.Серия)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Акциз.Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	Акциз.Организация,
	|	Акциз.Номенклатура,
	|	Акциз.СерияНоменклатуры,
	|	Акциз.Сделка,
	|	ЗНАЧЕНИЕ(Перечисление.бг_СостоянияПодакцизногоСырья.ВГотовойПродукции),
	|	Акциз.СостояниеОплатыАванса,
	|	Акциз.СтатусАкциза,
	|	Акциз.Продукция,
	|	Акциз.СерияПродукции,
	|	Акциз.ДатаРеализации,
	|	Акциз.Количество * (1 - КоэффициентыКорректировки.КоэффициентКорректировки)
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям КАК Акциз
	|		ПО (КорректировкаРеализации.Ссылка = &Ссылка)
	|			И КорректировкаРеализации.ДокументОснование = Акциз.Регистратор
	|			И (Акциз.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КоэффициентыКорректировки КАК КоэффициентыКорректировки
	|		ПО (Акциз.Продукция = КоэффициентыКорректировки.Номенклатура)
	|			И (Акциз.СерияПродукции = КоэффициентыКорректировки.Серия)";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностямПродукция() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АкцизПродукция.Номенклатура КАК Номенклатура,
	|	АкцизПродукция.СерияНоменклатуры КАК СерияНоменклатуры
	|ПОМЕСТИТЬ Продукция
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.бг_АкцизПоПриобретеннымЦенностямВыпускПродукции КАК АкцизПродукция
	|		ПО (КорректировкаРеализации.Ссылка = &Ссылка)
	|			И КорректировкаРеализации.ДокументОснование = АкцизПродукция.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(КорректировкаРеализацииРасхождения.Ссылка.ДокументОснование КАК Документ.РеализацияТоваровУслуг).Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	КорректировкаРеализацииРасхождения.Ссылка.Организация КАК Организация,
	|	КорректировкаРеализацииРасхождения.Номенклатура КАК Номенклатура,
	|	КорректировкаРеализацииРасхождения.Серия КАК СерияНоменклатуры,
	|	ЗНАЧЕНИЕ(Перечисление.бг_ТипыМестХраненияПодакцизнойПродукции.Склад) КАК ТипМестаХранения,
	|	КорректировкаРеализацииРасхождения.Количество КАК Количество
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК КорректировкаРеализацииРасхождения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Продукция КАК Продукция
	|		ПО КорректировкаРеализацииРасхождения.Номенклатура = Продукция.Номенклатура
	|			И КорректировкаРеализацииРасхождения.Серия = Продукция.СерияНоменклатуры
	|ГДЕ
	|	КорректировкаРеализацииРасхождения.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(КорректировкаРеализацииРасхождения.Ссылка.ДокументОснование КАК Документ.РеализацияТоваровУслуг).Дата,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	КорректировкаРеализацииРасхождения.Ссылка.Организация,
	|	КорректировкаРеализацииРасхождения.Номенклатура,
	|	КорректировкаРеализацииРасхождения.Серия,
	|	ЗНАЧЕНИЕ(Перечисление.бг_ТипыМестХраненияПодакцизнойПродукции.ВОжиданииПодтвержденияЭкспорта),
	|	КорректировкаРеализацииРасхождения.Количество
	|ИЗ
	|	Документ.КорректировкаРеализации.Расхождения КАК КорректировкаРеализацииРасхождения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Продукция КАК Продукция
	|		ПО КорректировкаРеализацииРасхождения.Номенклатура = Продукция.Номенклатура
	|			И КорректировкаРеализацииРасхождения.Серия = Продукция.СерияНоменклатуры
	|ГДЕ
	|	КорректировкаРеализацииРасхождения.Ссылка = &Ссылка
	|	И ЕСТЬNULL(КорректировкаРеализацииРасхождения.Ссылка.Контрагент.ЮрФизЛицо, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицоНеРезидент)";
	
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти
