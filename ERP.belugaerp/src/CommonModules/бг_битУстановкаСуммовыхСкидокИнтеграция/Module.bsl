
#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьЗагружаемыйОбъект(ЗагружаемыйОбъект, СтруктураОбъекта, СписокСвойств, ИсключаяСвойства, СтандартнаяОбработка) Экспорт
	
	Перем адаптер_ОбработчикиСобытийСтандартный;
	
	адаптер_ОбработчикиСобытийСтандартный = ОбщегоНазначения.ОбщийМодуль("адаптер_ОбработчикиСобытийСтандартный");
	
	СтандартнаяОбработка = Ложь;
	
	Если СтруктураОбъекта.Свойство("ДатаФормирования") Тогда
		СтруктураОбъекта.Вставить("Дата", СтруктураОбъекта.ДатаФормирования);
	КонецЕсли;

	СтруктураОбъекта.Вставить("Проведен", Истина);
	СтруктураОбъекта.Вставить("Комментарий", "Загружен из CRM");
	
	ЗагружаемыйОбъект.СкидкиГлобальные.Очистить();
	
	адаптер_ОбработчикиСобытийСтандартный.ЗаполнитьЗагружаемыйОбъект(
		ЗагружаемыйОбъект, 
		СтруктураОбъекта, 
		СписокСвойств, 
		ИсключаяСвойства);
		
	УдалитьЛишниеСтроки(ЗагружаемыйОбъект);
	
КонецПроцедуры

Процедура ЗаписатьЗагружаемыйОбъект(ЗагружаемыйОбъект, СтандартнаяОбработка) Экспорт
	
	Если ЗагружаемыйОбъект.СкидкиГлобальные.Количество() = 0 Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // Конец ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Процедура УдалитьЛишниеСтроки(ЗагружаемыйОбъект)
	
	Если ЗагружаемыйОбъект.СкидкиГлобальные.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СкидкиГлобальные = ЗагружаемыйОбъект.СкидкиГлобальные.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СкидкиГлобальные", СкидкиГлобальные);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СкидкиГлобальные.Контрагент КАК Контрагент,
	|	СкидкиГлобальные.ДокументРезерва КАК ДокументРезерва,
	|	СкидкиГлобальные.СуммаСкидки КАК СуммаСкидки,
	|	СкидкиГлобальные.СуммаРезерва КАК СуммаРезерва
	|ПОМЕСТИТЬ СкидкиГлобальные
	|ИЗ
	|	&СкидкиГлобальные КАК СкидкиГлобальные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СкидкиГлобальные.Контрагент КАК Контрагент,
	|	ЕСТЬNULL(ЗаказКлиента.Ссылка, НЕОПРЕДЕЛЕНО) КАК ДокументРезерва,
	|	СУММА(СкидкиГлобальные.СуммаСкидки) КАК СуммаСкидки,
	|	СУММА(СкидкиГлобальные.СуммаРезерва) КАК СуммаРезерва
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СкидкиГлобальные КАК СкидкиГлобальные
	|		ПО Контрагенты.Ссылка = СкидкиГлобальные.Контрагент
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиента
	|		ПО (СкидкиГлобальные.ДокументРезерва = ЗаказКлиента.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	СкидкиГлобальные.Контрагент,
	|	ЕСТЬNULL(ЗаказКлиента.Ссылка, НЕОПРЕДЕЛЕНО)";
	
	ЗагружаемыйОбъект.СкидкиГлобальные.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры

#КонецОбласти // Конец СлужебныеПроцедурыИФункции