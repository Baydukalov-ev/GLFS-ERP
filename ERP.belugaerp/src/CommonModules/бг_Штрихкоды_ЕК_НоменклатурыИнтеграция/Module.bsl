#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт

	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	Реквизиты = ВыгружаемыеРеквизиты(); 
	
	адаптер_НастройкиОбмена.УстановитьРеквизиты(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		Реквизиты);
    ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства);

КонецПроцедуры  

Функция ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения) Экспорт
	
	Перем адаптер_ОбработчикиСобытийСтандартный;
	адаптер_ОбработчикиСобытийСтандартный = ОбщегоНазначения.ОбщийМодуль("адаптер_ОбработчикиСобытийСтандартный");

	ДанныеВыгружаемогоОбъекта = адаптер_ОбработчикиСобытийСтандартный.ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения);
	
	Если ДанныеВыгружаемогоОбъекта.Реквизиты.Количество() > 0  Тогда
		
		РеквизитыОбъекта = ДанныеВыгружаемогоОбъекта.Реквизиты[0]; 
		
		Если РеквизитыОбъекта.Свойство("Штрихкод") Тогда
			РеквизитыОбъекта.Вставить("ТипШтрихкода", 
				МенеджерОборудованияКлиентСервер.ОпределитьТипШтрихкода(РеквизитыОбъекта.Штрихкод));
		КонецЕсли;			
	КонецЕсли;
	
	Возврат ДанныеВыгружаемогоОбъекта;

КонецФункции	

Процедура ВыгрузитьСвязанныеОбъекты(СправочникОбъект) Экспорт
	
	Если Не (ЗначениеЗаполнено(СправочникОбъект.Владелец)
		И ТипЗнч(СправочникОбъект.Владелец) = Тип("СправочникСсылка.УпаковкиЕдиницыИзмерения")) Тогда
		
		Возврат;
	КонецЕсли;
	
	НеобходимоВыгрузитьУпаковку = Ложь;
	
	Если СправочникОбъект.ДополнительныеСвойства.Свойство("ЭтоНовый")
		И СправочникОбъект.ДополнительныеСвойства.ЭтоНовый Тогда
		
		НеобходимоВыгрузитьУпаковку = Истина;
		
	ИначеЕсли СправочникОбъект.ДополнительныеСвойства.Свойство("ДанныеДоИзменения") Тогда
		
		Если СправочникОбъект.ДополнительныеСвойства.ДанныеДоИзменения.Штрихкод <> СправочникОбъект.Штрихкод
			Или СправочникОбъект.ДополнительныеСвойства.ДанныеДоИзменения.ЕдиницаИзмерения <> СправочникОбъект.ЕдиницаИзмерения Тогда
			
			НеобходимоВыгрузитьУпаковку = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если НеобходимоВыгрузитьУпаковку Тогда
		бг_ОбщегоНазначенияСервер.ЗарегистрироватьИсходящееСообщениеПриЗаписи(СправочникОбъект.Владелец);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВыгружаемыеРеквизиты()

	ВыгружаемыеРеквизиты = Новый Массив;

	ВыгружаемыеРеквизиты.Добавить("Код");
	ВыгружаемыеРеквизиты.Добавить("ПометкаУдаления");
	ВыгружаемыеРеквизиты.Добавить("Владелец");
	ВыгружаемыеРеквизиты.Добавить("ЕдиницаИзмерения");
	ВыгружаемыеРеквизиты.Добавить("Штрихкод");
	
	Возврат ВыгружаемыеРеквизиты;

КонецФункции  

Процедура ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства)

	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		,
		"ТипШтрихкода",
		ОбщегоНазначения.ОписаниеТипаСтрока(20));

КонецПроцедуры

#КонецОбласти
