#Область ПрограммныйИнтерфейс

#Область Проведение

// Формирует параметры для проведения документа по регистрам учетного механизма через общий механизм проведения.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  Свойства - См. ПроведениеДокументов.СвойстваДокумента
//
// Возвращаемое значение:
//  Структура - См. ПроведениеДокументов.ПараметрыУчетногоМеханизма
//
Функция ПараметрыДляПроведенияДокумента(Документ, Свойства) Экспорт
	
	Параметры = ПроведениеДокументов.ПараметрыУчетногоМеханизма();
	
	// Проведение
	Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.бг_БанковскиеГарантии);
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.бг_АкцизПоПриобретеннымЦенностям);
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.бг_АкцизПоПриобретеннымЦенностямВыпускПродукции);
		
	КонецЕсли;
	
	Если Свойства.РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		
		Параметры.КонтрольныеРегистрыИзменений.Добавить(Метаданные.РегистрыНакопления.бг_БанковскиеГарантии);
		Параметры.КонтрольныеРегистрыЗаданий.Добавить(Метаданные.РегистрыНакопления.бг_БанковскиеГарантии);
		
		Параметры.КонтрольныеРегистрыИзменений.Добавить(Метаданные.РегистрыНакопления.бг_АкцизПоПриобретеннымЦенностям);
		Параметры.КонтрольныеРегистрыЗаданий.Добавить(Метаданные.РегистрыНакопления.бг_АкцизПоПриобретеннымЦенностям);
		
		Параметры.КонтрольныеРегистрыИзменений.Добавить(Метаданные.РегистрыНакопления.бг_АкцизПоПриобретеннымЦенностямВыпускПродукции);
		Параметры.КонтрольныеРегистрыЗаданий.Добавить(Метаданные.РегистрыНакопления.бг_АкцизПоПриобретеннымЦенностямВыпускПродукции);
		
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// Возвращает тексты запросов для сторнирования движений при исправлении документов
// 
// Параметры:
// 	МетаданныеДокумента - ОбъектМетаданныхДокумент - Метаданные документа, который проводится.
// 
// Возвращаемое значение:
// 	Соответствие - Соответствие полного имени регистра тексту запроса сторнирования
//
Функция ТекстыЗапросовСторнирования(МетаданныеДокумента) Экспорт

	ТекстыЗапросов = Новый Соответствие();
	
	Возврат ТекстыЗапросов;
	
КонецФункции

// Дополняет текст запроса механизма проверки даты запрета по таблице изменений.
// 
// Параметры:
// 	Запрос - Запрос - используется для установки параметров запроса.
// 
// Возвращаемое значение:
//	Соответствие - соответствие имен таблиц изменения регистров и текстов запросов.
//	
Функция ТекстыЗапросовКонтрольДатыЗапретаПоТаблицеИзменений(Запрос) Экспорт

	СоответствиеТекстовЗапросов = Новый Соответствие;
	
	ИмяРегистра                 = Метаданные.РегистрыНакопления.бг_БанковскиеГарантии.Имя;
	ИмяТаблицыИзменений         = "Движениябг_БанковскиеГарантииИзменения"; 
	СтруктураТекстовЗапросов    = ПроведениеДокументов.ШаблонТекстЗапросаКонтрольДатыЗапрета(
		Запрос,
		ИмяРегистра, 
		ИмяТаблицыИзменений, 
		"ФинансовыйКонтур");
	СоответствиеТекстовЗапросов.Вставить(ИмяТаблицыИзменений, СтруктураТекстовЗапросов);
	
	ИмяРегистра                 = Метаданные.РегистрыНакопления.бг_АкцизПоПриобретеннымЦенностям.Имя;
	ИмяТаблицыИзменений         = "Движениябг_АкцизПоПриобретеннымЦенностям";
	СтруктураТекстовЗапросов    = ПроведениеДокументов.ШаблонТекстЗапросаКонтрольДатыЗапрета(
		Запрос,
		ИмяРегистра,
		ИмяТаблицыИзменений,
		"ФинансовыйКонтур");
	СоответствиеТекстовЗапросов.Вставить(ИмяТаблицыИзменений, СтруктураТекстовЗапросов);
	
	Возврат СоответствиеТекстовЗапросов;
	
КонецФункции

// Процедура формирования движений по подчиненным регистрам взаиморасчетов.
//
// Параметры:
//   ТаблицыДляДвижений - Структура - таблицы данных документа
//   Движения - КоллекцияДвижений - коллекция наборов записей движений документа
//   Отказ - Булево - признак отказа от проведения документа.
//
Процедура ОтразитьДвижения(ТаблицыДляДвижений, Движения, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
		
	Если ТаблицыДляДвижений.Свойство("Таблицабг_БанковскиеГарантии") Тогда
		
		Движения.бг_БанковскиеГарантии.Загрузить(ТаблицыДляДвижений.Таблицабг_БанковскиеГарантии);
		Движения.бг_БанковскиеГарантии.Записывать = Истина;
		
	КонецЕсли;
	
	Если ТаблицыДляДвижений.Свойство("Таблицабг_АкцизПоПриобретеннымЦенностям") Тогда
		
		Движения.бг_АкцизПоПриобретеннымЦенностям.Загрузить(ТаблицыДляДвижений.Таблицабг_АкцизПоПриобретеннымЦенностям);
		Движения.бг_АкцизПоПриобретеннымЦенностям.Записывать = Истина;
		
	КонецЕсли;
	
	Если ТаблицыДляДвижений.Свойство("Таблицабг_АкцизПоПриобретеннымЦенностямВыпускПродукции") Тогда
		
		Движения.бг_АкцизПоПриобретеннымЦенностямВыпускПродукции.Загрузить(ТаблицыДляДвижений.Таблицабг_АкцизПоПриобретеннымЦенностямВыпускПродукции);
		Движения.бг_АкцизПоПриобретеннымЦенностямВыпускПродукции.Записывать = Истина;
		
	КонецЕсли;
		
КонецПроцедуры

// Формирует тексты запросов для контроля изменений записанных движений регистров.
//
// Параметры:
//  Запрос - Запрос - запрос, хранящий параметры используемые в списке запросов
//  ТекстыЗапроса - СписокЗначений - список текстов запросов и их имен.
//  Документ - ДокументОбъект - записываемый документ.
//
Процедура ИнициализироватьДанныеКонтроляИзменений(Запрос, ТекстыЗапроса, Документ) Экспорт
	
	Если ПроведениеДокументов.ЕстьЗаписиВТаблице(Документ, "Движениябг_БанковскиеГарантииИзменения") Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаОстатков.БанковскаяГарантия КАК БанковскаяГарантия,
		|	ТаблицаОстатков.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.бг_БанковскиеГарантии.Остатки(
		|			,
		|			БанковскаяГарантия В
		|				(ВЫБРАТЬ
		|					Таблица.БанковскаяГарантия
		|				ИЗ
		|					Движениябг_БанковскиеГарантииИзменения КАК Таблица)) КАК ТаблицаОстатков
		|ГДЕ
		|	ТаблицаОстатков.КоличествоОстаток < 0";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ОшибкиБанковскиеГарантии");
		
	КонецЕсли;
	
	Если ПроведениеДокументов.ЕстьЗаписиВТаблице(Документ, "Движениябг_АкцизПоПриобретеннымЦенностямИзменения") Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаОстатков.Номенклатура КАК Номенклатура,
		|	ТаблицаОстатков.СерияНоменклатуры КАК СерияНоменклатуры,
		|	ТаблицаОстатков.Сделка КАК Сделка,
		|	ТаблицаОстатков.СостояниеСырья КАК СостояниеСырья,
		|	ТаблицаОстатков.СтатусАкциза КАК СтатусАкциза,
		|	ТаблицаОстатков.Продукция КАК Продукция,
		|	ТаблицаОстатков.СерияПродукции КАК СерияПродукции,
		|	ТаблицаОстатков.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям.Остатки(
		|			,
		|			(Организация, Номенклатура, СерияНоменклатуры, Сделка, Продукция, СерияПродукции) В
		|				(ВЫБРАТЬ
		|					Таблица.Организация,
		|					Таблица.Номенклатура,
		|					Таблица.СерияНоменклатуры,
		|					Таблица.Сделка,
		|					Таблица.Продукция,
		|					Таблица.СерияПродукции
		|				ИЗ
		|					Движениябг_АкцизПоПриобретеннымЦенностямИзменения КАК Таблица)) КАК ТаблицаОстатков
		|ГДЕ
		|	(ТаблицаОстатков.КоличествоОстаток < 0)";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, "бг_ОшибкиАкцизыПоПриобретеннымЦенностям");
		
	КонецЕсли;
	
КонецПроцедуры

// Выводит сообщения пользователю при наличии ошибок контроля изменений записанных движений регистров.
//
// Параметры:
//  РезультатыКонтроля - Структура - таблицы с результатами контроля изменений
//  Документ - ДокументОбъект - записываемый документ
//  Отказ - Булево - признак отказа от проведения документа.
//
Процедура СообщитьОРезультатахКонтроляИзменений(РезультатыКонтроля, Документ, Отказ) Экспорт
	Если ПроведениеДокументов.ЕстьЗаписиВТаблице(Документ, "Движениябг_БанковскиеГарантииИзменения") Тогда
		Для Каждого СтрокаОшибки Из РезультатыКонтроля.ОшибкиБанковскиеГарантии Цикл
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(
					НСтр(
						"ru = 'По банковской гарантии %1 использовано на %2 дкл больше зарегистрированного';
						|en = 'По банковской гарантии %1 использовано на %2 дкл больше зарегистрированного'"),
					СтрокаОшибки.БанковскаяГарантия,
					- СтрокаОшибки.КоличествоОстаток), 
				Документ,,, 
				Отказ);
		КонецЦикла;
	КонецЕсли;
	
	Если ПроведениеДокументов.ЕстьЗаписиВТаблице(Документ, "Движениябг_АкцизПоПриобретеннымЦенностямИзменения") Тогда
		Для Каждого СтрокаОшибки Из РезультатыКонтроля.бг_ОшибкиАкцизыПоПриобретеннымЦенностям Цикл
			ТекстСообщения = НСтр("ru = 'По банковской гарантии %1 списано на %2 дкл акциза больше зарегистрированного'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, СтрокаОшибки.Сделка, - СтрокаОшибки.КоличествоОстаток);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Документ, , , Отказ);
		КонецЦикла
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область АкцизыПоПриобретеннымЦенностям

Функция ВестиУчетАкцизовПоПриобретеннымЦенностям(ДокументОбъект) Экспорт
	Если Не ЗначениеЗаполнено(ДокументОбъект.Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДанныеОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОбъект.Организация,
			"бг_ВестиУчетАкцизовПоПриобретеннымЦенностям, бг_ДатаНачалаУчетаАкцизовПоПриобретеннымЦенностям");
	
	Если Не ДанныеОрганизации.бг_ВестиУчетАкцизовПоПриобретеннымЦенностям Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеОрганизации.бг_ДатаНачалаУчетаАкцизовПоПриобретеннымЦенностям)
		И ДокументОбъект.Дата < ДанныеОрганизации.бг_ДатаНачалаУчетаАкцизовПоПриобретеннымЦенностям Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

Функция ТребуетсяЗаполнениеАкцизовПоПриобретеннымЦенностям(ДокументОбъект) Экспорт
	Если Не ДокументОбъект.Проведен Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("бг_ЗаполнитьАкцизыПоПриобретеннымЦенностям")
		И ДокументОбъект.ДополнительныеСвойства.бг_ЗаполнитьАкцизыПоПриобретеннымЦенностям = Истина Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

Функция СтатусыСостоянияСписываемогоСырья() Экспорт
	СтатусыСостоянияСписываемогоСырья = Новый Структура;
	СтатусыСостоянияСписываемогоСырья.Вставить("СтатусыАкциза",  Новый Массив);
	СтатусыСостоянияСписываемогоСырья.Вставить("СостоянияСырья", Новый Массив);
	
	Возврат СтатусыСостоянияСписываемогоСырья;
КонецФункции

Функция ТекстЗапросаТаблицаМатериалы() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	ДатаВремя(01,01,01) КАК ДатаРеализации,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Серия КАК Серия,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Серия,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыТаблица1.ДатаРеализации,
	|	ТоварыТаблица1.Номенклатура КАК Номенклатура,
	|	ТоварыТаблица1.Серия КАК Серия,
	|	ЕСТЬNULL(СУММА(ТоварыТаблица2.Количество), 0) + &ТочностьУчета КАК ПорядокРаспределенияС,
	|	ЕСТЬNULL(СУММА(ТоварыТаблица2.Количество), 0) + ТоварыТаблица1.Количество КАК ПорядокРаспределенияПо
	|ПОМЕСТИТЬ ТоварыРаспределение
	|ИЗ
	|	Товары КАК ТоварыТаблица1
	|		ЛЕВОЕ СОЕДИНЕНИЕ Товары КАК ТоварыТаблица2
	|		ПО ТоварыТаблица1.Серия = ТоварыТаблица2.Серия
	|			И ТоварыТаблица1.НомерСтроки > ТоварыТаблица2.НомерСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыТаблица1.ДатаРеализации,
	|	ТоварыТаблица1.Номенклатура,
	|	ТоварыТаблица1.Серия,
	|	ТоварыТаблица1.Количество
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Серия";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаТаблицаТовары() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРеализации,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Серия КАК Серия,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ ВнешняяТаблицаТовары
	|ИЗ
	|	&Товары КАК Товары
	|ГДЕ
	|	Товары.Количество <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.ДатаРеализации КАК ДатаРеализации,
	|	Товары.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СерииНоменклатуры.бг_СерияРеализации, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ТОГДА Товары.Серия
	|		ИНАЧЕ СерииНоменклатуры.бг_СерияРеализации
	|	КОНЕЦ КАК Серия,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	ВнешняяТаблицаТовары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО Товары.Серия = СерииНоменклатуры.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Серия,
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаПродукция() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДокументТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ДокументТабличнаяЧасть.Серия КАК Серия,
	|	ДокументТабличнаяЧасть.Количество КАК Количество
	|ПОМЕСТИТЬ Продукция
	|ИЗ
	|	&ВыходныеИзделия КАК ДокументТабличнаяЧасть
	|ГДЕ
	|	ДокументТабличнаяЧасть.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаОстаткиАкцизовПоМатериалам() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	АкцизОстатки.Организация КАК Организация,
	|	АкцизОстатки.Номенклатура КАК Номенклатура,
	|	АкцизОстатки.СерияНоменклатуры КАК СерияНоменклатуры,
	|	АкцизОстатки.Сделка КАК Сделка,
	|	АкцизОстатки.СостояниеСырья КАК СостояниеСырья,
	|	АкцизОстатки.СтатусАкциза КАК СтатусАкциза,
	|	АкцизОстатки.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	АкцизОстатки.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ ОстаткиАкцизаПредварительно
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям.Остатки(
	|			&ДатаОстатков,
	|			Организация = &Организация
	|				И (Номенклатура, СерияНоменклатуры) В
	|					(ВЫБРАТЬ
	|						Товары.Номенклатура КАК Номенклатура,
	|						Товары.Серия КАК Серия
	|					ИЗ
	|						Товары КАК Товары)
	|				И СтатусАкциза В (&СтатусыАкциза)
	|				И СостояниеСырья В (&СостоянияСырья)
	|				И Продукция = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК АкцизОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АкцизПоПриобретеннымЦенностямДвижения.Организация,
	|	АкцизПоПриобретеннымЦенностямДвижения.Номенклатура,
	|	АкцизПоПриобретеннымЦенностямДвижения.СерияНоменклатуры,
	|	АкцизПоПриобретеннымЦенностямДвижения.Сделка,
	|	АкцизПоПриобретеннымЦенностямДвижения.СостояниеСырья,
	|	АкцизПоПриобретеннымЦенностямДвижения.СтатусАкциза,
	|	АкцизПоПриобретеннымЦенностямДвижения.СостояниеОплатыАванса,
	|	ВЫБОР
	|		КОГДА АкцизПоПриобретеннымЦенностямДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА АкцизПоПриобретеннымЦенностямДвижения.Количество
	|		ИНАЧЕ -АкцизПоПриобретеннымЦенностямДвижения.Количество
	|	КОНЕЦ
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям КАК АкцизПоПриобретеннымЦенностямДвижения
	|ГДЕ
	|	АкцизПоПриобретеннымЦенностямДвижения.Регистратор = &Ссылка
	|	И АкцизПоПриобретеннымЦенностямДвижения.СтатусАкциза В(&СтатусыАкциза)
	|	И АкцизПоПриобретеннымЦенностямДвижения.СостояниеСырья В(&СостоянияСырья)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АВТОНОМЕРЗАПИСИ() КАК НомерСтроки,
	|	ОстаткиАкцизаПредварительно.Организация КАК Организация,
	|	ОстаткиАкцизаПредварительно.Номенклатура КАК Номенклатура,
	|	ОстаткиАкцизаПредварительно.СерияНоменклатуры КАК СерияНоменклатуры,
	|	ОстаткиАкцизаПредварительно.Сделка КАК Сделка,
	|	ОстаткиАкцизаПредварительно.СостояниеСырья КАК СостояниеСырья,
	|	ОстаткиАкцизаПредварительно.СостояниеСырья.Порядок КАК СостояниеСырьяПорядокСписания,
	|	ОстаткиАкцизаПредварительно.СтатусАкциза КАК СтатусАкциза,
	|	ОстаткиАкцизаПредварительно.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	ОстаткиАкцизаПредварительно.СтатусАкциза.Порядок КАК СтатусАкцизаПорядокСписания,
	|	СУММА(ОстаткиАкцизаПредварительно.Количество) КАК Количество
	|ПОМЕСТИТЬ ОстаткиАкциза
	|ИЗ
	|	ОстаткиАкцизаПредварительно КАК ОстаткиАкцизаПредварительно
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиАкцизаПредварительно.Организация,
	|	ОстаткиАкцизаПредварительно.СерияНоменклатуры,
	|	ОстаткиАкцизаПредварительно.Номенклатура,
	|	ОстаткиАкцизаПредварительно.Сделка,
	|	ОстаткиАкцизаПредварительно.СтатусАкциза,
	|	ОстаткиАкцизаПредварительно.СостояниеСырья,
	|	ОстаткиАкцизаПредварительно.СостояниеОплатыАванса,
	|	ОстаткиАкцизаПредварительно.СостояниеСырья.Порядок,
	|	ОстаткиАкцизаПредварительно.СтатусАкциза.Порядок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СерияНоменклатуры,
	|	Сделка,
	|	СтатусАкцизаПорядокСписания,
	|	СостояниеСырьяПорядокСписания,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиАкцизаТаблица1.Организация КАК Организация,
	|	ОстаткиАкцизаТаблица1.Номенклатура КАК Номенклатура,
	|	ОстаткиАкцизаТаблица1.СерияНоменклатуры КАК СерияНоменклатуры,
	|	ОстаткиАкцизаТаблица1.Сделка КАК Сделка,
	|	ОстаткиАкцизаТаблица1.СостояниеСырья КАК СостояниеСырья,
	|	ОстаткиАкцизаТаблица1.СтатусАкциза КАК СтатусАкциза,
	|	ОстаткиАкцизаТаблица1.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	ОстаткиАкцизаТаблица1.Количество КАК Количество,
	|	ЕСТЬNULL(СУММА(ОстаткиАкцизаТаблица2.Количество), 0) + &ТочностьУчета КАК ПорядокРаспределенияС,
	|	ЕСТЬNULL(СУММА(ОстаткиАкцизаТаблица2.Количество), 0) + ОстаткиАкцизаТаблица1.Количество КАК ПорядокРаспределенияПо
	|ПОМЕСТИТЬ ОстаткиАкцизаРаспределение
	|ИЗ
	|	ОстаткиАкциза КАК ОстаткиАкцизаТаблица1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиАкциза КАК ОстаткиАкцизаТаблица2
	|		ПО ОстаткиАкцизаТаблица1.СерияНоменклатуры = ОстаткиАкцизаТаблица2.СерияНоменклатуры
	|			И ОстаткиАкцизаТаблица1.НомерСтроки > ОстаткиАкцизаТаблица2.НомерСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиАкцизаТаблица1.Организация,
	|	ОстаткиАкцизаТаблица1.Номенклатура,
	|	ОстаткиАкцизаТаблица1.СерияНоменклатуры,
	|	ОстаткиАкцизаТаблица1.Сделка,
	|	ОстаткиАкцизаТаблица1.СостояниеСырья,
	|	ОстаткиАкцизаТаблица1.СтатусАкциза,
	|	ОстаткиАкцизаТаблица1.СостояниеОплатыАванса,
	|	ОстаткиАкцизаТаблица1.Количество
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СерияНоменклатуры";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаСписаниеАкцизовПоМатериалам() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОстаткиАкцизаРаспределение.Номенклатура КАК Номенклатура,
	|	ОстаткиАкцизаРаспределение.СерияНоменклатуры КАК СерияНоменклатуры,
	|	ОстаткиАкцизаРаспределение.Сделка КАК Сделка,
	|	ОстаткиАкцизаРаспределение.СостояниеСырья КАК СостояниеСырья,
	|	ОстаткиАкцизаРаспределение.СтатусАкциза КАК СтатусАкциза,
	|	ОстаткиАкцизаРаспределение.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	ВЫБОР
	|		КОГДА ТоварыРаспределение.ПорядокРаспределенияПо < ОстаткиАкцизаРаспределение.ПорядокРаспределенияПо
	|			ТОГДА ТоварыРаспределение.ПорядокРаспределенияПо
	|		ИНАЧЕ ОстаткиАкцизаРаспределение.ПорядокРаспределенияПо
	|	КОНЕЦ - ВЫБОР
	|		КОГДА ТоварыРаспределение.ПорядокРаспределенияС > ОстаткиАкцизаРаспределение.ПорядокРаспределенияС
	|			ТОГДА ТоварыРаспределение.ПорядокРаспределенияС
	|		ИНАЧЕ ОстаткиАкцизаРаспределение.ПорядокРаспределенияС
	|	КОНЕЦ + &ТочностьУчета КАК Количество
	|ПОМЕСТИТЬ СписаниеАкцизаПоМатериалам
	|ИЗ
	|	ОстаткиАкцизаРаспределение КАК ОстаткиАкцизаРаспределение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварыРаспределение КАК ТоварыРаспределение
	|		ПО ОстаткиАкцизаРаспределение.СерияНоменклатуры = ТоварыРаспределение.Серия
	|			И ОстаткиАкцизаРаспределение.ПорядокРаспределенияС <= ТоварыРаспределение.ПорядокРаспределенияПо
	|			И ОстаткиАкцизаРаспределение.ПорядокРаспределенияПо >= ТоварыРаспределение.ПорядокРаспределенияС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписаниеАкцизаПоМатериалам.Номенклатура КАК Номенклатура,
	|	СписаниеАкцизаПоМатериалам.СерияНоменклатуры КАК СерияНоменклатуры,
	|	СписаниеАкцизаПоМатериалам.Сделка КАК Сделка,
	|	СписаниеАкцизаПоМатериалам.СостояниеСырья КАК СостояниеСырья,
	|	СписаниеАкцизаПоМатериалам.СтатусАкциза КАК СтатусАкциза,
	|	СписаниеАкцизаПоМатериалам.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	СписаниеАкцизаПоМатериалам.Количество КАК Количество
	|ИЗ
	|	СписаниеАкцизаПоМатериалам КАК СписаниеАкцизаПоМатериалам";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаОстаткиАкцизовПоПродукции() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	АкцизОстатки.Организация КАК Организация,
	|	АкцизОстатки.Номенклатура КАК Номенклатура,
	|	АкцизОстатки.СерияНоменклатуры КАК СерияНоменклатуры,
	|	АкцизОстатки.Продукция КАК Продукция,
	|	АкцизОстатки.СерияПродукции КАК СерияПродукции,
	|	АкцизОстатки.Сделка КАК Сделка,
	|	АкцизОстатки.СостояниеСырья КАК СостояниеСырья,
	|	АкцизОстатки.СтатусАкциза КАК СтатусАкциза,
	|	АкцизОстатки.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	АкцизОстатки.ДатаРеализации КАК ДатаРеализации,
	|	АкцизОстатки.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ ОстаткиАкцизаПредварительно
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям.Остатки(
	|			&ДатаОстатков,
	|			Организация = &Организация
	|				И (Продукция, СерияПродукции) В
	|					(ВЫБРАТЬ
	|						Товары.Номенклатура КАК Номенклатура,
	|						Товары.Серия КАК Серия
	|					ИЗ
	|						Товары КАК Товары)
	|				И СтатусАкциза В (&СтатусыАкциза)
	|				И СостояниеСырья В (&СостоянияСырья)) КАК АкцизОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АкцизПоПриобретеннымЦенностямДвижения.Организация,
	|	АкцизПоПриобретеннымЦенностямДвижения.Номенклатура,
	|	АкцизПоПриобретеннымЦенностямДвижения.СерияНоменклатуры,
	|	АкцизПоПриобретеннымЦенностямДвижения.Продукция,
	|	АкцизПоПриобретеннымЦенностямДвижения.СерияПродукции,
	|	АкцизПоПриобретеннымЦенностямДвижения.Сделка,
	|	АкцизПоПриобретеннымЦенностямДвижения.СостояниеСырья,
	|	АкцизПоПриобретеннымЦенностямДвижения.СтатусАкциза,
	|	АкцизПоПриобретеннымЦенностямДвижения.СостояниеОплатыАванса,
	|	АкцизПоПриобретеннымЦенностямДвижения.ДатаРеализации,
	|	ВЫБОР
	|		КОГДА АкцизПоПриобретеннымЦенностямДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА АкцизПоПриобретеннымЦенностямДвижения.Количество
	|		ИНАЧЕ -АкцизПоПриобретеннымЦенностямДвижения.Количество
	|	КОНЕЦ
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям КАК АкцизПоПриобретеннымЦенностямДвижения
	|ГДЕ
	|	АкцизПоПриобретеннымЦенностямДвижения.Регистратор = &Ссылка
	|	И АкцизПоПриобретеннымЦенностямДвижения.СтатусАкциза В(&СтатусыАкциза)
	|	И АкцизПоПриобретеннымЦенностямДвижения.СостояниеСырья В(&СостоянияСырья)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АВТОНОМЕРЗАПИСИ() КАК НомерСтроки,
	|	ОстаткиАкцизаПредварительно.Организация КАК Организация,
	|	ОстаткиАкцизаПредварительно.Номенклатура КАК Номенклатура,
	|	ОстаткиАкцизаПредварительно.СерияНоменклатуры КАК СерияНоменклатуры,
	|	ОстаткиАкцизаПредварительно.Продукция КАК Продукция,
	|	ОстаткиАкцизаПредварительно.СерияПродукции КАК СерияПродукции,
	|	ОстаткиАкцизаПредварительно.Сделка КАК Сделка,
	|	ОстаткиАкцизаПредварительно.СостояниеСырья КАК СостояниеСырья,
	|	ОстаткиАкцизаПредварительно.СостояниеСырья.Порядок КАК СостояниеСырьяПорядокСписания,
	|	ОстаткиАкцизаПредварительно.СтатусАкциза КАК СтатусАкциза,
	|	ОстаткиАкцизаПредварительно.СтатусАкциза.Порядок КАК СтатусАкцизаПорядокСписания,
	|	ОстаткиАкцизаПредварительно.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	ОстаткиАкцизаПредварительно.ДатаРеализации КАК ДатаРеализации,
	|	СУММА(ОстаткиАкцизаПредварительно.Количество) КАК Количество
	|ПОМЕСТИТЬ ОстаткиАкциза
	|ИЗ
	|	ОстаткиАкцизаПредварительно КАК ОстаткиАкцизаПредварительно
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиАкцизаПредварительно.Организация,
	|	ОстаткиАкцизаПредварительно.СерияНоменклатуры,
	|	ОстаткиАкцизаПредварительно.Номенклатура,
	|	ОстаткиАкцизаПредварительно.Сделка,
	|	ОстаткиАкцизаПредварительно.СтатусАкциза,
	|	ОстаткиАкцизаПредварительно.СостояниеСырья,
	|	ОстаткиАкцизаПредварительно.СостояниеСырья.Порядок,
	|	ОстаткиАкцизаПредварительно.СостояниеОплатыАванса,
	|	ОстаткиАкцизаПредварительно.Продукция,
	|	ОстаткиАкцизаПредварительно.СерияПродукции,
	|	ОстаткиАкцизаПредварительно.ДатаРеализации,
	|	ОстаткиАкцизаПредварительно.СтатусАкциза.Порядок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Продукция,
	|	СерияПродукции,
	|	СтатусАкцизаПорядокСписания,
	|	СостояниеСырьяПорядокСписания,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиАкцизаТаблица1.Организация КАК Организация,
	|	ОстаткиАкцизаТаблица1.Номенклатура КАК Номенклатура,
	|	ОстаткиАкцизаТаблица1.СерияНоменклатуры КАК СерияНоменклатуры,
	|	ОстаткиАкцизаТаблица1.Продукция КАК Продукция,
	|	ОстаткиАкцизаТаблица1.СерияПродукции КАК СерияПродукции,
	|	ОстаткиАкцизаТаблица1.Сделка КАК Сделка,
	|	ОстаткиАкцизаТаблица1.СостояниеСырья КАК СостояниеСырья,
	|	ОстаткиАкцизаТаблица1.СтатусАкциза КАК СтатусАкциза,
	|	ОстаткиАкцизаТаблица1.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	ОстаткиАкцизаТаблица1.ДатаРеализации КАК ДатаРеализации,
	|	ЕСТЬNULL(СУММА(ОстаткиАкцизаТаблица2.Количество), 0) + &ТочностьУчета КАК ПорядокРаспределенияС,
	|	ЕСТЬNULL(СУММА(ОстаткиАкцизаТаблица2.Количество), 0) + ОстаткиАкцизаТаблица1.Количество КАК ПорядокРаспределенияПо
	|ПОМЕСТИТЬ ОстаткиАкцизаРаспределение
	|ИЗ
	|	ОстаткиАкциза КАК ОстаткиАкцизаТаблица1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиАкциза КАК ОстаткиАкцизаТаблица2
	|		ПО ОстаткиАкцизаТаблица1.Продукция = ОстаткиАкцизаТаблица2.Продукция
	|			И ОстаткиАкцизаТаблица1.СерияПродукции = ОстаткиАкцизаТаблица2.СерияПродукции
	|			И ОстаткиАкцизаТаблица1.Номенклатура = ОстаткиАкцизаТаблица2.Номенклатура
	|			И ОстаткиАкцизаТаблица1.НомерСтроки > ОстаткиАкцизаТаблица2.НомерСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиАкцизаТаблица1.Организация,
	|	ОстаткиАкцизаТаблица1.Номенклатура,
	|	ОстаткиАкцизаТаблица1.СерияНоменклатуры,
	|	ОстаткиАкцизаТаблица1.Продукция,
	|	ОстаткиАкцизаТаблица1.СерияПродукции,
	|	ОстаткиАкцизаТаблица1.Сделка,
	|	ОстаткиАкцизаТаблица1.СостояниеСырья,
	|	ОстаткиАкцизаТаблица1.СтатусАкциза,
	|	ОстаткиАкцизаТаблица1.СостояниеОплатыАванса,
	|	ОстаткиАкцизаТаблица1.ДатаРеализации,
	|	ОстаткиАкцизаТаблица1.Количество
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Продукция,
	|	СерияПродукции,
	|	Номенклатура";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаОстаткиРеализованнойПродукции() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РеализацияПродукцииОбороты.Номенклатура КАК Номенклатура,
	|	РеализацияПродукцииОбороты.СерияНоменклатуры КАК СерияНоменклатуры,
	|	ВЫБОР
	|		КОГДА РеализацияПродукцииОбороты.Регистратор = &Ссылка
	|			ТОГДА 0
	|		ИНАЧЕ РеализацияПродукцииОбороты.КоличествоРасход
	|	КОНЕЦ КАК Количество,
	|	0 КАК КоличествоРасход
	|ПОМЕСТИТЬ ОстаткиПродукцииПредварительно
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностямВыпускПродукции.Обороты(
	|			,
	|			&ДатаОстатков,
	|			Регистратор,
	|			ТипМестаХранения = &ТипМестаХранения
	|				И (Номенклатура, СерияНоменклатуры) В
	|					(ВЫБРАТЬ
	|						Товары.Номенклатура КАК Номенклатура,
	|						Товары.Серия КАК Серия
	|					ИЗ
	|						Товары КАК Товары)) КАК РеализацияПродукцииОбороты
	|ГДЕ 
	|	ТИПЗНАЧЕНИЯ(РеализацияПродукцииОбороты.Регистратор) В (ТИП(Документ.РеализацияТоваровУслуг), ТИП(Документ.ВозвратТоваровОтКлиента), ТИП(Документ.КорректировкаРеализации))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Серия,
	|	0,
	|	СУММА(Товары.Количество)
	|ИЗ
	|	Товары КАК Товары
	|
	|СГРУППИРОВАТЬ ПО
	|	Номенклатура,
	|	Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиПродукцииПредварительно.Номенклатура КАК Номенклатура,
	|	ОстаткиПродукцииПредварительно.СерияНоменклатуры КАК СерияНоменклатуры,
	|	СУММА(ОстаткиПродукцииПредварительно.Количество) КАК Количество,
	|	СУММА(ОстаткиПродукцииПредварительно.КоличествоРасход) КАК КоличествоРасход
	|ПОМЕСТИТЬ ОстаткиПродукции
	|ИЗ
	|	ОстаткиПродукцииПредварительно КАК ОстаткиПродукцииПредварительно
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиПродукцииПредварительно.Номенклатура,
	|	ОстаткиПродукцииПредварительно.СерияНоменклатуры
	|
	|ИМЕЮЩИЕ
	|	СУММА(ОстаткиПродукцииПредварительно.Количество) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	СерияНоменклатуры";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаОстаткиВыпущеннойПродукции() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	АкцизВыпускПродукцииОстатки.Номенклатура КАК Номенклатура,
	|	АкцизВыпускПродукцииОстатки.СерияНоменклатуры КАК СерияНоменклатуры,
	|	АкцизВыпускПродукцииОстатки.КоличествоОстаток КАК Количество,
	|	0 КАК КоличествоРасход
	|ПОМЕСТИТЬ ОстаткиВыпущеннойПродукцииПредварительно
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностямВыпускПродукции.Остатки(
	|			&ДатаОстатков,
	|			ТипМестаХранения = &ТипМестаХранения
	|				И (Номенклатура, СерияНоменклатуры) В
	|					(ВЫБРАТЬ
	|						Товары.Номенклатура КАК Номенклатура,
	|						Товары.Серия КАК Серия
	|					ИЗ
	|						Товары КАК Товары)) КАК АкцизВыпускПродукцииОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АкцизВыпускПродукции.Номенклатура,
	|	АкцизВыпускПродукции.СерияНоменклатуры,
	|	АкцизВыпускПродукции.Количество,
	|	0
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностямВыпускПродукции КАК АкцизВыпускПродукции
	|ГДЕ
	|	АкцизВыпускПродукции.Регистратор = &Ссылка
	|	И АкцизВыпускПродукции.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Серия,
	|	0,
	|	Товары.Количество
	|ИЗ
	|	Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиВыпущеннойПродукцииПредварительно.Номенклатура КАК Номенклатура,
	|	ОстаткиВыпущеннойПродукцииПредварительно.СерияНоменклатуры КАК СерияНоменклатуры,
	|	СУММА(ОстаткиВыпущеннойПродукцииПредварительно.Количество) КАК Количество,
	|	СУММА(ОстаткиВыпущеннойПродукцииПредварительно.КоличествоРасход) КАК КоличествоРасход
	|ПОМЕСТИТЬ ОстаткиПродукции
	|ИЗ
	|	ОстаткиВыпущеннойПродукцииПредварительно КАК ОстаткиВыпущеннойПродукцииПредварительно
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиВыпущеннойПродукцииПредварительно.Номенклатура,
	|	ОстаткиВыпущеннойПродукцииПредварительно.СерияНоменклатуры
	|
	|ИМЕЮЩИЕ
	|	СУММА(ОстаткиВыпущеннойПродукцииПредварительно.Количество) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	СерияНоменклатуры";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаРасчетСписанияАкцизовПоПродукции() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	АВТОНОМЕРЗАПИСИ() КАК НомерСтроки,
	|	ОстаткиАкциза.Продукция КАК Продукция,
	|	ОстаткиАкциза.СерияПродукции КАК СерияПродукции,
	|	ОстаткиАкциза.Номенклатура КАК Номенклатура,
	|	ВЫРАЗИТЬ(СУММА(ВЫБОР
	|				КОГДА ОстаткиПродукции.Количество = 0
	|					ТОГДА 0
	|				ИНАЧЕ ОстаткиАкциза.Количество * ОстаткиПродукции.КоличествоРасход
	|														/ ОстаткиПродукции.Количество
	|			КОНЕЦ) КАК ЧИСЛО(15, 3)) КАК Количество
	|ПОМЕСТИТЬ МатериалыРаспределить
	|ИЗ
	|	ОстаткиАкциза КАК ОстаткиАкциза
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОстаткиПродукции КАК ОстаткиПродукции
	|		ПО ОстаткиАкциза.Продукция = ОстаткиПродукции.Номенклатура
	|			И ОстаткиАкциза.СерияПродукции = ОстаткиПродукции.СерияНоменклатуры
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиАкциза.Продукция,
	|	ОстаткиАкциза.СерияПродукции,
	|	ОстаткиАкциза.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Продукция,
	|	СерияПродукции,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МатериалыРаспределитьТаблица1.Продукция КАК Продукция,
	|	МатериалыРаспределитьТаблица1.СерияПродукции КАК СерияПродукции,
	|	МатериалыРаспределитьТаблица1.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(СУММА(МатериалыРаспределитьТаблица2.Количество), 0)
	|										+ &ТочностьУчета КАК ПорядокРаспределенияС,
	|	ЕСТЬNULL(СУММА(МатериалыРаспределитьТаблица2.Количество), 0)
	|										+ МатериалыРаспределитьТаблица1.Количество КАК ПорядокРаспределенияПо
	|ПОМЕСТИТЬ МатериалыРаспределение
	|ИЗ
	|	МатериалыРаспределить КАК МатериалыРаспределитьТаблица1
	|		ЛЕВОЕ СОЕДИНЕНИЕ МатериалыРаспределить КАК МатериалыРаспределитьТаблица2
	|		ПО МатериалыРаспределитьТаблица1.Продукция = МатериалыРаспределитьТаблица2.Продукция
	|			И МатериалыРаспределитьТаблица1.СерияПродукции = МатериалыРаспределитьТаблица2.СерияПродукции
	|			И МатериалыРаспределитьТаблица1.Номенклатура = МатериалыРаспределитьТаблица2.Номенклатура
	|			И МатериалыРаспределитьТаблица1.НомерСтроки > МатериалыРаспределитьТаблица2.НомерСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	МатериалыРаспределитьТаблица1.Продукция,
	|	МатериалыРаспределитьТаблица1.СерияПродукции,
	|	МатериалыРаспределитьТаблица1.Номенклатура,
	|	МатериалыРаспределитьТаблица1.Количество
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Продукция,
	|	СерияПродукции,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиАкцизаРаспределение.Номенклатура КАК Номенклатура,
	|	ОстаткиАкцизаРаспределение.СерияНоменклатуры КАК СерияНоменклатуры,
	|	ОстаткиАкцизаРаспределение.Сделка КАК Сделка,
	|	ОстаткиАкцизаРаспределение.СостояниеСырья КАК СостояниеСырья,
	|	ОстаткиАкцизаРаспределение.СтатусАкциза КАК СтатусАкциза,
	|	ОстаткиАкцизаРаспределение.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	ОстаткиАкцизаРаспределение.Продукция КАК Продукция,
	|	ОстаткиАкцизаРаспределение.СерияПродукции КАК СерияПродукции,
	|	ОстаткиАкцизаРаспределение.ДатаРеализации КАК ДатаРеализации,
	|	ВЫБОР
	|		КОГДА МатериалыРаспределение.ПорядокРаспределенияПо < ОстаткиАкцизаРаспределение.ПорядокРаспределенияПо
	|			ТОГДА МатериалыРаспределение.ПорядокРаспределенияПо
	|		ИНАЧЕ ОстаткиАкцизаРаспределение.ПорядокРаспределенияПо
	|	КОНЕЦ - ВЫБОР
	|		КОГДА МатериалыРаспределение.ПорядокРаспределенияС > ОстаткиАкцизаРаспределение.ПорядокРаспределенияС
	|			ТОГДА МатериалыРаспределение.ПорядокРаспределенияС
	|		ИНАЧЕ ОстаткиАкцизаРаспределение.ПорядокРаспределенияС
	|	КОНЕЦ + &ТочностьУчета КАК Количество
	|ПОМЕСТИТЬ СписаниеАкцизовПоПродукции
	|ИЗ
	|	ОстаткиАкцизаРаспределение КАК ОстаткиАкцизаРаспределение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МатериалыРаспределение КАК МатериалыРаспределение
	|		ПО ОстаткиАкцизаРаспределение.Продукция = МатериалыРаспределение.Продукция
	|			И ОстаткиАкцизаРаспределение.СерияПродукции = МатериалыРаспределение.СерияПродукции
	|			И ОстаткиАкцизаРаспределение.Номенклатура = МатериалыРаспределение.Номенклатура
	|			И ОстаткиАкцизаРаспределение.ПорядокРаспределенияС <= МатериалыРаспределение.ПорядокРаспределенияПо
	|			И ОстаткиАкцизаРаспределение.ПорядокРаспределенияПо >= МатериалыРаспределение.ПорядокРаспределенияС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписаниеАкцизовПоПродукции.Номенклатура КАК Номенклатура,
	|	СписаниеАкцизовПоПродукции.СерияНоменклатуры КАК СерияНоменклатуры,
	|	СписаниеАкцизовПоПродукции.Сделка КАК Сделка,
	|	СписаниеАкцизовПоПродукции.СостояниеСырья КАК СостояниеСырья,
	|	СписаниеАкцизовПоПродукции.СтатусАкциза КАК СтатусАкциза,
	|	СписаниеАкцизовПоПродукции.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	СписаниеАкцизовПоПродукции.Продукция КАК Продукция,
	|	СписаниеАкцизовПоПродукции.СерияПродукции КАК СерияПродукции,
	|	СписаниеАкцизовПоПродукции.ДатаРеализации,
	|	СписаниеАкцизовПоПродукции.Количество КАК Количество
	|ИЗ
	|	СписаниеАкцизовПоПродукции КАК СписаниеАкцизовПоПродукции";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаСписаниеВыпускПродукции() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СписаниеАкцизовПоПродукции.Номенклатура КАК Номенклатура,
	|	СписаниеАкцизовПоПродукции.СерияНоменклатуры КАК СерияНоменклатуры,
	|	СписаниеАкцизовПоПродукции.Сделка КАК Сделка,
	|	СписаниеАкцизовПоПродукции.СостояниеСырья КАК СостояниеСырья,
	|	СписаниеАкцизовПоПродукции.СтатусАкциза КАК СтатусАкциза,
	|	СписаниеАкцизовПоПродукции.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	СписаниеАкцизовПоПродукции.Продукция КАК Продукция,
	|	СписаниеАкцизовПоПродукции.СерияПродукции КАК СерияПродукции,
	|	СписаниеАкцизовПоПродукции.Количество КАК Количество
	|ИЗ
	|	СписаниеАкцизовПоПродукции КАК СписаниеАкцизовПоПродукции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписаниеАкцизовПоПродукции.Номенклатура КАК Номенклатура,
	|	СписаниеАкцизовПоПродукции.СерияНоменклатуры КАК СерияНоменклатуры,
	|	СписаниеАкцизовПоПродукции.Сделка КАК Сделка,
	|	СписаниеАкцизовПоПродукции.СостояниеСырья КАК СостояниеСырья,
	|	СписаниеАкцизовПоПродукции.СтатусАкциза КАК СтатусАкциза,
	|	СписаниеАкцизовПоПродукции.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	СписаниеАкцизовПоПродукции.Количество КАК Количество,
	|	ТаблицаПродукция.Номенклатура КАК Продукция,
	|	ТаблицаПродукция.Серия КАК СерияПродукции
	|ИЗ
	|	Продукция КАК ТаблицаПродукция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписаниеАкцизовПоПродукции КАК СписаниеАкцизовПоПродукции
	|		ПО (ИСТИНА)";
	
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти

// Производит автоматическое заполнение банковских гарантий в документе ПриобретениеТоваровУслуг
// Вызывается из формы документа, либо при проведении документа
//
// Параметры:
//  Документ - ДокументОбъект, либо ДанныеФормыСтруктура
//
Процедура ЗаполнитьБанковскиеГарантии(Документ) Экспорт

	Если Не бг_УчетБанковскихГарантий.ВестиУчетАкцизовПоПриобретеннымЦенностям(Документ) Тогда
		Возврат;
	КонецЕсли;

	Если Документ.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути
		И Документ.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС Тогда
		Возврат;
	КонецЕсли;
	
	Если Документ.БанковскиеГарантии.Количество() > 0 
		Или Документ.бг_РежимАвансовогоПлатежаЗаАкцизВБюджет = Перечисления.бг_РежимАвансовогоПлатежаЗаАкцизВБюджет.НеОплачен Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоСпирта = КоличествоСпиртаВДокументе(Документ);
	Если КоличествоСпирта = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РезультатЗапроса = РезультатЗапросаОстатковБанковскихГарантий(Документ);
	
	ВыборкаДиапазоныБанковскихГарантий = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Если ВыборкаДиапазоныБанковскихГарантий.Следующий() Тогда
		
		Если Не ЗначениеЗаполнено(Документ.бг_РежимАвансовогоПлатежаЗаАкцизВБюджет) Тогда
			Документ.бг_РежимАвансовогоПлатежаЗаАкцизВБюджет = Перечисления.бг_РежимАвансовогоПлатежаЗаАкцизВБюджет.ВСчетБанковскойГарантии;
		КонецЕсли;
		Документ.бг_ДиапазонБанковскихГарантий = ВыборкаДиапазоныБанковскихГарантий.ДиапазонБанковскихГарантий;
		
		// поиск Банковской гарантии полностю перекрывающей объем
		Выборка = ВыборкаДиапазоныБанковскихГарантий.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если КоличествоСпирта <= Выборка.Количество Тогда
				НоваяСтрока = Документ.БанковскиеГарантии.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				НоваяСтрока.Количество = КоличествоСпирта;
				НоваяСтрока.Сумма = НоваяСтрока.Количество * Выборка.НалоговаяСтавка;
				Возврат;
			КонецЕсли;
		КонецЦикла;
		
		// подбор Банковских гарантий по ФИФО
		Выборка.Сбросить();
		Пока Выборка.Следующий() Цикл
			Если КоличествоСпирта <= 0  Тогда
				Прервать;
			КонецЕсли;
			КоличествоБанковскойГарантии = мин(КоличествоСпирта, Выборка.Количество);
			НоваяСтрока = Документ.БанковскиеГарантии.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.Количество = КоличествоБанковскойГарантии;
			НоваяСтрока.Сумма = НоваяСтрока.Количество * Выборка.НалоговаяСтавка;
			
			КоличествоСпирта = КоличествоСпирта - НоваяСтрока.Количество;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция РезультатЗапросаОстатковБанковскихГарантий(Документ)

	Если Документ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС
		И ЗначениеЗаполнено(Документ.ДатаВходящегоДокумента) Тогда
		ДатаОперации = Документ.ДатаВходящегоДокумента;
	Иначе
		ДатаОперации = Документ.Дата;
	КонецЕсли;
	СледующийМесяц = ДобавитьМесяц(ДатаОперации, 1);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Контрагент", Документ.Контрагент);
	Запрос.УстановитьПараметр("Ссылка", Документ.Ссылка);
	Запрос.УстановитьПараметр("ДатаОстатков", КонецДня(ДатаОперации));
	Запрос.УстановитьПараметр("бг_ДиапазонБанковскихГарантий", Документ.бг_ДиапазонБанковскихГарантий);
	Запрос.УстановитьПараметр("ДатаНачалаСледующегоМесяца", НачалоМесяца(СледующийМесяц));
	Запрос.УстановитьПараметр("ДатаКонцаСледующегоМесяца", КонецМесяца(СледующийМесяц));

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БанковскиеГарантии.БанковскаяГарантия.Родитель КАК ДиапазонБанковскихГарантий,
	|	БанковскиеГарантии.БанковскаяГарантия КАК БанковскаяГарантия,
	|	БанковскиеГарантии.БанковскаяГарантия.ДатаВыдачи КАК ДатаВыдачи,
	|	СУММА(БанковскиеГарантии.Количество) КАК Количество,
	|	МАКСИМУМ(ЕСТЬNULL(БанковскиеГарантии.БанковскаяГарантия.Родитель.НалоговаяСтавка, 0)) КАК НалоговаяСтавка
	|ИЗ
	|	(ВЫБРАТЬ
	|		БанковскиеГарантии.БанковскаяГарантия КАК БанковскаяГарантия,
	|		БанковскиеГарантии.КоличествоОстаток КАК Количество
	|	ИЗ
	|		РегистрНакопления.бг_БанковскиеГарантии.Остатки(
	|				&ДатаОстатков,
	|				БанковскаяГарантия.Родитель.Поставщик = &Контрагент
	|					И (БанковскаяГарантия.ДатаНачалаСрокаДействия МЕЖДУ &ДатаНачалаСледующегоМесяца И &ДатаКонцаСледующегоМесяца)) КАК БанковскиеГарантии
	|	ГДЕ
	|		&бг_ДиапазонБанковскихГарантий = ЗНАЧЕНИЕ(Справочник.бг_БанковскиеГарантии.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		БанковскиеГарантии.БанковскаяГарантия,
	|		БанковскиеГарантии.КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.бг_БанковскиеГарантии.Остатки(
	|				&ДатаОстатков,
	|				БанковскаяГарантия.Родитель.Поставщик = &Контрагент
	|					И (БанковскаяГарантия.ДатаНачалаСрокаДействия МЕЖДУ &ДатаНачалаСледующегоМесяца И &ДатаКонцаСледующегоМесяца)
	|					И БанковскаяГарантия В ИЕРАРХИИ (&бг_ДиапазонБанковскихГарантий)) КАК БанковскиеГарантии
	|	ГДЕ
	|		&бг_ДиапазонБанковскихГарантий <> ЗНАЧЕНИЕ(Справочник.бг_БанковскиеГарантии.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		БанковскиеГарантии.БанковскаяГарантия,
	|		ВЫБОР
	|			КОГДА БанковскиеГарантии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -БанковскиеГарантии.Количество
	|			ИНАЧЕ БанковскиеГарантии.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.бг_БанковскиеГарантии КАК БанковскиеГарантии
	|	ГДЕ
	|		БанковскиеГарантии.БанковскаяГарантия.Родитель.Поставщик = &Контрагент
	|		И БанковскиеГарантии.Регистратор = &Ссылка
	|		И &бг_ДиапазонБанковскихГарантий В (БанковскиеГарантии.БанковскаяГарантия.Родитель, ЗНАЧЕНИЕ(Справочник.бг_БанковскиеГарантии.ПустаяСсылка))) КАК БанковскиеГарантии
	|
	|СГРУППИРОВАТЬ ПО
	|	БанковскиеГарантии.БанковскаяГарантия,
	|	БанковскиеГарантии.БанковскаяГарантия.ДатаВыдачи,
	|	БанковскиеГарантии.БанковскаяГарантия.Родитель
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаВыдачи
	|ИТОГИ ПО
	|	ДиапазонБанковскихГарантий";
	
	Возврат Запрос.Выполнить();

КонецФункции

Функция КоличествоСпиртаВДокументе(Документ)

	ЗначенияКонстант = бг_КонстантыПовтИсп.ЗначенияКонстант("ВидНоменклатурыСпирт, ИмпортныеССП");
	
	ВидыНоменклатурыСпирт = Новый Массив;
	ВидыНоменклатурыСпирт.Добавить(ЗначенияКонстант.ВидНоменклатурыСпирт);
	ВидыНоменклатурыСпирт.Добавить(ЗначенияКонстант.ИмпортныеССП);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидыНоменклатурыСпирт", ВидыНоменклатурыСпирт);
	Запрос.УстановитьПараметр("Товары", Документ.Товары.Выгрузить());
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(Товары.Количество) КАК Количество
	|ИЗ
	|	Справочник.Номенклатура КАК СпрНоменклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Товары КАК Товары
	|		ПО СпрНоменклатура.Ссылка = Товары.Номенклатура
	|ГДЕ
	|	СпрНоменклатура.ВидНоменклатуры В(&ВидыНоменклатурыСпирт)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий()
		И Выборка.Количество > 0 Тогда
		Возврат Выборка.Количество;
	Иначе
		Возврат 0;
	КонецЕсли;

КонецФункции

#КонецОбласти
