
#Область ПрограммныйИнтерфейс

Функция СоздатьНаборЗаписейРегистра(Отбор, ПолноеИмя) Экспорт
	
	Перем адаптер_ОбработчикиСобытийСтандартный;
	Перем адаптер_РаботаСДаннымиИБ;
	
	адаптер_ОбработчикиСобытийСтандартный = ОбщегоНазначения.ОбщийМодуль("адаптер_ОбработчикиСобытийСтандартный");
	адаптер_РаботаСДаннымиИБ = ОбщегоНазначения.ОбщийМодуль("адаптер_РаботаСДаннымиИБ");
	
	Если Не ЗначениеЗаполнено(Отбор.Штрихкод) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НовыйОтбор = Новый Структура("Штрихкод", Отбор.Штрихкод);	
	НаборЗаписей = адаптер_ОбработчикиСобытийСтандартный.СоздатьНаборЗаписейРегистра(НовыйОтбор, ПолноеИмя);
	НаборЗаписей.Отбор.Штрихкод.Установить(НовыйОтбор.Штрихкод);
	
	Возврат НаборЗаписей;
	
КонецФункции

Процедура ЗаполнитьЗагружаемыйОбъект(ЗагружаемыйОбъект, СтруктураОбъекта, СписокСвойств, ИсключаяСвойства, СтандартнаяОбработка) Экспорт
	
	Перем адаптер_ОбработчикиСобытийСтандартный;	
	адаптер_ОбработчикиСобытийСтандартный = ОбщегоНазначения.ОбщийМодуль("адаптер_ОбработчикиСобытийСтандартный");
	
	СтандартнаяОбработка = Ложь;
		
	Если СтруктураОбъекта.Свойство("Владелец") Тогда
		СтруктураОбъекта.Вставить("Номенклатура", СтруктураОбъекта.Владелец);
	КонецЕсли;
		
	адаптер_ОбработчикиСобытийСтандартный.ЗаполнитьЗагружаемыйОбъект(
		ЗагружаемыйОбъект, 
		СтруктураОбъекта, 
		СписокСвойств, 
		ИсключаяСвойства);

	Если СтруктураОбъекта.Свойство("ЕдиницаИзмерения")
		И СтруктураОбъекта.Свойство("Номенклатура") Тогда
		ЗагружаемыйОбъект.Упаковка = УпаковкаНоменклатурыПоБазовойЕдиницеИзмерения(
			СтруктураОбъекта.Номенклатура, СтруктураОбъекта.ЕдиницаИзмерения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт

	ДобавитьКлючевыеПоляКВыгрузке(РеквизитыИСвойства);

КонецПроцедуры

Процедура ЗаполнитьТекстыЗапросовУсловиями(ТекстЗапроса, ТекстЗапросаТаблицаКлючей, ПараметрыЗапроса, НастройкаВыгрузки, Объект, СтандартнаяОбработка) Экспорт
	
	ЗаменяемыйТекст =
		"ВыгружаемыйОбъект.Упаковка.Код КАК Упаковка_ЗначениеРеквизитаКод";
	
	ТекстЗамены =
		"ВыгружаемыйОбъект.Упаковка.ЕдиницаИзмерения.Код КАК Упаковка_ЗначениеРеквизитаКод";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменяемыйТекст, ТекстЗамены);
	
КонецПроцедуры

Процедура ВыгрузитьСвязанныеОбъекты(НаборЗаписей) Экспорт
	
	Перем адаптер_ПодпискиНаСобытияВызовСервера;
	адаптер_ПодпискиНаСобытияВызовСервера = ОбщегоНазначения.ОбщийМодуль("адаптер_ПодпискиНаСобытияВызовСервера");
	
	Если НаборЗаписей.ОбменДанными.Загрузка Или НаборЗаписей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	УстановитьПривилегированныйРежим(Истина);
	
	ДополнительныеСвойства = НаборЗаписей.ДополнительныеСвойства;
	ЭтоГрупповаяОбработкаДанных = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеСвойства, "адаптер_ЭтоГрупповаяОбработкаДанных", Ложь);
	ЭтоЗагрузкаДанных = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеСвойства, "адаптер_ЭтоЗагрузкаДанных", Ложь);
	
	Если ЭтоГрупповаяОбработкаДанных Или ЭтоЗагрузкаДанных Тогда
		Возврат;
	КонецЕсли;
	
	МассивУпаковок = НаборЗаписей.ВыгрузитьКолонку("Упаковка");
	МассивУпаковок = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивУпаковок);
	
	Для Каждого Упаковка Из МассивУпаковок Цикл
		Если ЗначениеЗаполнено(Упаковка) Тогда
			адаптер_ПодпискиНаСобытияВызовСервера.ЗарегистрироватьИсходящееСообщение(Упаковка, Неопределено, Неопределено);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // Конец ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция УпаковкаНоменклатурыПоБазовойЕдиницеИзмерения(Номенклатура, БазоваяЕдиницаИзмерения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	УпаковкиЕдиницыИзмерения.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
		|ГДЕ
		|	УпаковкиЕдиницыИзмерения.Владелец = &Номенклатура
		|	И УпаковкиЕдиницыИзмерения.ЕдиницаИзмерения = &БазоваяЕдиницаИзмерения
		|	И НЕ УпаковкиЕдиницыИзмерения.ПометкаУдаления";	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("БазоваяЕдиницаИзмерения", БазоваяЕдиницаИзмерения);	
	РезультатЗапроса = Запрос.Выполнить();
	
	УпаковкаНоменклатуры = Неопределено;
	Если Не РезультатЗапроса.Пустой() Тогда	
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		УпаковкаНоменклатуры = ВыборкаДетальныеЗаписи.Ссылка;	
	КонецЕсли;
	
	Возврат УпаковкаНоменклатуры;	

КонецФункции

Процедура ДобавитьКлючевыеПоляКВыгрузке(РеквизитыИСвойства)		
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.УпаковкиЕдиницыИзмерения,
		"Код");
	
КонецПроцедуры

#КонецОбласти // Конец СлужебныеПроцедурыИФункции