
#Область ПрограммныйИнтерфейс

Процедура ВыгрузитьДокументы() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не бг_КонстантыПовтИсп.ЗначениеКонстанты("ИспользоватьОптимизированныйАлгоритмОбменаЕГАИС") = Истина Тогда
		Возврат;
	КонецЕсли;
		
	НастройкиОбмена = НастройкиОбменаОрганизаций("Выгрузка");
	Для Каждого НастройкаОбмена Из НастройкиОбмена Цикл
		
		КлючЗадания = СтрШаблон("Выгрузка документов ФСРАР %1.", НастройкаОбмена.Представление);
		Если Не ЗапускЗаданияВозможен(КлючЗадания) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыВыполнения = Новый Массив;
		ПараметрыВыполнения.Добавить(НастройкаОбмена.Значение);
		
		ФоновыеЗадания.Выполнить(
			"ИнтеграцияЕГАИСВызовСервера.ОбработатьОчередьПередачиДанных",
			ПараметрыВыполнения,
			КлючЗадания,
			КлючЗадания);

	КонецЦикла;
	
КонецПроцедуры

Процедура ЗагрузитьДокументы() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не бг_КонстантыПовтИсп.ЗначениеКонстанты("ИспользоватьОптимизированныйАлгоритмОбменаЕГАИС") = Истина Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиОбмена = НастройкиОбменаОрганизаций("Загрузка");
	Для Каждого НастройкаОбмена Из НастройкиОбмена Цикл
		
		КлючЗадания = СтрШаблон("Загрузка документов ФСРАР %1.", НастройкаОбмена.Представление);
		Если Не ЗапускЗаданияВозможен(КлючЗадания) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыВыполнения = Новый Массив;
		ПараметрыВыполнения.Добавить(НастройкаОбмена.Значение.ОбменНаСервере);
		
		ФоновыеЗадания.Выполнить(
			"ИнтеграцияЕГАИСВызовСервера.ПолучитьВходящиеДокументы",
			ПараметрыВыполнения,
			КлючЗадания,
			КлючЗадания);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НастройкиОбменаОрганизаций(НаправлениеОбмена)
	
	НастройкиОбмена = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Если НаправлениеОбмена = "Выгрузка" Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Т.ОрганизацияЕГАИС.Код КАК КодФСРАР,
			|	Т.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС
			|ИЗ
			|	РегистрСведений.ОчередьПередачиДанныхЕГАИС КАК Т";
	Иначе
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Классификатор.Код КАК КодФСРАР,
			|	Классификатор.Ссылка КАК ОрганизацияЕГАИС
			|ИЗ
			|	Справочник.КлассификаторОрганизацийЕГАИС КАК Классификатор
			|ГДЕ
			|	Классификатор.СоответствуетОрганизации";
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НастройкиОбмена.Добавить(
			ИнтеграцияЕГАИСВызовСервера.НастройкиОбменаЕГАИС(Выборка.ОрганизацияЕГАИС), Выборка.КодФСРАР);
	КонецЦикла;
	
	Возврат НастройкиОбмена;
	
КонецФункции

Функция ЗапускЗаданияВозможен(КлючЗадания)
	
	КлючПоиска = Новый Структура;
	КлючПоиска.Вставить("Ключ", КлючЗадания);
	КлючПоиска.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	
	Возврат (ФоновыеЗадания.ПолучитьФоновыеЗадания(КлючПоиска).Количество() = 0);
	
КонецФункции

#КонецОбласти
