#Область ПрограммныйИнтерфейс

Процедура ВыгрузитьСвязанныеОбъекты(НаборЗаписей) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Сегмент = СегментНабораЗаписей(НаборЗаписей);	
	Если Не ТребуетсяВыгружатьСегментВSolvo(Сегмент) Тогда
		Возврат;	
	КонецЕсли;
	
	НоменклатураТаблица = НаборЗаписей.Выгрузить(, "Номенклатура");
	НоменклатураТаблица.Свернуть("Номенклатура");
	НоменклатураСегментаНовая = НоменклатураТаблица.ВыгрузитьКолонку("Номенклатура");
	
	НоменклатураСегментаСтарая = Неопределено;
	НаборЗаписей.ДополнительныеСвойства.Свойство("НоменклатураСегментаСтарая", НоменклатураСегментаСтарая);	

	// Регистрируем к выгрузке новую номенклутуру, которой еще не было в сегменте
	Для Каждого Номенклатура Из НоменклатураСегментаНовая Цикл
		Если ТребуетсяРегистрацияНоменклатурыКВыгрузке(Номенклатура, НоменклатураСегментаСтарая) Тогда  
			бг_ОбщегоНазначенияСервер.ЗарегистрироватьИсходящееСообщениеПриЗаписи(Номенклатура);
		КонецЕсли;
	КонецЦикла;

	// Регистрируем к выгрузке старую номенклутуру, которую исключили из сегмента
	Для Каждого Номенклатура Из НоменклатураСегментаСтарая Цикл
		Если ТребуетсяРегистрацияНоменклатурыКВыгрузке(Номенклатура, НоменклатураСегментаНовая) Тогда  
			бг_ОбщегоНазначенияСервер.ЗарегистрироватьИсходящееСообщениеПриЗаписи(Номенклатура);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // Конец ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ТребуетсяРегистрацияНоменклатурыКВыгрузке(Номенклатура, НоменклатураСегмента)

	ТребуетсяРегистрацияСообщения = Истина;
		
	Если НоменклатураСегмента <> Неопределено Тогда
		Если НоменклатураСегмента.Найти(Номенклатура) <> Неопределено Тогда
			ТребуетсяРегистрацияСообщения = Ложь;	
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТребуетсяРегистрацияСообщения;
	
КонецФункции

Функция СегментНабораЗаписей(НаборЗаписей)

	СегментОтбор = НаборЗаписей.Отбор.Найти("Сегмент");
	
	Сегмент = Неопределено;
	Если СегментОтбор <> Неопределено Тогда
		Сегмент = СегментОтбор.Значение;	
	КонецЕсли;
	
	Возврат Сегмент;
	
КонецФункции

Функция ТребуетсяВыгружатьСегментВSolvo(Сегмент)

	ТребуетсяВыгружатьСегмент = Истина;
		
	Если Не ЗначениеЗаполнено(Сегмент) Тогда
		ТребуетсяВыгружатьСегмент = Ложь;
	Иначе
		ТребуетсяВыгружатьСегмент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сегмент, "бг_ВыгружатьВSolvo");
	КонецЕсли;
	
	Возврат ТребуетсяВыгружатьСегмент;
	
КонецФункции


#КонецОбласти // Конец СлужебныеПроцедурыИФункции
