
#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	Реквизиты = ВыгружаемыеРеквизиты();
	
	ДобавитьСвязанныеРеквизитыКВыгрузке(Реквизиты);
	
	адаптер_НастройкиОбмена.УстановитьРеквизиты(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		Реквизиты);
	
	ДобавитьПроизвольныеРеквизитыКВыгрузке(РеквизитыИСвойства);
	ДобавитьКлючевыеПоляКВыгрузке(РеквизитыИСвойства);
	
КонецПроцедуры

Функция ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения) Экспорт
	
	Перем адаптер_обработчикиСобытийСтандартный;
	адаптер_обработчикиСобытийСтандартный = ОбщегоНазначения.ОбщийМодуль("адаптер_обработчикиСобытийСтандартный");
	
	ДанныеОбъекта = адаптер_обработчикиСобытийСтандартный.ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения);
	
	Если ДанныеОбъекта.Реквизиты.Количество() > 0 Тогда
		
		РеквизитыОбъекта = ДанныеОбъекта.Реквизиты[0];
		
		ЗаполнитьДанныеЛицензии(
			РеквизитыОбъекта, 
			Объект);
			
		ЗаполнитьДанныеКлиента(
			РеквизитыОбъекта, 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				Объект , 
				"Клиент"));
			
	КонецЕсли;
	
	Возврат ДанныеОбъекта;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВыгружаемыеРеквизиты()
	
	ВыгружаемыеРеквизиты = Новый Массив;
	
	ВыгружаемыеРеквизиты.Добавить("GLNКодМагазина");
	ВыгружаемыеРеквизиты.Добавить("Адрес");
	ВыгружаемыеРеквизиты.Добавить("АдресГеокодирования");
	ВыгружаемыеРеквизиты.Добавить("АдресЗначение");
	ВыгружаемыеРеквизиты.Добавить("ВидДоставки");
	ВыгружаемыеРеквизиты.Добавить("ВремяРаботыС");
	ВыгружаемыеРеквизиты.Добавить("ВремяРаботыПо");
	ВыгружаемыеРеквизиты.Добавить("Долгота");
	ВыгружаемыеРеквизиты.Добавить("ДополнительнаяИнформация");
	ВыгружаемыеРеквизиты.Добавить("ДопустимыйСрокДоОкончанияЛицензии");
	ВыгружаемыеРеквизиты.Добавить("ЗонаРазгрузкиСтраховойКомпании");
	ВыгружаемыеРеквизиты.Добавить("ИдентификаторCRM");
	ВыгружаемыеРеквизиты.Добавить("КаналПродаж");
	ВыгружаемыеРеквизиты.Добавить("Клиент");
	ВыгружаемыеРеквизиты.Добавить("КодТТSY");
	ВыгружаемыеРеквизиты.Добавить("КонтактноеЛицо");
	ВыгружаемыеРеквизиты.Добавить("КодЕГАИС");
	ВыгружаемыеРеквизиты.Добавить("КПП");
	ВыгружаемыеРеквизиты.Добавить("ЛичноеПоручительствоПунктаРазгрузки");
	ВыгружаемыеРеквизиты.Добавить("Маршрут");
	ВыгружаемыеРеквизиты.Добавить("Наименование");
	ВыгружаемыеРеквизиты.Добавить("НаименованиеЛогист");
	ВыгружаемыеРеквизиты.Добавить("НаименованиеПолное");
	ВыгружаемыеРеквизиты.Добавить("НомерМагазина");
	ВыгружаемыеРеквизиты.Добавить("ОбменСМТ");
	ВыгружаемыеРеквизиты.Добавить("ОбособленноеПодразделение");
	ВыгружаемыеРеквизиты.Добавить("ПометкаУдаления");
	ВыгружаемыеРеквизиты.Добавить("Предопределенный");
	ВыгружаемыеРеквизиты.Добавить("Телефон");
	ВыгружаемыеРеквизиты.Добавить("Территория");
	ВыгружаемыеРеквизиты.Добавить("Широта");
	ВыгружаемыеРеквизиты.Добавить("ФормироватьАктПерегрузки");
	ВыгружаемыеРеквизиты.Добавить("ШаблонMarkerПакетаSOLVOWMS");
	ВыгружаемыеРеквизиты.Добавить("ШаблонMarker2ПакетаSOLVOWMS");
	ВыгружаемыеРеквизиты.Добавить("ЭтоФилиал");
	ВыгружаемыеРеквизиты.Добавить("ЭтоАльтернативныйАдрес");
	
	Возврат ВыгружаемыеРеквизиты;
	
КонецФункции

Процедура ДобавитьСвязанныеРеквизитыКВыгрузке(Реквизиты)
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	Реквизиты.Добавить(
		адаптер_НастройкиОбмена.ОписаниеРеквизита(
			"Маршрут_Наименование", 
			ОбщегоНазначения.ОписаниеТипаСтрока(150), 
			"Маршрут.Наименование"));
			
	Реквизиты.Добавить(
		адаптер_НастройкиОбмена.ОписаниеРеквизита(
			"КонтактноеЛицо_Наименование", 
			ОбщегоНазначения.ОписаниеТипаСтрока(150), 
			"КонтактноеЛицо.Наименование"));
	
КонецПроцедуры

Процедура ДобавитьПроизвольныеРеквизитыКВыгрузке(РеквизитыИСвойства)
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства, 
		РеквизитыИСвойства.МетаданныеОбъекта, 
		,
		"Лицензия", 
		Новый ОписаниеТипов("СправочникСсылка.ЛицензииПоставщиковАлкогольнойПродукции"));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства, 
		РеквизитыИСвойства.МетаданныеОбъекта, 
		,
		"Слабоалкогольная", 
		Новый ОписаниеТипов("Булево"));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства, 
		РеквизитыИСвойства.МетаданныеОбъекта, 
		,
		"ГоловнойКонтрагент", 
		Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	
КонецПроцедуры

Процедура ЗаполнитьДанныеЛицензии(РеквизитыОбъекта, ПунктНазначения)
	
	Перем адаптер_РаботаСДаннымиИБ;
	адаптер_РаботаСДаннымиИБ = ОбщегоНазначения.ОбщийМодуль("адаптер_РаботаСДаннымиИБ");
	
	Лицензия = РегистрыСведений.бг_ЛицензииПунктовНазначения.ТекущаяЛицензияПунктаНазначения(ПунктНазначения);
	
	Если Лицензия = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	адаптер_РаботаСДаннымиИБ.ЗаполнитьЗначениеРеквизита(
		РеквизитыОбъекта, 
		"Лицензия_ЗначениеРеквизитаИдентификатор",
		Лицензия,
		Новый ТаблицаЗначений);
		
	РеквизитыОбъекта.Вставить(
		"Слабоалкогольная", 
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Лицензия,
			"бг_СлабоалкогольнаяПродукция"));
	
КонецПроцедуры

Процедура ЗаполнитьДанныеКлиента(РеквизитыОбъекта, Клиент)
	
	Если ТипЗнч(Клиент) <> Тип("СправочникСсылка.Контрагенты") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Контрагент", Клиент);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Контрагенты.ГоловнойКонтрагент КАК ГоловнойКонтрагент,
	|	Контрагенты.ГоловнойКонтрагент.ИНН КАК ГоловнойКонтрагентИНН,
	|	Контрагенты.ГоловнойКонтрагент.КПП КАК ГоловнойКонтрагентКПП,
	|	Контрагенты.ГоловнойКонтрагент.бг_КодКлиентаSY КАК КодSY,
	|	Контрагенты.ГоловнойКонтрагент.НаименованиеПолное КАК НаименованиеПолное,
	|	Контрагенты.ГоловнойКонтрагент.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Ссылка = &Контрагент";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	ГоловнойКонтрагент = Новый Структура;
	ГоловнойКонтрагент.Вставить("Идентификатор", Строка(Выборка.ГоловнойКонтрагент.УникальныйИдентификатор()));
	ГоловнойКонтрагент.Вставить("ИНН", Выборка.ГоловнойКонтрагентИНН);
	ГоловнойКонтрагент.Вставить("КПП", Выборка.ГоловнойКонтрагентКПП);
	ГоловнойКонтрагент.Вставить("КодSY", Выборка.КодSY);
	ГоловнойКонтрагент.Вставить("НаименованиеПолное", Выборка.НаименованиеПолное);
	ГоловнойКонтрагент.Вставить("Наименование", Выборка.Наименование);
	
	РеквизитыОбъекта.Вставить("ГоловнойКонтрагент", ГоловнойКонтрагент);
	
КонецПроцедуры

Процедура ДобавитьКлючевыеПоляКВыгрузке(РеквизитыИСвойства)
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.битКаналыПродаж,
		"Наименование");
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.битКаналыПродаж,
		"ИдентификаторCRM");
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.битТерриторииПунктовНазначения,
		"Наименование");
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.битТерриторииПунктовНазначения,
		"ИдентификаторCRM");
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Контрагенты,
		"бг_КодКлиентаSY",
		"КодSY");
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Контрагенты,
		"НаименованиеПолное");
	
КонецПроцедуры

#КонецОбласти
