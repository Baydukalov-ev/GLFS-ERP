#Область ПрограммныйИнтерфейс

&Перед("ЗаполнитьПараметрыВстраивания")
Процедура бг_ЗаполнитьПараметрыВстраивания(Форма, ПараметрыВстраивания) Экспорт
	
	Если Форма.ДоступныеОстаткиПараметрыВстраивания = Неопределено Тогда
		
	    Если ПараметрыВстраивания.Свойство("ОписаниеПолученияДанныхДокумента")
			И ПараметрыВстраивания.Свойство("ИмяТаблицы") Тогда
		
			ЕстьРеквизитТабЧастиУпаковкаПаллета = СтрСравнить(ПараметрыВстраивания.ИмяТаблицы, "Документ.ЗаказКлиента.Товары") = 0;
		
			СхемаЗапроса = Новый СхемаЗапроса;
			СхемаЗапроса.УстановитьТекстЗапроса(ПараметрыВстраивания.ОписаниеПолученияДанныхДокумента);
			ЗапросВыбора = СхемаЗапроса.ПакетЗапросов[0];	
			ИндексДобавляемойКолонки = ЗапросВыбора.Колонки.Количество(); // Будет добавлена колонка с этим индексом.
			Для каждого ОператорВыбора Из ЗапросВыбора.Операторы Цикл
				Если ЕстьРеквизитТабЧастиУпаковкаПаллета Тогда
					ОператорВыбора.ВыбираемыеПоля.Добавить("ТабЧасть.бг_УпаковкаПаллета", ИндексДобавляемойКолонки);
				Иначе	
					ОператорВыбора.ВыбираемыеПоля.Добавить("ТабЧасть.Серия.бг_УпаковкаПаллета", ИндексДобавляемойКолонки);
				КонецЕсли;
				
				ИсточникТабЧасть = ОператорВыбора.Источники.НайтиПоПсевдониму("ТабЧасть");
				ОператорВыбора.Источники.Добавить(
					"РегистрСведений.бг_ХарактеристикиУпаковокПаллет",
					"ХарактеристикиУпаковокПаллет");
				ИсточникХарактеристикиУпаковокПаллет = ОператорВыбора.Источники.НайтиПоПсевдониму("ХарактеристикиУпаковокПаллет");

				ИсточникТабЧасть.Соединения.Добавить(ИсточникХарактеристикиУпаковокПаллет, ИсточникТабЧасть.Источник.Псевдоним + 
					".Номенклатура = ХарактеристикиУпаковокПаллет.Номенклатура");
				Если ЕстьРеквизитТабЧастиУпаковкаПаллета Тогда	
					ИсточникТабЧасть.Соединения.Добавить(
						ИсточникХарактеристикиУпаковокПаллет, 
						ИсточникТабЧасть.Источник.Псевдоним + ".бг_УпаковкаПаллета = ХарактеристикиУпаковокПаллет.УпаковкаПаллета");	
				Иначе
					ИсточникТабЧасть.Соединения.Добавить(
						ИсточникХарактеристикиУпаковокПаллет, ИсточникТабЧасть.Источник.Псевдоним + 
						".Серия.бг_УпаковкаПаллета = ХарактеристикиУпаковокПаллет.УпаковкаПаллета");	
				КонецЕсли;
				
				ВыбираемыеПоля = ОператорВыбора.ВыбираемыеПоля;
				КоличествоПолей = ВыбираемыеПоля.Количество();
				Для ИндексПоля = 0 По КоличествоПолей - 1 Цикл
					ТекстПоля = Строка(ВыбираемыеПоля[ИндексПоля]);
					Если СтрСравнить(ТекстПоля, ИсточникТабЧасть.Источник.Псевдоним + ".Характеристика") = 0 Тогда	
						ВыражениеПоляХарактеристика = Новый ВыражениеСхемыЗапроса(
							"ЕСТЬNULL(ХарактеристикиУпаковокПаллет.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))"); 
						ВыбираемыеПоля.Установить(ИндексПоля, ВыражениеПоляХарактеристика);	
					КонецЕсли;
				КонецЦикла;
				
			КонецЦикла;
			ЗапросВыбора.Колонки[ИндексДобавляемойКолонки].Псевдоним = "бг_УпаковкаПаллета";	
			ПараметрыВстраивания.ОписаниеПолученияДанныхДокумента = СхемаЗапроса.ПолучитьТекстЗапроса();
			
			Если ЕстьРеквизитТабЧастиУпаковкаПаллета Тогда
				ПараметрыВстраивания.ШаблонСериализацииДанныхФормы.Объект.Товары.Вставить(
					"бг_УпаковкаПаллета", 
					Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения"));
			ИначеЕсли СтрСравнить(ПараметрыВстраивания.ИмяТаблицы, "Документ.ЗаказПереработчику.РаспоряжениеСпецификация") = 0 Тогда
				ПараметрыВстраивания.ШаблонСериализацииДанныхФормы.Объект.РаспоряжениеСпецификация.Вставить(
					"Серия", 
					Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
			КонецЕсли;	
			
		КонецЕсли;	
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СериализацияОбъекта

&После("ДобавитьВременнуюТаблицуДанныхДокументаДляВыделенныхСтрок")
Процедура бг_ДобавитьВременнуюТаблицуДанныхДокументаДляВыделенныхСтрок(Запрос, НомераСтрок)
	
	КолонкиВТ = Запрос.МенеджерВременныхТаблиц.Таблицы["ВременнаяТаблицаДанныхДокумента"].Колонки;
	
	Если КолонкиВТ.Найти("бг_УпаковкаПаллета") <> Неопределено Тогда
		СхемаЗапроса = Новый СхемаЗапроса;
		СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
		ЗапросВыбора = СхемаЗапроса.ПакетЗапросов[0];	
		ИндексДобавляемойКолонки = ЗапросВыбора.Колонки.Количество(); // Будет добавлена колонка с этим индексом.
		Для каждого ОператорВыбора Из ЗапросВыбора.Операторы Цикл
			ИсточникТабЧасть = ОператорВыбора.Источники.НайтиПоПсевдониму("ТабЧасть");
			Если ТипЗнч(ИсточникТабЧасть.Источник) = Тип("ОписаниеВременнойТаблицыСхемыЗапроса") Тогда
				ИсточникТабЧасть.Источник.ДоступныеПоля.Добавить(
					"бг_УпаковкаПаллета",
					Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения"));
			КонецЕсли;
			ОператорВыбора.ВыбираемыеПоля.Добавить("ТабЧасть.бг_УпаковкаПаллета", ИндексДобавляемойКолонки);			
		КонецЦикла;
		ЗапросВыбора.Колонки[ИндексДобавляемойКолонки].Псевдоним = "бг_УпаковкаПаллета";	
		ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
		
		Запрос.Текст = "УНИЧТОЖИТЬ ВременнаяТаблицаДанныхДокументаДляВыделенныхСтрок";
		Запрос.Выполнить();
		
		Запрос.Текст = ТекстЗапроса;
		Запрос.Выполнить();
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти

#КонецОбласти


