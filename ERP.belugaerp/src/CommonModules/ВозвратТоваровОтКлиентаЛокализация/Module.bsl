#Область ПрограммныйИнтерфейс

#Область Проведение

&После("ЗарегистрироватьУчетныеМеханизмы")
Процедура бг_ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента)
	
	МеханизмыДокумента.Добавить("бг_УчетБанковскихГарантий");
	
КонецПроцедуры

#КонецОбласти

#Область АкцизыПоПриобретеннымЦенностям

Функция бг_ТребуетсяЗаполнениеАкцизовПоПриобретеннымЦенностям(ДокументОбъект) Экспорт
	Если Не бг_УчетБанковскихГарантий.ВестиУчетАкцизовПоПриобретеннымЦенностям(ДокументОбъект) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если бг_УчетБанковскихГарантий.ТребуетсяЗаполнениеАкцизовПоПриобретеннымЦенностям(ДокументОбъект) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Серия КАК Серия,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Серия КАК Серия,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ Данные
	|ИЗ
	|	Товары КАК Товары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АкцизВыпускПродукции.Номенклатура,
	|	АкцизВыпускПродукции.СерияНоменклатуры,
	|	-АкцизВыпускПродукции.Количество
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностямВыпускПродукции КАК АкцизВыпускПродукции
	|ГДЕ
	|	АкцизВыпускПродукции.Регистратор = &Ссылка
	|	И АкцизВыпускПродукции.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Данные.Номенклатура КАК Номенклатура,
	|	Данные.Серия КАК Серия,
	|	СУММА(Данные.Количество) КАК Количество
	|ИЗ
	|	Данные КАК Данные
	|
	|СГРУППИРОВАТЬ ПО
	|	Данные.Номенклатура,
	|	Данные.Серия
	|
	|ИМЕЮЩИЕ
	|	СУММА(Данные.Количество) <> 0";
	Запрос.УстановитьПараметр("Товары", ДокументОбъект.Товары.Выгрузить(, "Номенклатура, Серия, Количество"));
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
КонецФункции

Функция бг_СтатусыСостоянияСписываемогоСырья() Экспорт
	СтатусыСостояния = бг_УчетБанковскихГарантий.СтатусыСостоянияСписываемогоСырья();
	
	СтатусыСостояния.СтатусыАкциза.Добавить(Перечисления.бг_СостоянияОплатыВходящегоАкциза.АвансовыйАкцизВСырье);
	СтатусыСостояния.СтатусыАкциза.Добавить(Перечисления.бг_СостоянияОплатыВходящегоАкциза.АкцизПоОплаченнымМЦ);
	СтатусыСостояния.СтатусыАкциза.Добавить(Перечисления.бг_СостоянияОплатыВходящегоАкциза.АкцизПоНеОплаченнымМЦ);
	
	СтатусыСостояния.СостоянияСырья.Добавить(Перечисления.бг_СостоянияПодакцизногоСырья.ВРеализованнойПродукции);
	СтатусыСостояния.СостоянияСырья.Добавить(Перечисления.бг_СостоянияПодакцизногоСырья.ВОжиданииПодтвержденияЭкспорта);
	СтатусыСостояния.СостоянияСырья.Добавить(Перечисления.бг_СостоянияПодакцизногоСырья.ВОжиданииПодтвержденияЭкспортаЕАЭС);
	СтатусыСостояния.СостоянияСырья.Добавить(Перечисления.бг_СостоянияПодакцизногоСырья.ВОжиданииПодтвержденияЭкспортаГарантииЕАЭС);
	СтатусыСостояния.СостоянияСырья.Добавить(Перечисления.бг_СостоянияПодакцизногоСырья.ВОжиданииПодтвержденияЭкспортаГарантии);
	
	СтатусыСостояния.СостоянияСырья.Добавить(Перечисления.бг_СостоянияПодакцизногоСырья.ПодтвержденЭкспорт);
	СтатусыСостояния.СостоянияСырья.Добавить(Перечисления.бг_СостоянияПодакцизногоСырья.ПодтвержденЭкспортЕАЭС);
	СтатусыСостояния.СостоянияСырья.Добавить(Перечисления.бг_СостоянияПодакцизногоСырья.ПодтвержденЭкспортГарантии);
	СтатусыСостояния.СостоянияСырья.Добавить(Перечисления.бг_СостоянияПодакцизногоСырья.ПодтвержденЭкспортЕАЭСГарантии);
	
	Возврат СтатусыСостояния;
КонецФункции

// бг_АкцизПоПриобретеннымЦенностям
Функция бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностям() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Документ.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	Документ.Организация КАК Организация,
	|	Акцизы.Номенклатура КАК Номенклатура,
	|	Акцизы.СерияНоменклатуры КАК СерияНоменклатуры,
	|	Акцизы.Сделка КАК Сделка,
	|	Акцизы.СостояниеСырья КАК СостояниеСырья,
	|	Акцизы.СтатусАкциза КАК СтатусАкциза,
	|	Акцизы.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	Акцизы.Продукция КАК Продукция,
	|	Акцизы.СерияПродукции КАК СерияПродукции,
	|	Акцизы.Количество КАК Количество
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.бг_АкцизПоПриобретеннымЦенностям КАК Акцизы
	|		ПО Документ.Ссылка = Акцизы.Ссылка
	|ГДЕ
	|	Документ.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
КонецФункции

// бг_АкцизПоПриобретеннымЦенностямВыпускПродукции
Функция бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностямПродукция() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АкцизПоПриобретеннымЦенностям.Продукция КАК Номенклатура,
	|	АкцизПоПриобретеннымЦенностям.СерияПродукции КАК СерияНоменклатуры
	|ПОМЕСТИТЬ Продукция
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.бг_АкцизПоПриобретеннымЦенностям КАК АкцизПоПриобретеннымЦенностям
	|ГДЕ
	|	АкцизПоПриобретеннымЦенностям.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документ.Дата КАК Период,
	|	Документ.Организация КАК Организация,
	|	ВозвратТоваровТовары.Номенклатура КАК Номенклатура,
	|	ВозвратТоваровТовары.Серия КАК СерияНоменклатуры,
	|	ВозвратТоваровТовары.Количество КАК Количество
	|ПОМЕСТИТЬ АкцизыПоПриобретеннымЦенностямПродукция
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.Товары КАК ВозвратТоваровТовары
	|		ПО (Документ.Ссылка = &Ссылка)
	|			И Документ.Ссылка = ВозвратТоваровТовары.Ссылка
	|ГДЕ    ВозвратТоваровТовары.Серия В (ВЫБРАТЬ Продукция.СерияНоменклатуры ИЗ Продукция)
	|	ИЛИ ВозвратТоваровТовары.Серия.бг_СерияРеализации В (ВЫБРАТЬ Продукция.СерияНоменклатуры ИЗ Продукция)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	Организация,
	|	Номенклатура,
	|	СерияНоменклатуры,
	|	ЗНАЧЕНИЕ(Перечисление.бг_ТипыМестХраненияПодакцизнойПродукции.Склад) КАК ТипМестаХранения,
	|	Количество
	|ИЗ
	|	АкцизыПоПриобретеннымЦенностямПродукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	Организация,
	|	Номенклатура,
	|	СерияНоменклатуры.бг_СерияРеализации,
	|	ЗНАЧЕНИЕ(Перечисление.бг_ТипыМестХраненияПодакцизнойПродукции.Склад) КАК ТипМестаХранения,
	|	-Количество
	|ИЗ
	|	АкцизыПоПриобретеннымЦенностямПродукция
	|ГДЕ
	|	СерияНоменклатуры.бг_СерияРеализации <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	Организация,
	|	Номенклатура,
	|	СерияНоменклатуры.бг_СерияРеализации,
	|	ЗНАЧЕНИЕ(Перечисление.бг_ТипыМестХраненияПодакцизнойПродукции.Склад) КАК ТипМестаХранения,
	|	-Количество
	|ИЗ
	|	АкцизыПоПриобретеннымЦенностямПродукция
	|ГДЕ
	|	СерияНоменклатуры.бг_СерияРеализации <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)";
	
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

&После("ОбработкаЗаполнения")
Процедура бг_ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ТТНВходящаяЕГАИС") Тогда
		бг_ЗаполнитьДокументНаОснованииТТНВходящаяЕГАИС(Объект, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

&После("ДополнитьТекстыЗапросовПроведения")
Процедура бг_ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры)
	
	бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностям(Запрос, ТекстыЗапроса, Регистры);
	бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностямПродукция(Запрос, ТекстыЗапроса, Регистры);
	
КонецПроцедуры

Процедура бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностям(Запрос, ТекстыЗапроса, Регистры) Экспорт
	ИмяРегистра = "бг_АкцизПоПриобретеннымЦенностям";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностям(), ИмяРегистра);
	
КонецПроцедуры

Процедура бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностямПродукция(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "бг_АкцизПоПриобретеннымЦенностямВыпускПродукции";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностямПродукция(), ИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура бг_ЗаполнитьДокументНаОснованииТТНВходящаяЕГАИС(Объект, ДанныеЗаполнения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекстЗапроса = бг_ТекстЗапросаДанныеТТНВходящаяЕГАИС();
	Запрос = Новый Запрос(ТекстЗапроса);	
	Запрос.УстановитьПараметр("ТТНВходящаяЕГАИС", ДанныеЗаполнения);
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ВыборкаШапка = ПакетЗапросов[0].Выбрать();
	ТаблицаТоваров = ПакетЗапросов[1].Выгрузить();	
	
	ВыборкаШапка.Следующий();
	ЗаполнитьЗначенияСвойств(Объект, ВыборкаШапка);
	Объект.Соглашение = Справочники.СоглашенияСКлиентами.бг_СоглашениеПоПартнеруИОрганизации(
		Объект.Партнер, Объект.Организация);
	Объект.бг_ЗаполнитьУсловияПродажПоУмолчанию(Истина);
	Если ЗначениеЗаполнено(Объект.Соглашение) Тогда
		Объект.Договор = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Соглашение, "бг_Договор");
		Если ЗначениеЗаполнено(Объект.Договор) Тогда
			Объект.ОплатаВВалюте = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Договор, "ОплатаВВалюте");
		КонецЕсли;
	КонецЕсли;
		
	Объект.Товары.Загрузить(ТаблицаТоваров);	
	
КонецПроцедуры

Функция бг_ТекстЗапросаДанныеТТНВходящаяЕГАИС()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТТНВходящаяЕГАИС.Организация КАК Организация,
	|	ТТНВходящаяЕГАИС.ТорговыйОбъект КАК Склад,
	|	ВЫБОР
	|		КОГДА НЕ ЕСТЬNULL(ТТНВходящаяЕГАИС.Грузоотправитель.бг_ПунктРазгрузки, ЗНАЧЕНИЕ(Справочник.битПунктыНазначения.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.битПунктыНазначения.ПустаяСсылка)
	|				И НЕ ТТНВходящаяЕГАИС.Грузоотправитель.бг_ПунктРазгрузки.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|			ТОГДА ТТНВходящаяЕГАИС.Грузоотправитель.бг_ПунктРазгрузки.Партнер
	|		ИНАЧЕ ТТНВходящаяЕГАИС.Грузоотправитель.Контрагент.Партнер
	|	КОНЕЦ КАК Партнер,
	|	ВЫБОР
	|		КОГДА НЕ ЕСТЬNULL(ТТНВходящаяЕГАИС.Грузоотправитель.бг_ПунктРазгрузки, ЗНАЧЕНИЕ(Справочник.битПунктыНазначения.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.битПунктыНазначения.ПустаяСсылка)
	|				И НЕ ТТНВходящаяЕГАИС.Грузоотправитель.бг_ПунктРазгрузки.ГоловнойКонтрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ТТНВходящаяЕГАИС.Грузоотправитель.бг_ПунктРазгрузки.ГоловнойКонтрагент
	|		ИНАЧЕ ТТНВходящаяЕГАИС.Грузоотправитель.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ТТНВходящаяЕГАИС.ДатаТТН КАК ДатаВходящегоДокумента,
	|	ТТНВходящаяЕГАИС.НомерТТН КАК НомерВходящегоДокумента
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС КАК ТТНВходящаяЕГАИС
	|ГДЕ
	|	ТТНВходящаяЕГАИС.Ссылка = &ТТНВходящаяЕГАИС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТТНВходящаяЕГАИСТовары.Номенклатура КАК Номенклатура,
	|	ТТНВходящаяЕГАИСТовары.Характеристика КАК Характеристика,
	|	ТТНВходящаяЕГАИСТовары.Серия КАК Серия,
	|	ТТНВходящаяЕГАИСТовары.Цена КАК Цена,
	|	ТТНВходящаяЕГАИСТовары.Количество КАК Количество,
	|	ТТНВходящаяЕГАИСТовары.Количество КАК КоличествоУпаковок,
	|	ТТНВходящаяЕГАИСТовары.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ТТНВходящаяЕГАИСТовары.Номенклатура.ВестиУчетПоГТД
	|				И ТТНВходящаяЕГАИСТовары.Номенклатура.ИмпортнаяАлкогольнаяПродукция
	|			ТОГДА ТТНВходящаяЕГАИСТовары.Серия.Справка2ЕГАИС.Справка1.бг_НомерГТД
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|	КОНЕЦ КАК НомерГТД
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК ТТНВходящаяЕГАИСТовары
	|ГДЕ
	|	ТТНВходящаяЕГАИСТовары.Ссылка = &ТТНВходящаяЕГАИС";

	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти
