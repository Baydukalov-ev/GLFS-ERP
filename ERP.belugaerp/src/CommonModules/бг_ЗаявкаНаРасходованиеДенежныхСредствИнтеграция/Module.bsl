#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьЗагружаемыйОбъект(ЗагружаемыйОбъект, СтруктураОбъекта, СписокСвойств, ИсключаяСвойства, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗагружаемыйОбъект.ЭтоНовый() Тогда
		ЗагружаемыйОбъект.ДополнительныеСвойства.Вставить("СтандартнаяЗаписьНеТребуется", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновитьЗаявку = Ложь;
	
	Если СтруктураОбъекта.Свойство("status") Тогда
		ТекущийСтатус = ЗагружаемыйОбъект.Статус;
		Если СтруктураОбъекта.status = "Авторизовано" Или СтруктураОбъекта.status = "Отменено" Тогда
			ЗагружаемыйОбъект.Статус = Перечисления.СтатусыЗаявокНаРасходованиеДенежныхСредств.НеСогласована;
		ИначеЕсли СтруктураОбъекта.status = "На оплате" Или СтруктураОбъекта.status = "Оплачено" Тогда
			ЗагружаемыйОбъект.Статус = Перечисления.СтатусыЗаявокНаРасходованиеДенежныхСредств.КОплате;
		ИначеЕсли СтруктураОбъекта.status = "Отклонено" Тогда
			ЗагружаемыйОбъект.Статус = Перечисления.СтатусыЗаявокНаРасходованиеДенежныхСредств.Отклонена;
		КонецЕсли;
		ОбновитьЗаявку = ТекущийСтатус <> ЗагружаемыйОбъект.Статус;
	КонецЕсли;
	
	Если ЗагружаемыйОбъект.бг_НомерЗаявкиМагистраль <> СтруктураОбъекта.Номер
		Или ЗагружаемыйОбъект.бг_СтатусЗаявкиМагистраль <> СтруктураОбъекта.status Тогда
		
		ЗагружаемыйОбъект.бг_НомерЗаявкиМагистраль = СтруктураОбъекта.Номер;
		ЗагружаемыйОбъект.бг_СтатусЗаявкиМагистраль = СтруктураОбъекта.status;
		
		ОбновитьЗаявку = Истина;
		
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Платежи")
		И ЗначениеЗаполнено(СтруктураОбъекта.Платежи) Тогда
		Для каждого Платеж Из СтруктураОбъекта.Платежи Цикл
			ЗаполнитьСчетОплатыВДокументе(ЗагружаемыйОбъект, Платеж);
			ОбновитьЗаявку = Истина;
		КонецЦикла;
	КонецЕсли;
	
	Если Не ОбновитьЗаявку Тогда
		ЗагружаемыйОбъект.ДополнительныеСвойства.Вставить("СтандартнаяЗаписьНеТребуется", Истина);
		Возврат;
	КонецЕсли;
	
	Комментарий = НСтр("ru = 'СКП: %1 от %2 - %3'");
	ЗагружаемыйОбъект.Комментарий = СтрШаблон(Комментарий, 
		СтруктураОбъекта.Номер, СтруктураОбъекта.Дата, СтруктураОбъекта.status);
	
КонецПроцедуры

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	Реквизиты = ВыгружаемыеРеквизиты(); 
	
	ДобавитьРеквизитыЧерезТочкуКВыгрузке(Реквизиты);
	
	адаптер_НастройкиОбмена.УстановитьРеквизиты(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		Реквизиты);

	ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства);
	ДобавитьКлючевыеПоляКВыгрузке(РеквизитыИСвойства);
	
КонецПроцедуры	

Процедура ЗаполнитьТекстыЗапросовУсловиями(ТекстЗапроса, ТекстЗапросаТаблицаКлючей, ПараметрыЗапроса, НастройкаВыгрузки, Объект, СтандартнаяОбработка) Экспорт

	ТекстЗапроса = бг_ОбщегоНазначенияСервер.ТекстЗапросаБезОбращенияЧерезТочкуКNull(ТекстЗапроса);
	ТекстЗапросаТаблицаКлючей = бг_ОбщегоНазначенияСервер.ТекстЗапросаБезОбращенияЧерезТочкуКNull(ТекстЗапросаТаблицаКлючей);
	
КонецПроцедуры

Функция ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения) Экспорт
	
	Перем адаптер_РаботаСДаннымиИБ;
	адаптер_РаботаСДаннымиИБ = ОбщегоНазначения.ОбщийМодуль("адаптер_РаботаСДаннымиИБ");
	
	ПараметрыВыполненияЗапросов = адаптер_РаботаСДаннымиИБ.ПолучитьПараметрыВыполненияЗапросов(Объект, ДанныеСообщения);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаВыгружаемыхДанных();
	Запрос.УстановитьПараметр("Ссылка", ПараметрыВыполненияЗапросов.ПараметрыЗапроса.Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	Реквизиты = адаптер_РаботаСДаннымиИБ.РезультатЗапросаВСтруктуруРеквизитов(
		РезультатЗапроса,
		ПараметрыВыполненияЗапросов.ТаблицаКлючей,
		ДанныеСообщения);
	
	Если Не Реквизиты[0].Свойство("СуммаВзаиморасчетов") Тогда
		Реквизиты[0].Вставить("СуммаНДСВзаиморасчетов", 0);
	Иначе
		ПроцентНДСЧислитель = 0.2;
		ПроцентНДСЗнаменатель = 1.2;
		Реквизиты[0].Вставить("СуммаНДСВзаиморасчетов",
			Окр(Реквизиты[0].СуммаВзаиморасчетов * ПроцентНДСЧислитель / ПроцентНДСЗнаменатель, 2));
	КонецЕсли;
	
	Если Реквизиты.Количество() > 0 Тогда
		Если Реквизиты[0].status = Перечисления.СтатусыЗаявокНаРасходованиеДенежныхСредств.НеСогласована Тогда
			Реквизиты[0].Вставить("status", "Авторизовано");
		ИначеЕсли Реквизиты[0].status = Перечисления.СтатусыЗаявокНаРасходованиеДенежныхСредств.Согласована Тогда
			Реквизиты[0].Вставить("status", "На оплате");
		ИначеЕсли Реквизиты[0].status = Перечисления.СтатусыЗаявокНаРасходованиеДенежныхСредств.КОплате Тогда
			Реквизиты[0].Вставить("status", "Оплачено");
		Иначе
			Реквизиты[0].Вставить("status", "Отменено");
		КонецЕсли;
	КонецЕсли;

	ДанныеВыгружаемогоОбъекта = Новый Структура;
	ДанныеВыгружаемогоОбъекта.Вставить("ПолноеИмя", ПараметрыВыполненияЗапросов.ПолноеИмя);
	ДанныеВыгружаемогоОбъекта.Вставить("Реквизиты", Реквизиты);
	
	Возврат ДанныеВыгружаемогоОбъекта;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВыгружаемыеРеквизиты()

	ВыгружаемыеРеквизиты = Новый Массив;

	ВыгружаемыеРеквизиты.Добавить("Дата");
	ВыгружаемыеРеквизиты.Добавить("Номер");
	ВыгружаемыеРеквизиты.Добавить("Проведен");
	ВыгружаемыеРеквизиты.Добавить("ПометкаУдаления");
	ВыгружаемыеРеквизиты.Добавить("Организация");
	ВыгружаемыеРеквизиты.Добавить("Контрагент");
	ВыгружаемыеРеквизиты.Добавить("НазначениеПлатежа");
	ВыгружаемыеРеквизиты.Добавить("БанковскийСчетКонтрагента");
	ВыгружаемыеРеквизиты.Добавить("ДатаПлатежа");
	ВыгружаемыеРеквизиты.Добавить("Валюта");
	ВыгружаемыеРеквизиты.Добавить("Комментарий");
	
	Возврат ВыгружаемыеРеквизиты;

КонецФункции

Процедура ДобавитьРеквизитыЧерезТочкуКВыгрузке(Реквизиты)
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	Реквизиты.Добавить(
		адаптер_НастройкиОбмена.ОписаниеРеквизита(
			"БанковскийСчетБанк",
			Новый ОписаниеТипов("СправочникСсылка.КлассификаторБанков"),
			"БанковскийСчетКонтрагента.Банк"));
	
	Реквизиты.Добавить(
		адаптер_НастройкиОбмена.ОписаниеРеквизита(
			"БанковскийСчетНомерСчета",
			ОбщегоНазначения.ОписаниеТипаСтрока(43),
			"БанковскийСчетКонтрагента.НомерСчета"));
	
	Реквизиты.Добавить(
		адаптер_НастройкиОбмена.ОписаниеРеквизита(
			"БанковскийСчетВалютаСчета",
			Новый ОписаниеТипов("СправочникСсылка.Валюты"),
			"БанковскийСчетКонтрагента.ВалютаДенежныхСредств"));
	
	Реквизиты.Добавить(
		адаптер_НастройкиОбмена.ОписаниеРеквизита(
			"Ответственный",
			ОбщегоНазначения.ОписаниеТипаСтрока(100),
			"КтоЗаявил.Наименование"));
	
	Реквизиты.Добавить(
		адаптер_НастройкиОбмена.ОписаниеРеквизита(
			"ДоговорНомер",
			ОбщегоНазначения.ОписаниеТипаСтрока(30),
			"Договор.Номер"));
	
	Реквизиты.Добавить(
		адаптер_НастройкиОбмена.ОписаниеРеквизита(
			"ДоговорДата",
			ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата),
			"Договор.Дата"));
	
	// Факторинг
	Реквизиты.Добавить(
		адаптер_НастройкиОбмена.ОписаниеРеквизита(
			"Фактор",
			Новый ОписаниеТипов("СправочникСсылка.Контрагенты"),
			"бг_БанковскийСчетФактора.Владелец"));
	
	Реквизиты.Добавить(
		адаптер_НастройкиОбмена.ОписаниеРеквизита(
			"ФакторБанковскийСчетБанк",
			Новый ОписаниеТипов("СправочникСсылка.КлассификаторБанков"),
			"бг_БанковскийСчетФактора.Банк"));
	
	Реквизиты.Добавить(
		адаптер_НастройкиОбмена.ОписаниеРеквизита(
			"ФакторБанковскийСчетНомерСчета",
			ОбщегоНазначения.ОписаниеТипаСтрока(43),
			"бг_БанковскийСчетФактора.НомерСчета"));
	
	Реквизиты.Добавить(
		адаптер_НастройкиОбмена.ОписаниеРеквизита(
			"ФакторБанковскийСчетВалютаСчета",
			Новый ОписаниеТипов("СправочникСсылка.Валюты"),
			"бг_БанковскийСчетФактора.ВалютаДенежныхСредств"));
	
КонецПроцедуры

Процедура ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства)
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	// Реквизиты шапки
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		,
		"status",
		ОбщегоНазначения.ОписаниеТипаСтрока(20));
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		,
		"ВалютаВзаиморасчетов",
		Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		,
		"Сумма",
		ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		,
		"СуммаНДС",
		ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		,
		"СуммаВзаиморасчетов",
		ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		,
		"СуммаНДСВзаиморасчетов",
		ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		,
		"СтатьяДвиженияДенежныхСредствКод",
		ОбщегоНазначения.ОписаниеТипаСтрока(30));
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		,
		"НомерВходящегоДокумента",
		ОбщегоНазначения.ОписаниеТипаСтрока(30));
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		,
		"ДатаВходящегоДокумента",
		ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		"бг_БанковскийСчетФактора",
		"ФакторБанковскийСчет",
		Новый ОписаниеТипов("СправочникСсылка.БанковскиеСчетаКонтрагентов"));
	
КонецПроцедуры

Процедура ДобавитьКлючевыеПоляКВыгрузке(РеквизитыИСвойства)
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Организации,
		"ИНН");
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Организации,
		"КПП");
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Контрагенты,
		"ИНН");
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Контрагенты,
		"КПП");
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Контрагенты,
		"Наименование");
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Контрагенты,
		"НаименованиеПолное");
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Контрагенты,
		"СтранаРегистрации",
		,
		Новый ОписаниеТипов("СправочникСсылка.СтраныМира"));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Контрагенты,
		"ЮрФизЛицо",
		,
		Новый ОписаниеТипов("ПеречислениеСсылка.ЮрФизЛицо"));
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.СтраныМира,
		"Код");
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.КлассификаторБанков,
		"Код");
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Валюты,
		"Наименование");
	
КонецПроцедуры

Функция ТекстЗапросаВыгружаемыхДанных()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СУММА(ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.Сумма) КАК Сумма,
	|	СУММА(ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов,
	|	СУММА(ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.СуммаНДС) КАК СуммаНДС,
	|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств.Код КАК СтатьяДвиженияДенежныхСредствКод,
	|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.ОбъектРасчетов.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.ОбъектРасчетов.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.ОбъектРасчетов.Договор.Номер КАК ДоговорНомер,
	|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.ОбъектРасчетов.Договор.Дата КАК ДоговорДата,
	|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_Расшифровка
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеДенежныхСредств.РасшифровкаПлатежа КАК ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа
	|
	|ГДЕ
	|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств.Код,
	|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.ВалютаВзаиморасчетов,
	|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.ОбъектРасчетов.НомерВходящегоДокумента,
	|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.ОбъектРасчетов.ДатаВходящегоДокумента,
	|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.ОбъектРасчетов.Договор.Номер,
	|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.ОбъектРасчетов.Договор.Дата,
	|	ЗаявкаНаРасходованиеДенежныхСредствРасшифровкаПлатежа.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК Идентификатор,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Ссылка КАК ТаблицаКлючей,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Дата КАК Дата,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Номер КАК Номер,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Проведен КАК Проведен,
	|	ЗаявкаНаРасходованиеДенежныхСредств.ПометкаУдаления КАК ПометкаУдаления,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Статус КАК status,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Организация КАК Организация_ЗначениеРеквизитаИдентификатор,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Организация КАК Организация_ЗначениеРеквизитаТаблицаКлючей,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Организация.КПП КАК Организация_ЗначениеРеквизитаКПП,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Организация.ИНН КАК Организация_ЗначениеРеквизитаИНН,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Контрагент КАК Контрагент_ЗначениеРеквизитаИдентификатор,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Контрагент КАК Контрагент_ЗначениеРеквизитаТаблицаКлючей,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Контрагент.КПП КАК Контрагент_ЗначениеРеквизитаКПП,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Контрагент.ИНН КАК Контрагент_ЗначениеРеквизитаИНН,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Контрагент.НаименованиеПолное КАК Контрагент_ЗначениеРеквизитаНаименованиеПолное,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Контрагент.Наименование КАК Контрагент_ЗначениеРеквизитаНаименование,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Контрагент.СтранаРегистрации КАК Контрагент_ЗначениеРеквизитаСтранаРегистрации_ЗначениеРеквизитаИдентификатор,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Контрагент.СтранаРегистрации.Ссылка КАК Контрагент_ЗначениеРеквизитаСтранаРегистрации_ЗначениеРеквизитаТаблицаКлючей,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Контрагент.СтранаРегистрации.Код КАК Контрагент_ЗначениеРеквизитаСтранаРегистрации_ЗначениеРеквизитаКод,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Контрагент.СтранаРегистрации.КодАльфа2 КАК Контрагент_ЗначениеРеквизитаСтранаРегистрации_ЗначениеРеквизитаКодАльфа2,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Контрагент.ЮрФизЛицо КАК Контрагент_ЗначениеРеквизитаЮрФизЛицо_ЗначениеРеквизитаЗначениеПеречисления,
	|	ЗаявкаНаРасходованиеДенежныхСредств.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента_ЗначениеРеквизитаИдентификатор,
	|	ЗаявкаНаРасходованиеДенежныхСредств.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента_ЗначениеРеквизитаТаблицаКлючей,
	|	ЗаявкаНаРасходованиеДенежныхСредств.БанковскийСчетКонтрагента.Банк КАК БанковскийСчетБанк_ЗначениеРеквизитаИдентификатор,
	|	ЗаявкаНаРасходованиеДенежныхСредств.БанковскийСчетКонтрагента.Банк КАК БанковскийСчетБанк_ЗначениеРеквизитаТаблицаКлючей,
	|	ЗаявкаНаРасходованиеДенежныхСредств.БанковскийСчетКонтрагента.Банк.Код КАК БанковскийСчетБанк_ЗначениеРеквизитаКод,
	|	ЗаявкаНаРасходованиеДенежныхСредств.БанковскийСчетКонтрагента.НомерСчета КАК БанковскийСчетНомерСчета,
	|	ЗаявкаНаРасходованиеДенежныхСредств.БанковскийСчетКонтрагента.ВалютаДенежныхСредств КАК БанковскийСчетВалютаСчета_ЗначениеРеквизитаИдентификатор,
	|	ЗаявкаНаРасходованиеДенежныхСредств.БанковскийСчетКонтрагента.ВалютаДенежныхСредств КАК БанковскийСчетВалютаСчета_ЗначениеРеквизитаТаблицаКлючей,
	|	ЗаявкаНаРасходованиеДенежныхСредств.БанковскийСчетКонтрагента.ВалютаДенежныхСредств.Код КАК БанковскийСчетВалютаСчета_ЗначениеРеквизитаКод,
	|	ЗаявкаНаРасходованиеДенежныхСредств.БанковскийСчетКонтрагента.ВалютаДенежныхСредств.Наименование КАК БанковскийСчетВалютаСчета_ЗначениеРеквизитаНаименование,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора.Владелец КАК Фактор_ЗначениеРеквизитаИдентификатор,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора.Владелец КАК Фактор_ЗначениеРеквизитаТаблицаКлючей,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора.Владелец.КПП КАК Фактор_ЗначениеРеквизитаКПП,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора.Владелец.ИНН КАК Фактор_ЗначениеРеквизитаИНН,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора.Владелец.НаименованиеПолное КАК Фактор_ЗначениеРеквизитаНаименованиеПолное,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора.Владелец.НаименованиеПолное КАК Фактор_ЗначениеРеквизитаНаименование,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора.Владелец.СтранаРегистрации КАК Фактор_ЗначениеРеквизитаСтранаРегистрации_ЗначениеРеквизитаИдентификатор,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора.Владелец.СтранаРегистрации.Ссылка КАК Фактор_ЗначениеРеквизитаСтранаРегистрации_ЗначениеРеквизитаТаблицаКлючей,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора.Владелец.СтранаРегистрации.Код КАК Фактор_ЗначениеРеквизитаСтранаРегистрации_ЗначениеРеквизитаКод,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора.Владелец.СтранаРегистрации.КодАльфа2 КАК Фактор_ЗначениеРеквизитаСтранаРегистрации_ЗначениеРеквизитаКодАльфа2,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора.Владелец.ЮрФизЛицо КАК Фактор_ЗначениеРеквизитаЮрФизЛицо_ЗначениеРеквизитаЗначениеПеречисления,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора КАК ФакторБанковскийСчет_ЗначениеРеквизитаИдентификатор,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора КАК ФакторБанковскийСчет_ЗначениеРеквизитаТаблицаКлючей,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора.Банк КАК ФакторБанковскийСчетБанк_ЗначениеРеквизитаИдентификатор,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора.Банк КАК ФакторБанковскийСчетБанк_ЗначениеРеквизитаТаблицаКлючей,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора.Банк.Код КАК ФакторБанковскийСчетБанк_ЗначениеРеквизитаКод,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора.НомерСчета КАК ФакторБанковскийСчетНомерСчета,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора.ВалютаДенежныхСредств КАК ФакторБанковскийСчетВалютаСчета_ЗначениеРеквизитаИдентификатор,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора.ВалютаДенежныхСредств КАК ФакторБанковскийСчетВалютаСчета_ЗначениеРеквизитаТаблицаКлючей,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора.ВалютаДенежныхСредств.Код КАК ФакторБанковскийСчетВалютаСчета_ЗначениеРеквизитаКод,
	|	ЗаявкаНаРасходованиеДенежныхСредств.бг_БанковскийСчетФактора.ВалютаДенежныхСредств.Наименование КАК ФакторБанковскийСчетВалютаСчета_ЗначениеРеквизитаНаименование,
	|	ПРЕДСТАВЛЕНИЕ(ЗаявкаНаРасходованиеДенежныхСредств.КтоЗаявил) КАК Ответственный,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Валюта.Код КАК Валюта_ЗначениеРеквизитаКод,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Валюта.Наименование КАК Валюта_ЗначениеРеквизитаНаименование,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Валюта КАК Валюта_ЗначениеРеквизитаИдентификатор,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Валюта КАК Валюта_ЗначениеРеквизитаТаблицаКлючей,
	|	ЗаявкаНаРасходованиеДенежныхСредств.НазначениеПлатежа КАК НазначениеПлатежа,
	|	ЗаявкаНаРасходованиеДенежныхСредств.ДатаПлатежа КАК ДатаПлатежа,
	|	ЗаявкаНаРасходованиеДенежныхСредств.Комментарий КАК Комментарий,
	|	ВТ_Расшифровка.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	ВТ_Расшифровка.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	ВТ_Расшифровка.ДоговорНомер КАК ДоговорНомер,
	|	ВТ_Расшифровка.ДоговорДата КАК ДоговорДата,
	|	ВТ_Расшифровка.Сумма КАК Сумма,
	|	ВТ_Расшифровка.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ВТ_Расшифровка.СуммаНДС КАК СуммаНДС,
	|	0 КАК СуммаНДСВзаиморасчетов,
	|	ВТ_Расшифровка.ВалютаВзаиморасчетов.Код КАК ВалютаВзаиморасчетов_ЗначениеРеквизитаКод,
	|	ВТ_Расшифровка.ВалютаВзаиморасчетов.Наименование КАК ВалютаВзаиморасчетов_ЗначениеРеквизитаНаименование,
	|	ВТ_Расшифровка.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов_ЗначениеРеквизитаИдентификатор,
	|	ВТ_Расшифровка.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов_ЗначениеРеквизитаТаблицаКлючей,
	|	ВТ_Расшифровка.СтатьяДвиженияДенежныхСредствКод КАК СтатьяДвиженияДенежныхСредствКод
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Расшифровка КАК ВТ_Расшифровка
	|		ПО ЗаявкаНаРасходованиеДенежныхСредств.Ссылка = ВТ_Расшифровка.Ссылка
	|ГДЕ
	|	ЗаявкаНаРасходованиеДенежныхСредств.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;

КонецФункции

Процедура ЗаполнитьСчетОплатыВДокументе(ЗагружаемыйОбъект, Платеж)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БанковскиеСчетаОрганизаций.Ссылка КАК Счет
		|ИЗ
		|	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизаций
		|ГДЕ
		|	БанковскиеСчетаОрганизаций.Владелец = &Владелец
		|	И БанковскиеСчетаОрганизаций.НомерСчета = &НомерСчета";
	
	Запрос.УстановитьПараметр("Владелец", ЗагружаемыйОбъект.Организация);
	Запрос.УстановитьПараметр("НомерСчета", Платеж.НомерРСОтправителя);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗагружаемыйОбъект.БанковскийСчет = Выборка.Счет;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
