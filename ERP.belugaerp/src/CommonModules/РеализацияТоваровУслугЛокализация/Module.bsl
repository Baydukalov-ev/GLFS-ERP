#Область ПрограммныйИнтерфейс

#Область Проведение

&После("ЗарегистрироватьУчетныеМеханизмы")
Процедура бг_ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента)
	
	МеханизмыДокумента.Добавить("бг_УчетБанковскихГарантий");
	МеханизмыДокумента.Добавить("бг_РасчетыСПокупателямиРозница");
	
КонецПроцедуры

#КонецОбласти

#Область АкцизыПоПриобретеннымЦенностям

Функция бг_ТребуетсяЗаполнениеАкцизовПоПриобретеннымЦенностям(ДокументОбъект) Экспорт
	Если Не бг_УчетБанковскихГарантий.ВестиУчетАкцизовПоПриобретеннымЦенностям(ДокументОбъект) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если бг_УчетБанковскихГарантий.ТребуетсяЗаполнениеАкцизовПоПриобретеннымЦенностям(ДокументОбъект) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Серия КАК Серия,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Серия КАК Серия,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ Данные
	|ИЗ
	|	Товары КАК Товары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АкцизВыпускПродукции.Номенклатура,
	|	АкцизВыпускПродукции.СерияНоменклатуры,
	|	-АкцизВыпускПродукции.Количество
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностямВыпускПродукции КАК АкцизВыпускПродукции
	|ГДЕ
	|	АкцизВыпускПродукции.Регистратор = &Ссылка
	|	И АкцизВыпускПродукции.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Данные.Номенклатура КАК Номенклатура,
	|	Данные.Серия КАК Серия,
	|	СУММА(Данные.Количество) КАК Количество
	|ИЗ
	|	Данные КАК Данные
	|
	|СГРУППИРОВАТЬ ПО
	|	Данные.Номенклатура,
	|	Данные.Серия
	|
	|ИМЕЮЩИЕ
	|	СУММА(Данные.Количество) <> 0";
	Запрос.УстановитьПараметр("Товары", ДокументОбъект.Товары.Выгрузить(, "Номенклатура, Серия, Количество"));
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
КонецФункции

Функция бг_СтатусыСостоянияСписываемогоСырья() Экспорт
	СтатусыСостояния = бг_УчетБанковскихГарантий.СтатусыСостоянияСписываемогоСырья();
	
	СтатусыСостояния.СтатусыАкциза.Добавить(Перечисления.бг_СостоянияОплатыВходящегоАкциза.АвансовыйАкцизВСырье);
	СтатусыСостояния.СтатусыАкциза.Добавить(Перечисления.бг_СостоянияОплатыВходящегоАкциза.АкцизПоОплаченнымМЦ);
	СтатусыСостояния.СтатусыАкциза.Добавить(Перечисления.бг_СостоянияОплатыВходящегоАкциза.АкцизПоНеОплаченнымМЦ);
	СтатусыСостояния.СтатусыАкциза.Добавить(Перечисления.бг_СостоянияОплатыВходящегоАкциза.АкцизПоВозвращеннойПродукции);
	
	СтатусыСостояния.СостоянияСырья.Добавить(Перечисления.бг_СостоянияПодакцизногоСырья.ВГотовойПродукции);
	
	Возврат СтатусыСостояния;
КонецФункции

Функция бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностям() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Документ.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	Документ.Организация КАК Организация,
	|	Акцизы.Номенклатура КАК Номенклатура,
	|	Акцизы.СерияНоменклатуры КАК СерияНоменклатуры,
	|	Акцизы.Сделка КАК Сделка,
	|	Акцизы.СостояниеСырья КАК СостояниеСырья,
	|	Акцизы.СтатусАкциза КАК СтатусАкциза,
	|	Акцизы.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	Акцизы.Продукция КАК Продукция,
	|	Акцизы.СерияПродукции КАК СерияПродукции,
	|	ДатаВремя(01,01,01) КАК ДатаРеализации,
	|	Акцизы.Количество КАК Количество
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.бг_АкцизПоПриобретеннымЦенностям КАК Акцизы
	|		ПО (Документ.Ссылка = &Ссылка)
	|			И Документ.Ссылка = Акцизы.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Документ.Дата,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	Документ.Организация,
	|	Акцизы.Номенклатура,
	|	Акцизы.СерияНоменклатуры,
	|	Акцизы.Сделка,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Документ.Контрагент.ЮрФизЛицо, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицоНеРезидент)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.бг_СостоянияПодакцизногоСырья.ВРеализованнойПродукции)
	|		КОГДА ТИПЗНАЧЕНИЯ(Акцизы.Сделка) <> ТИП(Справочник.бг_БанковскиеГарантии)
	|				И ЕСТЬNULL(Документ.Контрагент.СтранаРегистрации.УчастникЕАЭС, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.бг_СостоянияПодакцизногоСырья.ВОжиданииПодтвержденияЭкспортаЕАЭС)
	|		КОГДА ТИПЗНАЧЕНИЯ(Акцизы.Сделка) <> ТИП(Справочник.бг_БанковскиеГарантии)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.бг_СостоянияПодакцизногоСырья.ВОжиданииПодтвержденияЭкспорта)
	|		КОГДА ЕСТЬNULL(Документ.Контрагент.СтранаРегистрации.УчастникЕАЭС, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.бг_СостоянияПодакцизногоСырья.ВОжиданииПодтвержденияЭкспортаГарантииЕАЭС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.бг_СостоянияПодакцизногоСырья.ВОжиданииПодтвержденияЭкспортаГарантии)
	|	КОНЕЦ,
	|	Акцизы.СтатусАкциза,
	|	Акцизы.СостояниеОплатыАванса,
	|	Акцизы.Продукция,
	|	Акцизы.СерияПродукции,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Документ.Контрагент.ЮрФизЛицо, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицоНеРезидент)
	|			ТОГДА ДатаВремя(01,01,01)
	|		ИНАЧЕ НачалоПериода(Документ.Дата, День)
	|	КОНЕЦ,
	|	Акцизы.Количество
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.бг_АкцизПоПриобретеннымЦенностям КАК Акцизы
	|		ПО (Документ.Ссылка = &Ссылка)
	|			И Документ.Ссылка = Акцизы.Ссылка";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностямПродукция() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АкцизПоПриобретеннымЦенностям.Продукция КАК Номенклатура,
	|	АкцизПоПриобретеннымЦенностям.СерияПродукции КАК СерияНоменклатуры
	|ПОМЕСТИТЬ Продукция
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.бг_АкцизПоПриобретеннымЦенностям КАК АкцизПоПриобретеннымЦенностям
	|ГДЕ
	|	АкцизПоПриобретеннымЦенностям.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документ.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	Документ.Организация КАК Организация,
	|	РеализацияТовары.Номенклатура КАК Номенклатура,
	|	РеализацияТовары.Серия КАК СерияНоменклатуры,
	|	ЗНАЧЕНИЕ(Перечисление.бг_ТипыМестХраненияПодакцизнойПродукции.Склад) КАК ТипМестаХранения,
	|	ДатаВремя(01,01,01) КАК ДатаРеализации,
	|	РеализацияТовары.Количество КАК Количество
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТовары
	|		ПО (Документ.Ссылка = &Ссылка)
	|			И Документ.Ссылка = РеализацияТовары.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Продукция КАК Продукция
	|		ПО (РеализацияТовары.Номенклатура = Продукция.Номенклатура)
	|			И (РеализацияТовары.Серия = Продукция.СерияНоменклатуры)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Документ.Дата,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	Документ.Организация,
	|	РеализацияТовары.Номенклатура,
	|	РеализацияТовары.Серия,
	|	ЗНАЧЕНИЕ(Перечисление.бг_ТипыМестХраненияПодакцизнойПродукции.ВОжиданииПодтвержденияЭкспорта),
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Документ.Контрагент.ЮрФизЛицо, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицоНеРезидент)
	|			ТОГДА ДатаВремя(01,01,01)
	|		ИНАЧЕ НачалоПериода(Документ.Дата, День)
	|	КОНЕЦ КАК ДатаРеализации,
	|	РеализацияТовары.Количество
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТовары
	|		ПО (Документ.Ссылка = &Ссылка)
	|			И (ЕСТЬNULL(Документ.Контрагент.ЮрФизЛицо, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицоНеРезидент))
	|			И Документ.Ссылка = РеализацияТовары.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Продукция КАК Продукция
	|		ПО (РеализацияТовары.Номенклатура = Продукция.Номенклатура)
	|			И (РеализацияТовары.Серия = Продукция.СерияНоменклатуры)";
	
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти

#Область РасчетыСКлиентамиРозница

Функция бг_ТекстЗапросаДвиженияРасчетыСРозничнымиКлиентами()
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	               |	РеализацияТоваровУслугТовары.Ссылка.Дата КАК Период,
	               |	РеализацияТоваровУслугТовары.Номенклатура КАК Номенклатура,
	               |	РеализацияТоваровУслугТовары.ВидЦены КАК ВидЦеныВГО,
	               |	РеализацияТоваровУслугТовары.Цена КАК ЦенаВГО,
	               |	СУММА(РеализацияТоваровУслугТовары.Сумма) КАК СуммаВГО,
	               |	РеализацияТоваровУслугТовары.Ссылка.бг_ЗаказРозничногоПокупателя КАК ЗаказРозничногоПокупателя,
	               |	СУММА(РеализацияТоваровУслугТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
	               |	РеализацияТоваровУслугТовары.Ссылка КАК ДокументОтгрузки,
	               |	РеализацияТоваровУслугТовары.Характеристика КАК Характеристика
	               |ПОМЕСТИТЬ ДанныеДокумента
	               |ИЗ
	               |	Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	               |ГДЕ
	               |	РеализацияТоваровУслугТовары.Ссылка = &Ссылка
	               |	И НЕ РеализацияТоваровУслугТовары.Ссылка.бг_ЗаказРозничногоПокупателя = ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	РеализацияТоваровУслугТовары.Ссылка.Дата,
	               |	РеализацияТоваровУслугТовары.Номенклатура,
	               |	РеализацияТоваровУслугТовары.ВидЦены,
	               |	РеализацияТоваровУслугТовары.Цена,
	               |	РеализацияТоваровУслугТовары.Ссылка.бг_ЗаказРозничногоПокупателя,
	               |	РеализацияТоваровУслугТовары.Ссылка,
	               |	РеализацияТоваровУслугТовары.Характеристика
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
	               |	ЕСТЬNULL(ЗаказКлиентаТовары.Цена, 0) КАК ЦенаРозница,
	               |	ЕСТЬNULL(ЗаказКлиентаТовары.бг_ПроцентПримененнойСкидки, 0) КАК ПроцентПримененнойСкидки,
	               |	ЗаказКлиентаТовары.ВидЦены КАК ВидЦеныРозница,
	               |	ЗаказКлиентаТовары.Ссылка.Договор КАК Договор,
	               |	ЗаказКлиентаТовары.Ссылка.Контрагент КАК Контрагент,
	               |	ЗаказКлиентаТовары.Ссылка.Организация КАК Организация
	               |ПОМЕСТИТЬ ДанныеЗаказаКлиента
	               |ИЗ
	               |	ДанныеДокумента КАК ДанныеДокумента
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	               |		ПО ДанныеДокумента.ЗаказРозничногоПокупателя = ЗаказКлиентаТовары.Ссылка
	               |			И ДанныеДокумента.Номенклатура = ЗаказКлиентаТовары.Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеДокумента.ВидДвижения КАК ВидДвижения,
	               |	ДанныеДокумента.Период КАК Период,
	               |	СУММА(ДанныеДокумента.СуммаВГО) КАК Сумма,
	               |	СУММА(ЕСТЬNULL(ДанныеЗаказаКлиента.ЦенаРозница, 0) * (1 - ЕСТЬNULL(ДанныеЗаказаКлиента.ПроцентПримененнойСкидки, 0) / 100) * ДанныеДокумента.КоличествоУпаковок) КАК СуммаВРозницу,
	               |	ДанныеДокумента.ВидЦеныВГО КАК ВидЦеныВГО,
	               |	ДанныеЗаказаКлиента.ВидЦеныРозница КАК ВидЦеныРозница,
	               |	ДанныеЗаказаКлиента.Организация КАК Организация,
	               |	ДанныеЗаказаКлиента.Контрагент КАК Контрагент,
	               |	ДанныеЗаказаКлиента.Договор КАК Договор,
	               |	ДанныеДокумента.ЗаказРозничногоПокупателя КАК ЗаказРозничногоПокупателя,
	               |	ДанныеДокумента.ДокументОтгрузки КАК ДокументОтгрузки
	               |ИЗ
	               |	ДанныеДокумента КАК ДанныеДокумента
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеЗаказаКлиента КАК ДанныеЗаказаКлиента
	               |		ПО ДанныеДокумента.Номенклатура = ДанныеЗаказаКлиента.Номенклатура
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ДанныеДокумента.ДокументОтгрузки,
	               |	ДанныеДокумента.ЗаказРозничногоПокупателя,
	               |	ДанныеЗаказаКлиента.Контрагент,
	               |	ДанныеЗаказаКлиента.Договор,
	               |	ДанныеЗаказаКлиента.Организация,
	               |	ДанныеЗаказаКлиента.ВидЦеныРозница,
	               |	ДанныеДокумента.ВидЦеныВГО,
	               |	ДанныеДокумента.Период,
	               |	ДанныеДокумента.ВидДвижения";
	 
	 Возврат ТекстЗапроса;		
	
КонецФункции	

#КонецОбласти


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

&После("ДополнитьТекстыЗапросовПроведения")
Процедура бг_ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры)
	
	бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностям(Запрос, ТекстыЗапроса, Регистры);
	бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностямПродукция(Запрос, ТекстыЗапроса, Регистры);
	бг_ДополнитьТекстыЗапросовПроведенияРасчетыСРозничнымиКлиентами(Запрос, ТекстыЗапроса, Регистры);	
	
КонецПроцедуры

Процедура бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностям(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "бг_АкцизПоПриобретеннымЦенностям";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностям(), ИмяРегистра);
	
КонецПроцедуры

Процедура бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностямПродукция(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "бг_АкцизПоПриобретеннымЦенностямВыпускПродукции";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностямПродукция(), ИмяРегистра);
	
КонецПроцедуры

Процедура бг_ДополнитьТекстыЗапросовПроведенияРасчетыСРозничнымиКлиентами(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = Метаданные.РегистрыНакопления.бг_РасчетыСРозничнымиКлиентами.Имя;	
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(бг_ТекстЗапросаДвиженияРасчетыСРозничнымиКлиентами(), ИмяРегистра);
		
КонецПроцедуры

&Перед("ДобавитьКомандыПечати")
Процедура бг_ДобавитьКомандыПечати(КомандыПечати)
	
	ЭтоПартнер = ПраваПользователяПовтИсп.ЭтоПартнер();	
	
	Если НЕ ЭтоПартнер Тогда
		
		// Товарная накладная ТОРГ-12) Основной
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "ПФ_MXL_ТОРГ12_ОсновнойМакет_ru";
		КомандаПечати.Представление = НСтр("ru = 'Товарная накладная (ТОРГ-12 основной)';
											|en = 'Invoice (TORG-12 general)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьУслуги", Ложь); 
		КомандаПечати.ДополнительныеПараметры.Вставить("бг_ВыводитьСерииНоменклатуры", Истина);
		КомандаПечати.ДополнительныеПараметры.Вставить("бг_ПереопределитьМакетТОРГ12", Истина);
		КомандаПечати.ДополнительныеПараметры.Вставить("бг_ПересчитыватьВВалютуРегл", Ложь);
		КомандаПечати.Порядок = 17;
		
		// Универсальный передаточный документ (УПД) Основной
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "бг_УПД534Прослеж_ОсновнойМакет";
		КомандаПечати.Представление = НСтр("ru = 'Универсальный передаточный документ (УПД - Основной)';
											|en = 'Universal transfer document (UTD - general)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("бг_ВыводитьСерии", Истина);
		КомандаПечати.ДополнительныеПараметры.Вставить("бг_ВыводитьШКТовара", Ложь);
		КомандаПечати.ДополнительныеПараметры.Вставить("бг_ИспользоватьПечатныеФормыСФ2_5", Истина);
		КомандаПечати.ДополнительныеПараметры.Вставить("бг_ПереопределитьМакетУПД", Истина);
		КомандаПечати.Порядок = 17;
		
		// Счет-фактура Основной
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "бг_СчетФактура";
		КомандаПечати.Представление = НСтр("ru = 'Счет-фактура (основной)';
											|en = 'Tax Invoice (General)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("бг_ПереопределитьМакетСчетФактуры", Истина);
		КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВВалюте", Ложь);
		КомандаПечати.Порядок = 20;
		
	КонецЕсли;
		
КонецПроцедуры

&ИзменениеИКонтроль("ПолучитьДанныеДляПечатнойФормыТОРГ12")
Функция бг_ПолучитьДанныеДляПечатнойФормыТОРГ12(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка  КАК Ссылка,
	|	ДанныеДокументов.Валюта  КАК Валюта,
	|	ДанныеДокументов.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Выполнить();
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	Если ПараметрыПечати.Свойство("ВыводитьГТД") Тогда
		ПараметрыЗаполнения.ВключаяНомераГТД = ПараметрыПечати.ВыводитьГТД;
	КонецЕсли;
#Вставка
	Если ПараметрыПечати.Свойство("бг_ВыводитьСерииНоменклатуры") Тогда
		ПараметрыЗаполнения.ВыводитьСерииНоменклатуры = ПараметрыПечати.бг_ВыводитьСерииНоменклатуры;
	КонецЕсли;
	Если ПараметрыПечати.Свойство("бг_ПересчитыватьВВалютуРегл") Тогда
		ПараметрыЗаполнения.ПересчитыватьВВалютуРегл = ПараметрыПечати.бг_ПересчитыватьВВалютуРегл;
	КонецЕсли;
#КонецВставки
	ПараметрыЗаполнения.ОбработатьНастройкиПечатиНаборов = Истина;
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	ПродажиСервер.ПоместитьВременнуюТаблицуКоэффициентыУпаковок(МенеджерВременныхТаблиц, "РеализацияТоваровУслугТаблицаТоваров");
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос.Текст =
	"ВЫБРАТЬ
#Вставка
	|	ПунктыНазначения.АдресПунктаНазначения КАК АдресПунктаНазначения,
	|	ПунктыНазначения.ТелефонПунктаНазначения КАК ТелефонПунктаНазначения,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.ЗаказКлиента.НомерПоДаннымКлиента, """") КАК НомерПоДаннымКлиента,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Партнер.бг_КатегорияМакетовПечатныхФорм,
	|				Значение(Перечисление.бг_КатегорииМакетовПечатныхФорм.ПустаяСсылка)) КАК бг_КатегорияМакета,
	|	РеализацияТоваровУслуг.бг_Лицензия.бг_ТипЛицензии КАК бг_ТипЛицензии,
	|	РеализацияТоваровУслуг.бг_ПунктНазначения.Адрес КАК АдресПунктаНазначенияКонтрагента,
	|	РеализацияТоваровУслуг.Договор.НаименованиеДляПечати КАК ДоговорНаименованиеДляПечати,
	|	РеализацияТоваровУслуг.Договор.Дата КАК ДатаДоговора,
	|	РеализацияТоваровУслуг.Договор.Номер КАК НомерДоговора,
	|	РеализацияТоваровУслуг.бг_ДоверенностьДолжность КАК ГрузПринялДолжность,
	|	ЕСТЬNULL(ТаблицаОтветственныеЛица.Руководитель.ФизическоеЛицо,
	|		ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК бг_РуководительФизЛицо,
	|	ЕСТЬNULL(ТаблицаОтветственныеЛица.ГлавныйБухгалтер.ФизическоеЛицо,
	|		ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК бг_ГлавныйБухгалтерФизЛицо,
	|	ВЫРАЗИТЬ(ТаблицаОтветственныеЛица.ГлавныйБухгалтер.ФизическоеЛицо КАК Справочник.ФизическиеЛица).бг_Должность
	|		КАК бг_ДолжностьГлавБухгалтер,
	|	ВЫРАЗИТЬ(ТаблицаОтветственныеЛица.ГлавныйБухгалтер.ФизическоеЛицо КАК Справочник.ФизическиеЛица).бг_Должность
	|		КАК бг_ДолжностьРуководитель,	
	|	ЕСТЬNULL(ТаблицаОтветственныеЛица.Руководитель.НомерДокументаПраваПодписи,
	|		"""") КАК бг_РуководительНомерОснования,
	|	ЕСТЬNULL(ТаблицаОтветственныеЛица.ГлавныйБухгалтер.НомерДокументаПраваПодписи,
	|		"""") КАК бг_ГлавныйБухгалтерНомерОснования,
	|	ЕСТЬNULL(ТаблицаОтветственныеЛица.Руководитель.ДатаДокументаПраваПодписи,
	|		ДАТАВРЕМЯ(1, 1, 1)) КАК бг_РуководительДатаОснования,
	|	ЕСТЬNULL(ТаблицаОтветственныеЛица.ГлавныйБухгалтер.ДатаДокументаПраваПодписи,
	|		ДАТАВРЕМЯ(1, 1, 1)) КАК бг_ГлавныйБухгалтерДатаОснования,
	|   Рейсы.Рейс.Водитель КАК ВодительРейса,
	|	Рейсы.Рейс.Экспедитор КАК Экспедитор,
#КонецВставки
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Номер КАК Номер,
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	РеализацияТоваровУслуг.Статус КАК Статус,
	|	РеализацияТоваровУслуг.Партнер КАК Партнер,
	|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Организация.ОбособленноеПодразделение
	|			ТОГДА РеализацияТоваровУслуг.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ РеализацияТоваровУслуг.Организация
	|	КОНЕЦ КАК Организация,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	РеализацияТоваровУслуг.Отпустил КАК Кладовщик,
	|	РеализацияТоваровУслуг.ОтпустилДолжность КАК ДолжностьКладовщика,
	|	РеализацияТоваровУслуг.Организация.Префикс КАК Префикс,
	|	РеализацияТоваровУслуг.Основание КАК Основание,
	|	РеализацияТоваровУслуг.ОснованиеДата КАК ОснованиеДата,
	|	РеализацияТоваровУслуг.ОснованиеНомер КАК ОснованиеНомер,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА РеализацияТоваровУслуг.Контрагент
	|		ИНАЧЕ РеализацияТоваровУслуг.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА РеализацияТоваровУслуг.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА РеализацияТоваровУслуг.Организация
	|		ИНАЧЕ РеализацияТоваровУслуг.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	РеализацияТоваровУслуг.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	РеализацияТоваровУслуг.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	РеализацияТоваровУслуг.БанковскийСчетГрузоотправителя КАК БанковскийСчетГрузоотправителя,
	|	РеализацияТоваровУслуг.БанковскийСчетГрузополучателя КАК БанковскийСчетГрузополучателя,
	|	РеализацияТоваровУслуг.АдресДоставки КАК АдресДоставки,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	РеализацияТоваровУслуг.Валюта КАК Валюта,
	|	РеализацияТоваровУслуг.НалогообложениеНДС КАК НалогообложениеНДС,
	|	РеализацияТоваровУслуг.ДоверенностьНомер КАК ДоверенностьНомер,
	|	РеализацияТоваровУслуг.ДоверенностьДата КАК ДоверенностьДата,
	|	РеализацияТоваровУслуг.ДоверенностьВыдана КАК ДоверенностьВыдана,
	|	РеализацияТоваровУслуг.ДоверенностьЛицо КАК ДоверенностьЛицо,
	|	&ЕдиницаИзмеренияВеса КАК ЕдиницаИзмеренияВеса
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО РеализацияТоваровУслуг.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
#Удаление
	|		ПО РеализацияТоваровУслуг.Ссылка = ТаблицаОтветственныеЛица.Ссылка
#КонецУдаления
#Вставка
	|		ПО РеализацияТоваровУслуг.Ссылка = ТаблицаОтветственныеЛица.Ссылка,
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МАКСИМУМ(МаршрутныйЛистЗаказы.Ссылка) КАК Рейс,
	|			ВЫРАЗИТЬ(МаршрутныйЛистЗаказы.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг) КАК Реализация
	|		ИЗ
	|			Документ.битМаршрутныйЛист.Заказы КАК МаршрутныйЛистЗаказы
	|		ГДЕ
	|			ВЫРАЗИТЬ(МаршрутныйЛистЗаказы.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг) В (&МассивОбъектов)
	|			И МаршрутныйЛистЗаказы.Ссылка.ВидДокумента = ЗНАЧЕНИЕ(Перечисление.бг_ВидыМаршрутныхЛистов.Рейс)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ВЫРАЗИТЬ(МаршрутныйЛистЗаказы.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг)) КАК Рейсы
	|		ПО (РеализацияТоваровУслуг.Ссылка = Рейсы.Реализация)
	|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|		ПунктыНазначенияОрганизации.Ссылка.Адрес КАК АдресПунктаНазначения,
	|		ПунктыНазначенияОрганизации.Организация КАК Организация,
	|		ПунктыНазначенияОрганизации.Ссылка.Телефон КАК ТелефонПунктаНазначения
	|	ИЗ
	|		(ВЫБРАТЬ
	|			МАКСИМУМ(битПунктыНазначения.Ссылка) КАК Ссылка,
	|			ВЫРАЗИТЬ(битПунктыНазначения.Клиент КАК Справочник.Организации) КАК Организация
	|		ИЗ
	|			Справочник.битПунктыНазначения КАК битПунктыНазначения
	|		ГДЕ
	|			ВЫРАЗИТЬ(битПунктыНазначения.Клиент КАК Справочник.Организации) В
	|					(ВЫБРАТЬ
	|						ТаблицаДанныхДокументов.Ссылка.Организация
	|					ИЗ
	|						ТаблицаДанныхДокументов)
	|			И НЕ битПунктыНазначения.ОбособленноеПодразделение
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВЫРАЗИТЬ(битПунктыНазначения.Клиент КАК Справочник.Организации)) КАК ПунктыНазначенияОрганизации) КАК ПунктыНазначения
	|		ПО ПунктыНазначения.Организация = РеализацияТоваровУслуг.Организация
#КонецВставки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ &ТекстЗапросаКодЕдиницыИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
	|	ТаблицаТоваров.Упаковка КАК Упаковка,	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 1
	|			ТОГДА """"
	|		ИНАЧЕ ТаблицаТоваров.Упаковка.Наименование
	|	КОНЕЦ КАК УпаковкаНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА &ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения2
	|	КОНЕЦ КАК ВидУпаковки,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ ТаблицаТоваров.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|				И НЕ ТаблицаТоваров.ЭтоНабор
	|			ТОГДА ВЫБОР
	|					КОГДА КоэффициентыУпаковок.Количество < КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|						ИЛИ ТаблицаТоваров.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|						И ((ТаблицаТоваров.Номенклатура.ВесИспользовать 
	|						И ТаблицаТоваров.Номенклатура.ВесМожноУказыватьВДокументах 
	|						И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ВесЕдиницаИзмерения)
	|						ИЛИ (ТаблицаТоваров.Номенклатура.ОбъемИспользовать 
	|						И ТаблицаТоваров.Номенклатура.ОбъемМожноУказыватьВДокументах 
	|						И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ОбъемЕдиницаИзмерения)
	|						ИЛИ (ТаблицаТоваров.Номенклатура.ДлинаИспользовать 
	|						И ТаблицаТоваров.Номенклатура.ДлинаМожноУказыватьВДокументах 
	|						И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ДлинаЕдиницаИзмерения)
	|						ИЛИ (ТаблицаТоваров.Номенклатура.ПлощадьИспользовать 
	|						И ТаблицаТоваров.Номенклатура.ПлощадьМожноУказыватьВДокументах 
	|						И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ПлощадьЕдиницаИзмерения))
	|					ТОГДА КоэффициентыУпаковок.КоличествоУпаковок
	|					ИНАЧЕ ТаблицаТоваров.Количество / КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблицаТоваров.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|  						И &ВыводитьБазовыеЕдиницыИзмерения
	|						И ((ТаблицаТоваров.Номенклатура.ВесИспользовать 
	|						И ТаблицаТоваров.Номенклатура.ВесМожноУказыватьВДокументах 
	|						И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ВесЕдиницаИзмерения)
	|						ИЛИ (ТаблицаТоваров.Номенклатура.ОбъемИспользовать 
	|						И ТаблицаТоваров.Номенклатура.ОбъемМожноУказыватьВДокументах 
	|						И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ОбъемЕдиницаИзмерения)
	|						ИЛИ (ТаблицаТоваров.Номенклатура.ДлинаИспользовать 
	|						И ТаблицаТоваров.Номенклатура.ДлинаМожноУказыватьВДокументах 
	|						И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ДлинаЕдиницаИзмерения)
	|						ИЛИ (ТаблицаТоваров.Номенклатура.ПлощадьИспользовать 
	|						И ТаблицаТоваров.Номенклатура.ПлощадьМожноУказыватьВДокументах 
	|						И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ПлощадьЕдиницаИзмерения))
	|					ТОГДА ТаблицаТоваров.КоличествоУпаковок
	|
	|				КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|					ИЛИ ТаблицаТоваров.Упаковка = (ВЫБОР
	|											КОГДА &ВыводитьБазовыеЕдиницыИзмерения 
	|											ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
	|											ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения 
	|											КОНЕЦ)
	|					ТОГДА ТаблицаТоваров.Количество
	|
	|				ИНАЧЕ ВЫРАЗИТЬ(ТаблицаТоваров.Количество / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК ЧИСЛО(12, 3))
	|			КОНЕЦ
	|	КОНЕЦ КАК КоличествоМестДробное,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения И Не ТаблицаТоваров.ЭтоНабор
	|			ТОГДА ВЫБОР
	|					КОГДА КоэффициентыУпаковок.Количество < КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|						ТОГДА КоэффициентыУпаковок.Количество
	|					ИНАЧЕ 
	|						ВЫБОР КОГДА ТаблицаТоваров.Упаковка.ЕдиницаИзмерения = &ТекстЗапросаЕдиницаИзмерения
	|								ИЛИ ТаблицаТоваров.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|								И ((ТаблицаТоваров.Номенклатура.ВесИспользовать 
	|								И ТаблицаТоваров.Номенклатура.ВесМожноУказыватьВДокументах 
	|								И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ВесЕдиницаИзмерения)
	|								ИЛИ (ТаблицаТоваров.Номенклатура.ОбъемИспользовать 
	|								И ТаблицаТоваров.Номенклатура.ОбъемМожноУказыватьВДокументах 
	|								И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ОбъемЕдиницаИзмерения)
	|								ИЛИ (ТаблицаТоваров.Номенклатура.ДлинаИспользовать 
	|								И ТаблицаТоваров.Номенклатура.ДлинаМожноУказыватьВДокументах 
	|								И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ДлинаЕдиницаИзмерения)
	|								ИЛИ (ТаблицаТоваров.Номенклатура.ПлощадьИспользовать 
	|								И ТаблицаТоваров.Номенклатура.ПлощадьМожноУказыватьВДокументах 
	|								И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ПлощадьЕдиницаИзмерения))
	|							ТОГДА 1
	|							ИНАЧЕ КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|						КОНЕЦ
	|					КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|					ИЛИ ТаблицаТоваров.Упаковка = (ВЫБОР
	|										КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|										ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
	|										ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения 
	|										КОНЕЦ)  
	|					ТОГДА 1
	|				ИНАЧЕ &ТекстЗапросаКоэффициентУпаковки
	|			КОНЕЦ
	|	КОНЕЦ КАК КоличествоВОдномМесте,
	|	ВЫБОР КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА &ТекстЗапросаВесУпаковки ИНАЧЕ &ТекстЗапросаВесВидаУпаковки КОНЕЦ КАК МассаБруттоОдногоМеста,
	|	ЕСТЬNULL(КоэффициентыУпаковок.КоэффициентУпаковки, 1) * ТаблицаТоваров.МассаНеттоНоменклатуры КАК МассаНеттоОднойУпаковки,
	|	ТаблицаТоваров.НомерГТД КАК НомерГТД,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика
	|
	|ПОМЕСТИТЬ РасчитанныеДанныеУпаковокЕдиницИзмерения
	|ИЗ
	|	РеализацияТоваровУслугТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыУпаковок КАК КоэффициентыУпаковок
	|		ПО ТаблицаТоваров.Ссылка = КоэффициентыУпаковок.Ссылка
	|			И ТаблицаТоваров.НомерСтроки = КоэффициентыУпаковок.НомерСтроки
	|			И ТаблицаТоваров.Номенклатура = КоэффициентыУпаковок.Номенклатура
	|			И ТаблицаТоваров.КоличествоУпаковок = КоэффициентыУпаковок.КоличествоУпаковок
	|			И (НЕ &ВыводитьБазовыеЕдиницыИзмерения)
	|ГДЕ
	|	(ТаблицаТоваров.ЭтоТовар
	|			ИЛИ &ВыводитьУслуги)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НомерСтроки,
	|	Номенклатура,
	|	Характеристика,
	|	НомерГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
#Вставка
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТоваров.Номенклатура.ВидНоменклатуры.бг_ЕдиницаИзмеренияМест,
	|				ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА ТаблицаТоваров.Номенклатура.ВидНоменклатуры.бг_ЕдиницаИзмеренияМест
	|		ИНАЧЕ ДанныеУпаковок.ВидУпаковки 
	|	КОНЕЦ КАК бг_ВидУпаковки, 
	|	ВЫБОР 
	|		КОГДА ТаблицаТоваров.Серия.бг_УпаковкаПаллета.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Паллета) 
	|				И ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Коробка)  
	|		ТОГДА
	|				Выбор КОГДА ЕСТЬNULL(ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.КоличествоУпаковок, 0) <> 0
	|				ТОГДА ТаблицаТоваров.КоличествоУпаковок / ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.КоличествоУпаковок
	|			КОНЕЦ
	|		КОГДА ТаблицаТоваров.Серия.бг_УпаковкаПаллета.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Коробка) 
	|			И ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Бутылка)  
	|		ТОГДА
	|			Выбор КОГДА ЕСТЬNULL(ТаблицаТоваров.Серия.бг_УпаковкаПаллета.КоличествоУпаковок, 0) <> 0
	|			ТОГДА ТаблицаТоваров.КоличествоУпаковок / ТаблицаТоваров.Серия.бг_УпаковкаПаллета.КоличествоУпаковок
	|		КОНЕЦ 
	|	ИНАЧЕ
	|		ВЫБОР КОГДА ДанныеУпаковок.КоличествоМестДробное = (ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное КАК ЧИСЛО(12, 0)))
	|			ТОГДА ДанныеУпаковок.КоличествоМестДробное
	|		ИНАЧЕ ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное + 0.5 КАК ЧИСЛО(12, 0))
	|			КОНЕЦ 
	|	КОНЕЦ КАК бг_КоличествоМест,
	|ВЫБОР 
	|	КОГДА ТаблицаТоваров.Серия.бг_УпаковкаПаллета.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Паллета) 
	|		И ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Коробка)
	|		И ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.Родитель.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Бутылка)  
	|	ТОГДА
	|		ВЫБОР КОГДА ЕСТЬNULL(ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.КоличествоУпаковок, 0) <> 0
	|		ТОГДА ТаблицаТоваров.КоличествоУпаковок * ЕСТЬNULL(ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.Родитель.Вес, 0)
	|		КОНЕЦ
	|	КОГДА ТаблицаТоваров.Серия.бг_УпаковкаПаллета.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Коробка) 
	|		И ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Бутылка)  
	|	ТОГДА
	|		Выбор КОГДА ЕСТЬNULL(ТаблицаТоваров.Серия.бг_УпаковкаПаллета.КоличествоУпаковок, 0) <> 0
	|		ТОГДА ТаблицаТоваров.КоличествоУпаковок * ЕСТЬNULL(ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.Вес, 0)
	|		КОНЕЦ 
	|	ИНАЧЕ
	|		ТаблицаТоваров.МассаНетто
	|	КОНЕЦ КАК бг_МассаНетто,
	|	ВЫБОР 
	|		КОГДА ТаблицаТоваров.Серия.бг_УпаковкаПаллета.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Паллета) 
	|				И ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Коробка)  
	|		ТОГДА
	|				Выбор КОГДА ЕСТЬNULL(ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.КоличествоУпаковок, 0) <> 0
	|				ТОГДА ТаблицаТоваров.КоличествоУпаковок / ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.КоличествоУпаковок
	|			КОНЕЦ * ЕСТЬNULL(ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.Вес, 0) 
	|		КОГДА ТаблицаТоваров.Серия.бг_УпаковкаПаллета.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Коробка) 
	|			И ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Бутылка)  
	|		ТОГДА
	|			Выбор КОГДА ЕСТЬNULL(ТаблицаТоваров.Серия.бг_УпаковкаПаллета.КоличествоУпаковок, 0) <> 0
	|			ТОГДА ТаблицаТоваров.КоличествоУпаковок / ТаблицаТоваров.Серия.бг_УпаковкаПаллета.КоличествоУпаковок
	|		КОНЕЦ * ЕСТЬNULL(ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Вес, 0)
	|	ИНАЧЕ ВЫБОР
	|		КОГДА ДанныеУпаковок.КоличествоМестДробное = (ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное КАК ЧИСЛО(12, 0)))
	|				ТОГДА ДанныеУпаковок.КоличествоМестДробное
	|			ИНАЧЕ ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное + 0.5 КАК ЧИСЛО(12, 0))
	|		КОНЕЦ * ЕСТЬNULL(ДанныеУпаковок.МассаБруттоОдногоМеста, 0) - (ВЫБОР
	|			КОГДА ДанныеУпаковок.КоличествоМестДробное = (ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное КАК ЧИСЛО(12, 0)))
	|				ТОГДА ДанныеУпаковок.КоличествоМестДробное
	|			ИНАЧЕ ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное + 0.5 КАК ЧИСЛО(12, 0))
	|		КОНЕЦ * ЕСТЬNULL(ДанныеУпаковок.КоличествоВОдномМесте, 0) - ВЫБОР
	|			КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|				ТОГДА ТаблицаТоваров.Количество
	|			ИНАЧЕ ТаблицаТоваров.КоличествоУпаковок
	|		КОНЕЦ) * ДанныеУпаковок.МассаНеттоОднойУпаковки 
	|	КОНЕЦ КАК бг_МассаБрутто,
	|ВЫБОР 
	|	КОГДА ТаблицаТоваров.Серия.бг_УпаковкаПаллета.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Паллета) 
	|		И ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Коробка)
	|		И ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.Родитель.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Бутылка)  
	|	ТОГДА
	|		ВЫБОР КОГДА ЕСТЬNULL(ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.КоличествоУпаковок, 0) <> 0
	|		ТОГДА ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.КоличествоУпаковок
	|		КОНЕЦ
	|	КОГДА ТаблицаТоваров.Серия.бг_УпаковкаПаллета.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Коробка) 
	|		И ТаблицаТоваров.Серия.бг_УпаковкаПаллета.Родитель.ЕдиницаИзмерения.бг_ТипЕдиницыИзмерения = ЗНАЧЕНИЕ(Перечисление.бг_ТипыЕдиницИзмерения.Бутылка)  
	|	ТОГДА
	|		Выбор КОГДА ЕСТЬNULL(ТаблицаТоваров.Серия.бг_УпаковкаПаллета.КоличествоУпаковок, 0) <> 0
	|		ТОГДА ТаблицаТоваров.Серия.бг_УпаковкаПаллета.КоличествоУпаковок
	|		КОНЕЦ 
	|	ИНАЧЕ
	|		ДанныеУпаковок.КоличествоВОдномМесте
	|	КОНЕЦ КАК бг_КоличествоВОдномМесте,
#КонецВставки
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	ТаблицаТоваров.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ТаблицаТоваров.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	ТаблицаТоваров.НоменклатураНабора КАК НоменклатураНабора,
	|	ТаблицаТоваров.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ТаблицаТоваров.ЭтоКомплектующие КАК ЭтоКомплектующие,
	|	ТаблицаТоваров.ЭтоНабор КАК ЭтоНабор,
	|	ТаблицаТоваров.ПолныйНабор КАК ПолныйНабор,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	ТаблицаТоваров.Номенклатура.Наименование КАК НоменклатураНаименованиеКраткое,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаТоваров.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаТоваров.Номенклатура.Код
	|	КОНЕЦ КАК НоменклатураКод,
	|	ДанныеУпаковок.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ДанныеУпаковок.ЕдиницаИзмеренияНаименование КАК ЕдиницаИзмеренияНаименование,
	|	ДанныеУпаковок.ЕдиницаИзмеренияКод КАК ЕдиницаИзмеренияКод,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТаблицаТоваров.Серия.Наименование КАК СерияНаименование,
	|	ДанныеУпаковок.Упаковка КАК Упаковка,
	|	ДанныеУпаковок.УпаковкаНаименование КАК УпаковкаНаименование,
	|	ДанныеУпаковок.ВидУпаковки КАК ВидУпаковки,
	|	ТаблицаТоваров.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаТоваров.НомерГТД КАК НомерГТД,
	|	ТаблицаТоваров.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ДанныеУпаковок.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ДанныеУпаковок.КоличествоМестДробное = (ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное КАК ЧИСЛО(12, 0)))
	|			ТОГДА ДанныеУпаковок.КоличествоМестДробное
	|		ИНАЧЕ ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное + 0.5 КАК ЧИСЛО(12, 0))
	|	КОНЕЦ КАК КоличествоМест,
	|	ДанныеУпаковок.КоличествоВОдномМесте КАК КоличествоВОдномМесте,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ВЫБОР КОГДА НЕ ТаблицаТоваров.КоличествоУпаковок = 0 ТОГДА ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.КоличествоУпаковок ИНАЧЕ 0 КОНЕЦ
	|		ИНАЧЕ ВЫБОР КОГДА НЕ ТаблицаТоваров.Количество = 0 ТОГДА ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.Количество ИНАЧЕ 0 КОНЕЦ
	|	КОНЕЦ КАК Цена,
	|	ТаблицаТоваров.СуммаБезНДС КАК СуммаБезНДС,
	|	ТаблицаТоваров.СуммаНДС КАК СуммаНДС,
	|	ТаблицаТоваров.СуммаБезНДС + ТаблицаТоваров.СуммаНДС КАК СуммаСНДС,
	|	ТаблицаТоваров.МассаНетто КАК МассаНетто,
	|	ВЫБОР
	|		КОГДА ДанныеУпаковок.КоличествоМестДробное = (ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное КАК ЧИСЛО(12, 0)))
	|			ТОГДА ДанныеУпаковок.КоличествоМестДробное
	|		ИНАЧЕ ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное + 0.5 КАК ЧИСЛО(12, 0))
	|	КОНЕЦ * ЕСТЬNULL(ДанныеУпаковок.МассаБруттоОдногоМеста, 0) - (ВЫБОР
	|		КОГДА ДанныеУпаковок.КоличествоМестДробное = (ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное КАК ЧИСЛО(12, 0)))
	|			ТОГДА ДанныеУпаковок.КоличествоМестДробное
	|		ИНАЧЕ ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное + 0.5 КАК ЧИСЛО(12, 0))
	|	КОНЕЦ * ЕСТЬNULL(ДанныеУпаковок.КоличествоВОдномМесте, 0) - ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.Количество
	|		ИНАЧЕ ТаблицаТоваров.КоличествоУпаковок
	|	КОНЕЦ) * ДанныеУпаковок.МассаНеттоОднойУпаковки КАК МассаБрутто,
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоваров.НомерСтрокиНаборы КАК НомерСтрокиНаборы,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.Ссылка.ВернутьМногооборотнуюТару
	|				И ТаблицаТоваров.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	РеализацияТоваровУслугТаблицаТоваров КАК ТаблицаТоваров
	|	ЛЕВОЕ СОЕДИНЕНИЕ РасчитанныеДанныеУпаковокЕдиницИзмерения КАК ДанныеУпаковок
	|		ПО ТаблицаТоваров.Ссылка = ДанныеУпаковок.Ссылка
	|			И ТаблицаТоваров.НомерСтроки = ДанныеУпаковок.НомерСтроки
	|			И ТаблицаТоваров.Номенклатура = ДанныеУпаковок.Номенклатура
	|			И ТаблицаТоваров.Характеристика = ДанныеУпаковок.Характеристика
	|			И ТаблицаТоваров.НомерГТД = ДанныеУпаковок.НомерГТД
	|ГДЕ
	|	(ТаблицаТоваров.ЭтоТовар
	|			ИЛИ &ВыводитьУслуги)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтрокиНаборы,
	|	ЭтоНабор УБЫВ,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст, 
		"&ТекстЗапросаВесУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));

	Запрос.Текст = СтрЗаменить(
		Запрос.Текст, 
		"&ТекстЗапросаВесВидаУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"КоэффициентыУпаковок.ВидУпаковки",
			"ТаблицаТоваров.Номенклатура"));

	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"КоэффициентыУпаковок.ВидУпаковки",
			"ТаблицаТоваров.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.УстановитьПараметр("ВыводитьУслуги",                  ПараметрыПечати.ВыводитьУслуги);
	Запрос.УстановитьПараметр("КолонкаКодов",                    КолонкаКодов);
	Запрос.УстановитьПараметр("ЕдиницаИзмеренияВеса",            Константы.ЕдиницаИзмеренияВеса.Получить());
	Запрос.УстановитьПараметр("ЗаполненаЕдиницаИзмеренияВеса",   ЗначениеЗаполнено(Константы.ЕдиницаИзмеренияВеса.Получить()));
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения",
		Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить() ИЛИ ПараметрыЗаполнения.ВключаяНомераГТД);
	
	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоШапке          = МассивРезультатов[0];
	РезультатПоТабличнойЧасти = МассивРезультатов[МассивРезультатов.ВГраница()];
	
	СтруктураДанныхДляПечати 	= Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти",
	                                               РезультатПоШапке, РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

&ИзменениеИКонтроль("ПолучитьДанныеДляПечатнойФормыУПД")
Функция бг_ПолучитьДанныеДляПечатнойФормыУПД(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;

	// ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	Контракт = Неопределено;
	ЕстьЭлектронноеАктирование = Ложь;
	Для Каждого Основание Из МассивОбъектов Цикл
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Основание, "Договор")
			И ТипЗнч(Основание.Договор) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
			Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание.Договор, "Организация");
			ПараметрыОтправкиВЕИС = ЭлектронноеАктированиеЕИСУТ.ПараметрыОтправкиВЕИС(
					Организация, Основание.Договор);
			ЕстьЭлектронноеАктирование = ПараметрыОтправкиВЕИС.ВозможнаОтправка
				И ЭлектронноеАктированиеЕИСУТ.ДокументОтправляетсяВЕИС(Основание);
			Контракт = ПараметрыОтправкиВЕИС.Контракт;
		КонецЕсли;
	КонецЦикла;
	// Конец ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.Валюта КАК Валюта,
	|	ДанныеДокументов.Организация КАК Организация,
	|	ДанныеДокументов.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	|	ДанныеДокументов.Подразделение КАК Подразделение,
	|	ДанныеДокументов.Склад КАК Склад
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	УчетНДСРФ.ДополнитьЗапросПолученияДанныхДляПечатиУПД(Запрос);

	Запрос.Выполнить();
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	ПараметрыЗаполнения.ВключаяНомераГТД                 = Истина;
	ПараметрыЗаполнения.ОбработатьНастройкиПечатиНаборов = Истина;
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	Обработки.ПечатьОбщихФорм.ПоместитьВременнуюТаблицуДанныхПоставщика(МенеджерВременныхТаблиц);
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА НЕ ТаблицаТоваров.ЭтоТовар
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьУслуги,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ТаблицаТоваров.ЭтоТовар
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьТовары,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(ТаблицаТоваров.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьПрослеживаемыеТовары
	|ПОМЕСТИТЬ НоменклатураДокументов
	|ИЗ
	|	РеализацияТоваровУслугТаблицаТоваров КАК ТаблицаТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
#Вставка
	|	ДанныеДокумента.Партнер КАК бг_Партнер,
	|	ДанныеДокумента.Договор.Номер КАК бг_НомерДоговора,
	|	ДанныеДокумента.Договор.Дата КАК бг_ДатаДоговора,
	|	ДанныеДокумента.бг_ПунктНазначения.Адрес КАК бг_АдресГрузополучателя,
	|	ПунктыНазначения.АдресПунктаНазначения КАК бг_АдресГрузоотправителя,
	|	ДанныеДокумента.бг_ДоверенностьДолжность КАК бг_ДолжностьТоварПолучил,
	|	ДанныеДокумента.ДоверенностьЛицо КАК бг_ДоверенностьЛицо,
	|	ЕСТЬNULL(ДанныеДокумента.ЗаказКлиента.НомерПоДаннымКлиента, """") КАК бг_НомерПоДаннымКлиента,
	|	ЕСТЬNULL(ДанныеДокумента.Партнер.бг_КатегорияМакетовПечатныхФорм,
	|				Значение(Перечисление.бг_КатегорииМакетовПечатныхФорм.ПустаяСсылка)) КАК бг_КатегорияМакета,
	|	ДанныеДокумента.бг_Лицензия.бг_ТипЛицензии КАК бг_ТипЛицензии,
	|	ЕСТЬNULL(бг_ТранспортнаяИнформация.ВидПеревозки.Самовывоз, Ложь) КАК бг_ЭтоСамовывоз,
#КонецВставки
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	&ПредставлениеСчетФактура КАК ПредставлениеДокумента,
	|	2 КАК СтатусУПД,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Дата КАК Дата,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправления,
	|	ЛОЖЬ КАК Исправление,
	|	НЕОПРЕДЕЛЕНО КАК НомерСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправленияСчетаФактуры,
	|	ЛОЖЬ КАК КорректировочныйСчетФактура,
	|	"""" КАК СтрокаПоДокументу,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаСчетаФактуры,
	|	ДанныеДокумента.Партнер КАК Партнер,
	|
	|	ДанныеДокумента.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ДанныеПоставщика.ГоловнаяОрганизация КАК Организация,
	|
	|	ВЫБОР КОГДА &РеализацияЧерезКомиссионера ТОГДА
	|		ДанныеДокумента.КлиентКонтрагент
	|	ИНАЧЕ ВЫБОР
	|			КОГДА ДанныеДокумента.Контрагент.ОбособленноеПодразделение
	|				ТОГДА ДанныеДокумента.Контрагент.ГоловнойКонтрагент
	|			ИНАЧЕ ДанныеДокумента.Контрагент
	|		КОНЕЦ 
	|	КОНЕЦ КАК Контрагент,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ДанныеДокумента.Организация
	|		ИНАЧЕ ДанныеДокумента.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) ТОГДА
	|			ВЫБОР КОГДА &РеализацияЧерезКомиссионера ТОГДА
	|					ДанныеДокумента.КлиентКонтрагент
	|				ИНАЧЕ ДанныеДокумента.Контрагент
	|			КОНЕЦ
	|		ИНАЧЕ ДанныеДокумента.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|
	|	ДанныеДокумента.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	ДанныеДокумента.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	ДанныеДокумента.БанковскийСчетГрузоотправителя КАК БанковскийСчетГрузоотправителя,
	|	ДанныеДокумента.БанковскийСчетГрузополучателя КАК БанковскийСчетГрузополучателя,
	|
	|	ЕСТЬNULL(ДанныеПоставщика.ИндексПодразделения, 0) КАК ИндексПодразделения,
	|
	|	ДанныеПоставщика.КПППоставщика КАК КПППоставщика,
	|
	|	ВЫБОР КОГДА &РеализацияЧерезКомиссионера ТОГДА
	|		ДанныеДокумента.КлиентКонтрагент.КПП
	|	ИНАЧЕ ВЫБОР
	|			КОГДА
	|				ДанныеДокумента.Грузополучатель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				И ДанныеДокумента.Грузополучатель.ГоловнойКонтрагент = ДанныеДокумента.Контрагент
	|			ТОГДА
	|				ДанныеДокумента.Грузополучатель.КПП
	|			ИНАЧЕ
	|				ДанныеДокумента.Контрагент.КПП
	|			КОНЕЦ
	|	КОНЕЦ КАК КПППокупателя,
	|
	|	ВЫБОР КОГДА &РеализацияЧерезКомиссионера ТОГДА
	|		ДанныеДокумента.КлиентКонтрагент
	|	ИНАЧЕ
	|		ДанныеДокумента.Контрагент.ИНН 
	|	КОНЕЦ КАК ИННПокупателя,
	|
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	ДанныеДокумента.АдресДоставки КАК АдресДоставки,
	|
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.Валюта.НаименованиеПолное КАК ВалютаНаименованиеПолное,
	|	ДанныеДокумента.Валюта.Код КАК ВалютаКод,
	|	ВЫБОР
	|		КОГДА НоменклатураДокументов.ЕстьУслуги
	|				И НЕ НоменклатураДокументов.ЕстьТовары
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТолькоУслуги,
	|	НоменклатураДокументов.ЕстьПрослеживаемыеТовары КАК ЕстьПрослеживаемыеТовары,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоПередачаНаКомиссию,
	|	ДанныеДокумента.Основание КАК Основание,
	|	ДанныеДокумента.ОснованиеДата КАК ОснованиеДата,
	|	ДанныеДокумента.ОснованиеНомер КАК ОснованиеНомер,
	|	ДанныеДокумента.ДоверенностьНомер КАК ДоверенностьНомер,
	|	ДанныеДокумента.ДоверенностьДата КАК ДоверенностьДата,
	|	ДанныеДокумента.ДоверенностьВыдана КАК ДоверенностьВыдана,
	|	ДанныеДокумента.ДоверенностьЛицо КАК ДоверенностьЛицо,
	|	ДанныеДокумента.Менеджер КАК Менеджер,	
	|	ДанныеДокумента.Отпустил КАК Кладовщик,
	|	ДанныеДокумента.ОтпустилДолжность КАК ДолжностьКладовщика,
	|	ДанныеДокументов.ТребуетсяНаличиеСФ КАК ТребуетсяНаличиеСФ
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ДанныеДокумента.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоставщика КАК ДанныеПоставщика
	|		ПО ДанныеДокумента.Ссылка = ДанныеПоставщика.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураДокументов КАК НоменклатураДокументов
	|		ПО ДанныеДокумента.Ссылка = НоменклатураДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацииВНалоговомОргане КАК ДанныеПодразделений
	|		ПО ДанныеПодразделений.Организация = ДанныеДокумента.Организация
	|			И ДанныеПодразделений.Подразделение = ЕСТЬNULL(ДанныеДокумента.Склад.Подразделение, ДанныеДокумента.Подразделение)
	|			И ДанныеПодразделений.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
#Вставка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бг_ТранспортнаяИнформация КАК бг_ТранспортнаяИнформация
	|		ПО (ДанныеДокумента.Ссылка = бг_ТранспортнаяИнформация.Объект),
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|		ПунктыНазначенияОрганизации.Организация КАК Организация,
	|		ПунктыНазначенияОрганизации.Ссылка.Адрес КАК АдресПунктаНазначения
	|	ИЗ
	|		(ВЫБРАТЬ
	|			МАКСИМУМ(битПунктыНазначения.Ссылка) КАК Ссылка,
	|			ВЫРАЗИТЬ(битПунктыНазначения.Клиент КАК Справочник.Организации) КАК Организация
	|		ИЗ
	|			Справочник.битПунктыНазначения КАК битПунктыНазначения
	|		ГДЕ
	|			ВЫРАЗИТЬ(битПунктыНазначения.Клиент КАК Справочник.Организации) В
	|					(ВЫБРАТЬ
	|						ТаблицаДанныхДокументов.Ссылка.Организация
	|					ИЗ
	|						ТаблицаДанныхДокументов)
	|			И НЕ битПунктыНазначения.ОбособленноеПодразделение
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВЫРАЗИТЬ(битПунктыНазначения.Клиент КАК Справочник.Организации)) КАК ПунктыНазначенияОрганизации) КАК ПунктыНазначения
	|		ПО ПунктыНазначения.Организация = ДанныеДокумента.Организация
#КонецВставки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ТаблицаДокумента.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
	|	ТаблицаДокумента.НоменклатураНабора КАК НоменклатураНабора,
	|	ТаблицаДокумента.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ТаблицаДокумента.ЭтоКомплектующие КАК ЭтоКомплектующие,
	|	ТаблицаДокумента.ЭтоНабор КАК ЭтоНабор,
	|	ТаблицаДокумента.ПолныйНабор КАК ПолныйНабор,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.НоменклатураПартнера КАК НоменклатураПартнера,
	|	ТаблицаДокумента.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаДокумента.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаДокумента.Номенклатура.Код
	|	КОНЕЦ КАК НоменклатураКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ &ТекстЗапросаКодЕдиницыИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
	|	ТаблицаДокумента.Номенклатура.ЕдиницаИзмеренияТНВЭД          КАК ЕдиницаИзмеренияТНВЭД,
	|	ТаблицаДокумента.Номенклатура.ЕдиницаИзмеренияТНВЭД.Представление КАК ЕдиницаИзмеренияТНВЭДНаименование,
	|	ТаблицаДокумента.Номенклатура.ЕдиницаИзмеренияТНВЭД.Код      КАК ЕдиницаИзмеренияТНВЭДКод,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТаблицаДокумента.Серия.Наименование КАК СерияНаименование,	
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.НомерГТД.РегистрационныйНомер = """"
	|			ТОГДА ТаблицаДокумента.НомерГТД
	|		ИНАЧЕ ТаблицаДокумента.НомерГТД.РегистрационныйНомер
	|	КОНЕЦ КАК НомерГТД,
	|	ТаблицаДокумента.КодТНВЭД КАК КодТНВЭД,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Количество
	|		ИНАЧЕ ТаблицаДокумента.КоличествоУпаковок
	|	КОНЕЦ КАК Количество,
	|	ТаблицаДокумента.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ВЫБОР КОГДА НЕ ТаблицаДокумента.КоличествоУпаковок = 0 ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.КоличествоУпаковок ИНАЧЕ 0 КОНЕЦ
	|		ИНАЧЕ ВЫБОР КОГДА НЕ ТаблицаДокумента.Количество = 0 ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.Количество ИНАЧЕ 0 КОНЕЦ
	|	КОНЕЦ КАК Цена,
	|	ТаблицаДокумента.СуммаБезНДС КАК СуммаБезНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС КАК СуммаСНДС,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Серия КАК Серия,
	|	ТаблицаДокумента.Упаковка КАК Упаковка,
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	РеализацияТоваровУслугТаблицаТоваров КАК ТаблицаДокумента
	|ГДЕ
	|	(ТаблицаДокумента.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			ИЛИ ТаблицаДокумента.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|				И НЕ ТаблицаДокумента.Ссылка.ВернутьМногооборотнуюТару)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДокумента.НомерСтрокиНаборы,
	|	ТаблицаДокумента.ЭтоНабор УБЫВ,
	|	ТаблицаДокумента.НомерСтроки
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ШтрихкодУпаковки КАК Штрихкод
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.ШтрихкодыУпаковок КАК ТаблицаДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаДокумента.Ссылка = ДокументыПрямогоОбмена.ДокументОснование
	|		И НЕ ДокументыПрямогоОбмена.ПометкаУдаления
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В(&МассивОбъектов)
	|	И ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL";

	
	// ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	Если ЕстьЭлектронноеАктирование Тогда
		Запрос.Текст = Запрос.Текст	+
		"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка КАК Ссылка,
		|	ТаблицаДокумента.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
		|	ТаблицаДокумента.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
		|	ТаблицаДокумента.НоменклатураНабора КАК НоменклатураНабора,
		|	ТаблицаДокумента.ХарактеристикаНабора КАК ХарактеристикаНабора,
		|	ТаблицаДокумента.ЭтоКомплектующие КАК ЭтоКомплектующие,
		|	ТаблицаДокумента.ЭтоНабор КАК ЭтоНабор,
		|	ТаблицаДокумента.ПолныйНабор КАК ПолныйНабор,
		|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
		|	ТаблицаДокумента.НоменклатураПартнера КАК НоменклатураПартнера,
		|	ТаблицаДокумента.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
		|	ВЫБОР
		|		КОГДА &КолонкаКодов = ""Артикул""
		|			ТОГДА ТаблицаДокумента.Номенклатура.Артикул
		|		ИНАЧЕ ТаблицаДокумента.Номенклатура.Код
		|	КОНЕЦ КАК НоменклатураКод,
		|	ВЫБОР
		|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
		|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения
		|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения
		|	КОНЕЦ КАК ЕдиницаИзмерения,
		|	ВЫБОР
		|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
		|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Представление
		|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения
		|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
		|	ВЫБОР
		|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
		|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Код
		|		ИНАЧЕ &ТекстЗапросаКодЕдиницыИзмерения
		|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
		|	ТаблицаДокумента.Номенклатура.ЕдиницаИзмеренияТНВЭД          КАК ЕдиницаИзмеренияТНВЭД,
		|	ТаблицаДокумента.Номенклатура.ЕдиницаИзмеренияТНВЭД.Представление КАК ЕдиницаИзмеренияТНВЭДНаименование,
		|	ТаблицаДокумента.Номенклатура.ЕдиницаИзмеренияТНВЭД.Код      КАК ЕдиницаИзмеренияТНВЭДКод,
		|	ТаблицаДокумента.Характеристика КАК Характеристика,
		|	ТаблицаДокумента.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
		|	ТаблицаДокумента.Серия.Наименование КАК СерияНаименование,	
		|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.НомерГТД.РегистрационныйНомер = """"
		|			ТОГДА ТаблицаДокумента.НомерГТД
		|		ИНАЧЕ ТаблицаДокумента.НомерГТД.РегистрационныйНомер
		|	КОНЕЦ КАК НомерГТД,
		|	ТаблицаДокумента.КодТНВЭД КАК КодТНВЭД,
		|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
		|	ВЫБОР
		|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
		|			ТОГДА ТаблицаДокумента.Количество
		|		ИНАЧЕ ТаблицаДокумента.КоличествоУпаковок
		|	КОНЕЦ КАК Количество,
		|	ТаблицаДокумента.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
		|	ВЫБОР
		|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
		|			ТОГДА ВЫБОР КОГДА НЕ ТаблицаДокумента.КоличествоУпаковок = 0 ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.КоличествоУпаковок ИНАЧЕ 0 КОНЕЦ
		|		ИНАЧЕ ВЫБОР КОГДА НЕ ТаблицаДокумента.Количество = 0 ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.Количество ИНАЧЕ 0 КОНЕЦ
		|	КОНЕЦ КАК Цена,
		|	ТаблицаДокумента.СуммаБезНДС КАК СуммаБезНДС,
		|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
		|	ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС КАК СуммаСНДС,
		|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
		|	ТаблицаДокумента.Серия КАК Серия,
		|	ТаблицаДокумента.Упаковка КАК Упаковка,
		|	ЛОЖЬ КАК ЭтоВозвратнаяТара
		|
		|ПОМЕСТИТЬ ТаблицаТоваровДокументов
		|ИЗ
		|	РеализацияТоваровУслугТаблицаТоваров КАК ТаблицаДокумента
		|ГДЕ
		|	(ТаблицаДокумента.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|			ИЛИ ТаблицаДокумента.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|				И НЕ ТаблицаДокумента.Ссылка.ВернутьМногооборотнуюТару)
		|"
		+ ЭлектронноеАктированиеЕИСУТ.ТекстЗапросаДанныеДляПечатиСчетовФактур("МассивОбъектов");
		Запрос.УстановитьПараметр("ГосударственныйКонтрактЕИС", Контракт);
		Запрос.УстановитьПараметр("ЭтапИсполненияКонтрактаЕИС", МассивОбъектов[0].ЭтапГосконтрактаЕИС);
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&РеализацияЧерезКомиссионера",
		КомиссионнаяТорговляСервер.ТекстЗапросаРеализацияЧерезКомиссионера("ДанныеДокумента.ХозяйственнаяОперация"));
	
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
	Запрос.УстановитьПараметр("ПредставлениеСчетФактура", НСтр("ru = 'счет-фактура';
																|en = 'tax invoice'"));
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	
	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоШапке          = МассивРезультатов[1];
	РезультатПоТабличнойЧасти = МассивРезультатов[2];
	ШтрихкодыУпаковок         = МассивРезультатов[3].Выгрузить();
	Маркировка                = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое(ШтрихкодыУпаковок);
	СтруктураДанныхДляПечати  = Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти, Маркировка",
	                                             РезультатПоШапке, РезультатПоТабличнойЧасти, Маркировка);

	// ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	Если ЕстьЭлектронноеАктирование Тогда	
		КоличествоРезультатов     = МассивРезультатов.Количество() - 5;
		ЭлектронноеАктированиеЕИСУТ.ПоместитьРезультатВыполненияЗапросаВДанныеДляПечати(
			МассивРезультатов, СтруктураДанныхДляПечати, КоличествоРезультатов);
	КонецЕсли;
	// Конец ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС
	
#Вставка
	Если ПараметрыПечати.Свойство("бг_ПереопределитьМакетУПД") Тогда
		СтруктураДанныхДляПечати.Вставить(
			"бг_ВыводитьШКТовара",
			ПараметрыПечати.бг_ВыводитьШКТовара);
		СтруктураДанныхДляПечати.Вставить(
			"бг_ИспользоватьПечатныеФормыСФ2_5",
			ПараметрыПечати.бг_ИспользоватьПечатныеФормыСФ2_5);
		СтруктураДанныхДляПечати.Вставить(
			"бг_ПереопределитьМакетУПД",
			ПараметрыПечати.бг_ПереопределитьМакетУПД);
	КонецЕсли;	
#КонецВставки
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

#КонецОбласти

#КонецОбласти
