#Область ПрограммныйИнтерфейс

Процедура бг_ЗаполнитьОстаткиФСМ(ДвижениеМатериаловОбъект) Экспорт
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Склад", ДвижениеМатериаловОбъект.Отправитель);
	Запрос.УстановитьПараметр("Регистратор", ДвижениеМатериаловОбъект.Ссылка);
	Запрос.УстановитьПараметр("Период", НачалоДня(ДвижениеМатериаловОбъект.Дата) + 1);
	Запрос.УстановитьПараметр("ФСМ", бг_КонстантыПовтИсп.ЗначениеКонстанты("ФедеральнаяСпецМарка"));
	
	ПодготовитьВременнуюТаблицуОстатковФСМ(Запрос);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОстаткиНаСкладе.Номенклатура КАК Номенклатура,
	|	ОстаткиНаСкладе.Характеристика КАК Характеристика,
	|	ОстаткиНаСкладе.Серия КАК Серия,
	|	ОстаткиНаСкладе.Назначение КАК Назначение,
	|	ОстаткиНаСкладе.Количество КАК Количество
	|ИЗ
	|	ОстаткиНаСкладе КАК ОстаткиНаСкладе";
	Результат = Запрос.Выполнить();
	
	ЗаполнитьТоварыСериямиФСМ(ДвижениеМатериаловОбъект, Результат);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьВременнуюТаблицуОстатковФСМ(Запрос)
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладахОстатки.Характеристика КАК Характеристика,
	|	ТоварыНаСкладахОстатки.Серия КАК Серия,
	|	ТоварыНаСкладахОстатки.Назначение КАК Назначение,
	|	ТоварыНаСкладахОстатки.ВНаличииОстаток КАК Количество
	|ПОМЕСТИТЬ ОстаткиНаСкладеПредварительно
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|			,
	|			Склад = &Склад
	|				И Номенклатура.ВидНоменклатуры = &ФСМ) КАК ТоварыНаСкладахОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыНаСкладах.Номенклатура,
	|	ТоварыНаСкладах.Характеристика,
	|	ТоварыНаСкладах.Серия,
	|	ТоварыНаСкладах.Назначение,
	|	ТоварыНаСкладах.ВНаличии
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах КАК ТоварыНаСкладах
	|ГДЕ
	|	ТоварыНаСкладах.Регистратор = &Регистратор
	|	И ТоварыНаСкладах.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ТоварыНаСкладах.Номенклатура.ВидНоменклатуры = &ФСМ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиНаСкладеПредварительно.Номенклатура КАК Номенклатура,
	|	ОстаткиНаСкладеПредварительно.Характеристика КАК Характеристика,
	|	ОстаткиНаСкладеПредварительно.Серия КАК Серия,
	|	ОстаткиНаСкладеПредварительно.Назначение КАК Назначение,
	|	СУММА(ОстаткиНаСкладеПредварительно.Количество) КАК Количество
	|ПОМЕСТИТЬ ОстаткиНаСкладе
	|ИЗ
	|	ОстаткиНаСкладеПредварительно КАК ОстаткиНаСкладеПредварительно
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиНаСкладеПредварительно.Номенклатура,
	|	ОстаткиНаСкладеПредварительно.Характеристика,
	|	ОстаткиНаСкладеПредварительно.Серия,
	|	ОстаткиНаСкладеПредварительно.Назначение
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Серия";
	Запрос.Выполнить();
КонецПроцедуры

Процедура ЗаполнитьТоварыСериямиФСМ(ДвижениеМатериаловОбъект, РезультатЗапроса)
	ДвижениеМатериаловОбъект.Товары.Очистить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	КэшированныеЗначения = Неопределено;
	
	СтруктураДействий = Новый Структура;
	ДобавитьВСтруктуруДействияПриИзмененииНоменклатуры(ДвижениеМатериаловОбъект, СтруктураДействий);
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрокаТовар = ДвижениеМатериаловОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТовар, Выборка);
		НоваяСтрокаТовар.КоличествоУпаковок = НоваяСтрокаТовар.Количество;
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрокаТовар, СтруктураДействий, КэшированныеЗначения);
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьВСтруктуруДействияПриИзмененииНоменклатуры(ДвижениеМатериаловОбъект, СтруктураДействий)
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", Новый Структура("НужноОкруглять", Ложь));
	СтруктураДействий.Вставить("ЗаполнитьПризнакТипНоменклатуры", Новый Структура("Номенклатура", "ТипНоменклатуры"));
	
	ПараметрыУказанияСерий = Новый ФиксированнаяСтруктура(НоменклатураСервер.ПараметрыУказанияСерий(ДвижениеМатериаловОбъект, Документы.ДвижениеПродукцииИМатериалов));
	ПараметрыПроверкиСерий = Новый Структура("Склад, ПараметрыУказанияСерий");
	ПараметрыПроверкиСерий.Склад = Новый Структура("Отправитель, Получатель", ДвижениеМатериаловОбъект.Отправитель, ДвижениеМатериаловОбъект.Получатель);
	ПараметрыПроверкиСерий.ПараметрыУказанияСерий = ПараметрыУказанияСерий;
	СтруктураДействий.Вставить("ПроверитьСериюРассчитатьСтатус", ПараметрыПроверкиСерий);
	
	ОбеспечениеКлиентСервер.СтруктураДействийВставитьПриИзмененииНазначения(СтруктураДействий);
КонецПроцедуры

#КонецОбласти
