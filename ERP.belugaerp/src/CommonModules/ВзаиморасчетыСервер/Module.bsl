
#Область ПрограммныйИнтерфейс

// Заполнить плановую дату платежа по объектам расчетов
//
// Параметры:
//  ТаблицаОстатковРасчетов - ТаблицаЗначений - Таблица с остатками расчетов
//                            (см. ВзаиморасчетыСервер.ЗаполнитьТаблицуОстатковРасчетов())
//
Процедура бг_ЗаполнитьПлановуюДатуПлатежаВТаблицеОстатковРасчетов(ТаблицаОстатковРасчетов) Экспорт
	
	ИмяКолонкиПлановаяДатаПлатежа = "бг_ПлановаяДатаПлатежа";
	
	КолонкиТаблицыОстатковВзаиморасчетов = ТаблицаОстатковРасчетов.Колонки;
	
	Если КолонкиТаблицыОстатковВзаиморасчетов.Найти(ИмяКолонкиПлановаяДатаПлатежа) = Неопределено Тогда
		ОписаниеТипаДатаВремя = ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя);
		КолонкиТаблицыОстатковВзаиморасчетов.Добавить(ИмяКолонкиПлановаяДатаПлатежа, ОписаниеТипаДатаВремя);
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОстатковРасчетов.ОбъектРасчетов КАК ОбъектРасчетов
	|ПОМЕСТИТЬ ВременнаяТаблица
	|ИЗ
	|	&ТаблицаОстатковРасчетов КАК ТаблицаОстатковРасчетов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыСКлиентамиПоСрокамОстатки.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСКлиентамиПоСрокамОстатки.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения
	|ПОМЕСТИТЬ РасчетыСКлиентамиПоСрокамОстатки
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам.Остатки(
	|			,
	|			ОбъектРасчетов В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					ВременнаяТаблица.ОбъектРасчетов
	|				ИЗ
	|					ВременнаяТаблица КАК ВременнаяТаблица)) КАК РасчетыСКлиентамиПоСрокамОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблица.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ЕСТЬNULL(РасчетыСКлиентамиПоСрокамОстатки.ДатаПлановогоПогашения, ДАТАВРЕМЯ(2999, 1, 1)) КАК ДатаПлановогоПогашения
	|ИЗ
	|	ВременнаяТаблица КАК ВременнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РасчетыСКлиентамиПоСрокамОстатки КАК РасчетыСКлиентамиПоСрокамОстатки
	|		ПО ВременнаяТаблица.ОбъектРасчетов = РасчетыСКлиентамиПоСрокамОстатки.ОбъектРасчетов
	|";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ТаблицаОстатковРасчетов", ТаблицаОстатковРасчетов);
	Результат = Запрос.Выполнить();
	
	МаксимальнаяДатаДляСортировки = Дата("29990101");
	
	ТаблицаОстатковРасчетов.ЗаполнитьЗначения(МаксимальнаяДатаДляСортировки, ИмяКолонкиПлановаяДатаПлатежа);
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаТаблицы = ТаблицаОстатковРасчетов.Найти(Выборка.ОбъектРасчетов, "ОбъектРасчетов");
		Если СтрокаТаблицы <> Неопределено Тогда
			СтрокаТаблицы[ИмяКолонкиПлановаяДатаПлатежа] = Выборка.ДатаПлановогоПогашения;
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры

&После("ИнициализироватьДанныеКонтроляИзменений")
Процедура бг_ИнициализироватьДанныеКонтроляИзменений(Запрос, ТекстыЗапроса, Документ)

	Если ПроведениеДокументов.ЕстьЗаписиВТаблице(Документ, "ДвиженияРасчетыСКлиентамиИзменениеОплачивается")
		И бг_КонстантыПовтИсп.ЗначениеКонстанты("ИспользоватьКонтролиПриОплатеДокументов") = Истина Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ТаблицаОстатков.АналитикаУчетаПоПартнерам           КАК АналитикаУчетаПоПартнерам,
		|	ТаблицаОстатков.ОбъектРасчетов                      КАК ОбъектРасчетов,
		|	ТаблицаОстатков.Валюта                              КАК Валюта,
		|	ТаблицаОстатков.ЗаявкаНаРасходованиеДенежныхСредств КАК ЗаявкаНаРасходованиеДенежныхСредств,
		|	СУММА(ВЫБОР КОГДА ТаблицаОстатков.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|			-ТаблицаОстатков.КОплате
		|		ИНАЧЕ
		|			ТаблицаОстатков.КОплате
		|		КОНЕЦ) КАК Сумма
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК ТаблицаОстатков
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияРасчетыСКлиентамиИзменениеОплачивается КАК Таблица
		|	ПО Таблица.АналитикаУчетаПоПартнерам = ТаблицаОстатков.АналитикаУчетаПоПартнерам
		|		И Таблица.ОбъектРасчетов = ТаблицаОстатков.ОбъектРасчетов
		|		И Таблица.Валюта = ТаблицаОстатков.Валюта
		|		И Таблица.ЗаявкаНаРасходованиеДенежныхСредств = ТаблицаОстатков.ЗаявкаНаРасходованиеДенежныхСредств
		|ГДЕ
		|	ТаблицаОстатков.ОбъектРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаОстатков.АналитикаУчетаПоПартнерам,
		|	ТаблицаОстатков.ОбъектРасчетов,
		|	ТаблицаОстатков.Валюта,
		|	ТаблицаОстатков.ЗаявкаНаРасходованиеДенежныхСредств
		|ИМЕЮЩИЕ
		|	СУММА(ВЫБОР КОГДА ТаблицаОстатков.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|			ТаблицаОстатков.КОплате
		|		ИНАЧЕ
		|			-ТаблицаОстатков.КОплате
		|		КОНЕЦ) < 0
		|";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ОшибкиПереплатаРеализации");
		

		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	Таблица.ОбъектРасчетов КАК ОбъектРасчетов
		|ИЗ
		|	ДвиженияРасчетыСКлиентамиИзменениеОплачивается КАК Таблица,
		|	РеестрДокументовИзменения КАК ДокументИзменения
		|ГДЕ
		|	Таблица.ОбъектРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
		|	И ДокументИзменения.ДатаОтраженияВУчете < Таблица.ОбъектРасчетов.Дата
		|
		|СГРУППИРОВАТЬ ПО
		|	Таблица.ОбъектРасчетов
		|";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ОшибкиДатаРеализацииБольше");

	КонецЕсли;
	
КонецПроцедуры

&После("СообщитьОРезультатахКонтроляИзменений")
Процедура бг_СообщитьОРезультатахКонтроляИзменений(РезультатыКонтроля, Документ, Отказ)
	#Область Оплачивается
	
	Если РезультатыКонтроля.Свойство("ОшибкиПереплатаРеализации") Тогда
	
		ШаблонСообщения = НСтр("ru = 'По объекту расчетов %1 оплачивается больше, чем было реализовано, на %2 %3'");
		
		Для Каждого СтрокаОшибки Из РезультатыКонтроля.ОшибкиПереплатаРеализации Цикл
			ТекстСообщения = СтрШаблон(ШаблонСообщения,
				СтрокаОшибки.ОбъектРасчетов, СтрокаОшибки.Сумма, СтрокаОшибки.Валюта);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Документ,,, Отказ);
		КонецЦикла;
	
	КонецЕсли;

	Если РезультатыКонтроля.Свойство("ОшибкиДатаРеализацииБольше") Тогда
	
		ШаблонСообщения = НСтр("ru = 'Объекту расчетов %1 имеет дату большую, чем дата оплаты'");
		
		Для Каждого СтрокаОшибки Из РезультатыКонтроля.ОшибкиДатаРеализацииБольше Цикл
			ТекстСообщения = СтрШаблон(ШаблонСообщения, СтрокаОшибки.ОбъектРасчетов);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Документ,,, Отказ);
		КонецЦикла;
	
	КонецЕсли;
	
	#КонецОбласти
КонецПроцедуры

#КонецОбласти
