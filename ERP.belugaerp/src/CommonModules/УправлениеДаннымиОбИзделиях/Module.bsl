&Вместо("ТаблицаДанныхОбИзделииДляВыбораСпецификации")
Функция бг_ТаблицаДанныхОбИзделииДляВыбораСпецификации() Экспорт
	
	Результат = ПродолжитьВызов();
	
	Результат.Колонки.Добавить("бг_ОсновноеИзделиеУпаковкаПаллета", Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения"));
	
	Возврат Результат;
	
КонецФункции

&ИзменениеИКонтроль("СпецификацииИзделий")
Функция бг_СпецификацииИзделий(ДанныеОбИзделиях, ПараметрыВыбораСпецификаций) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДанныеОбИзделиях) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Т.ИндексДанных                            КАК ИндексДанных,
	|	Т.Номенклатура                            КАК Номенклатура,
	#Вставка
	|	Т.бг_ОсновноеИзделиеУпаковкаПаллета       КАК бг_ОсновноеИзделиеУпаковкаПаллета,
	#КонецВставки
	|	Т.Характеристика                          КАК Характеристика,
	|	НАЧАЛОПЕРИОДА(Т.НачалоПроизводства, ДЕНЬ) КАК НачалоПроизводства,
	|	Т.ПодразделениеДиспетчер                  КАК ПодразделениеДиспетчер,
	|	Т.НаправлениеДеятельности                 КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ СписокНоменклатуры
	|ИЗ
	|	&СписокНоменклатуры КАК Т
	|
	|;
	|
	|ВЫБРАТЬ
	|	Т.*,
	|	Т.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры
	|ПОМЕСТИТЬ ВтСписокНоменклатуры
	|ИЗ
	|	СписокНоменклатуры КАК Т
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура, Характеристика
	|;
	|
	|";
	
	СписокНоменклатуры = ТаблицаДанныхОбИзделииДляВыбораСпецификации();
	Для ИндексДанных = 0 По ДанныеОбИзделиях.ВГраница() Цикл
		НоваяСтрока = СписокНоменклатуры.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеОбИзделиях[ИндексДанных]);
		НоваяСтрока.ИндексДанных = ИндексДанных;
	КонецЦикла;
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	
	ПараметрыТекстаЗапроса = ПараметрыТекстаЗапросаСпецификацийНоменклатуры();
	
	#Вставка
	
	ТекстУсловиеОтборПоПаллете = "(СписокНоменклатуры.бг_ОсновноеИзделиеУпаковкаПаллета = Значение(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
					|	Или СпецификацииИзделий.Спецификация.бг_ОсновноеИзделиеУпаковкаПаллета = СписокНоменклатуры.бг_ОсновноеИзделиеУпаковкаПаллета)";
	
	ПараметрыТекстаЗапроса.УсловияОтбораВходящихДанных.Добавить(ТекстУсловиеОтборПоПаллете);
	
	#КонецВставки
	
	Запрос.Текст = Запрос.Текст + ТекстЗапросаСпецификацийНоменклатуры(
		ПараметрыТекстаЗапроса,
		ПараметрыВыбораСпецификаций);
	
	УстановитьПараметрыЗапросаСпецификацийНоменклатуры(Запрос, ПараметрыВыбораСпецификаций);
	
	СписокСпецификаций = Запрос.Выполнить().Выгрузить();
	
	Результат = Новый Массив(ДанныеОбИзделиях.Количество());
	
	ПрекратитьОбработку = Новый Соответствие;
	
	Для Индекс = 0 По СписокСпецификаций.Количество() - 1 Цикл
		
		СтрокаСпецификация = СписокСпецификаций[Индекс];
		ИндексДанных       = СтрокаСпецификация.ИндексДанных;
		
		Если ПрекратитьОбработку.Получить(ИндексДанных) = Истина Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеОбИзделии        = ДанныеОбИзделиях[ИндексДанных];
		ПодобратьАвтоматически = (Результат[ИндексДанных] = Неопределено И СтрокаСпецификация.ПодбираетсяАвтоматически = Истина);
		
		Если ДанныеОбИзделии.Свойство("ТекущаяСпецификация") И ЗначениеЗаполнено(ДанныеОбИзделии.ТекущаяСпецификация) Тогда
		
			ЭтоТекущаяСпецификация = (СтрокаСпецификация.Спецификация = ДанныеОбИзделии.ТекущаяСпецификация);
			
			Если ЭтоТекущаяСпецификация ИЛИ ПодобратьАвтоматически Тогда
				Результат[ИндексДанных] = Новый Структура("Спецификация,"+ПараметрыВыбораСпецификаций.РеквизитыСпецификации);
				ЗаполнитьЗначенияСвойств(Результат[ИндексДанных], СтрокаСпецификация);
			КонецЕсли;
			
			Если ЭтоТекущаяСпецификация Тогда
				ПрекратитьОбработку.Вставить(ИндексДанных, Истина);
			КонецЕсли;
		
		ИначеЕсли ПодобратьАвтоматически Тогда
			
			Результат[ИндексДанных] = Новый Структура("Спецификация,"+ПараметрыВыбораСпецификаций.РеквизитыСпецификации);
			ЗаполнитьЗначенияСвойств(Результат[ИндексДанных], СтрокаСпецификация);
			ПрекратитьОбработку.Вставить(ИндексДанных, Истина);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&ИзменениеИКонтроль("СпецификацииИзделия")
Функция бг_СпецификацииИзделия(ДанныеОбИзделии, ПараметрыВыбораСпецификаций) Экспорт
	
	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("РесурсныеСпецификации.СпецификацииИзделия");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	0                                                                   КАК ИндексДанных,
	|	ВЫРАЗИТЬ(&Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры КАК ВидНоменклатуры,
	|	&Номенклатура                                                       КАК Номенклатура,
	#Вставка
	|	&бг_ОсновноеИзделиеУпаковкаПаллета                                  КАК бг_ОсновноеИзделиеУпаковкаПаллета,
	#КонецВставки
	|	&Характеристика                                                     КАК Характеристика,
	|	НАЧАЛОПЕРИОДА(&НачалоПроизводства, ДЕНЬ)                            КАК НачалоПроизводства,
	|	&ПодразделениеДиспетчер                                             КАК ПодразделениеДиспетчер,
	|	&НаправлениеДеятельности                                            КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ ВтСписокНоменклатуры
	|;
	|";
	
	Запрос.УстановитьПараметр("Номенклатура",            ДанныеОбИзделии.Номенклатура);
	Запрос.УстановитьПараметр("Характеристика",          ДанныеОбИзделии.Характеристика);
	Запрос.УстановитьПараметр("НачалоПроизводства",      ДанныеОбИзделии.НачалоПроизводства);
	Запрос.УстановитьПараметр("ПодразделениеДиспетчер",  ДанныеОбИзделии.ПодразделениеДиспетчер);
	Запрос.УстановитьПараметр("НаправлениеДеятельности", ДанныеОбИзделии.НаправлениеДеятельности);
	
	ПараметрыТекстаЗапроса = ПараметрыТекстаЗапросаСпецификацийНоменклатуры();
	
	#Вставка
	
	Запрос.УстановитьПараметр("бг_ОсновноеИзделиеУпаковкаПаллета", ДанныеОбИзделии.бг_ОсновноеИзделиеУпаковкаПаллета);
	
	ТекстУсловиеОтборПоПаллете = "(СписокНоменклатуры.бг_ОсновноеИзделиеУпаковкаПаллета = Значение(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
					|	Или СпецификацииИзделий.Спецификация.бг_ОсновноеИзделиеУпаковкаПаллета = СписокНоменклатуры.бг_ОсновноеИзделиеУпаковкаПаллета)";
	
	ПараметрыТекстаЗапроса.УсловияОтбораВходящихДанных.Добавить(ТекстУсловиеОтборПоПаллете);
	
	#КонецВставки
	
	Запрос.Текст = Запрос.Текст +  ТекстЗапросаСпецификацийНоменклатуры(
		ПараметрыТекстаЗапроса,
		ПараметрыВыбораСпецификаций);
	
	УстановитьПараметрыЗапросаСпецификацийНоменклатуры(Запрос, ПараметрыВыбораСпецификаций);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СписокСпецификаций = Новый Массив;
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ДанныеСпецификации = Новый Структура("Спецификация,ПодбираетсяАвтоматически,"+ПараметрыВыбораСпецификаций.РеквизитыСпецификации);
			ЗаполнитьЗначенияСвойств(ДанныеСпецификации, Выборка);
			СписокСпецификаций.Добавить(ДанныеСпецификации);
		КонецЦикла;
		
	КонецЕсли;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, СписокСпецификаций.Количество());
	
	Возврат СписокСпецификаций;
	
КонецФункции
