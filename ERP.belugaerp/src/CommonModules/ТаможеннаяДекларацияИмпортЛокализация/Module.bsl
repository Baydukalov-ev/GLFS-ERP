#Область ПрограммныйИнтерфейс

#Область Проведение

&После("ЗарегистрироватьУчетныеМеханизмы")
Процедура бг_ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента)
	
	МеханизмыДокумента.Добавить("бг_УчетБанковскихГарантий");
	
КонецПроцедуры

Функция бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностям() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Документ.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	Документ.Организация КАК Организация,
	|	АкцизПоПриобретеннымЦенностям.Номенклатура КАК Номенклатура,
	|	АкцизПоПриобретеннымЦенностям.Серия КАК СерияНоменклатуры,
	|	АкцизПоПриобретеннымЦенностям.ДокументПоступления КАК Сделка,
	|	ЗНАЧЕНИЕ(Перечисление.бг_СостоянияПодакцизногоСырья.ВМестахХранения) КАК СостояниеСырья,
	|	ЗНАЧЕНИЕ(Перечисление.бг_СостоянияОплатыВходящегоАкциза.АкцизПоОплаченнымМЦ) КАК СтатусАкциза,
	|	СУММА(АкцизПоПриобретеннымЦенностям.Количество) КАК Количество
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияИмпорт.Товары КАК АкцизПоПриобретеннымЦенностям
	|		ПО (Документ.Ссылка = &Ссылка)
	|			И Документ.Ссылка = АкцизПоПриобретеннымЦенностям.Ссылка
	|ГДЕ
	|	Документ.Организация.бг_ВестиУчетАкцизовПоПриобретеннымЦенностям
	|	И Документ.Дата >= Документ.Организация.бг_ДатаНачалаУчетаАкцизовПоПриобретеннымЦенностям
	|	И АкцизПоПриобретеннымЦенностям.Номенклатура.АлкогольнаяПродукция
	|
	|СГРУППИРОВАТЬ ПО
	|	Документ.Дата,
	|	Документ.Организация,
	|	АкцизПоПриобретеннымЦенностям.Номенклатура,
	|	АкцизПоПриобретеннымЦенностям.Серия,
	|	АкцизПоПриобретеннымЦенностям.ДокументПоступления";
	
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

&Вместо("ТекстОтраженияВРеглУчете")
Функция бг_ТекстОтраженияВРеглУчете()
	
	Результат = ПродолжитьВызов();
	
	ТекстыОтражения = Новый Массив;
	ТекстыОтражения.Добавить(Результат);
	ТекстыОтражения.Добавить(бг_ТекстОтраженияАкцизов());
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

Функция бг_ТекстОтраженияАкцизов()
	
	ТекстАкцизы = "ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СУММА(Строки.бг_СуммаАкциза) КАК Сумма,
	|	Строки.СуммаПошлины КАК СуммаУУ,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПустаяСсылка) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	бг_ВидыОперацийНалогообложенияАкцизом.СчетДт КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПустаяСсылка) КАК ВидСчетаКт,
	|	Операция.Контрагент КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	Операция.ВалютаВзаиморасчетов КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКТ,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	бг_ВидыОперацийНалогообложенияАкцизом.СчетКт КАК СчетКт,
	|	Операция.Контрагент КАК СубконтоКт1,
	|	Операция.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	СУММА(Строки.бг_СуммаАкциза) КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Начислен акциз"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияИмпорт КАК Операция
	|		ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияИмпорт.Товары КАК Строки
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.бг_ВидыОперацийНалогообложенияАкцизом КАК бг_ВидыОперацийНалогообложенияАкцизом
	|			ПО Строки.бг_ВидОперацииАкциза = бг_ВидыОперацийНалогообложенияАкцизом.Ссылка
	|		ПО (Строки.Ссылка = Операция.Ссылка)
	|ГДЕ
	|	Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|	И Операция.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыТаможенныхДеклараций.ВыпущеноСТаможни)
	|	И Строки.бг_СуммаАкциза <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	бг_ВидыОперацийНалогообложенияАкцизом.СчетКт,
	|	Операция.Ссылка,
	|	бг_ВидыОперацийНалогообложенияАкцизом.СчетДт,
	|	Операция.Дата,
	|	Операция.Организация,
	|	Операция.ВалютаВзаиморасчетов,
	|	Операция.Подразделение,
	|	Операция.Контрагент,
	|	Операция.Договор,
	|	Строки.ИдентификаторСтроки,
	|	Строки.СуммаПошлины,
	|	Операция.Контрагент";
	
	Возврат ТекстАкцизы;
	 
КонецФункции

&После("ДополнитьТекстыЗапросовПроведения")
Процедура бг_ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры)
	
	бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностям(Запрос, ТекстыЗапроса, Регистры);
	
КонецПроцедуры

Процедура бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностям(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "бг_АкцизПоПриобретеннымЦенностям";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностям(), ИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
