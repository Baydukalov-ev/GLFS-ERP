
#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	адаптер_НастройкиОбмена.ОставитьРеквизиты(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		ВыгружаемыеРеквизиты());
		
	ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства);
	
КонецПроцедуры

Функция ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения) Экспорт
	
	Перем адаптер_обработчикиСобытийСтандартный;
	адаптер_обработчикиСобытийСтандартный = ОбщегоНазначения.ОбщийМодуль("адаптер_обработчикиСобытийСтандартный");
	
	ДанныеОбъекта = адаптер_обработчикиСобытийСтандартный.ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения);
	
	РеквизитыОбъекта = ДанныеОбъекта.Реквизиты[0];
	
	ТекущийСтатусЕГАИС = РегистрыСведений.СтатусыДокументовЕГАИС.ТекущееСостояние(Объект.Ссылка).Статус;
	РеквизитыОбъекта.Вставить("бг_ТекущийСтатусЕГАИС", бг_ОбщегоНазначенияСервер.ЗначениеПеречислениеВСтруктуру(ТекущийСтатусЕГАИС));
	
	СписываемыеАкцизныеМарки = СписываемыеАкцизныеМарки(Объект.Ссылка);
	Если СписываемыеАкцизныеМарки <> Неопределено Тогда
		РеквизитыОбъекта.Вставить("бг_АкцизныеМарки", СписываемыеАкцизныеМарки);
	КонецЕсли;

	Возврат ДанныеОбъекта;
	
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция ВыгружаемыеРеквизиты()

	ВыгружаемыеРеквизиты = Новый Массив;
	
	//Шапка документа
	ВыгружаемыеРеквизиты.Добавить("Дата");
	ВыгружаемыеРеквизиты.Добавить("Номер");
	ВыгружаемыеРеквизиты.Добавить("ПометкаУдаления");
	ВыгружаемыеРеквизиты.Добавить("Проведен");
	ВыгружаемыеРеквизиты.Добавить("ВидДокумента");
	ВыгружаемыеРеквизиты.Добавить("ОрганизацияЕГАИС");
	ВыгружаемыеРеквизиты.Добавить("ИдентификаторЕГАИС");
	ВыгружаемыеРеквизиты.Добавить("ПричинаСписания");
	ВыгружаемыеРеквизиты.Добавить("ДатаРегистрацииДвижений");
	ВыгружаемыеРеквизиты.Добавить("ДокументОснование");
	ВыгружаемыеРеквизиты.Добавить("Ответственный");
	ВыгружаемыеРеквизиты.Добавить("Комментарий");
	ВыгружаемыеРеквизиты.Добавить("ДанныеПроверкиИПодбора");
	ВыгружаемыеРеквизиты.Добавить("Производитель");
	ВыгружаемыеРеквизиты.Добавить("СтатусПроверкиИПодбора");
	
	//Табличная часть "Товары"
	ВыгружаемыеРеквизиты.Добавить("Товары.АлкогольнаяПродукция");
	ВыгружаемыеРеквизиты.Добавить("Товары.Количество");
	ВыгружаемыеРеквизиты.Добавить("Товары.Номенклатура");
	ВыгружаемыеРеквизиты.Добавить("Товары.Характеристика");
	ВыгружаемыеРеквизиты.Добавить("Товары.Серия");
	ВыгружаемыеРеквизиты.Добавить("Товары.Упаковка");
	ВыгружаемыеРеквизиты.Добавить("Товары.КоличествоУпаковок");
	ВыгружаемыеРеквизиты.Добавить("Товары.Справка2");
	ВыгружаемыеРеквизиты.Добавить("Товары.Цена");
	ВыгружаемыеРеквизиты.Добавить("Товары.Сумма");
	ВыгружаемыеРеквизиты.Добавить("Товары.ИдентификаторСтроки");
	ВыгружаемыеРеквизиты.Добавить("Товары.СтатусУказанияСерий");
	
	//Табличная часть "АкцизныеМарки"
	ВыгружаемыеРеквизиты.Добавить("АкцизныеМарки.КодАкцизнойМарки");
	ВыгружаемыеРеквизиты.Добавить("АкцизныеМарки.АкцизнаяМарка");
	ВыгружаемыеРеквизиты.Добавить("АкцизныеМарки.ШтрихкодУпаковки");
	ВыгружаемыеРеквизиты.Добавить("АкцизныеМарки.Справка2");
	ВыгружаемыеРеквизиты.Добавить("АкцизныеМарки.Количество");
	ВыгружаемыеРеквизиты.Добавить("АкцизныеМарки.ИдентификаторСтроки");

	ВыгружаемыеРеквизиты = СтрСоединить(ВыгружаемыеРеквизиты, ",");
	
	Возврат ВыгружаемыеРеквизиты;
	
КонецФункции

Процедура ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства)

	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	// Реквизиты шапки
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		,
		"бг_ТекущийСтатусЕГАИС",
		Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыОбработкиАктаСписанияЕГАИС"));
		
	// Таблица бг_АкцизныеМарки
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		, // ИмяРеквизита
		"бг_АкцизныеМарки.ИдентификаторМарки",
		Метаданные.РегистрыСведений.бг_ИдентификаторыМарок.Ресурсы.ИдентификаторМарки.Тип);
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		, // ИмяРеквизита
		"бг_АкцизныеМарки.НомерСтрокиАкта",
		ОбщегоНазначения.ОписаниеТипаЧисло(5));

КонецПроцедуры

Функция СписываемыеАкцизныеМарки(АктСписанияЕГАИС)
	
	ДанныеСписываемыхШтрихкодов = Документы.АктСписанияЕГАИС.бг_ДанныеСписываемыхШтрихкодов(АктСписанияЕГАИС);	
	Если ДанныеСписываемыхШтрихкодов = Неопределено Тогда
		Возврат Неопределено;	
	КонецЕсли;
	
	СписываемыеМарки = ДанныеСписываемыхШтрихкодов.Марки.Скопировать(, "ИдентификаторМарки, НомерСтрокиАкта");
	
	Возврат СписываемыеМарки;
	
КонецФункции

#КонецОбласти
