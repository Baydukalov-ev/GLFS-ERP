
#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	Реквизиты = ВыгружаемыеРеквизиты();
	
	адаптер_НастройкиОбмена.УстановитьРеквизиты(
		РеквизитыИСвойства, 
		РеквизитыИСвойства.МетаданныеОбъекта, 
		Реквизиты);
		
	ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства);
	
КонецПроцедуры

Процедура ЗаполнитьТекстыЗапросовУсловиями(ТекстЗапроса, ТекстЗапросаТаблицаКлючей, ПараметрыЗапроса, НастройкаВыгрузки, Объект, СтандартнаяОбработка) Экспорт
	
	ТекстПоиска =
	"ВЫБРАТЬ 
	|	NULL КАК Реквизит
	|	
	|ИЗ
	|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК ВыгружаемыйОбъект";
	
	ТекстЗамены =
	"ВЫБРАТЬ 
	|	ВыгружаемыйОбъект.ОбъектРасчетов.Объект КАК Реквизит
	|	
	|ИЗ
	|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК ВыгружаемыйОбъект";
	
	ТекстЗапросаТаблицаКлючей = СтрЗаменить(ТекстЗапросаТаблицаКлючей, ТекстПоиска, ТекстЗамены);
	
КонецПроцедуры

Функция ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения) Экспорт
	
	Перем адаптер_РаботаСДаннымиИБ;
	адаптер_РаботаСДаннымиИБ = ОбщегоНазначения.ОбщийМодуль("адаптер_РаботаСДаннымиИБ");
	
	ПараметрыВыполненияЗапросов = адаптер_РаботаСДаннымиИБ.ПолучитьПараметрыВыполненияЗапросов(Объект, ДанныеСообщения);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДанные();
	Запрос.УстановитьПараметр("Ссылка", ПараметрыВыполненияЗапросов.ПараметрыЗапроса.Ссылка);
	Результат = Запрос.ВыполнитьПакет();
	
	СтруктураРезультат = Новый Структура;
	СтруктураРезультат.Вставить("ПолноеИмя", ПараметрыВыполненияЗапросов.ПолноеИмя);
	СтруктураРезультат.Вставить("Реквизиты",
			адаптер_РаботаСДаннымиИБ.РезультатЗапросаВСтруктуруРеквизитов(Результат[1], ПараметрыВыполненияЗапросов.ТаблицаКлючей, ДанныеСообщения));
	СтруктураРезультат.Реквизиты[0].Вставить("ПрочиеРасходы",
			адаптер_РаботаСДаннымиИБ.РезультатЗапросаВСтруктуруРеквизитов(Результат[2], ПараметрыВыполненияЗапросов.ТаблицаКлючей, ДанныеСообщения));
	СтруктураРезультат.Реквизиты[0].Вставить("ОплатаПоставщикам",
			адаптер_РаботаСДаннымиИБ.РезультатЗапросаВСтруктуруРеквизитов(Результат[3], ПараметрыВыполненияЗапросов.ТаблицаКлючей, ДанныеСообщения));
	
	Возврат СтруктураРезультат;
	
КонецФункции

#КонецОбласти // Конец ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ВыгружаемыеРеквизиты()

	ВыгружаемыеРеквизиты = Новый Массив;
	
	ВыгружаемыеРеквизиты.Добавить("Дата");
	ВыгружаемыеРеквизиты.Добавить("Номер");
	ВыгружаемыеРеквизиты.Добавить("Проведен");
	ВыгружаемыеРеквизиты.Добавить("ПометкаУдаления");
	ВыгружаемыеРеквизиты.Добавить("Организация");
	ВыгружаемыеРеквизиты.Добавить("ПодотчетноеЛицо");
	ВыгружаемыеРеквизиты.Добавить("Валюта");
	ВыгружаемыеРеквизиты.Добавить("НазначениеАванса");
	ВыгружаемыеРеквизиты.Добавить("КоличествоДокументов");
	ВыгружаемыеРеквизиты.Добавить("КоличествоЛистов");
	ВыгружаемыеРеквизиты.Добавить("Подразделение");
	ВыгружаемыеРеквизиты.Добавить("Автор");
	ВыгружаемыеРеквизиты.Добавить("Комментарий");
	
	ВыгружаемыеРеквизиты.Добавить("ПрочиеРасходы.НомерВходящегоДокумента");
	ВыгружаемыеРеквизиты.Добавить("ПрочиеРасходы.ДатаВходящегоДокумента");
	ВыгружаемыеРеквизиты.Добавить("ПрочиеРасходы.НаименованиеВходящегоДокумента");
	ВыгружаемыеРеквизиты.Добавить("ПрочиеРасходы.Контрагент");
	ВыгружаемыеРеквизиты.Добавить("ПрочиеРасходы.Содержание");
	ВыгружаемыеРеквизиты.Добавить("ПрочиеРасходы.Сумма");
	ВыгружаемыеРеквизиты.Добавить("ПрочиеРасходы.СтавкаНДС");
	ВыгружаемыеРеквизиты.Добавить("ПрочиеРасходы.СуммаНДС");
	ВыгружаемыеРеквизиты.Добавить("ПрочиеРасходы.СпособУчетаНДС");
	ВыгружаемыеРеквизиты.Добавить("ПрочиеРасходы.Подразделение");
	ВыгружаемыеРеквизиты.Добавить("ПрочиеРасходы.СтатьяРасходов");
	
	ВыгружаемыеРеквизиты.Добавить("ОплатаПоставщикам.Контрагент");
	ВыгружаемыеРеквизиты.Добавить("ОплатаПоставщикам.Сумма");
	ВыгружаемыеРеквизиты.Добавить("ОплатаПоставщикам.ВалютаВзаиморасчетов");
	ВыгружаемыеРеквизиты.Добавить("ОплатаПоставщикам.СуммаВзаиморасчетов");
	ВыгружаемыеРеквизиты.Добавить("ОплатаПоставщикам.НомерВходящегоДокумента");
	ВыгружаемыеРеквизиты.Добавить("ОплатаПоставщикам.ДатаВходящегоДокумента");
	ВыгружаемыеРеквизиты.Добавить("ОплатаПоставщикам.Комментарий");
	
	Возврат ВыгружаемыеРеквизиты;
	
КонецФункции

Процедура ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства)
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	// ПрочиеРасходы
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		,
		"ПрочиеРасходы.СчетУчета",
		Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	
	// ОплатаПоставщикам
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		,
		"ОплатаПоставщикам.Договор",
		Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
		
КонецПроцедуры

Функция ТекстЗапросаДанные()
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ХозрасчетныйДвиженияССубконто.СубконтоДт1 КАК СтатьяРасходов,
	|	МАКСИМУМ(ХозрасчетныйДвиженияССубконто.СчетДт) КАК Счет
	|ПОМЕСТИТЬ втСчетаУчета
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто КАК ХозрасчетныйДвиженияССубконто
	|ГДЕ
	|	ХозрасчетныйДвиженияССубконто.Регистратор = &Ссылка
	|	И ТИПЗНАЧЕНИЯ(ХозрасчетныйДвиженияССубконто.СубконтоДт1) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйДвиженияССубконто.СубконтоДт1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыгружаемыйОбъект.Автор.Наименование КАК Автор_ЗначениеРеквизитаНаименование,
	|	ВыгружаемыйОбъект.Автор.Ссылка КАК Автор_ЗначениеРеквизитаИдентификатор,
	|	ВыгружаемыйОбъект.Автор.Ссылка КАК Автор_ЗначениеРеквизитаТаблицаКлючей,
	|	ВыгружаемыйОбъект.Валюта.Код КАК Валюта_ЗначениеРеквизитаКод,
	|	ВыгружаемыйОбъект.Валюта.Ссылка КАК Валюта_ЗначениеРеквизитаИдентификатор,
	|	ВыгружаемыйОбъект.Валюта.Ссылка КАК Валюта_ЗначениеРеквизитаТаблицаКлючей,
	|	ВыгружаемыйОбъект.Дата КАК Дата,
	|	ВыгружаемыйОбъект.Комментарий КАК Комментарий,
	|	ВыгружаемыйОбъект.НазначениеАванса КАК НазначениеАванса,
	|	ВыгружаемыйОбъект.Номер КАК Номер,
	|	ВыгружаемыйОбъект.Организация.Ссылка КАК Организация_ЗначениеРеквизитаИдентификатор,
	|	ВыгружаемыйОбъект.Организация.Ссылка КАК Организация_ЗначениеРеквизитаТаблицаКлючей,
	|	ВыгружаемыйОбъект.ПодотчетноеЛицо.Код КАК ПодотчетноеЛицо_ЗначениеРеквизитаКод,
	|	ВыгружаемыйОбъект.ПодотчетноеЛицо.бг_КодБК КАК ПодотчетноеЛицо_ЗначениеРеквизитабг_КодБК,
	|	ВыгружаемыйОбъект.ПодотчетноеЛицо.Ссылка КАК ПодотчетноеЛицо_ЗначениеРеквизитаИдентификатор,
	|	ВыгружаемыйОбъект.ПодотчетноеЛицо.Ссылка КАК ПодотчетноеЛицо_ЗначениеРеквизитаТаблицаКлючей,
	|	ВыгружаемыйОбъект.Подразделение.Код КАК Подразделение_ЗначениеРеквизитаКод,
	|	ВыгружаемыйОбъект.Подразделение.Ссылка КАК Подразделение_ЗначениеРеквизитаИдентификатор,
	|	ВыгружаемыйОбъект.Подразделение.Ссылка КАК Подразделение_ЗначениеРеквизитаТаблицаКлючей,
	|	ВыгружаемыйОбъект.КоличествоДокументов КАК КоличествоДокументов,
	|	ВыгружаемыйОбъект.КоличествоЛистов КАК КоличествоЛистов,
	|	ВыгружаемыйОбъект.Проведен КАК Проведен,
	|	ВыгружаемыйОбъект.Ссылка КАК Идентификатор,
	|	ВыгружаемыйОбъект.Ссылка КАК ТаблицаКлючей
	|ИЗ
	|	Документ.АвансовыйОтчет КАК ВыгружаемыйОбъект
	|ГДЕ
	|	ВыгружаемыйОбъект.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрочиеРасходы.НаименованиеВходящегоДокумента КАК НаименованиеВходящегоДокумента,
	|	ПрочиеРасходы.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	ПрочиеРасходы.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	ПрочиеРасходы.Сумма КАК Сумма,
	|	ВЫРАЗИТЬ(ПрочиеРасходы.СтатьяРасходов КАК ПланВидовХарактеристик.СтатьиРасходов).Код КАК СтатьяРасходов_ЗначениеРеквизитаКод,
	|	ВЫРАЗИТЬ(ПрочиеРасходы.СтатьяРасходов КАК ПланВидовХарактеристик.СтатьиРасходов).Ссылка КАК СтатьяРасходов_ЗначениеРеквизитаИдентификатор,
	|	ВЫРАЗИТЬ(ПрочиеРасходы.СтатьяРасходов КАК ПланВидовХарактеристик.СтатьиРасходов).Ссылка КАК СтатьяРасходов_ЗначениеРеквизитаТаблицаКлючей,
	|	ВЫРАЗИТЬ(ПрочиеРасходы.СтатьяРасходов КАК ПланВидовХарактеристик.СтатьиАктивовПассивов).Ссылка КАК СтатьяРасходов_ЗначениеРеквизитаИдентификатор_Сч1,
	|	ВЫРАЗИТЬ(ПрочиеРасходы.СтатьяРасходов КАК ПланВидовХарактеристик.СтатьиАктивовПассивов).Ссылка КАК СтатьяРасходов_ЗначениеРеквизитаТаблицаКлючей_Сч1,
	|	ТИПЗНАЧЕНИЯ(ПрочиеРасходы.СтатьяРасходов) КАК СтатьяРасходов_ЗначениеРеквизитаТипЗначения,
	|	ПрочиеРасходы.Подразделение.Код КАК Подразделение_ЗначениеРеквизитаКод,
	|	ПрочиеРасходы.Подразделение.Ссылка КАК Подразделение_ЗначениеРеквизитаИдентификатор,
	|	ПрочиеРасходы.Подразделение.Ссылка КАК Подразделение_ЗначениеРеквизитаТаблицаКлючей,
	|	ПрочиеРасходы.СтавкаНДС.Предопределенный КАК СтавкаНДС_ЗначениеРеквизитаПредопределенный,
	|	ПрочиеРасходы.СтавкаНДС.Ставка КАК СтавкаНДС_ЗначениеРеквизитаСтавка,
	|	ПрочиеРасходы.СтавкаНДС.Ссылка КАК СтавкаНДС_ЗначениеРеквизитаИдентификатор,
	|	ПрочиеРасходы.СтавкаНДС.Ссылка КАК СтавкаНДС_ЗначениеРеквизитаТаблицаКлючей,
	|	ПрочиеРасходы.СуммаНДС КАК СуммаНДС,
	|	ПрочиеРасходы.Контрагент.ИНН КАК Контрагент_ЗначениеРеквизитаИНН,
	|	ПрочиеРасходы.Контрагент.КПП КАК Контрагент_ЗначениеРеквизитаКПП,
	|	ПрочиеРасходы.Контрагент.Ссылка КАК Контрагент_ЗначениеРеквизитаИдентификатор,
	|	ПрочиеРасходы.Контрагент.Ссылка КАК Контрагент_ЗначениеРеквизитаТаблицаКлючей,
	|	ПрочиеРасходы.Содержание КАК Содержание,
	|	втСчетаУчета.Счет КАК СчетУчета_ЗначениеРеквизитаИдентификатор,
	|	втСчетаУчета.Счет КАК СчетУчета_ЗначениеРеквизитаТаблицаКлючей,
	|	втСчетаУчета.Счет.Код КАК СчетУчета_ЗначениеРеквизитаКод
	|ИЗ
	|	Документ.АвансовыйОтчет.ПрочиеРасходы КАК ПрочиеРасходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаУчета КАК втСчетаУчета
	|		ПО ПрочиеРасходы.СтатьяРасходов = втСчетаУчета.СтатьяРасходов
	|ГДЕ
	|	ПрочиеРасходы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АвансовыйОтчетОплатаПоставщикам.Контрагент.ИНН КАК Контрагент_ЗначениеРеквизитаИНН,
	|	АвансовыйОтчетОплатаПоставщикам.Контрагент.КПП КАК Контрагент_ЗначениеРеквизитаКПП,
	|	АвансовыйОтчетОплатаПоставщикам.Контрагент.Ссылка КАК Контрагент_ЗначениеРеквизитаИдентификатор,
	|	АвансовыйОтчетОплатаПоставщикам.Контрагент.Ссылка КАК Контрагент_ЗначениеРеквизитаТаблицаКлючей,
	|	АвансовыйОтчетОплатаПоставщикам.Сумма КАК Сумма,
	|	АвансовыйОтчетОплатаПоставщикам.ВалютаВзаиморасчетов.Код КАК ВалютаВзаиморасчетов_ЗначениеРеквизитаКод,
	|	АвансовыйОтчетОплатаПоставщикам.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов_ЗначениеРеквизитаИдентификатор,
	|	АвансовыйОтчетОплатаПоставщикам.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов_ЗначениеРеквизитаТаблицаКлючей,
	|	АвансовыйОтчетОплатаПоставщикам.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	АвансовыйОтчетОплатаПоставщикам.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	АвансовыйОтчетОплатаПоставщикам.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	АвансовыйОтчетОплатаПоставщикам.ОбъектРасчетов.Объект КАК Договор_ЗначениеРеквизитаИдентификатор,
	|	АвансовыйОтчетОплатаПоставщикам.ОбъектРасчетов.Объект КАК Договор_ЗначениеРеквизитаТаблицаКлючей,
	|	АвансовыйОтчетОплатаПоставщикам.Комментарий КАК Комментарий
	|ИЗ
	|	Документ.АвансовыйОтчет.ОплатаПоставщикам КАК АвансовыйОтчетОплатаПоставщикам
	|ГДЕ
	|	АвансовыйОтчетОплатаПоставщикам.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти // Конец СлужебныеПроцедурыИФункции
