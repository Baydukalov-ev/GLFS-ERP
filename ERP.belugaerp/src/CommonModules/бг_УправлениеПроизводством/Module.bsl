#Область ПрограммныйИнтерфейс

Процедура СнятиеРезерваВЦеховыхКладовых() Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Сейчас = ТекущаяДатаСеанса();
	ДатаСнятияРезерва = НачалоДня(Сейчас) - 1;
	
	ДанныеОРезерве = ДанныеОРезервеВЦеховыхКладовых(Сейчас, ДатаСнятияРезерва);
	
	ВыборкаОрганизации = ДанныеОРезерве.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаОрганизации.Следующий() Цикл
		Если ВыборкаОрганизации.Количество > 0 Тогда
			ВыборкаТовары = ВыборкаОрганизации.Выбрать();
			СоздатьДокументСнятиеРезерва(Сейчас, ВыборкаОрганизации.Организация, ВыборкаТовары);
		КонецЕсли;
	КонецЦикла
КонецПроцедуры

#КонецОбласти 


#Область СлужебныеПроцедурыИФункции

Процедура СоздатьДокументСнятиеРезерва(Период, Организация, ВыборкаТовары)
	Если ВыборкаТовары.Количество() > 0 Тогда
		ДокументОбъект = Документы.КорректировкаНазначенияТоваров.СоздатьДокумент();
		ДокументОбъект.Дата = Период;
		ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийКорректировкиНазначения.СнятьРезервПоМногимНазначениям;
		ДокументОбъект.Организация = Организация;
		ДокументОбъект.Комментарий = НСтр("ru='Создан РЗ Снятие резерва в цеховых кладовых'"); 
		ДокументОбъект.Автор = Пользователи.ТекущийПользователь();
		Пока ВыборкаТовары.Следующий() Цикл
			НоваяСтрока = ДокументОбъект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары);
			НоваяСтрока.КоличествоУпаковок = НоваяСтрока.Количество;
			НоваяСтрока.ИсходноеНазначениеДвиженияПоСкладскимРегистрам = Истина;
			НоваяСтрока.СтатусУказанияСерий = ?(ЗначениеЗаполнено(НоваяСтрока.Серия), 14, 0);
		КонецЦикла;
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ТекстОшибки = НСтр("ru='Не удалось провести документ снятие резерва по причине %1'"); 
			ТекстОшибки = СтрШаблон(ТекстОшибки, ОписаниеОшибки());
			ЖурналРегистрации.ДобавитьСообщениеДляЖурналаРегистрации("бг_СнятиеРезерваВЦеховыхКладовых", 
					УровеньЖурналаРегистрации.Ошибка,
					,
					,
					ТекстОшибки);
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

Функция ДанныеОРезервеВЦеховыхКладовых(ДатаОстатков, ГраницаСнятияРезерва)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаОстатков", ДатаОстатков);
	Запрос.УстановитьПараметр("ГраницаСнятияРезерва", ГраницаСнятияРезерва);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Склады.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВтСклады
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	Склады.ЦеховаяКладовая
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладахОстатки.Назначение КАК ИсходноеНазначение,
	|	ТоварыНаСкладахОстатки.Склад КАК Склад,
	|	ТоварыНаСкладахОстатки.Характеристика КАК Характеристика,
	|	ТоварыНаСкладахОстатки.Серия КАК Серия,
	|	ЭтапПроизводства2_2.Ссылка КАК Заказ,
	|	ЭтапПроизводства2_2.Организация КАК Организация,
	|	СУММА(ТоварыНаСкладахОстатки.ВНаличииОстаток) КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|			&ДатаОстатков,
	|			Склад В
	|				(ВЫБРАТЬ
	|					втСклады.Ссылка
	|				ИЗ
	|					втСклады КАК втСклады)) КАК ТоварыНаСкладахОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Назначения КАК Назначения
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
	|			ПО ((ВЫРАЗИТЬ(Назначения.Заказ КАК Документ.ЭтапПроизводства2_2)) = ЭтапПроизводства2_2.Ссылка)
	|				И (ЭтапПроизводства2_2.Дата < &ГраницаСнятияРезерва)
	|		ПО ТоварыНаСкладахОстатки.Назначение = Назначения.Ссылка
	|ГДЕ
	|	ТоварыНаСкладахОстатки.ВНаличииОстаток > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ЭтапПроизводства2_2.Ссылка,
	|	ЭтапПроизводства2_2.Организация,
	|	ТоварыНаСкладахОстатки.Номенклатура,
	|	ТоварыНаСкладахОстатки.Назначение,
	|	ТоварыНаСкладахОстатки.Склад,
	|	ТоварыНаСкладахОстатки.Характеристика,
	|	ТоварыНаСкладахОстатки.Серия
	|ИТОГИ
	|	СУММА(Количество)
	|ПО
	|	Организация";
	Результат = Запрос.Выполнить();
	Возврат Результат;
КонецФункции

#КонецОбласти 