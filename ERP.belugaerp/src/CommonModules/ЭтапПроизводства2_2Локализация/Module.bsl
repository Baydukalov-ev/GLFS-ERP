#Область ПрограммныйИнтерфейс

#Область Проведение

&После("ЗарегистрироватьУчетныеМеханизмы")
Процедура бг_ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента)
	
	МеханизмыДокумента.Добавить("бг_УчетБанковскихГарантий");
	
КонецПроцедуры

#КонецОбласти

#Область АкцизыПоПриобретеннымЦенностям

Функция бг_ТребуетсяЗаполнениеАкцизовПоПриобретеннымЦенностям(ДокументОбъект) Экспорт
	Если Не бг_УчетБанковскихГарантий.ВестиУчетАкцизовПоПриобретеннымЦенностям(ДокументОбъект) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если бг_УчетБанковскихГарантий.ТребуетсяЗаполнениеАкцизовПоПриобретеннымЦенностям(ДокументОбъект) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВыходныеИзделия.Номенклатура КАК Номенклатура,
	|	ВыходныеИзделия.Серия КАК Серия,
	|	ВыходныеИзделия.Количество КАК Количество
	|ПОМЕСТИТЬ ВыходныеИзделия
	|ИЗ
	|	&ВыходныеИзделия КАК ВыходныеИзделия
	|ГДЕ
	|	НЕ ВыходныеИзделия.Отменено
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходМатериаловИРабот.Номенклатура КАК Номенклатура,
	|	РасходМатериаловИРабот.Серия КАК Серия,
	|	РасходМатериаловИРабот.Количество КАК Количество
	|ПОМЕСТИТЬ РасходМатериаловИРабот
	|ИЗ
	|	&РасходМатериаловИРабот КАК РасходМатериаловИРабот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Акцизбороты.Номенклатура КАК Номенклатура,
	|	Акцизбороты.СерияНоменклатуры КАК СерияНоменклатуры
	|ПОМЕСТИТЬ ПодакцизныеМатериалы
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям.Обороты(
	|			,
	|			,
	|			,
	|			(Номенклатура, СерияНоменклатуры) В
	|				(ВЫБРАТЬ
	|					РасходМатериаловИРабот.Номенклатура КАК Номенклатура,
	|					РасходМатериаловИРабот.Серия КАК Серия
	|				ИЗ
	|					РасходМатериаловИРабот КАК РасходМатериаловИРабот)) КАК Акцизбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Акцизбороты.Продукция,
	|	Акцизбороты.СерияПродукции
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям.Обороты(
	|			,
	|			,
	|			,
	|			(Продукция, СерияПродукции) В
	|				(ВЫБРАТЬ
	|					РасходМатериаловИРабот.Номенклатура КАК Номенклатура,
	|					РасходМатериаловИРабот.Серия КАК Серия
	|				ИЗ
	|					РасходМатериаловИРабот КАК РасходМатериаловИРабот)) КАК Акцизбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходМатериаловИРабот.Номенклатура КАК Номенклатура,
	|	РасходМатериаловИРабот.Серия КАК Серия,
	|	РасходМатериаловИРабот.Количество КАК Количество
	|ПОМЕСТИТЬ АкцизыМатериалыИРаботы
	|ИЗ
	|	РасходМатериаловИРабот КАК РасходМатериаловИРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПодакцизныеМатериалы КАК ПодакцизныеМатериалы
	|		ПО РасходМатериаловИРабот.Номенклатура = ПодакцизныеМатериалы.Номенклатура
	|			И РасходМатериаловИРабот.Серия = ПодакцизныеМатериалы.СерияНоменклатуры
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВыходныеИзделия.Номенклатура,
	|	ВыходныеИзделия.Серия,
	|	ВыходныеИзделия.Количество
	|ИЗ
	|	ВыходныеИзделия КАК ВыходныеИзделия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Акцизы.Номенклатура,
	|	Акцизы.СерияНоменклатуры,
	|	-Акцизы.Количество
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.бг_АкцизПоПриобретеннымЦенностямМатериалы КАК Акцизы
	|ГДЕ
	|	Акцизы.Ссылка = &Ссылка
	|	И Акцизы.Продукция = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АкцизВыпускПродукции.Номенклатура,
	|	АкцизВыпускПродукции.СерияНоменклатуры,
	|	-АкцизВыпускПродукции.Количество
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностямВыпускПродукции КАК АкцизВыпускПродукции
	|ГДЕ
	|	АкцизВыпускПродукции.Регистратор = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	АкцизыМатериалыИРаботы.Номенклатура КАК Номенклатура,
	|	АкцизыМатериалыИРаботы.Серия КАК Серия,
	|	СУММА(АкцизыМатериалыИРаботы.Количество) КАК Количество
	|ИЗ
	|	АкцизыМатериалыИРаботы КАК АкцизыМатериалыИРаботы
	|
	|СГРУППИРОВАТЬ ПО
	|	АкцизыМатериалыИРаботы.Номенклатура,
	|	АкцизыМатериалыИРаботы.Серия
	|
	|ИМЕЮЩИЕ
	|	СУММА(АкцизыМатериалыИРаботы.Количество) <> 0";
	Запрос.УстановитьПараметр("ВыходныеИзделия", ДокументОбъект.ВыходныеИзделия.Выгрузить(, "Номенклатура, Серия, Количество, Отменено"));
	Запрос.УстановитьПараметр("РасходМатериаловИРабот", ДокументОбъект.РасходМатериаловИРабот.Выгрузить(, "Номенклатура, Серия, Количество"));
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
КонецФункции

Функция бг_СтатусыСостоянияСписываемогоСырья() Экспорт
	СтатусыСостояния = бг_УчетБанковскихГарантий.СтатусыСостоянияСписываемогоСырья();
	
	СтатусыСостояния.СтатусыАкциза.Добавить(Перечисления.бг_СостоянияОплатыВходящегоАкциза.АвансовыйАкцизВСырье);
	СтатусыСостояния.СтатусыАкциза.Добавить(Перечисления.бг_СостоянияОплатыВходящегоАкциза.АкцизПоОплаченнымМЦ);
	СтатусыСостояния.СтатусыАкциза.Добавить(Перечисления.бг_СостоянияОплатыВходящегоАкциза.АкцизПоНеОплаченнымМЦ);
	СтатусыСостояния.СтатусыАкциза.Добавить(Перечисления.бг_СостоянияОплатыВходящегоАкциза.АкцизПоВозвращеннойПродукции);
	
	СтатусыСостояния.СостоянияСырья.Добавить(Перечисления.бг_СостоянияПодакцизногоСырья.ВПроизводстве);
	
	Возврат СтатусыСостояния;
КонецФункции

Функция бг_ТекстЗапросаАкцизыСписаниеМатериалов() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	МатериалыРаспределение.Номенклатура КАК Номенклатура,
	|	МатериалыРаспределение.СерияНоменклатуры КАК СерияНоменклатуры,
	|	МатериалыРаспределение.Сделка КАК Сделка,
	|	МатериалыРаспределение.СостояниеСырья КАК СостояниеСырья,
	|	МатериалыРаспределение.СтатусАкциза КАК СтатусАкциза,
	|	МатериалыРаспределение.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	МатериалыРаспределение.Количество КАК Количество
	|ИЗ
	|	СписаниеАкцизаПоМатериалам КАК МатериалыРаспределение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписаниеАкцизаПоМатериалам.Номенклатура КАК Номенклатура,
	|	СписаниеАкцизаПоМатериалам.СерияНоменклатуры КАК СерияНоменклатуры,
	|	СписаниеАкцизаПоМатериалам.Сделка КАК Сделка,
	|	СписаниеАкцизаПоМатериалам.СостояниеСырья КАК СостояниеСырья,
	|	СписаниеАкцизаПоМатериалам.СтатусАкциза КАК СтатусАкциза,
	|	СписаниеАкцизаПоМатериалам.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	СписаниеАкцизаПоМатериалам.Количество КАК Количество,
	|	Продукция.Номенклатура КАК Продукция,
	|	Продукция.Серия КАК СерияПродукции
	|ИЗ
	|	Продукция КАК Продукция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписаниеАкцизаПоМатериалам КАК СписаниеАкцизаПоМатериалам
	|		ПО (ИСТИНА)";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностям() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Документ.ДатаПроизводства <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА Документ.ДатаПроизводства
	|		ИНАЧЕ Документ.Дата
	|	КОНЕЦ КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	Документ.Организация КАК Организация,
	|	АкцизПоПриобретеннымЦенностям.Номенклатура КАК Номенклатура,
	|	АкцизПоПриобретеннымЦенностям.СерияНоменклатуры КАК СерияНоменклатуры,
	|	АкцизПоПриобретеннымЦенностям.Сделка КАК Сделка,
	|	АкцизПоПриобретеннымЦенностям.СостояниеСырья КАК СостояниеСырья,
	|	АкцизПоПриобретеннымЦенностям.СтатусАкциза КАК СтатусАкциза,
	|	АкцизПоПриобретеннымЦенностям.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	СУММА(АкцизПоПриобретеннымЦенностям.Количество) КАК Количество,
	|	АкцизПоПриобретеннымЦенностям.Продукция КАК Продукция,
	|	АкцизПоПриобретеннымЦенностям.СерияПродукции КАК СерияПродукции
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.бг_АкцизПоПриобретеннымЦенностямМатериалы КАК АкцизПоПриобретеннымЦенностям
	|		ПО (Документ.Ссылка = &Ссылка)
	|			И Документ.Ссылка = АкцизПоПриобретеннымЦенностям.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Документ.Организация,
	|	АкцизПоПриобретеннымЦенностям.Номенклатура,
	|	АкцизПоПриобретеннымЦенностям.СерияНоменклатуры,
	|	АкцизПоПриобретеннымЦенностям.Продукция,
	|	АкцизПоПриобретеннымЦенностям.СерияПродукции,
	|	АкцизПоПриобретеннымЦенностям.Сделка,
	|	АкцизПоПриобретеннымЦенностям.СостояниеСырья,
	|	АкцизПоПриобретеннымЦенностям.СтатусАкциза,
	|	АкцизПоПриобретеннымЦенностям.СостояниеОплатыАванса,
	|	ВЫБОР
	|		КОГДА Документ.ДатаПроизводства <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА Документ.ДатаПроизводства
	|		ИНАЧЕ Документ.Дата
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Документ.ДатаПроизводства <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА Документ.ДатаПроизводства
	|		ИНАЧЕ Документ.Дата
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	Документ.Организация,
	|	АкцизПоПриобретеннымЦенностям.Номенклатура,
	|	АкцизПоПриобретеннымЦенностям.СерияНоменклатуры,
	|	АкцизПоПриобретеннымЦенностям.Сделка,
	|	ЗНАЧЕНИЕ(Перечисление.бг_СостоянияПодакцизногоСырья.ВГотовойПродукции),
	|	АкцизПоПриобретеннымЦенностям.СтатусАкциза,
	|	АкцизПоПриобретеннымЦенностям.СостояниеОплатыАванса,
	|	АкцизПоПриобретеннымЦенностям.Количество,
	|	АкцизПоПриобретеннымЦенностям.Продукция,
	|	АкцизПоПриобретеннымЦенностям.СерияПродукции
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.бг_АкцизПоПриобретеннымЦенностямПродукция КАК АкцизПоПриобретеннымЦенностям
	|		ПО (Документ.Ссылка = &Ссылка)
	|			И Документ.Ссылка = АкцизПоПриобретеннымЦенностям.Ссылка";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностямПродукция() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АкцизМатериалы.Продукция КАК Номенклатура,
	|	АкцизМатериалы.СерияПродукции КАК СерияНоменклатуры
	|ПОМЕСТИТЬ Продукция
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.бг_АкцизПоПриобретеннымЦенностямМатериалы КАК АкцизМатериалы
	|ГДЕ
	|	АкцизМатериалы.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АкцизПродукция.Продукция,
	|	АкцизПродукция.СерияПродукции
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.бг_АкцизПоПриобретеннымЦенностямПродукция КАК АкцизПродукция
	|ГДЕ
	|	АкцизПродукция.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Документ.ДатаПроизводства <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА Документ.ДатаПроизводства
	|		ИНАЧЕ Документ.Дата
	|	КОНЕЦ КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	Документ.Организация КАК Организация,
	|	ЭтапВыходныеИзделия.Номенклатура КАК Номенклатура,
	|	ЭтапВыходныеИзделия.Серия КАК СерияНоменклатуры,
	|	ЗНАЧЕНИЕ(Перечисление.бг_ТипыМестХраненияПодакцизнойПродукции.Склад) КАК ТипМестаХранения,
	|	ЭтапВыходныеИзделия.Количество КАК Количество
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ЭтапВыходныеИзделия
	|		ПО (Документ.Ссылка = &Ссылка)
	|			И Документ.Ссылка = ЭтапВыходныеИзделия.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Продукция КАК Продукция
	|		ПО (ЭтапВыходныеИзделия.Номенклатура = Продукция.Номенклатура)
	|			И (ЭтапВыходныеИзделия.Серия = Продукция.СерияНоменклатуры)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Документ.ДатаПроизводства <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА Документ.ДатаПроизводства
	|		ИНАЧЕ Документ.Дата
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	Документ.Организация,
	|	ЭтапРасходМатериаловИРабот.Номенклатура,
	|	ЭтапРасходМатериаловИРабот.Серия,
	|	ЗНАЧЕНИЕ(Перечисление.бг_ТипыМестХраненияПодакцизнойПродукции.Подразделение),
	|	ЭтапРасходМатериаловИРабот.Количество
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.РасходМатериаловИРабот КАК ЭтапРасходМатериаловИРабот
	|		ПО (Документ.Ссылка = &Ссылка)
	|			И Документ.Ссылка = ЭтапРасходМатериаловИРабот.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Продукция КАК Продукция
	|		ПО (ЭтапРасходМатериаловИРабот.Номенклатура = Продукция.Номенклатура)
	|			И (ЭтапРасходМатериаловИРабот.Серия = Продукция.СерияНоменклатуры)";
	
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти

#Область Печать

&ИзменениеИКонтроль("ПолучитьДанныеДляПечатнойФормыМХ18")
Функция бг_ПолучитьДанныеДляПечатнойФормыМХ18(ПараметрыПечати, МассивОбъектов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если НЕ ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "
	#Вставка
	|ВЫБРАТЬ
	|	ВыходныеИзделия.Ссылка КАК Ссылка,
	|	МИНИМУМ(ВыходныеИзделия.ДатаПроизводства) КАК ДатаПроизводства
	|ПОМЕСТИТЬ ВТ_ДатаПроизводства
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ВыходныеИзделия
	|ГДЕ
	|	ВыходныеИзделия.Ссылка В(&МассивДокументов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыходныеИзделия.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	#КонецВставки
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка						КАК Ссылка,
	|	ДанныеДокумента.Номер						КАК Номер,
	|	ДанныеДокумента.Дата						КАК Дата,
	#Удаление	
	|	ДанныеДокумента.Дата						КАК ДатаДокумента,
	#КонецУдаления
	#Вставка
	|	ЕСТЬNULL(ВТ_ДатаПроизводства.ДатаПроизводства, ДанныеДокумента.Дата) КАК ДатаДокумента,
	#КонецВставки
	|	ДанныеДокумента.Организация					КАК Организация,
	|	ДанныеДокумента.Организация.Префикс			КАК Префикс,
	|	ДанныеДокумента.Подразделение.Представление	КАК ПредставлениеПодразделения
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК ДанныеДокумента
	#Вставка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДатаПроизводства КАК ВТ_ДатаПроизводства
	|		ПО ДанныеДокумента.Ссылка = ВТ_ДатаПроизводства.Ссылка
	#КонецВставки
	|ГДЕ
	|	ДанныеДокумента.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// ЗАПРОС ПО ТАБЛИЧНЫМ ЧАСТЯМ
	|
	|ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка														КАК Ссылка,
	|	ТаблицаТовары.Номенклатура													КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.НаименованиеПолное								КАК НоменклатураНаименование,
	|	ТаблицаТовары.Номенклатура." + КолонкаКодов + "								КАК НоменклатураКод,
	|	ТаблицаТовары.Характеристика.НаименованиеПолное								КАК ХарактеристикаНаименование,
	|	ТаблицаТовары.Характеристика												КАК Характеристика,
	|	ТаблицаТовары.Серия.Наименование											КАК СерияНаименование,
	|	ТаблицаТовары.Серия															КАК Серия,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения 									КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения 											КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения 									КАК ВидУпаковки,
	|	ВЫБОР КОГДА ТаблицаТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) ТОГДА
	|		1
	|	ИНАЧЕ
	|		ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1)
	|	КОНЕЦ																		КАК КоличествоВОдномМесте,
	|	СУММА(ТаблицаТовары.КоличествоУпаковок * &ТекстЗапросаВес)					КАК МассаБрутто,
	|	СУММА(ТаблицаТовары.КоличествоУпаковок)										КАК Количество,
	|	СУММА(ТаблицаТовары.КоличествоУпаковок) 									КАК КоличествоМест,
	|	МИНИМУМ(ТаблицаТовары.НомерСтроки)											КАК НомерСтроки,
	|	ЛОЖЬ																		КАК ЭтоВозвратнаяТара,
	|	ТаблицаТовары.Цена															КАК Цена,
	|	СУММА(ТаблицаТовары.Сумма)													КАК Сумма,
	|	ТаблицаТовары.Получатель.Представление                                      КАК Получатель
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаТовары.Ссылка				КАК Ссылка,
	|		ТаблицаТовары.НомерСтроки			КАК НомерСтроки,
	|		ТаблицаТовары.Получатель			КАК Получатель,
	|		ТаблицаТовары.Номенклатура			КАК Номенклатура,
	|		ТаблицаТовары.Характеристика		КАК Характеристика,
	|		ТаблицаТовары.Серия					КАК Серия,
	|		ТаблицаТовары.Упаковка				КАК Упаковка,
	|		0				                    КАК Цена,
	|		0				                    КАК Сумма,
	|		ТаблицаТовары.КоличествоУпаковок	КАК КоличествоУпаковок
	|	ИЗ 
	|		Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка В(&МассивДокументов)
	|		И НЕ ТаблицаТовары.СписатьНаРасходы
	|		И ТаблицаТовары.Номенклатура.ТипНоменклатуры В (
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		И НЕ ТаблицаТовары.Отменено
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Ссылка				КАК Ссылка,
	|		ТаблицаТовары.НомерСтроки			КАК НомерСтроки,
	|		ТаблицаТовары.Получатель			КАК Получатель,
	|		ТаблицаТовары.Номенклатура			КАК Номенклатура,
	|		ТаблицаТовары.Характеристика		КАК Характеристика,
	|		ТаблицаТовары.Серия					КАК Серия,
	|		ТаблицаТовары.Упаковка				КАК Упаковка,
	|		ТаблицаТовары.Цена				    КАК Цена,
	|		ТаблицаТовары.Сумма				    КАК Сумма,
	|		ТаблицаТовары.КоличествоУпаковок	КАК КоличествоУпаковок
	|	ИЗ
	|		Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка В(&МассивДокументов)
	|		И НЕ ТаблицаТовары.СписатьНаРасходы
	|		И ТаблицаТовары.Номенклатура.ТипНоменклатуры В (
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|		И НЕ ТаблицаТовары.Отменено
	|
	|	) КАК ТаблицаТовары
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Ссылка,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия,
	|	ТаблицаТовары.Упаковка,
	|	ТаблицаТовары.Цена,
	|	ТаблицаТовары.Получатель,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения,
	|	&ТекстЗапросаКодЕдиницыИзмерения,
	|	ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|
	|ИТОГИ ПО
	|	Ссылка,
	|	Получатель";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаВес",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"ТаблицаТовары.Упаковка",
			"ТаблицаТовары.Номенклатура"));
			
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаТовары.Упаковка",
			"ТаблицаТовары.Номенклатура"));
			
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТовары.Упаковка",
			"ТаблицаТовары.Номенклатура"));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаТовары.Упаковка",
			"ТаблицаТовары.Номенклатура"));
	
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	#Удаление	
	РезультатПоШапке = МассивРезультатов[0];
	РезультатПоТабличнойЧасти = МассивРезультатов[1];
	#КонецУдаления	
	#Вставка
	РезультатПоШапке 		  = МассивРезультатов[1];
	РезультатПоТабличнойЧасти = МассивРезультатов[2];
	#КонецВставки	
	
	СтруктураДанныхДляПечати = Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти",
		РезультатПоШапке,
		РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

#КонецОбласти // Печать

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

&После("ДополнитьТекстыЗапросовПроведения")
Процедура бг_ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры)
	
	бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностям(Запрос, ТекстыЗапроса, Регистры);
	бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностямПродукция(Запрос, ТекстыЗапроса, Регистры);
	
КонецПроцедуры

Процедура бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностям(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "бг_АкцизПоПриобретеннымЦенностям";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностям(), ИмяРегистра);
	
КонецПроцедуры

Процедура бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностямПродукция(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "бг_АкцизПоПриобретеннымЦенностямВыпускПродукции";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностямПродукция(), ИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
