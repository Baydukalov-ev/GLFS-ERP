#Область ПрограммныйИнтерфейс

#Область Проведение

&После("ЗарегистрироватьУчетныеМеханизмы")
Процедура бг_ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента)
	
	МеханизмыДокумента.Добавить("бг_УчетБанковскихГарантий");
	
КонецПроцедуры

#КонецОбласти

#Область АкцизыПоПриобретеннымЦенностям

Функция бг_ТребуетсяЗаполнениеАкцизовПоПриобретеннымЦенностям(ДокументОбъект) Экспорт
	Если Не бг_УчетБанковскихГарантий.ВестиУчетАкцизовПоПриобретеннымЦенностям(ДокументОбъект) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПоддерживаемыеОперации = ПоддерживаемыеОперацииУчетАкцизов();
	Если ПоддерживаемыеОперации.Найти(ДокументОбъект.ХозяйственнаяОперация) = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если бг_УчетБанковскихГарантий.ТребуетсяЗаполнениеАкцизовПоПриобретеннымЦенностям(ДокументОбъект) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Серия КАК Серия,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Серия КАК Серия,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ Данные
	|ИЗ
	|	Товары КАК Товары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Акцизы.Номенклатура,
	|	Акцизы.СерияНоменклатуры,
	|	-Акцизы.Количество
	|ИЗ
	|	Документ.ДвижениеПродукцииИМатериалов.бг_АкцизПоПриобретеннымЦенностям КАК Акцизы
	|ГДЕ
	|	Акцизы.Ссылка = &Ссылка
	|	И Акцизы.Продукция = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АкцизВыпускПродукции.Номенклатура,
	|	АкцизВыпускПродукции.СерияНоменклатуры,
	|	-АкцизВыпускПродукции.Количество
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностямВыпускПродукции КАК АкцизВыпускПродукции
	|ГДЕ
	|	АкцизВыпускПродукции.Регистратор = &Ссылка
	|	И АкцизВыпускПродукции.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Данные.Номенклатура КАК Номенклатура,
	|	Данные.Серия КАК Серия,
	|	СУММА(Данные.Количество) КАК Количество
	|ИЗ
	|	Данные КАК Данные
	|
	|СГРУППИРОВАТЬ ПО
	|	Данные.Номенклатура,
	|	Данные.Серия
	|
	|ИМЕЮЩИЕ
	|	СУММА(Данные.Количество) <> 0";
	Запрос.УстановитьПараметр("Товары", ДокументОбъект.Товары.Выгрузить(, "Номенклатура, Серия, Количество"));
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
КонецФункции

Функция бг_СтатусыСостоянияСписываемогоСырья(ХозяйственнаяОперация) Экспорт
	СтатусыСостояния = Новый Структура;
	
	// Списание акциза по материалам
	СтатусыСостояния.Вставить("Материалы", бг_УчетБанковскихГарантий.СтатусыСостоянияСписываемогоСырья());
	
	СтатусыСостояния.Материалы.СтатусыАкциза.Добавить(Перечисления.бг_СостоянияОплатыВходящегоАкциза.АвансовыйАкцизВСырье);
	СтатусыСостояния.Материалы.СтатусыАкциза.Добавить(Перечисления.бг_СостоянияОплатыВходящегоАкциза.АкцизПоОплаченнымМЦ);
	СтатусыСостояния.Материалы.СтатусыАкциза.Добавить(Перечисления.бг_СостоянияОплатыВходящегоАкциза.АкцизПоНеОплаченнымМЦ);
	СтатусыСостояния.Материалы.СтатусыАкциза.Добавить(Перечисления.бг_СостоянияОплатыВходящегоАкциза.АкцизПоВозвращеннойПродукции);
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзКладовой Тогда
		СтатусыСостояния.Материалы.СостоянияСырья.Добавить(Перечисления.бг_СостоянияПодакцизногоСырья.ВПроизводстве);
	Иначе
		СтатусыСостояния.Материалы.СостоянияСырья.Добавить(Перечисления.бг_СостоянияПодакцизногоСырья.ВМестахХранения);
	КонецЕсли;
	
	// Списание акцизов по продукции (выпущенная спиртосодержащая смесь)
	СтатусыСостояния.Вставить("Продукция", бг_УчетБанковскихГарантий.СтатусыСостоянияСписываемогоСырья());
	
	СтатусыСостояния.Продукция.СтатусыАкциза.Добавить(Перечисления.бг_СостоянияОплатыВходящегоАкциза.АвансовыйАкцизВСырье);
	СтатусыСостояния.Продукция.СтатусыАкциза.Добавить(Перечисления.бг_СостоянияОплатыВходящегоАкциза.АкцизПоОплаченнымМЦ);
	СтатусыСостояния.Продукция.СтатусыАкциза.Добавить(Перечисления.бг_СостоянияОплатыВходящегоАкциза.АкцизПоНеОплаченнымМЦ);
	СтатусыСостояния.Продукция.СтатусыАкциза.Добавить(Перечисления.бг_СостоянияОплатыВходящегоАкциза.АкцизПоВозвращеннойПродукции);
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзКладовой Тогда
		СтатусыСостояния.Продукция.СостоянияСырья.Добавить(Перечисления.бг_СостоянияПодакцизногоСырья.ВПроизводстве);
	Иначе
		СтатусыСостояния.Продукция.СостоянияСырья.Добавить(Перечисления.бг_СостоянияПодакцизногоСырья.ВСпиртосодержащейСмеси);
		СтатусыСостояния.Продукция.СостоянияСырья.Добавить(Перечисления.бг_СостоянияПодакцизногоСырья.ВГотовойПродукции);
	КонецЕсли;
	
	Возврат СтатусыСостояния;
КонецФункции

Функция бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностям() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Документ.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	Документ.Организация КАК Организация,
	|	АкцизПоПриобретеннымЦенностям.Номенклатура КАК Номенклатура,
	|	АкцизПоПриобретеннымЦенностям.СерияНоменклатуры КАК СерияНоменклатуры,
	|	АкцизПоПриобретеннымЦенностям.Сделка КАК Сделка,
	|	АкцизПоПриобретеннымЦенностям.СостояниеСырья КАК СостояниеСырья,
	|	АкцизПоПриобретеннымЦенностям.СтатусАкциза КАК СтатусАкциза,
	|	АкцизПоПриобретеннымЦенностям.СостояниеОплатыАванса КАК СостояниеОплатыАванса,
	|	АкцизПоПриобретеннымЦенностям.Продукция КАК Продукция,
	|	АкцизПоПриобретеннымЦенностям.СерияПродукции КАК СерияПродукции,
	|	АкцизПоПриобретеннымЦенностям.Количество КАК Количество
	|ИЗ
	|	Документ.ДвижениеПродукцииИМатериалов КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДвижениеПродукцииИМатериалов.бг_АкцизПоПриобретеннымЦенностям КАК АкцизПоПриобретеннымЦенностям
	|		ПО (Документ.Ссылка = &Ссылка)
	|			И Документ.Ссылка = АкцизПоПриобретеннымЦенностям.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Документ.Дата,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	Документ.Организация,
	|	АкцизПоПриобретеннымЦенностям.Номенклатура,
	|	АкцизПоПриобретеннымЦенностям.СерияНоменклатуры,
	|	АкцизПоПриобретеннымЦенностям.Сделка,
	|	ВЫБОР
	|		КОГДА Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратМатериаловИзКладовой)
	|				И АкцизПоПриобретеннымЦенностям.Продукция = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.бг_СостоянияПодакцизногоСырья.ВМестахХранения)
	|		КОГДА Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратМатериаловИзКладовой)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.бг_СостоянияПодакцизногоСырья.ВСпиртосодержащейСмеси)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.бг_СостоянияПодакцизногоСырья.ВПроизводстве)
	|	КОНЕЦ,
	|	АкцизПоПриобретеннымЦенностям.СтатусАкциза,
	|	АкцизПоПриобретеннымЦенностям.СостояниеОплатыАванса,
	|	АкцизПоПриобретеннымЦенностям.Продукция,
	|	АкцизПоПриобретеннымЦенностям.СерияПродукции,
	|	АкцизПоПриобретеннымЦенностям.Количество
	|ИЗ
	|	Документ.ДвижениеПродукцииИМатериалов КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДвижениеПродукцииИМатериалов.бг_АкцизПоПриобретеннымЦенностям КАК АкцизПоПриобретеннымЦенностям
	|		ПО (Документ.Ссылка = &Ссылка)
	|			И Документ.Ссылка = АкцизПоПриобретеннымЦенностям.Ссылка";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностямПродукция() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АкцизПоПриобретеннымЦенностям.Продукция КАК Номенклатура,
	|	АкцизПоПриобретеннымЦенностям.СерияПродукции КАК СерияНоменклатуры
	|ПОМЕСТИТЬ Продукция
	|ИЗ
	|	Документ.ДвижениеПродукцииИМатериалов.бг_АкцизПоПриобретеннымЦенностям КАК АкцизПоПриобретеннымЦенностям
	|ГДЕ
	|	АкцизПоПриобретеннымЦенностям.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Документ.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	Документ.Организация КАК Организация,
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.Серия КАК СерияНоменклатуры,
	|	ЗНАЧЕНИЕ(Перечисление.бг_ТипыМестХраненияПодакцизнойПродукции.Склад) КАК ТипМестаХранения,
	|	ДокументТовары.Количество КАК Количество
	|ИЗ
	|	Документ.ДвижениеПродукцииИМатериалов КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДвижениеПродукцииИМатериалов.Товары КАК ДокументТовары
	|		ПО (Документ.Ссылка = &Ссылка)
	|			И (Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаМатериаловВКладовую))
	|			И Документ.Ссылка = ДокументТовары.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Продукция КАК Продукция
	|		ПО (ДокументТовары.Номенклатура = Продукция.Номенклатура)
	|			И (ДокументТовары.Серия = Продукция.СерияНоменклатуры)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Документ.Дата,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	Документ.Организация,
	|	ДокументТовары.Номенклатура,
	|	ДокументТовары.Серия,
	|	ЗНАЧЕНИЕ(Перечисление.бг_ТипыМестХраненияПодакцизнойПродукции.Подразделение),
	|	ДокументТовары.Количество
	|ИЗ
	|	Документ.ДвижениеПродукцииИМатериалов КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДвижениеПродукцииИМатериалов.Товары КАК ДокументТовары
	|		ПО (Документ.Ссылка = &Ссылка)
	|			И (Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаМатериаловВКладовую))
	|			И Документ.Ссылка = ДокументТовары.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Продукция КАК Продукция
	|		ПО (ДокументТовары.Номенклатура = Продукция.Номенклатура)
	|			И (ДокументТовары.Серия = Продукция.СерияНоменклатуры)";
	
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ПоддерживаемыеОперацииУчетАкцизов()
	ПоддерживаемыеОперации = Новый Массив;
	ПоддерживаемыеОперации.Добавить(Перечисления.ХозяйственныеОперации.ПередачаМатериаловВКладовую);
	ПоддерживаемыеОперации.Добавить(Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзКладовой);
	
	Возврат ПоддерживаемыеОперации;
КонецФункции

#Область Проведение

&После("ДополнитьТекстыЗапросовПроведения")
Процедура бг_ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры)
	
	бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностям(Запрос, ТекстыЗапроса, Регистры);
	бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностямПродукция(Запрос, ТекстыЗапроса, Регистры);
	
КонецПроцедуры

Процедура бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностям(Запрос, ТекстыЗапроса, Регистры) Экспорт
	ИмяРегистра = "бг_АкцизПоПриобретеннымЦенностям";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностям(), ИмяРегистра);
	
КонецПроцедуры

Процедура бг_ДополнитьТекстыЗапросовПроведенияАкцизыПоПриобретеннымЦенностямПродукция(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "бг_АкцизПоПриобретеннымЦенностямВыпускПродукции";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(бг_ТекстЗапросаДвиженияАкцизыПоПриобретеннымЦенностямПродукция(), ИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
