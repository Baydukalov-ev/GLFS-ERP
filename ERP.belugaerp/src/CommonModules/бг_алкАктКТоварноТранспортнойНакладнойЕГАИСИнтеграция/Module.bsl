
#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьЗагружаемыйОбъект(ЗагружаемыйОбъект, СтруктураОбъекта, СписокСвойств, 
			ИсключаяСвойства, СтандартнаяОбработка) Экспорт

	СтандартнаяОбработка = Ложь;		

	ВидДокумента = бг_ОбщегоНазначенияСервер.ЗначениеПеречисленияИзСтруктурыОбъекта(СтруктураОбъекта, "ВидДокумента");	
	ВидАкта = бг_ОбщегоНазначенияСервер.ЗначениеПеречисленияИзСтруктурыОбъекта(СтруктураОбъекта, "ВидАкта");
	СтатусДокумента = бг_ОбщегоНазначенияСервер.ЗначениеПеречисленияИзСтруктурыОбъекта(СтруктураОбъекта, "СтатусДокумента");	
	
	ЗагружаемыйОбъект = Новый Массив;
	
	Если СтрСравнить(ВидДокумента, "Исходящий") <> 0 Тогда
		// Обрабатываем только исходящие акты.
		Возврат;
	КонецЕсли;

	Если СтрСравнить(ВидАкта, "АктПодтверждения") = 0 
		И СтрСравнить(СтатусДокумента, "Подтвержден") = 0 Тогда
		ОтразитьАктПодтвержденияПоУчетнымДокументам(СтруктураОбъекта);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // Конец ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Процедура ОтразитьАктПодтвержденияПоУчетнымДокументам(СтруктураОбъекта)
	
	УчетныеДокументы = УчетныеДокументы(СтруктураОбъекта);
	
	Для каждого УчетныйДокумент Из УчетныеДокументы Цикл
		Если ТипЗнч(УчетныйДокумент) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
			ОформитьПриходныйОрдерПоПриобретению(УчетныйДокумент);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

// Возвращаеи массив ссылок на учетные документы ЕГАИС.
//
Функция УчетныеДокументы(СтруктураОбъекта)
	
	Перем адаптер_РаботаСДаннымиИБ;    
	адаптер_РаботаСДаннымиИБ = ОбщегоНазначения.ОбщийМодуль("адаптер_РаботаСДаннымиИБ");
	
	СсылкиНаУчетныеДокументы = Новый Массив;		
	
	УчетныеДокументы = Неопределено;
	СтруктураОбъекта.Свойство("УчетныеДокументы", УчетныеДокументы); 
			
	Если ТипЗнч(УчетныеДокументы) = Тип("Массив") Тогда
		Для каждого СтрокаТЧ Из УчетныеДокументы Цикл
			УчетныйДокументСсылка = адаптер_РаботаСДаннымиИБ.НайтиСсылкуПоЗагружаемымДанным(СтрокаТЧ.УчетныйДокумент);
			Если адаптер_РаботаСДаннымиИБ.СсылкаСуществует(УчетныйДокументСсылка) Тогда
				СсылкиНаУчетныеДокументы.Добавить(УчетныйДокументСсылка);	
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат СсылкиНаУчетныеДокументы;
	
КонецФункции	

Процедура ОформитьПриходныйОрдерПоПриобретению(УчетныйДокумент)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Распоряжение", УчетныйДокумент);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриходныйОрдерНаТовары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПриходныйОрдерНаТовары КАК ПриходныйОрдерНаТовары
	|ГДЕ
	|	ПриходныйОрдерНаТовары.Распоряжение = &Распоряжение
	|	И ПриходныйОрдерНаТовары.Проведен
	|	И ПриходныйОрдерНаТовары.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыПриходныхОрдеров.Принят)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ПриходныйОрдерОбъект = Выборка.Ссылка.ПолучитьОбъект();		
		ПриходныйОрдерОбъект.Статус = Перечисления.СтатусыПриходныхОрдеров.Принят;
		ПриходныйОрдерОбъект.Записать(РежимЗаписиДокумента.Проведение);
	КонецЦикла;
	
КонецПроцедуры	

#КонецОбласти
