#Область ПрограммныйИнтерфейс

// Процедура - Фиксирует печать документов 
//
// Параметры:
//  ТабличныеДокументы - СписокЗначений 
//  ОбъектыПечати - СписокЗначений - Список документов 
//  СписокПечати - СписокЗначений - Идентификаторы макетов
//
Процедура ЗафиксироватьПечатьДокументов(ТабличныеДокументы, ОбъектыПечати, СписокПечати) Экспорт
	
	АдресПовторноФиксируемыхДокументов = Неопределено;
	
	бг_ФиксацияПечатиДокументовВызовСервера.ЗафиксироватьПечатьДокументов(
		ТабличныеДокументы, 
		ОбъектыПечати, 
		СписокПечати,
		АдресПовторноФиксируемыхДокументов);
		
	ПредложитьПовторноЗафиксироватьПечатьДокументов(АдресПовторноФиксируемыхДокументов);
	
КонецПроцедуры

// Процедура - Фиксирует печать комплекта документов
//
// Параметры:
//  КоллекцияПечатныхФормПоОбъектам - ТаблицаЗначений 
//
Процедура ЗафиксироватьПечатьКомплектаДокументов(КоллекцияПечатныхФормПоОбъектам) Экспорт
	
	АдресПовторноФиксируемыхДокументов = Неопределено;
	
	бг_ФиксацияПечатиДокументовВызовСервера.ЗафиксироватьПечатьКомплектаДокументов(
		КоллекцияПечатныхФормПоОбъектам, 
		АдресПовторноФиксируемыхДокументов);
		
	ПредложитьПовторноЗафиксироватьПечатьДокументов(АдресПовторноФиксируемыхДокументов);
	
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПредложитьПовторноЗафиксироватьПечатьДокументов(АдресВременногоХранилища)

	Если АдресВременногоХранилища = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	ПовторноФиксируемыеДокументы = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПредложитьПовторноЗафиксироватьПечатьДокументовЗавершение", 
		ЭтотОбъект, 
		ПовторноФиксируемыеДокументы);
		
	ПредставлениеПовторноПечатаемыхДокументов = "";
	ШаблонРаспечатанногоДокумента = НСтр("ru='%1 по документу %2'");
	
	Для Каждого РаспечатанныйДокумент Из ПовторноФиксируемыеДокументы Цикл
		
		ПредставлениеРаспечатанногоДокумента = СтрШаблон(
			ШаблонРаспечатанногоДокумента, 
			РаспечатанныйДокумент.ПечатнаяФормаНаименование, 
			РаспечатанныйДокумент.ОбъектПечати); 
		
		ПредставлениеПовторноПечатаемыхДокументов = ПредставлениеРаспечатанногоДокумента + Символы.ПС;
		
	КонецЦикла;
	
	Если Не ПустаяСтрока(ПредставлениеПовторноПечатаемыхДокументов) Тогда
		СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(
			ПредставлениеПовторноПечатаемыхДокументов, 
			1);
	КонецЕсли;
		
	ТекстВопроса = НСтр("ru='Печатные формы:
						|%1 
						|ранее выводились на печать и были переданы в электронный архив.
						|Выполнить повторную передачу?'");
	
	ТекстВопроса = СтрШаблон(ТекстВопроса, ПредставлениеПовторноПечатаемыхДокументов); 
		
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет); 
		
КонецПроцедуры

Процедура ПредложитьПовторноЗафиксироватьПечатьДокументовЗавершение(Результат, ДанныеРаспечатанныхДокументов) Экспорт

	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;	
	КонецЕсли;
	
	бг_ФиксацияПечатиДокументовВызовСервера.ОтразитьПовторнуюПечатьДокументов(ДанныеРаспечатанныхДокументов);
		
КонецПроцедуры

#КонецОбласти