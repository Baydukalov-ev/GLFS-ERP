#Область ПрограммныйИнтерфейс

#Область ПрограммноеДобавлениеЭлементовНаФорму

Функция ДобавитьСтраницуНаФорму(Форма, ИмяСтраницы, ЗаголовокСтраницы, ГруппаСтраницы = "ГруппаСтраницы") Экспорт
	
	НовыйЭлементФормыСтраница = Форма.Элементы.Вставить(ИмяСтраницы, Тип("ГруппаФормы"), Форма.Элементы[ГруппаСтраницы]);
	НовыйЭлементФормыСтраница.Вид = ВидГруппыФормы.Страница;
	НовыйЭлементФормыСтраница.Заголовок = ЗаголовокСтраницы;
	
	Возврат НовыйЭлементФормыСтраница;
	
КонецФункции

// Функция - Добавить поле на форму
//
// Параметры:
//  Форма		 - ФормаКлиентскогоПриложения - 
//  ИмяЭлемента	 - Строка - 
//  Родитель	 - ГруппаФормы, ТаблицаФормы, ФормаКлиентскогоПриложения - 
//  ПутьКДанным	 - Строка - 
//  ТипЭлемента	 - Строка - создается элемент с типом Тип(ТипЭлемента)
//  Элемент		 - ДекорацияФормы, ГруппаФормы, КнопкаФормы, ТаблицаФормы, ПолеФормы, Неопределено - Элемент перед которым будет вставлен создаваемый элемент, Неопределено - добавить в конец
//  ВидПоля		 - Строка, Неопределено - Вид поля формы ВидПоляФормы[ВидПоля], Неопределено для ПолеВвода
// 
// Возвращаемое значение:
//  ДекорацияФормы, ГруппаФормы, КнопкаФормы, ТаблицаФормы, ПолеФормы - 
//
Функция ДобавитьПолеНаФорму(Форма,
							ИмяЭлемента,
							Родитель,
							ПутьКДанным,
							ТипЭлемента = "ПолеФормы",
							Элемент     = Неопределено,
							ВидПоля     = Неопределено) Экспорт
	
	НовыйЭлементФормы = ?(Элемент = Неопределено,
							Форма.Элементы.Вставить(ИмяЭлемента, Тип(ТипЭлемента), Родитель),
							Форма.Элементы.Вставить(ИмяЭлемента, Тип(ТипЭлемента), Родитель, Элемент));
							
	НовыйЭлементФормы.Вид = ?(ВидПоля = Неопределено, ВидПоляФормы.ПолеВвода, ВидПоляФормы[ВидПоля]);
	НовыйЭлементФормы.ПутьКДанным = ПутьКДанным;
	
	Если ВидПоля = "ПолеФлажка" Тогда
		НовыйЭлементФормы.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	КонецЕсли;
	
	Возврат НовыйЭлементФормы;
	
КонецФункции
	
Функция ДобавитьКнопкуНаФорму(Форма,
							  ИмяЭлемента,
							  Родитель,
							  Заголовок,
							  ИмяКоманды,
							  ИмяДействия,
							  Элемент = Неопределено,
							  ВидКнопки = Неопределено) Экспорт
	НоваяКоманда = Форма.Команды.Добавить(ИмяКоманды);
	НоваяКоманда.Действие = ИмяДействия;
	НоваяКоманда.Заголовок = Заголовок;
	
	Если Элемент = Неопределено Тогда
		НовыйЭлементФормы = Форма.Элементы.Вставить(ИмяЭлемента, Тип("КнопкаФормы"), Родитель);
	Иначе
		НовыйЭлементФормы = Форма.Элементы.Вставить(ИмяЭлемента, Тип("КнопкаФормы"), Родитель, Элемент);
	КонецЕсли; 
	
	Если ВидКнопки = Неопределено Тогда
		Видкнопки = ВидКнопкиФормы.ОбычнаяКнопка;
	КонецЕсли; 
	
	НовыйЭлементФормы.Заголовок = Заголовок;
	НовыйЭлементФормы.Вид = ВидКнопки;
	НовыйЭлементФормы.ИмяКоманды = ИмяКоманды;
	
	Возврат НовыйЭлементФормы;
КонецФункции // ()

Функция ДобавитьТаблицуНаФорму(Форма, ИмяТаблицы, Родитель, ПутьКданным, Элемент = Неопределено) Экспорт
	Если Элемент = Неопределено Тогда
		НоваяТаблицаФормы = Форма.Элементы.Вставить(ИмяТаблицы, Тип("ТаблицаФормы"), Родитель);
	Иначе
		НоваяТаблицаФормы = Форма.Элементы.Вставить(ИмяТаблицы, Тип("ТаблицаФормы"), Родитель, Элемент);
	КонецЕсли; 
	НоваяТаблицаФормы.ПутьКДанным = ПутьКданным;
	Возврат НоваяТаблицаФормы;
КонецФункции

Функция ДобавитьГруппуНаФорму(Форма,
							ИмяГруппы,
							Родитель,
							ВидГруппы = Неопределено,
							Элемент = Неопределено
							) Экспорт
							
	Если Элемент = Неопределено Тогда
		НовыйЭлементФормы = Форма.Элементы.Вставить(ИмяГруппы, Тип("ГруппаФормы"), Родитель);
	Иначе
		НовыйЭлементФормы = Форма.Элементы.Вставить(ИмяГруппы, Тип("ГруппаФормы"), Родитель, Элемент);
	КонецЕсли; 
	
	Если ВидГруппы = Неопределено Тогда
		НовыйЭлементФормы.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Иначе
		НовыйЭлементФормы.Вид = ВидГруппы;
	КонецЕсли; 
	
	Возврат НовыйЭлементФормы;
КонецФункции 
 
Функция ДобавитьДекорациюНаФорму(Форма,
							ИмяДекорации,
							Родитель,
							Заголовок = "",
							ВидДекорации = Неопределено,
							Картинка = Неопределено,
							Элемент = Неопределено
							) Экспорт
							
	Если Элемент = Неопределено Тогда
		НовыйЭлементФормы = Форма.Элементы.Вставить(ИмяДекорации, Тип("ДекорацияФормы"), Родитель);
	иначе
		НовыйЭлементФормы = Форма.Элементы.Вставить(ИмяДекорации, Тип("ДекорацияФормы"), Родитель, Элемент);
	КонецЕсли; 
	
	НовыйЭлементФормы.Заголовок = Заголовок;
	
	Если ВидДекорации = Неопределено Тогда
		НовыйЭлементФормы.Вид = ВидДекорацииФормы.Надпись;
	иначе
		НовыйЭлементФормы.Вид = ВидДекорации;
	КонецЕсли; 
	
	Если Картинка <> Неопределено Тогда
		НовыйЭлементФормы.Картинка = Картинка;
	КонецЕсли; 
	
	
	Возврат НовыйЭлементФормы;
КонецФункции // ()
	
#КонецОбласти

#Область МодификацияЭлементовФормы

Процедура УстановитьЭлементамФормыЗначениеТолькоПросмотр(Форма, МассивИсключений, Значение = Истина) Экспорт
	
	Для каждого Элемент Из Форма.Элементы Цикл
		
		Если ТипЗнч(Элемент) = Тип("ПолеФормы") ИЛИ ТипЗнч(Элемент) = Тип("ТаблицаФормы") Тогда
			Если МассивИсключений.Найти(Элемент.Имя) = Неопределено Тогда 
				Элемент.ТолькоПросмотр = Значение;
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(Элемент) <> Тип("ДекорацияФормы") Тогда
			Если МассивИсключений.Найти(Элемент.Имя) = Неопределено Тогда 
				Элемент.Доступность = НЕ Значение;
			КонецЕсли;
			
		Иначе
			Продолжить;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьПодчиненныеЭлементыФормыВМассив(Элемент, МассивЭлементов)  Экспорт
	
	Для каждого ПодчиненныйЭлемент Из Элемент.ПодчиненныеЭлементы Цикл
		
		МассивЭлементов.Добавить(ПодчиненныйЭлемент.Имя);
		
		УсловиеПоТипуЭлемента = (ТипЗнч(ПодчиненныйЭлемент) = Тип("ГруппаКнопокФормы") 
									Или ТипЗнч(ПодчиненныйЭлемент) = Тип("ГруппаФормы"));
		
		Если УсловиеПоТипуЭлемента И ПодчиненныйЭлемент.ПодчиненныеЭлементы.Количество() > 0 Тогда
			ДобавитьПодчиненныеЭлементыФормыВМассив(ПодчиненныйЭлемент, МассивЭлементов);
		КонецЕсли; 

	КонецЦикла;
	
КонецПроцедуры

// Устанавливает свойство Доступность = Ложь или ТолькоПросмотр = Истина
// в зависимости от типа значения элемента
// Параметры
//  ЭлементФормы - вся форма, или группа формы, или таблица формы
//  ДоступныеЭлементы - массив, содержащий в себе доступные элементы формы 
//
Процедура ИзменитьДоступностьЭлементовФормыРекурсивно(ЭлементФормы, ДоступныеЭлементы) Экспорт
	
	ТипРодителя = ТипЗнч(ЭлементФормы);
	
	БлокироватьЭлементКоманднаяПанель = Ложь;
	БлокироватьЭлементКоманднаяПанельТаблицы = Ложь;
	БлокироватьЭлементКонтекстноеМенюТаблицы = Ложь;
	
	Если ТипРодителя = Тип("УправляемаяФорма") Тогда
		
		ЭлементКоманднаяПанель = ЭлементФормы.КоманднаяПанель;
		Если ДоступныеЭлементы.Найти(ЭлементКоманднаяПанель) = Неопределено Тогда
			БлокироватьЭлементКоманднаяПанель = Истина
		КонецЕсли;
		
	ИначеЕсли ТипРодителя = Тип("ТаблицаФормы") Тогда
		
		ЭлементКоманднаяПанельТаблицы = ЭлементФормы.КоманднаяПанель;
		Если ДоступныеЭлементы.Найти(ЭлементКоманднаяПанельТаблицы) = Неопределено Тогда
			БлокироватьЭлементКоманднаяПанельТаблицы = Истина;
		КонецЕсли;
		
		ЭлементКонтекстноеМенюТаблицы = ЭлементФормы.КонтекстноеМеню;
		Если ДоступныеЭлементы.Найти(ЭлементКонтекстноеМенюТаблицы) = Неопределено Тогда
			БлокироватьЭлементКонтекстноеМенюТаблицы = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Для Каждого ТекущийЭлемент Из ЭлементФормы.ПодчиненныеЭлементы Цикл
		
		ТипРодителя = ТипЗнч(ТекущийЭлемент);
		Если ДоступныеЭлементы.Найти(ТекущийЭлемент) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипРодителя = Тип("ГруппаФормы") 
			Или ТипРодителя = Тип("ТаблицаФормы") Тогда
			ИзменитьДоступностьЭлементовФормыРекурсивно(ТекущийЭлемент, ДоступныеЭлементы);
		ИначеЕсли ТипРодителя = Тип("ПолеФормы") Тогда
			ТекущийЭлемент.ТолькоПросмотр = Истина;
		ИначеЕсли ТипРодителя = Тип("ДекорацияФормы") Тогда
			ТекущийЭлемент.Доступность = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Если БлокироватьЭлементКоманднаяПанель Тогда
		ИзменитьДоступностьЭлементовФормыРекурсивно(ЭлементКоманднаяПанель, ДоступныеЭлементы);
	КонецЕсли;
	
	Если БлокироватьЭлементКоманднаяПанельТаблицы Тогда
		ИзменитьДоступностьЭлементовФормыРекурсивно(ЭлементКоманднаяПанельТаблицы, ДоступныеЭлементы);
	КонецЕсли;
	
	Если БлокироватьЭлементКонтекстноеМенюТаблицы Тогда
		ИзменитьДоступностьЭлементовФормыРекурсивно(ЭлементКонтекстноеМенюТаблицы, ДоступныеЭлементы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Добавляет на форму реквизиты, перед этим выполняя проверку наличия этих реквизитов.
// Может быть удобным для использования в тех случаях, когда наличие реквизитов необходимо
// и при чтении на сервере (например, при нажатии "Перечитать" или при открытии существующего объекта),
// и при создании на сервере (когда открывается форма нового объекта).
//
// Параметры:
//  Форма - управляемая форма.
//  РеквизитыКДобавлению - Массив - Массив элементов типа РеквизитФормы, которые требуется добавить на форму.
//
Процедура ДобавитьРеквизитыНаФорму(Форма, РеквизитыКДобавлению) Экспорт
	
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизитыИмена = Новый Массив;
	
	Для каждого РеквизитКДобавлению Из РеквизитыКДобавлению Цикл
		
		Контейнер = КонтейнерДобавленияРеквизита(Форма, РеквизитКДобавлению, ДобавляемыеРеквизитыИмена);
		
		Если Контейнер <> Неопределено Тогда
			// Если получилось определить контейнер добавления реквизита, то проверяем, нет ли в нем уже этого реквизита.
			Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контейнер, РеквизитКДобавлению.Имя) Тогда
				ДобавляемыеРеквизиты.Добавить(РеквизитКДобавлению);
				ДобавляемыеРеквизитыИмена.Добавить(РеквизитКДобавлению.Имя);
			КонецЕсли;
		Иначе
			// Если не получилось определить контейнер добавления реквизита, то проверку не выполняем, просто добавляем.
			ДобавляемыеРеквизиты.Добавить(РеквизитКДобавлению);
			ДобавляемыеРеквизитыИмена.Добавить(РеквизитКДобавлению.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Если ДобавляемыеРеквизиты.Количество() > 0 Тогда
		Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	КонецЕсли;

КонецПроцедуры

// Возвращает признак наличия элемента на форме.
// Может быть удобным для использования в тех случаях, когда наличие элементов необходимо
// и при чтении на сервере (например, при нажатии "Перечитать" или при открытии существующего объекта),
// и при создании на сервере (когда открывается форма нового объекта).
//
// Параметры:
//  Форма - управляемая форма.
//  ИмяЭлемента - Строка - Имя проверяемого элемента.
//
// Возвращаемое значение:
//   Булево - признак наличия элемента на форме.
//
Функция ЭлементДобавлен(Форма, ИмяЭлемента) Экспорт
	
	Возврат Форма.Элементы.Найти(ИмяЭлемента) <> Неопределено;	

КонецФункции

Процедура ОтключитьПолнотекстовыйПоискСпискаФормы(Элемент) Экспорт

	Если ТипЗнч(Элемент) <> Тип("ТаблицаФормы") Тогда
		Возврат;
	КонецЕсли;		
	
	Элемент.ПоложениеСтрокиПоиска = ПоложениеСтрокиПоиска.Нет;
	Элемент.ПоискПриВводе = ПоискВТаблицеПриВводе.НеИспользовать;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КонтейнерДобавленияРеквизита(Форма, РеквизитКДобавлению, ДобавляемыеРеквизитыИмена)

	ПутьПустой = ПустаяСтрока(РеквизитКДобавлению.Путь);
	
	Если СтрНайти(РеквизитКДобавлению.Путь, ".") > 0 Тогда
		
		ЧастиПути = СтрРазделить(РеквизитКДобавлению.Путь, ".", Ложь);
		ОжидаемоеКоличествоЧастейПути = 2;
		Если ЧастиПути.Количество() <> ОжидаемоеКоличествоЧастейПути Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Если ДобавляемыеРеквизитыИмена.Найти(РеквизитКДобавлению.Путь) <> Неопределено Тогда
			// Реквизит добавляется в контейнер, который добавился в этой же итерации добавления реквизитов. Проверка не нужна.
			// Например, сперва добавляется реквизит "Объект.Таблица", а затем добавляется "Объект.НоваяТаблица.Номенклатура".
			Возврат Неопределено;
		КонецЕсли;
		
		КоллекцияФормы = Форма[ЧастиПути[0]][ЧастиПути[1]];
		
		Если ТипЗнч(КоллекцияФормы) = Тип("ДанныеФормыКоллекция") Тогда
			
			// Реквизит добавляется в табличную часть, например, в Объект.НоваяТаблица.
			Контейнер = КоллекцияФормы.Выгрузить().Добавить();
			
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
	ИначеЕсли Не ПутьПустой Тогда
		
		Если ДобавляемыеРеквизитыИмена.Найти(РеквизитКДобавлению.Путь) <> Неопределено Тогда
			
			// Реквизит добавляется в контейнер, который добавился в этой же итерации добавления реквизитов. Проверка не нужна.
			// Например, сперва добавляется реквизит "Таблица", а затем добавляется "Таблица.Номенклатура".
			Возврат Неопределено;
		КонецЕсли;
			
		КоллекцияФормы = Форма[РеквизитКДобавлению.Путь];
		
		ТипКоллекцииФормы = ТипЗнч(КоллекцияФормы);
		
		Если ТипКоллекцииФормы = Тип("ДанныеФормыКоллекция") Тогда
			
			// Реквизит добавляется в табличную часть в корне формы, например, в ТаблицаНастроек.
			Контейнер = КоллекцияФормы.Выгрузить().Добавить();
			
		ИначеЕсли ТипКоллекцииФормы = Тип("ДанныеФормыСтруктура") Тогда
			
			// Реквизит добавляется в структуру в корне формы, например, в Объект.
			Контейнер = КоллекцияФормы;
			
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
	ИначеЕсли ПутьПустой Тогда
		
		// Реквизит добавляется в корень формы.
		Контейнер = Форма;
		
	Иначе
		
		// Ситуация, когда проверку на существование реквизита делать не нужно, либо не представляется возможным.
		Контейнер = Неопределено;
		
	КонецЕсли;
	
	Возврат Контейнер;

КонецФункции

#КонецОбласти
