#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	адаптер_НастройкиОбмена.ОставитьРеквизиты(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		ВыгружаемыеРеквизиты());
	
	ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства);
			
КонецПроцедуры	

Процедура ЗаполнитьТекстыЗапросовУсловиями(ТекстЗапроса, ТекстЗапросаТаблицаКлючей, ПараметрыЗапроса, НастройкаВыгрузки, Объект, СтандартнаяОбработка) Экспорт

	ТекстЗапроса = бг_ОбщегоНазначенияСервер.ТекстЗапросаБезОбращенияЧерезТочкуКNull(ТекстЗапроса);
	ТекстЗапросаТаблицаКлючей = бг_ОбщегоНазначенияСервер.ТекстЗапросаБезОбращенияЧерезТочкуКNull(ТекстЗапросаТаблицаКлючей);
	
КонецПроцедуры

Функция ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения) Экспорт
	
	Перем адаптер_РаботаСДаннымиИБ;
	адаптер_РаботаСДаннымиИБ = ОбщегоНазначения.ОбщийМодуль("адаптер_РаботаСДаннымиИБ");
	
	ПараметрыВыполненияЗапросов = адаптер_РаботаСДаннымиИБ.ПолучитьПараметрыВыполненияЗапросов(Объект, ДанныеСообщения);
	
	ВыгружаемыеДанные = ВыгружаемыеДанные(Объект);
	
	Реквизиты = адаптер_РаботаСДаннымиИБ.РезультатЗапросаВСтруктуруРеквизитов(
		ВыгружаемыеДанные.РезультатПоШапке,
		ПараметрыВыполненияЗапросов.ТаблицаКлючей,
		ДанныеСообщения);
		
	Если Реквизиты.Количество() > 0 Тогда
		
		бг_битЗаявлениеОВыдачеФСМИнтеграция.ЗаполнитьИдентификаторПунктаРазгрузки(Реквизиты);	
		
		Товары = адаптер_РаботаСДаннымиИБ.РезультатЗапросаВСтруктуруРеквизитов(
			ВыгружаемыеДанные.РезультатПоТоварам,
			ПараметрыВыполненияЗапросов.ТаблицаКлючей,
			ДанныеСообщения);
			
		Реквизиты[0].Вставить("Товары", Товары);
	КонецЕсли;
		
	ДанныеВыгружаемогоОбъекта = Новый Структура;
	ДанныеВыгружаемогоОбъекта.Вставить("ПолноеИмя", ПараметрыВыполненияЗапросов.ПолноеИмя);
	ДанныеВыгружаемогоОбъекта.Вставить("Реквизиты", Реквизиты);
	
	Возврат ДанныеВыгружаемогоОбъекта;
	
КонецФункции

#КонецОбласти // Конец ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Функция ВыгружаемыеРеквизиты()

	ВыгружаемыеРеквизиты = Новый Массив;
	
	// Реквизиты шапки
	ВыгружаемыеРеквизиты.Добавить("Дата");
	ВыгружаемыеРеквизиты.Добавить("Номер");
	ВыгружаемыеРеквизиты.Добавить("Проведен");
	ВыгружаемыеРеквизиты.Добавить("ПометкаУдаления");
	ВыгружаемыеРеквизиты.Добавить("ДатаФактическойОтгрузки");
	ВыгружаемыеРеквизиты.Добавить("ДатаВходящегоДокумента");
	ВыгружаемыеРеквизиты.Добавить("НомерВходящегоДокумента");
	ВыгружаемыеРеквизиты.Добавить("Организация");
	ВыгружаемыеРеквизиты.Добавить("ОрганизацияЕГАИС");
	ВыгружаемыеРеквизиты.Добавить("ЗаявлениеОВыдачеФСМ");
	ВыгружаемыеРеквизиты.Добавить("Отправитель");
	ВыгружаемыеРеквизиты.Добавить("НаименованиеПроизводителяМарок");
	ВыгружаемыеРеквизиты.Добавить("Комментарий");
	
	// Реквизиты ТЧ "Товары"
	ВыгружаемыеРеквизиты.Добавить("Товары.Номенклатура");
	ВыгружаемыеРеквизиты.Добавить("Товары.Серия");
	ВыгружаемыеРеквизиты.Добавить("Товары.ТипМарки");
	ВыгружаемыеРеквизиты.Добавить("Товары.бг_СерияМарки");
	ВыгружаемыеРеквизиты.Добавить("Товары.бг_НомерРулона");
	ВыгружаемыеРеквизиты.Добавить("Товары.бг_НомерДиапазонаВРулоне");
	ВыгружаемыеРеквизиты.Добавить("Товары.бг_НачальныйНомерДиапазона");
	ВыгружаемыеРеквизиты.Добавить("Товары.бг_КонечныйНомерДиапазона");
	ВыгружаемыеРеквизиты.Добавить("Товары.КоличествоМарок");
	
	Возврат СтрСоединить(ВыгружаемыеРеквизиты, ",");
	
КонецФункции

Процедура ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства)
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	// ПунктРазгрузки: в ERP это склад или контрагент, в УПП - отдельный справочник алкПунктыРазгрузки.
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		, // ИмяРеквизита
		"ПунктРазгрузки",
		Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(36)));	
		
	// ИдентификаторЕГАИС: необходим для сопоставления документа в ЕРП с регистром алкВходящиеДокументыУТМ в УПП для отметки о загрузке.
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		, // ИмяРеквизита
		"ИдентификаторЕГАИС",
		Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(50)));
		
КонецПроцедуры

Функция ВыгружаемыеДанные(Ссылка)

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаВыгружаемыхДанных();
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	ВыгружаемыеДанные = Новый Структура;
	ВыгружаемыеДанные.Вставить("РезультатПоШапке", РезультатыЗапросов[0]);
	ВыгружаемыеДанные.Вставить("РезультатПоТоварам", РезультатыЗапросов[1]);
	
	Возврат ВыгружаемыеДанные;

КонецФункции

Функция ТекстЗапросаВыгружаемыхДанных()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	битНакладнаяНаВыдачуФСМ.Ссылка КАК Идентификатор,
	|	битНакладнаяНаВыдачуФСМ.Ссылка КАК ТаблицаКлючей,
	|	битНакладнаяНаВыдачуФСМ.ПометкаУдаления КАК ПометкаУдаления,
	|	битНакладнаяНаВыдачуФСМ.Проведен КАК Проведен,
	|	битНакладнаяНаВыдачуФСМ.Дата КАК Дата,
	|	битНакладнаяНаВыдачуФСМ.Номер КАК Номер,
	|	битНакладнаяНаВыдачуФСМ.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	битНакладнаяНаВыдачуФСМ.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	битНакладнаяНаВыдачуФСМ.ДатаФактическойОтгрузки КАК ДатаФактическойОтгрузки,
	|	битНакладнаяНаВыдачуФСМ.НаименованиеПроизводителяМарок КАК НаименованиеПроизводителяМарок,
	|	битНакладнаяНаВыдачуФСМ.ЗаявлениеОВыдачеФСМ КАК ЗаявлениеОВыдачеФСМ_ЗначениеРеквизитаИдентификатор,
	|	битНакладнаяНаВыдачуФСМ.ЗаявлениеОВыдачеФСМ.ИдентификаторЕГАИС КАК ИдентификаторЕГАИС,
	|	битНакладнаяНаВыдачуФСМ.Комментарий КАК Комментарий,
	|	битНакладнаяНаВыдачуФСМ.Организация КАК Организация_ЗначениеРеквизитаИдентификатор,
	|	битНакладнаяНаВыдачуФСМ.Организация КАК Организация_ЗначениеРеквизитаТаблицаКлючей,
	|	битНакладнаяНаВыдачуФСМ.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС_ЗначениеРеквизитаИдентификатор,
	|	битНакладнаяНаВыдачуФСМ.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС_ЗначениеРеквизитаТаблицаКлючей,
	|	битНакладнаяНаВыдачуФСМ.ОрганизацияЕГАИС.Код КАК ОрганизацияЕГАИС_ЗначениеРеквизитаКод,
	|	битНакладнаяНаВыдачуФСМ.Отправитель КАК Отправитель_ЗначениеРеквизитаИдентификатор,
	|	битНакладнаяНаВыдачуФСМ.Отправитель КАК Отправитель_ЗначениеРеквизитаТаблицаКлючей,
	|	битНакладнаяНаВыдачуФСМ.Отправитель.Код КАК Отправитель_ЗначениеРеквизитаКод,
	|	ВЫБОР
	|		КОГДА битНакладнаяНаВыдачуФСМ.ОрганизацияЕГАИС.СоответствуетОрганизации = ИСТИНА
	|			ТОГДА ВЫРАЗИТЬ(битНакладнаяНаВыдачуФСМ.ОрганизацияЕГАИС.ТорговыйОбъект КАК Справочник.Склады).Ссылка
	|		ИНАЧЕ ВЫРАЗИТЬ(битНакладнаяНаВыдачуФСМ.ОрганизацияЕГАИС.Контрагент КАК Справочник.Контрагенты).Ссылка
	|	КОНЕЦ КАК ПунктРазгрузки
	|ИЗ
	|	Документ.битНакладнаяНаВыдачуФСМ КАК битНакладнаяНаВыдачуФСМ
	|ГДЕ
	|	битНакладнаяНаВыдачуФСМ.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	битНакладнаяНаВыдачуФСМТовары.бг_СерияМарки КАК бг_СерияМарки,
	|	битНакладнаяНаВыдачуФСМТовары.бг_НомерРулона КАК бг_НомерРулона,
	|	битНакладнаяНаВыдачуФСМТовары.бг_НомерДиапазонаВРулоне КАК бг_НомерДиапазонаВРулоне,
	|	битНакладнаяНаВыдачуФСМТовары.бг_НачальныйНомерДиапазона КАК бг_НачальныйНомерДиапазона,
	|	битНакладнаяНаВыдачуФСМТовары.бг_КонечныйНомерДиапазона КАК бг_КонечныйНомерДиапазона,
	|	битНакладнаяНаВыдачуФСМТовары.КоличествоМарок КАК КоличествоМарок,
	|	битНакладнаяНаВыдачуФСМТовары.Номенклатура КАК Номенклатура_ЗначениеРеквизитаИдентификатор,
	|	битНакладнаяНаВыдачуФСМТовары.Номенклатура КАК Номенклатура_ЗначениеРеквизитаТаблицаКлючей,
	|	битНакладнаяНаВыдачуФСМТовары.Номенклатура.Код КАК Номенклатура_ЗначениеРеквизитаКод,
	|	битНакладнаяНаВыдачуФСМТовары.Серия КАК Серия_ЗначениеРеквизитаИдентификатор,
	|	битНакладнаяНаВыдачуФСМТовары.Серия КАК Серия_ЗначениеРеквизитаТаблицаКлючей,
	|	битНакладнаяНаВыдачуФСМТовары.ТипМарки КАК ТипМарки_ЗначениеРеквизитаИдентификатор,
	|	битНакладнаяНаВыдачуФСМТовары.ТипМарки КАК ТипМарки_ЗначениеРеквизитаТаблицаКлючей,
	|	битНакладнаяНаВыдачуФСМТовары.ТипМарки.Код КАК ТипМарки_ЗначениеРеквизитаКод
	|ИЗ
	|	Документ.битНакладнаяНаВыдачуФСМ.Товары КАК битНакладнаяНаВыдачуФСМТовары
	|ГДЕ
	|	битНакладнаяНаВыдачуФСМТовары.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти // Конец СлужебныеПроцедурыИФункции
