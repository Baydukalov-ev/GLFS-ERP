
#Область ПрограммныйИнтерфейс

// Формирует в переданном менеджере временных таблиц таблицы ВТНоменклатура и ВТСклады,
//  содеращие данные, необходимые для формирования заказов.
//
//  Параметры:
//   МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджере временных таблиц, 
//                             в которром будут сформированы временные таблицы. 
//   ДополнительныеПараметры - Структура - Дополнительные параметры запроса для формирования таблиц.
//     Ключи: 
//      * Склад              - СправочникСсылка.Склады - ссылка на склад, по которому будет отгрузка по заказу.
//      * ТоварныеКатегории  - Массив - Список товарных категорий, по которым формируются данные в ВТНоменклатура.
//      * Партнер            - СправочникСсылка.Партнеры - ссылка на партнера, которому будет отгрузка по заказу.
//
Процедура СоздатьВспомогательныеВТ(МенеджерВременныхТаблиц, ДополнительныеПараметры) Экспорт
	
	СоздатьВТНоменклатура(МенеджерВременныхТаблиц, ДополнительныеПараметры);
	СоздатьВТСклады(МенеджерВременныхТаблиц, ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьВТСклады(МенеджерВременныхТаблиц, ДополнительныеПараметры)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Склад", ДополнительныеПараметры.Склад);
	Запрос.УстановитьПараметр("ТоварныеКатегории", ДополнительныеПараметры.ТоварныеКатегории);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ СписокНоменклатуры
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.ТоварнаяКатегория В(&ТоварныеКатегории)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СпрНоменклатура.ВидНоменклатуры КАК ВидНоменклатуры
	|ПОМЕСТИТЬ ВидыНоменклатуры
	|ИЗ
	|	СписокНоменклатуры КАК СписокНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО СписокНоменклатуры.Ссылка = СпрНоменклатура.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Склады.Ссылка КАК Склад,
	|	ЕСТЬNULL(ВидыНоменклатуры.ВидНоменклатуры, ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)) КАК ВидНоменклатуры,
	|	0 КАК Приоритет,
	|	ИСТИНА КАК ИспользоватьДляОтложенногоОбеспечения
	|ПОМЕСТИТЬ ВТСклады
	|ИЗ
	|	Справочник.Склады КАК Склады
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО (ИСТИНА)
	|ГДЕ
	|	Склады.Ссылка = &Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СписокНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВидыНоменклатуры";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьВТНоменклатура(МенеджерВременныхТаблиц, ДополнительныеПараметры)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Партнер", ДополнительныеПараметры.Партнер);
	Запрос.УстановитьПараметр("ТоварныеКатегории", ДополнительныеПараметры.ТоварныеКатегории);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Контрагенты.Партнер КАК Партнер
	|ПОМЕСТИТЬ ВТПартнеры
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Партнер = &Партнер
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Партнер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпрНоменклатура.Ссылка КАК Номенклатура,
	|	СпрНоменклатура.ВидНоменклатуры КАК ВидНоменклатуры,
	|	СпрНоменклатура.бг_УчетОстатковИРезервовВРазрезеУпаковокПаллет КАК УчетОстатковИРезервовВРазрезеУпаковокПаллет,
	|	СпрНоменклатура.ЕдиницаИзмерения.Код КАК КодЕИ,
	|	СпрНоменклатура.Код КАК КодНоменклатуры,
	|	ВЫРАЗИТЬ(СпрНоменклатура.НаименованиеПолное КАК СТРОКА(1024)) КАК НаименованиеНоменклатуры,
	|	ВЫРАЗИТЬ(СпрНоменклатура.ТоварнаяКатегория КАК Справочник.ТоварныеКатегории).бг_СкюМТ КАК СКЮ_МТ,
	|	ВЫРАЗИТЬ(СпрНоменклатура.ТоварнаяКатегория КАК Справочник.ТоварныеКатегории).бг_КодНСИ КАК КодДляПоискаНоменклатуры
	|ПОМЕСТИТЬ ВТНоменклатура
	|ИЗ
	|	Справочник.Номенклатура КАК СпрНоменклатура
	|ГДЕ
	|	НЕ СпрНоменклатура.ПометкаУдаления
	|	И СпрНоменклатура.ТоварнаяКатегория В(&ТоварныеКатегории)
	|	И СпрНоменклатура.ТоварнаяКатегория <> ЗНАЧЕНИЕ(Справочник.ТоварныеКатегории.ПустаяСсылка)
	|	И СпрНоменклатура.ТоварнаяКатегория.бг_КодНСИ <> """"
	|	И ВЫБОР
	|			КОГДА СпрНоменклатура.АлкогольнаяПродукция
	|				ТОГДА СпрНоменклатура.ВидАлкогольнойПродукции <> ЗНАЧЕНИЕ(Справочник.ВидыАлкогольнойПродукции.ПустаяСсылка)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура";
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти
