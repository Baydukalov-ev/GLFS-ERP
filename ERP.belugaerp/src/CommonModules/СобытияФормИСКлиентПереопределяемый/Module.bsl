
#Область ОбработчикиСобытийЭлементовФормы

#Область Номенклатура

&ИзменениеИКонтроль("ПриНачалеВыбораНоменклатуры")
Процедура бг_ПриНачалеВыбораНоменклатуры(Владелец, ВидыПродукции, СтандартнаяОбработка, ОписаниеОповещения, Знач Реквизиты)

	//++ НЕ ГОСИС
	СтандартнаяОбработка = Ложь;

	Если НЕ(Реквизиты = Неопределено) Тогда
		ДополнительныеПараметры = Реквизиты;
	Иначе
		ДополнительныеПараметры = Новый Структура;
	КонецЕсли;

	ОсобенностиУчета = Новый Массив;

	Если ТипЗнч(ВидыПродукции) = Тип("Массив") Тогда
		Для Каждого ВидПродукцииИС Из ВидыПродукции Цикл
			ОсобенностьУчета = ИнтеграцияИСУТКлиентСервер.ОсобенностьУчетаПоВидуПродукции(ВидПродукцииИС);
			Если ЗначениеЗаполнено(ОсобенностьУчета) Тогда
				ОсобенностиУчета.Добавить(ОсобенностьУчета);
			КонецЕсли;
		КонецЦикла;
	Иначе
		ОсобенностьУчета = ИнтеграцияИСУТКлиентСервер.ОсобенностьУчетаПоВидуПродукции(ВидыПродукции);
		Если ЗначениеЗаполнено(ОсобенностьУчета) Тогда
			ОсобенностиУчета.Добавить(ОсобенностьУчета);
		КонецЕсли;
	КонецЕсли;

	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ТипНоменклатуры", ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар"));

	Если ОсобенностиУчета.Количество() > 0 Тогда
		ПараметрыОтбора.Вставить("ОсобенностьУчета", ОсобенностиУчета);
	КонецЕсли;

	Если ДополнительныеПараметры.Свойство("ВидАлкогольнойПродукцииЕГАИС")
		И ЗначениеЗаполнено(Реквизиты.ВидАлкогольнойПродукцииЕГАИС) Тогда
		ПараметрыОтбора.Вставить("ВидАлкогольнойПродукции", ДополнительныеПараметры.ВидАлкогольнойПродукцииЕГАИС);
	КонецЕсли;
	
#Вставка
	Если Реквизиты <> Неопределено И Реквизиты.МаркируемаяПродукцияЕГАИС Тогда
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Владелец.Элементы, "Товары") Тогда
			
			ТекущаяСтрокаТовары = Владелец.Элементы.Товары.ТекущиеДанные;
			
			Если ТекущаяСтрокаТовары <> Неопределено
				И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрокаТовары, "Справка2") Тогда
				
				Если Не ЗначениеЗаполнено(ТекущаяСтрокаТовары.Справка2) Тогда
					ПоказатьПредупреждение(, НСтр("ru='Необходимо заполнить справку 2'"));
					Возврат;
				КонецЕсли;
				
				ДанныеСправки2 = бг_ИнтеграцияЕГАИСВызовСервера.ДанныеСправки2(ТекущаяСтрокаТовары.Справка2);
				
				Если Не ЗначениеЗаполнено(ДанныеСправки2.Справка1) Тогда
					ПоказатьПредупреждение(, НСтр("ru='Не указана справка 1 в справке 2'"));
					Возврат;
				КонецЕсли;
				
				ПараметрыОтбора.Вставить("Крепость", ДанныеСправки2.Крепость);
				
			КонецЕсли;
		КонецЕсли;
		
		ОбъемВЛитрах = Реквизиты.ОбъемЕГАИС;
		КоэффициентДАЛ = 10; // В 1 ДАЛ содержится 10 литров.
		ПараметрыОтбора.Вставить("ОбъемДАЛ", ОбъемВЛитрах / КоэффициентДАЛ);
		
	КонецЕсли;
#КонецВставки
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВыборГруппИЭлементов",    ИспользованиеГруппИЭлементов.Элементы);
	ПараметрыФормы.Вставить("Отбор",                   ПараметрыОтбора);
	ПараметрыФормы.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);

	Если ДополнительныеПараметры.Свойство("ВидАлкогольнойПродукцииМаркируемый") Тогда
		ПараметрыФормы.Вставить("ВидАлкогольнойПродукцииМаркируемый", ДополнительныеПараметры.ВидАлкогольнойПродукцииМаркируемый);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыФормы, Владелец,,,, ОписаниеОповещения);
	//-- НЕ ГОСИС

	Возврат;

КонецПроцедуры

#КонецОбласти

#КонецОбласти
