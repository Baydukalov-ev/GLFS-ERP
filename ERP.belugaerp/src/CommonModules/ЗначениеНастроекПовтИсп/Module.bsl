
#Область ПрограммныйИнтерфейс

#Область ФункцииПолученияЗначенийПоУмолчанию

#Область СкидкиНаценки

&ИзменениеИКонтроль("КоличествоИсточниковСкидокНаценок")
Функция бг_КоличествоИсточниковСкидокНаценок()

	СтруктураКВозврату = Новый Структура;
	СтруктураКВозврату.Вставить("ВсегоНаСкладах", 0);
	СтруктураКВозврату.Вставить("ВсегоВТиповыхСоглашениях", 0);
	СтруктураКВозврату.Вставить("ВсегоВИндивидуальныхСоглашениях", 0);
	СтруктураКВозврату.Вставить("ВсегоВКартахЛояльности", 0);
#Вставка
	СтруктураКВозврату.Вставить("бг_ВсегоВДоговорахКонтрагентов", 0);
	СтруктураКВозврату.Вставить("бг_ВсегоВПунктахНазначения", 0);
#КонецВставки

	ТекстЗапросаПоВидамКартамЛояльности = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(ВидыКартЛояльности.Ссылка) КАК КоличествоОбъектов
	|ИЗ
	|	Справочник.ВидыКартЛояльности КАК ВидыКартЛояльности";

	ТекстЗапросаПоСкладам = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(Склады.Ссылка) КАК КоличествоОбъектов
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	НЕ Склады.ЭтоГруппа";

	ТекстЗапросаПоИндивидуальнымСоглашениям = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(СоглашенияСКлиентами.Ссылка) КАК КоличествоОбъектов
	|ИЗ
	|	Справочник.СоглашенияСКлиентами КАК СоглашенияСКлиентами
	|ГДЕ
	|	НЕ СоглашенияСКлиентами.Типовое";

	ТекстЗапросаПоТиповымСоглашениям = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(СоглашенияСКлиентами.Ссылка) КАК КоличествоОбъектов
	|ИЗ
	|	Справочник.СоглашенияСКлиентами КАК СоглашенияСКлиентами
	|ГДЕ
	|	СоглашенияСКлиентами.Типовое";

	Запрос = Новый Запрос;
	КоличествоЗапросов = 0;
	СоответствиеДанныхНомерамЗапроса = Новый Соответствие;
	ТекстЗапросаПостфикс = "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";

	Если ПолучитьФункциональнуюОпцию("ИспользоватьКартыЛояльности")
		И ПравоДоступа("Чтение", Метаданные.Справочники.КартыЛояльности) Тогда

		Запрос.Текст = Запрос.Текст + ТекстЗапросаПоВидамКартамЛояльности;
		СоответствиеДанныхНомерамЗапроса.Вставить(КоличествоЗапросов, "ВсегоВКартахЛояльности");
		КоличествоЗапросов = КоличествоЗапросов + 1;

	КонецЕсли;

	Если ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов")
		И ПравоДоступа("Чтение", Метаданные.Справочники.Склады) Тогда

		Запрос.Текст = Запрос.Текст + ?(КоличествоЗапросов = 0 , "", ТекстЗапросаПостфикс) + ТекстЗапросаПоСкладам;
		СоответствиеДанныхНомерамЗапроса.Вставить(КоличествоЗапросов, "ВсегоНаСкладах");
		КоличествоЗапросов = КоличествоЗапросов + 1;

	КонецЕсли;

	Если ПолучитьФункциональнуюОпцию("ИспользоватьИндивидуальныеСоглашенияСКлиентами")
		И ПравоДоступа("Чтение", Метаданные.Справочники.СоглашенияСКлиентами) Тогда

		Запрос.Текст = Запрос.Текст + ?(КоличествоЗапросов = 0 , "", ТекстЗапросаПостфикс) + ТекстЗапросаПоИндивидуальнымСоглашениям;
		СоответствиеДанныхНомерамЗапроса.Вставить(КоличествоЗапросов, "ВсегоВИндивидуальныхСоглашениях");
		КоличествоЗапросов = КоличествоЗапросов + 1;

	КонецЕсли;

	Если ПолучитьФункциональнуюОпцию("ИспользоватьТиповыеСоглашенияСКлиентами") 
		И ПравоДоступа("Чтение", Метаданные.Справочники.СоглашенияСКлиентами) Тогда

		Запрос.Текст = Запрос.Текст + ?(КоличествоЗапросов = 0 , "", ТекстЗапросаПостфикс) + ТекстЗапросаПоТиповымСоглашениям;
		СоответствиеДанныхНомерамЗапроса.Вставить(КоличествоЗапросов, "ВсегоВТиповыхСоглашениях");
		КоличествоЗапросов = КоличествоЗапросов + 1;

	КонецЕсли;

#Вставка
	Если ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами") 
		И ПравоДоступа("Чтение", Метаданные.Справочники.ДоговорыКонтрагентов) Тогда
		
		Запрос.Текст = Запрос.Текст + ?(КоличествоЗапросов = 0 , "", ТекстЗапросаПостфикс) + "
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	КОЛИЧЕСТВО(Ссылка) КАК КоличествоОбъектов
			|ИЗ
			|	Справочник.ДоговорыКонтрагентов Т
			|ГДЕ
			|	Т.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СПокупателем)";
		СоответствиеДанныхНомерамЗапроса.Вставить(КоличествоЗапросов, "бг_ВсегоВДоговорахКонтрагентов");
		КоличествоЗапросов = КоличествоЗапросов + 1;
		
	КонецЕсли;
	
	Если ПравоДоступа("Чтение", Метаданные.Справочники.битПунктыНазначения) Тогда
		
		Запрос.Текст = Запрос.Текст + ?(КоличествоЗапросов = 0 , "", ТекстЗапросаПостфикс) + "
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	КОЛИЧЕСТВО(Ссылка) КАК КоличествоОбъектов
			|ИЗ
			|	Справочник.битПунктыНазначения Т";
		СоответствиеДанныхНомерамЗапроса.Вставить(КоличествоЗапросов, "бг_ВсегоВПунктахНазначения");
		КоличествоЗапросов = КоличествоЗапросов + 1;
		
	КонецЕсли;
#КонецВставки
	Если КоличествоЗапросов > 0 Тогда

		РезультатЗапроса = Запрос.ВыполнитьПакет(); 

		Для Инд = 1 По КоличествоЗапросов Цикл 

			Выборка = РезультатЗапроса[Инд - 1].Выбрать();
			Если Выборка.Следующий() Тогда
				СтруктураКВозврату[СоответствиеДанныхНомерамЗапроса.Получить(Инд-1)] = Выборка.КоличествоОбъектов;
			КонецЕсли;

		КонецЦикла

	КонецЕсли;

	Возврат СтруктураКВозврату;

КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти
