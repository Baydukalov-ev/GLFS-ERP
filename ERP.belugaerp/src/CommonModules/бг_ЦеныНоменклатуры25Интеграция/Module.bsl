#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт

	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	адаптер_НастройкиОбмена.УстановитьРеквизиты(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		ВыгружаемыеРеквизиты());	
	
	ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства);
	
КонецПроцедуры

Процедура ЗаполнитьТекстыЗапросовУсловиями(ТекстЗапроса, ТекстЗапросаТаблицаКлючей, ПараметрыЗапроса, НастройкаВыгрузки, Объект, СтандартнаяОбработка) Экспорт
	
	Перем адаптер_РаботаСДаннымиИБ;
	адаптер_РаботаСДаннымиИБ = ОбщегоНазначения.ОбщийМодуль("адаптер_РаботаСДаннымиИБ");
	
	СтандартнаяОбработка = Ложь;
	
	ТекстПоиска = "NULL КАК КодСкюМТ";
	ТекстЗамены = "ВыгружаемыйОбъект.Номенклатура.ТоварнаяКатегория.бг_СкюМТ.Код КАК КодСкюМТ";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстПоиска, ТекстЗамены);
	
	ТекстПоиска =
	"&УсловиеЗапроса";
	ТекстЗамены =
	"НЕ ВыгружаемыйОбъект.Номенклатура.ТоварнаяКатегория.бг_СкюМТ.Код ЕСТЬ NULL
	|	И &УсловиеЗапроса";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстПоиска, ТекстЗамены);
	
	адаптер_РаботаСДаннымиИБ.ЗаполнитьТекстыЗапросовУсловиями(ТекстЗапроса, ТекстЗапросаТаблицаКлючей, ПараметрыЗапроса, НастройкаВыгрузки, Объект);
	
КонецПроцедуры

Функция ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения) экспорт
	
	Перем адаптер_РаботаСДаннымиИБ;
	адаптер_РаботаСДаннымиИБ = ОбщегоНазначения.ОбщийМодуль("адаптер_РаботаСДаннымиИБ");
	
	ПараметрыВыполненияЗапросов = адаптер_РаботаСДаннымиИБ.ПолучитьПараметрыВыполненияЗапросов(Объект, ДанныеСообщения);
	
	ПроверятьДополнительныеУсловия = Ложь;
	Если НЕ ДанныеСообщения.ДополнительныеСвойстваОбъекта.Свойство(                  
		"адаптер_ПроверятьДополнительныеУсловияПриПолученииДанныхВыгружаемогоОбъекта", ПроверятьДополнительныеУсловия) Тогда	
		ПроверятьДополнительныеУсловия = Ложь;
	КонецЕсли;
	
	РезультатЗапроса = адаптер_РаботаСДаннымиИБ.ПолучитьРезультатЗапроса(
		ПараметрыВыполненияЗапросов.ТекстыЗапросов.ТекстЗапроса,
		ПараметрыВыполненияЗапросов.ПараметрыЗапроса,
		ПараметрыВыполненияЗапросов.ТаблицаОтбора,
		ПроверятьДополнительныеУсловия);
		
	Реквизиты = адаптер_РаботаСДаннымиИБ.РезультатЗапросаВСтруктуруРеквизитов(РезультатЗапроса,
		ПараметрыВыполненияЗапросов.ТаблицаКлючей, ДанныеСообщения);
		
	СтруктураРезультат = Новый Структура;
	СтруктураРезультат.Вставить("ПолноеИмя", ПараметрыВыполненияЗапросов.ПолноеИмя);
	СтруктураРезультат.Вставить("Реквизиты", Реквизиты);
	СтруктураРезультат.Вставить("Отбор", Новый Массив);

	Возврат СтруктураРезультат;
	
КонецФункции

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ВыгружаемыеРеквизиты()
		
	ВыгружаемыеРеквизиты = Новый Массив;
	
	ВыгружаемыеРеквизиты.Добавить("ВидЦены");
	ВыгружаемыеРеквизиты.Добавить("Номенклатура");
	ВыгружаемыеРеквизиты.Добавить("Цена");
		
	Возврат ВыгружаемыеРеквизиты;
	
КонецФункции

Процедура ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства)
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		,
		"КодСкюМТ",
		ОбщегоНазначения.ОписаниеТипаСтрока(15));
				
КонецПроцедуры

#КонецОбласти // Конец СлужебныеПроцедурыИФункции
