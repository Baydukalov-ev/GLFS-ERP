#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьЗагружаемыйОбъект(ЗагружаемыйОбъект, СтруктураОбъекта, СписокСвойств, ИсключаяСвойства, СтандартнаяОбработка) Экспорт
	
	ЗначениеКонстантыТомХраненияФотоНСИ = бг_КонстантыПовтИсп.ЗначениеКонстанты("ТомХраненияФотоНСИ");
	Если Не ЗначениеЗаполнено(ЗначениеКонстантыТомХраненияФотоНСИ) Тогда
		ЗагружаемыйОбъект.ДополнительныеСвойства.Вставить("СтандартнаяЗаписьНеТребуется", Истина);
		Возврат;
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("Владелец") Тогда
		Если СтруктураОбъекта.Владелец.Свойство("ПолноеИмя") И СтруктураОбъекта.Владелец.ПолноеИмя = "Справочник.Номенклатура" Тогда
			ЗагружаемыйОбъект.ДополнительныеСвойства.Вставить("СтандартнаяЗаписьНеТребуется", Истина);
			Возврат;
		КонецЕсли;
		СтруктураОбъекта.Вставить("ВладелецФайла", СтруктураОбъекта.Владелец);
		СтруктураОбъекта.Удалить("Владелец");
	Иначе
		ЗагружаемыйОбъект.ДополнительныеСвойства.Вставить("СтандартнаяЗаписьНеТребуется", Истина);
		Возврат;
	КонецЕсли;
	
	СтруктураОбъекта.Вставить("Том", ЗначениеКонстантыТомХраненияФотоНСИ);
	
	Если СтруктураОбъекта.Свойство("ОтносительныйПуть") И ЗначениеЗаполнено(СтруктураОбъекта.ОтносительныйПуть) Тогда
		ПолныйПутьТомаХраненияФотоНСИ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначениеКонстантыТомХраненияФотоНСИ, "ПолныйПутьWindows");
		СтруктураОбъекта.Вставить("ТипХраненияФайла", Перечисления.ТипыХраненияФайлов.ВТомахНаДиске);
		СтруктураОбъекта.Вставить("ПутьКФайлу", СтрЗаменить(СтруктураОбъекта.ОтносительныйПуть, ПолныйПутьТомаХраненияФотоНСИ, ""));
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("НазначениеИзображения") Тогда
		СтруктураОбъекта.Вставить("бг_НазначениеИзображения", СтруктураОбъекта.НазначениеИзображения);
	КонецЕсли;
	
	Если СтруктураОбъекта.Свойство("ДатаЗагрузки") Тогда
		СтруктураОбъекта.Вставить("ДатаСоздания", СтруктураОбъекта.ДатаЗагрузки);
		СтруктураОбъекта.Вставить("ДатаМодификацииУниверсальная", ТекущаяДата());
	КонецЕсли;
	Если СтруктураОбъекта.Свойство("РасширениеФайла")Тогда
		СтруктураОбъекта.Вставить("Расширение", СтруктураОбъекта.РасширениеФайла);
	КонецЕсли;
	СтруктураОбъекта.Вставить("Автор", Неопределено);
	
КонецПроцедуры

Процедура ВыгрузитьСвязанныеОбъекты(СправочникОбъект) Экспорт
	
	бг_ОбщегоНазначенияСервер.ЗарегистрироватьИсходящееСообщениеПриЗаписи(СправочникОбъект.Ссылка);

КонецПроцедуры

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт

	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	Реквизиты = ВыгружаемыеРеквизиты(); 
	
	ДобавитьРеквизитыЧерезТочкуКВыгрузке(Реквизиты);
	
	адаптер_НастройкиОбмена.УстановитьРеквизиты(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		Реквизиты);
	
КонецПроцедуры

Функция ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения) Экспорт
	
	Перем адаптер_обработчикиСобытийСтандартный;
	адаптер_обработчикиСобытийСтандартный = ОбщегоНазначения.ОбщийМодуль("адаптер_обработчикиСобытийСтандартный");
	
	ДанныеОбъекта = адаптер_обработчикиСобытийСтандартный.ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения);
	
	Если ДанныеОбъекта.Реквизиты.Количество() <> 0 Тогда
		СтруктураОбъекта = ДанныеОбъекта.Реквизиты[0];
		Если СтруктураОбъекта.Свойство("ПутьКФайлу")
			И СтруктураОбъекта.Свойство("Том_ПолныйПутьWindows") Тогда
			СтруктураОбъекта.Вставить("ПутьКФайлу", 
				СтруктураОбъекта.Том_ПолныйПутьWindows + СтруктураОбъекта.ПутьКФайлу);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеОбъекта;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВыгружаемыеРеквизиты()
	
	ВыгружаемыеРеквизиты = Новый Массив;
	
	ВыгружаемыеРеквизиты.Добавить("Предопределенный");
	ВыгружаемыеРеквизиты.Добавить("ПометкаУдаления");
	ВыгружаемыеРеквизиты.Добавить("Наименование");
	ВыгружаемыеРеквизиты.Добавить("Автор");
	ВыгружаемыеРеквизиты.Добавить("ВладелецФайла");
	ВыгружаемыеРеквизиты.Добавить("ДатаСоздания");
	ВыгружаемыеРеквизиты.Добавить("ПутьКФайлу");
	ВыгружаемыеРеквизиты.Добавить("Размер");
	ВыгружаемыеРеквизиты.Добавить("Расширение");
	ВыгружаемыеРеквизиты.Добавить("бг_НазначениеИзображения");
	
	Возврат ВыгружаемыеРеквизиты;
	
КонецФункции

Процедура ДобавитьРеквизитыЧерезТочкуКВыгрузке(Реквизиты)

	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	Реквизиты.Добавить(
		адаптер_НастройкиОбмена.ОписаниеРеквизита(
			"ВладелецФайла_бг_СкюМТ", 
			Новый ОписаниеТипов("СправочникСсылка.бг_ЕК_СКЮ_СкюМТ"), 
			"ВладелецФайла.бг_СкюМТ"));

	Реквизиты.Добавить(
		адаптер_НастройкиОбмена.ОписаниеРеквизита(
			"Том_ПолныйПутьWindows", 
			ОбщегоНазначения.ОписаниеТипаСтрока(200), 
			"Том.ПолныйПутьWindows"));   
			
КонецПроцедуры

#КонецОбласти
