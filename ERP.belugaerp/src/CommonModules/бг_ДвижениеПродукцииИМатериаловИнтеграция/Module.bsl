#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	ИменаРеквизитов = ВыгружаемыеРеквизиты();
	адаптер_НастройкиОбмена.ОставитьРеквизиты(РеквизитыИСвойства, РеквизитыИСвойства.МетаданныеОбъекта, ИменаРеквизитов);
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, Метаданные.Документы.ДвижениеПродукцииИМатериалов, 
							, "бг_ПодразделениеИдентификатор", ОбщегоНазначения.ОписаниеТипаСтрока(36));
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, Метаданные.Документы.ДвижениеПродукцииИМатериалов, 
							, "бг_ПодразделениеКод", ОбщегоНазначения.ОписаниеТипаСтрока(9));
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, Метаданные.Документы.ДвижениеПродукцииИМатериалов, 
							, "бг_ПодразделениеОтправительИдентификатор", ОбщегоНазначения.ОписаниеТипаСтрока(36));
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, Метаданные.Документы.ДвижениеПродукцииИМатериалов, 
							, "бг_ПодразделениеОтправительКод", ОбщегоНазначения.ОписаниеТипаСтрока(9));
КонецПроцедуры

Процедура ЗаполнитьТекстыЗапросовУсловиями(ТекстЗапроса, ТекстЗапросаТаблицаКлючей, ПараметрыЗапроса, НастройкаВыгрузки, Объект, СтандартнаяОбработка) Экспорт
	
	ТекстПоиска = "&УсловиеЗапроса";
	
	ТекстЗамены = "&УсловиеЗапроса 
	|	И ВыгружаемыйОбъект.ХозяйственнаяОперация В (
	|			Значение(Перечисление.ХозяйственныеОперации.ПередачаМатериаловВКладовую),
	|			Значение(Перечисление.ХозяйственныеОперации.ПередачаМатериаловВПроизводство),
	|			Значение(Перечисление.ХозяйственныеОперации.ПеремещениеПолуфабрикатов),
	|			Значение(Перечисление.ХозяйственныеОперации.ВозвратМатериаловИзКладовой))";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстПоиска, ТекстЗамены);

КонецПроцедуры

Функция ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения) Экспорт
	
	Перем адаптер_ОбработчикиСобытийСтандартный;
	адаптер_ОбработчикиСобытийСтандартный = ОбщегоНазначения.ОбщийМодуль("адаптер_ОбработчикиСобытийСтандартный");
	
	ДанныеОбъекта = адаптер_ОбработчикиСобытийСтандартный.ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения);
	Если Не ДанныеОбъекта = Неопределено 
		И ДанныеОбъекта.Реквизиты.Количество() > 0 Тогда
		РеквизитыОбъекта = ДанныеОбъекта.Реквизиты[0];
		Если (Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВКладовую
					Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеПолуфабрикатов) Тогда
			
			Если ЗначениеЗаполнено(Объект.Получатель.Подразделение) Тогда
				РеквизитыОбъекта.Вставить("бг_ПодразделениеИдентификатор", Строка(Объект.Получатель.Подразделение.УникальныйИдентификатор()));
				РеквизитыОбъекта.Вставить("бг_ПодразделениеКод", Строка(Объект.Получатель.Подразделение.Код));
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Объект.Отправитель.Подразделение) Тогда
				РеквизитыОбъекта.Вставить("бг_ПодразделениеОтправительИдентификатор", Строка(Объект.Отправитель.Подразделение.УникальныйИдентификатор()));
				РеквизитыОбъекта.Вставить("бг_ПодразделениеОтправительКод", Строка(Объект.Отправитель.Подразделение.Код));
			КонецЕсли;
		ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВПроизводство 
			И ЗначениеЗаполнено(Объект.Получатель) Тогда
			РеквизитыОбъекта.Вставить("бг_ПодразделениеИдентификатор", Строка(Объект.Получатель.УникальныйИдентификатор()));
			РеквизитыОбъекта.Вставить("бг_ПодразделениеКод", Строка(Объект.Получатель.Код));
		ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзКладовой Тогда
			ПодразделениеОтправитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Отправитель, "Подразделение");
			
			Если ЗначениеЗаполнено(ПодразделениеОтправитель) Тогда
				РеквизитыОбъекта.Вставить("бг_ПодразделениеИдентификатор", Строка(ПодразделениеОтправитель.УникальныйИдентификатор()));
				РеквизитыОбъекта.Вставить("бг_ПодразделениеКод", Строка(ПодразделениеОтправитель.Код));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат ДанныеОбъекта;
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция ВыгружаемыеРеквизиты()
	Результат = "Дата,Номер,Проведен,ПометкаУдаления,Организация,ОрганизацияПолучатель,Отправитель,Получатель,Распоряжение,
				|Ответственный,Комментарий,ХозяйственнаяОперация,Статус,ДокументОснования,Валюта,
				|Товары.Номенклатура,Товары.Характеристика,Товары.Серия,Товары.Упаковка,Товары.Количество,Товары.КоличествоУпаковок, 
				|Товары.Распоряжение,Товары.Цена,Товары.Сумма";
	Возврат СтрЗаменить(Результат, Символы.ПС, "");
КонецФункции

#КонецОбласти 
