
#Область ПрограммныйИнтерфейс

&После("ОтменитьРучныеСкидки")
Процедура бг_ОтменитьРучныеСкидки(Объект, ИмяТЧ, ИспользуютсяАвтоматическиеСкидки, РассчитыватьСуммуСНДС, РассчитыватьСуммуВзаиморасчетов, РеализацияСверхЗаказа, ДополнительныеПараметры)
	бг_РасчетСкидок.ЗаполнитьИнформационныеПоляСкидок(Объект, ИмяТЧ);
КонецПроцедуры

&После("НазначитьРучнуюСкидку")
Процедура бг_НазначитьРучнуюСкидку(Объект, ИмяТЧ, СуммаСкидкиКРаспределению, ВходныеПараметры)
	бг_РасчетСкидок.ЗаполнитьИнформационныеПоляСкидок(Объект, ИмяТЧ);
КонецПроцедуры

&После("ОтменитьСкидки")
Процедура бг_ОтменитьСкидки(Объект, ИмяТЧ, РассчитыватьСуммуСНДС, РассчитыватьСуммуВзаиморасчетов, ОтменитьТолькоАвтоСкидки)
	бг_РасчетСкидок.ЗаполнитьИнформационныеПоляСкидок(Объект, ИмяТЧ);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&ИзменениеИКонтроль("ЗапросСкидкиНаценкиПоЗаказу")
Функция бг_ЗапросСкидкиНаценкиПоЗаказу(Объект)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КурсВалютыВзаиморасчетов.КурсЧислитель * КурсВалюты.КурсЗнаменатель / (КурсВалюты.КурсЧислитель * КурсВалютыВзаиморасчетов.КурсЗнаменатель) КАК КоэффициентПересчета
	|ПОМЕСТИТЬ ВалютаЗаказаВВалютуДокумента
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&Дата, Валюта = &Валюта И БазоваяВалюта = &БазоваяВалюта) КАК КурсВалюты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&Дата, Валюта = &ВалютаВзаиморасчетов И БазоваяВалюта = &БазоваяВалюта) КАК КурсВалютыВзаиморасчетов
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.ЗаказКлиента КАК ЗаказКлиента,
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(Товары.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
	|	ВЫРАЗИТЬ(Товары.КодСтроки КАК ЧИСЛО) КАК КодСтроки,
	|	ВЫРАЗИТЬ(Товары.КлючСвязи КАК ЧИСЛО) КАК КлючСвязи,
	|	ВЫРАЗИТЬ(Товары.Количество КАК ЧИСЛО) КАК Количество
	|ПОМЕСТИТЬ ТоварыДокумента
	|ИЗ
	|	&ТаблицаТоваров КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыЗаказа.КодСтроки КАК КодСтроки,
	|	ТоварыЗаказа.КлючСвязи КАК КлючСвязи,
	|	ТоварыРеализации.Количество / ТоварыЗаказа.Количество КАК Коэффициент,
	|	ТоварыЗаказа.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки,
	|	ТоварыЗаказа.ПроцентРучнойСкидки КАК ПроцентРучнойСкидки,
	|	ТоварыЗаказа.Ссылка КАК ЗаказКлиента,
	|	ТоварыРеализации.Количество КАК РеализацияКоличество,
	|	ТоварыЗаказа.Количество КАК ЗаказКоличество,
	|	ТоварыЗаказа.СуммаАвтоматическойСкидки КАК ЗаказСуммаАвтоматическойСкидки,
	|	ТоварыЗаказа.СуммаРучнойСкидки КАК ЗаказСуммаРучнойСкидки,
	|	ВалютаЗаказаВВалютуДокумента.КоэффициентПересчета КАК ВалютаКоэффициентПересчета,
	|	ТоварыЗаказа.СуммаАвтоматическойСкидки * (ТоварыРеализации.Количество / ТоварыЗаказа.Количество) * ВалютаЗаказаВВалютуДокумента.КоэффициентПересчета КАК СуммаАвтоматическойСкидки,
	|	ТоварыЗаказа.СуммаРучнойСкидки * (ТоварыРеализации.Количество / ТоварыЗаказа.Количество) * ВалютаЗаказаВВалютуДокумента.КоэффициентПересчета КАК СуммаРучнойСкидки
	|ПОМЕСТИТЬ ТоварыЗаказа
	|ИЗ
	|	ТоварыДокумента КАК ТоварыРеализации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК ТоварыЗаказа
	|		ПО ТоварыРеализации.КодСтроки = ТоварыЗаказа.КодСтроки
	|			И ТоварыРеализации.Номенклатура = ТоварыЗаказа.Номенклатура
	|			И ТоварыРеализации.Характеристика = ТоварыЗаказа.Характеристика
	|			И ТоварыРеализации.ЗаказКлиента = ТоварыЗаказа.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВалютаЗаказаВВалютуДокумента КАК ВалютаЗаказаВВалютуДокумента
	|		ПО (ИСТИНА)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыЗаказа.КодСтроки,
	|	ТоварыЗаказа.КлючСвязи,
	|	ТоварыРеализации.Количество / ТоварыЗаказа.Количество,
	|	ТоварыЗаказа.ПроцентАвтоматическойСкидки,
	|	ТоварыЗаказа.ПроцентРучнойСкидки,
	|	ТоварыЗаказа.Ссылка,
	|	ТоварыРеализации.Количество,
	|	ТоварыЗаказа.Количество,
	|	ТоварыЗаказа.СуммаАвтоматическойСкидки,
	|	ТоварыЗаказа.СуммаРучнойСкидки,
	|	ВалютаЗаказаВВалютуДокумента.КоэффициентПересчета,
	|	ТоварыЗаказа.СуммаАвтоматическойСкидки * (ТоварыРеализации.Количество / ТоварыЗаказа.Количество) * ВалютаЗаказаВВалютуДокумента.КоэффициентПересчета,
	|	ТоварыЗаказа.СуммаРучнойСкидки * (ТоварыРеализации.Количество / ТоварыЗаказа.Количество) * ВалютаЗаказаВВалютуДокумента.КоэффициентПересчета
	|ИЗ
	|	ТоварыДокумента КАК ТоварыРеализации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаВозвратТоваровОтКлиента.ЗаменяющиеТовары КАК ТоварыЗаказа
	|		ПО ТоварыРеализации.КодСтроки = ТоварыЗаказа.КодСтроки
	|			И ТоварыРеализации.Номенклатура = ТоварыЗаказа.Номенклатура
	|			И ТоварыРеализации.Характеристика = ТоварыЗаказа.Характеристика
	|			И ТоварыРеализации.ЗаказКлиента = ТоварыЗаказа.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВалютаЗаказаВВалютуДокумента КАК ВалютаЗаказаВВалютуДокумента
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыЗаказа.КодСтроки КАК КодСтроки,
	|	ТоварыЗаказа.КлючСвязи КАК КлючСвязи,
	|	ТоварыЗаказа.Коэффициент КАК Коэффициент,
	|	ТоварыЗаказа.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки,
	|	ТоварыЗаказа.ПроцентРучнойСкидки КАК ПроцентРучнойСкидки,
	|	ТоварыЗаказа.ЗаказКлиента КАК ЗаказКлиента,
	|	ТоварыЗаказа.РеализацияКоличество КАК РеализацияКоличество,
	|	ТоварыЗаказа.ЗаказКоличество КАК ЗаказКоличество,
	|	ТоварыЗаказа.ЗаказСуммаАвтоматическойСкидки КАК ЗаказСуммаАвтоматическойСкидки,
	|	ТоварыЗаказа.ЗаказСуммаРучнойСкидки КАК ЗаказСуммаРучнойСкидки,
	|	ТоварыЗаказа.ВалютаКоэффициентПересчета КАК ВалютаКоэффициентПересчета,
	|	ТоварыЗаказа.СуммаАвтоматическойСкидки КАК СуммаАвтоматическойСкидки,
	|	ТоварыЗаказа.СуммаРучнойСкидки КАК СуммаРучнойСкидки
	|ИЗ
	|	ТоварыЗаказа КАК ТоварыЗаказа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СкидкиНаценки.КлючСвязи КАК КлючСвязи,
	|	СкидкиНаценки.СкидкаНаценка КАК СкидкаНаценка,
	|	СкидкиНаценки.Сумма * ВалютаЗаказаВВалютуДокумента.КоэффициентПересчета КАК Сумма,
#Вставка
	|	СкидкиНаценки.бг_СуммаНачислено * ВалютаЗаказаВВалютуДокумента.КоэффициентПересчета КАК бг_СуммаНачислено,
#КонецВставки
	|	СкидкиНаценки.Ссылка КАК ЗаказКлиента
	|ИЗ
	|	ТоварыЗаказа КАК ТоварыЗаказа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.СкидкиНаценки КАК СкидкиНаценки
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВалютаЗаказаВВалютуДокумента КАК ВалютаЗаказаВВалютуДокумента
	|			ПО (ИСТИНА)
	|		ПО ТоварыЗаказа.ЗаказКлиента = СкидкиНаценки.Ссылка
	|			И ТоварыЗаказа.КлючСвязи = СкидкиНаценки.КлючСвязи
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СкидкиНаценки.КлючСвязи,
	|	СкидкиНаценки.СкидкаНаценка,
	|	СкидкиНаценки.Сумма * ВалютаЗаказаВВалютуДокумента.КоэффициентПересчета,
#Вставка
	|	NULL КАК бг_СуммаНачислено,
#КонецВставки
	|	СкидкиНаценки.Ссылка
	|ИЗ
	|	ТоварыЗаказа КАК ТоварыЗаказа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаВозвратТоваровОтКлиента.СкидкиНаценки КАК СкидкиНаценки
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВалютаЗаказаВВалютуДокумента КАК ВалютаЗаказаВВалютуДокумента
	|			ПО (ИСТИНА)
	|		ПО ТоварыЗаказа.ЗаказКлиента = СкидкиНаценки.Ссылка
	|			И ТоварыЗаказа.КлючСвязи = СкидкиНаценки.КлючСвязи");

	Запрос.УстановитьПараметр("Дата", ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("Валюта", Объект.Валюта);
	Запрос.УстановитьПараметр("ВалютаВзаиморасчетов", Объект.ВалютаВзаиморасчетов);
	Запрос.УстановитьПараметр("БазоваяВалюта", ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию());
	Запрос.УстановитьПараметр("ТаблицаТоваров", Объект.Товары.Выгрузить(, "ЗаказКлиента, Номенклатура, Характеристика, КодСтроки, Количество, КлючСвязи"));

	Возврат Запрос;

КонецФункции

#КонецОбласти
