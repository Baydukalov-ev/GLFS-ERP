
#Область ПрограммныйИнтерфейс

#Область РасчетСтатусаОформления

&ИзменениеИКонтроль("ИменаДокументовДляДокументаОснования")
Функция бг_ИменаДокументовДляДокументаОснования(ДокументОснование, ТипОснований)
	
#Вставка
	ТипыОснований = ТипОснований.Типы();
		
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ВнутреннееПотреблениеТоваров")
		И ТипыОснований.Найти(Тип("ДокументСсылка.ПрочееОприходованиеТоваров")) <> Неопределено Тогда
		
		// Типовой алгоритм по определяемому типу ДокументыЕГАИСПоддерживающиеСтатусыОформления
		// некорректно определяет для документа основания "Внутренне потребление" тип основания
		// "Прочее оприходование товаров", хотя они оба являются основаниями для документов ЕГАИС,
		// но типовой алгоритм ошибочно полагает их основаниями друг для друга.
		ТипыОснований.Удалить(ТипыОснований.Найти(Тип("ДокументСсылка.ПрочееОприходованиеТоваров")));
	КонецЕсли;
#КонецВставки
	
	ИменаДокументов = Новый Массив;
	
#Удаление
	Для Каждого Тип Из ТипОснований.Типы() Цикл
#КонецУдаления
#Вставка
	Для Каждого Тип Из ТипыОснований Цикл
#КонецВставки
		
		МетаданныеПоТипу = Метаданные.НайтиПоТипу(Тип);
		ТипыОснования    = ИнтеграцияИС.РеквизитДокументОснованиеДокументаИС(МетаданныеПоТипу).Тип;
		
		Если ТипыОснования.СодержитТип(ТипЗнч(ДокументОснование)) Тогда
			ИменаДокументов.Добавить(МетаданныеПоТипу.Имя);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ИменаДокументов;
	
КонецФункции

#КонецОбласти

#КонецОбласти
