
#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	адаптер_НастройкиОбмена.ОставитьРеквизиты(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		ВыгружаемыеРеквизиты());
		
	ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства);
	
КонецПроцедуры

Функция ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения) Экспорт
	
	Перем адаптер_обработчикиСобытийСтандартный;
	адаптер_обработчикиСобытийСтандартный = ОбщегоНазначения.ОбщийМодуль("адаптер_обработчикиСобытийСтандартный");
	
	ДанныеОбъекта = адаптер_обработчикиСобытийСтандартный.ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения);
	
	РеквизитыОбъекта = ДанныеОбъекта.Реквизиты[0];
	
	СтатусЕГАИС = РегистрыСведений.СтатусыДокументовЕГАИС.ТекущееСостояние(Объект).Статус;
	РеквизитыОбъекта.Вставить(
		"СтатусЕГАИС",
		бг_ОбщегоНазначенияСервер.ЗначениеПеречислениеВСтруктуру(СтатусЕГАИС));
	
	Марки = Марки(Объект);
	Если Марки <> Неопределено Тогда
		РеквизитыОбъекта.Вставить("Марки", Марки);
	КонецЕсли;

	Возврат ДанныеОбъекта;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВыгружаемыеРеквизиты()
	
	ВыгружаемыеРеквизиты = Новый Массив;
	
	// Шапка документа
	ВыгружаемыеРеквизиты.Добавить("Дата");
	ВыгружаемыеРеквизиты.Добавить("Номер");
	ВыгружаемыеРеквизиты.Добавить("ПометкаУдаления");
	ВыгружаемыеРеквизиты.Добавить("Проведен");
	ВыгружаемыеРеквизиты.Добавить("ВидДокумента");
	ВыгружаемыеРеквизиты.Добавить("ОрганизацияЕГАИС");
	ВыгружаемыеРеквизиты.Добавить("ИдентификаторЕГАИС");
	ВыгружаемыеРеквизиты.Добавить("ПричинаПостановкиНаБаланс");
	ВыгружаемыеРеквизиты.Добавить("ДатаРегистрацииДвижений");
	ВыгружаемыеРеквизиты.Добавить("ДокументОснование");
	ВыгружаемыеРеквизиты.Добавить("Ответственный");
	ВыгружаемыеРеквизиты.Добавить("Комментарий");
	ВыгружаемыеРеквизиты.Добавить("ДанныеПроверкиИПодбора");
	ВыгружаемыеРеквизиты.Добавить("СтатусПроверкиИПодбора");
	
	// Табличная часть "Товары"
	ВыгружаемыеРеквизиты.Добавить("Товары.АлкогольнаяПродукция");
	ВыгружаемыеРеквизиты.Добавить("Товары.Количество");
	ВыгружаемыеРеквизиты.Добавить("Товары.Номенклатура");
	ВыгружаемыеРеквизиты.Добавить("Товары.Характеристика");
	ВыгружаемыеРеквизиты.Добавить("Товары.Серия");
	ВыгружаемыеРеквизиты.Добавить("Товары.Упаковка");
	ВыгружаемыеРеквизиты.Добавить("Товары.КоличествоУпаковок");
	ВыгружаемыеРеквизиты.Добавить("Товары.КоличествоПоСправке1");
	ВыгружаемыеРеквизиты.Добавить("Товары.НомерТТН");
	ВыгружаемыеРеквизиты.Добавить("Товары.ДатаТТН");
	ВыгружаемыеРеквизиты.Добавить("Товары.ДатаРозлива");
	ВыгружаемыеРеквизиты.Добавить("Товары.НомерПодтвержденияЕГАИС");
	ВыгружаемыеРеквизиты.Добавить("Товары.ДатаПодтвержденияЕГАИС");
	ВыгружаемыеРеквизиты.Добавить("Товары.Справка2");
	ВыгружаемыеРеквизиты.Добавить("Товары.ИдентификаторСтроки");
	ВыгружаемыеРеквизиты.Добавить("Товары.СтатусУказанияСерий");
	
	Возврат  СтрСоединить(ВыгружаемыеРеквизиты, ",");
	
КонецФункции

Процедура ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства)

	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	// Реквизиты шапки
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"СтатусЕГАИС",
		Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС"));
		
	ДлиныШтрихкодовМарок = бг_МаркируемаяПродукция.ДлиныШтрихкодовМарок();
	
	// Таблица Марки
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"Марки.ИдентификаторМарки",
		ОбщегоНазначения.ОписаниеТипаСтрока(ДлиныШтрихкодовМарок.ПолнаяМарка));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"Марки.КодУпаковки",
		ОбщегоНазначения.ОписаниеТипаСтрока(ДлиныШтрихкодовМарок.Упаковка));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"Марки.НомерСтрокиАкта",
		ОбщегоНазначения.ОписаниеТипаЧисло(5));

КонецПроцедуры

Функция Марки(АктПостановкиНаБалансЕГАИС)
	
	ДанныеПриходуемыхШтрихкодов = Документы.АктПостановкиНаБалансЕГАИС.бг_ДанныеПриходуемыхШтрихкодов(
		АктПостановкиНаБалансЕГАИС);
		
	Если ДанныеПриходуемыхШтрихкодов = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ДанныеПриходуемыхШтрихкодов.Марки.Скопировать(, "ИдентификаторМарки, КодУпаковки, НомерСтрокиАкта");
	
КонецФункции

#КонецОбласти
