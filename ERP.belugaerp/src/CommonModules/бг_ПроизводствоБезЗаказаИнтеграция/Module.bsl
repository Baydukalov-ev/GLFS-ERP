#Область ПрограммныИнтерфейс

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	адаптер_НастройкиОбмена.ОставитьРеквизиты(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		ВыгружаемыеРеквизиты());
	
	ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства);
	
КонецПроцедуры

Функция ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения) Экспорт
	
	Перем адаптер_ОбработчикиСобытийСтандартный, адаптер_РаботаСДаннымиИБ;
	адаптер_ОбработчикиСобытийСтандартный = ОбщегоНазначения.ОбщийМодуль("адаптер_ОбработчикиСобытийСтандартный");
	адаптер_РаботаСДаннымиИБ = ОбщегоНазначения.ОбщийМодуль("адаптер_РаботаСДаннымиИБ");
	
	ДанныеОбъекта = адаптер_ОбработчикиСобытийСтандартный.ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения);
	
	Если ДанныеОбъекта <> Неопределено 
		И ДанныеОбъекта.Реквизиты.Количество() > 0 Тогда
		
		Организация       = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект, "Организация");
		ОрганизацияСклада = ОрганизацияСклада(Объект);
		
		РеквизитыОбъекта = ДанныеОбъекта.Реквизиты[0];
		
		РеквизитыОбъекта.Вставить("ВыпускВЛабораторию", Организация = Справочники.Организации.УправленческаяОрганизация);
		Если РеквизитыОбъекта.Свойство("Организация")
			И ОрганизацияСклада <> Неопределено Тогда
			РеквизитыОбъекта.Организация.Идентификатор = адаптер_РаботаСДаннымиИБ.ПолучитьУникальныйИдентификатор(ОрганизацияСклада);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеОбъекта;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства)
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	// Реквизиты шапки
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		,
		"ВыпускВЛабораторию",
		Новый ОписаниеТипов("Булево"));
	
КонецПроцедуры

Функция ОрганизацияСклада(ДокументСсылка)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 2
	|	ВЫРАЗИТЬ(ПроизводствоБезЗаказаВыходныеИзделия.Получатель КАК Справочник.Склады).бг_Организация КАК Организация
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ПроизводствоБезЗаказаВыходныеИзделия
	|ГДЕ
	|	ПроизводствоБезЗаказаВыходныеИзделия.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Количество() = 1
		И Выборка.Следующий()
		И ЗначениеЗаполнено(Выборка.Организация) Тогда
		Возврат Выборка.Организация;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

Функция ВыгружаемыеРеквизиты()

	ВыгружаемыеРеквизиты = Новый Массив;
	
	// Реквизиты шапки
	ВыгружаемыеРеквизиты.Добавить("Дата");
	ВыгружаемыеРеквизиты.Добавить("Номер");
	ВыгружаемыеРеквизиты.Добавить("Проведен");
	ВыгружаемыеРеквизиты.Добавить("ПометкаУдаления");
	ВыгружаемыеРеквизиты.Добавить("Организация");
	ВыгружаемыеРеквизиты.Добавить("ГруппировкаЗатрат");
	ВыгружаемыеРеквизиты.Добавить("Подразделение");
	ВыгружаемыеРеквизиты.Добавить("Комментарий");
	
	// Реквизиты ТЧ "ВыходныеИзделия"
	ВыгружаемыеРеквизиты.Добавить("ВыходныеИзделия.НомерГруппыЗатрат");
	ВыгружаемыеРеквизиты.Добавить("ВыходныеИзделия.Номенклатура");
	ВыгружаемыеРеквизиты.Добавить("ВыходныеИзделия.Количество");
	ВыгружаемыеРеквизиты.Добавить("ВыходныеИзделия.Спецификация");
	ВыгружаемыеРеквизиты.Добавить("ВыходныеИзделия.Серия");
	ВыгружаемыеРеквизиты.Добавить("ВыходныеИзделия.Получатель");
	ВыгружаемыеРеквизиты.Добавить("ВыходныеИзделия.ДоляСтоимости");
	
	// Реквизиты ТЧ "МатериалыИРаботы"
	ВыгружаемыеРеквизиты.Добавить("МатериалыИРаботы.НомерГруппыЗатрат");
	ВыгружаемыеРеквизиты.Добавить("МатериалыИРаботы.Номенклатура");
	ВыгружаемыеРеквизиты.Добавить("МатериалыИРаботы.Характеристика");
	ВыгружаемыеРеквизиты.Добавить("МатериалыИРаботы.Серия");
	ВыгружаемыеРеквизиты.Добавить("МатериалыИРаботы.Количество");
	
	ВыгружаемыеРеквизиты = СтрСоединить(ВыгружаемыеРеквизиты, ",");
	
	Возврат ВыгружаемыеРеквизиты;

КонецФункции

#КонецОбласти
