#Область ПрограммныйИнтерфейс

Функция ТипУпаковкиШтрихкода(Штрихкод, ДлиныШтрихкодовМарок = Неопределено, ЗначенияТиповУпаковок = Неопределено) Экспорт

	Если ДлиныШтрихкодовМарок = Неопределено Тогда
		ДлиныШтрихкодовМарок = бг_МаркируемаяПродукцияВызовСервераПовтИсп.ДлиныШтрихкодовМарок();
	КонецЕсли;
	
	Если ЗначенияТиповУпаковок = Неопределено Тогда
		ЗначенияТиповУпаковок = бг_МаркируемаяПродукцияВызовСервераПовтИсп.ЗначенияТиповУпаковок();
	КонецЕсли;
	
	ДлинаШтрихкода = СтрДлина(Штрихкод);
	
	Если ДлинаШтрихкода = ДлиныШтрихкодовМарок.ПолнаяМарка
		Или ДлинаШтрихкода = ДлиныШтрихкодовМарок.СтараяМарка Тогда
		
		Возврат ЗначенияТиповУпаковок.Бутылка;
		
	ИначеЕсли ДлинаШтрихкода = ДлиныШтрихкодовМарок.СтараяУпаковка Тогда
		
		Возврат ЗначенияТиповУпаковок.Коробка;
		
	ИначеЕсли ДлинаШтрихкода = ДлиныШтрихкодовМарок.Упаковка Тогда
		
		ТринадцатыйСимволКодаУпаковки = Сред(Штрихкод, 13, 1);
		
		Если ТринадцатыйСимволКодаУпаковки = "0"
			Или ТринадцатыйСимволКодаУпаковки = "1"
			Или ТринадцатыйСимволКодаУпаковки = "3" Тогда
			
			Возврат ЗначенияТиповУпаковок.Коробка;	
			
		ИначеЕсли ТринадцатыйСимволКодаУпаковки = "2" Или ТринадцатыйСимволКодаУпаковки = "4" Тогда
			
			Возврат ЗначенияТиповУпаковок.Паллета;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;

КонецФункции

Функция КартинкаПоТипуУпаковкиШтрихкода(ТипУпаковкиШтрихкода, ЗначенияТиповУпаковок = Неопределено, КартинкиТиповУпаковок = Неопределено) Экспорт

	Если ЗначенияТиповУпаковок = Неопределено Тогда
		ЗначенияТиповУпаковок = бг_МаркируемаяПродукцияВызовСервераПовтИсп.ЗначенияТиповУпаковок();
	КонецЕсли;
	
	Если КартинкиТиповУпаковок = Неопределено Тогда
		КартинкиТиповУпаковок = бг_МаркируемаяПродукцияВызовСервераПовтИсп.КартинкиТиповУпаковок();
	КонецЕсли;
	
	Если ТипУпаковкиШтрихкода = ЗначенияТиповУпаковок.Бутылка Тогда
		Возврат КартинкиТиповУпаковок.Бутылка;
	ИначеЕсли ТипУпаковкиШтрихкода = ЗначенияТиповУпаковок.Коробка Тогда
		Возврат КартинкиТиповУпаковок.Коробка;
	ИначеЕсли ТипУпаковкиШтрихкода = ЗначенияТиповУпаковок.Паллета Тогда
		Возврат КартинкиТиповУпаковок.Паллета;
	Иначе
		Возврат КартинкиТиповУпаковок.Пустая;
	КонецЕсли;
		
КонецФункции

Процедура ЗаполнитьДанныеТипаУпаковкиВСтрокеТаблицы(СтрокаТаблицы, Контекст) Экспорт

	Если Не ЗначениеЗаполнено(СтрокаТаблицы[Контекст.ИменаПолей.Штрихкод]) Тогда
		
		СтрокаТаблицы[Контекст.ИменаПолей.ТипУпаковки] = Контекст.ЗначенияТиповУпаковок.Пустая;
		СтрокаТаблицы[Контекст.ИменаПолей.Картинка] = Контекст.КартинкиТиповУпаковок.Пустая;
		
		Возврат;
	КонецЕсли;
	
	СтрокаТаблицы[Контекст.ИменаПолей.ТипУпаковки] = бг_МаркируемаяПродукцияКлиентСервер.ТипУпаковкиШтрихкода(
		СтрокаТаблицы[Контекст.ИменаПолей.Штрихкод],
		Контекст.ДлиныШтрихкодовМарок,
		Контекст.ЗначенияТиповУпаковок);
		
	Если Не ЗначениеЗаполнено(СтрокаТаблицы[Контекст.ИменаПолей.ТипУпаковки]) Тогда
		
		СтрокаТаблицы[Контекст.ИменаПолей.Картинка] = Контекст.КартинкиТиповУпаковок.Пустая;
		
		Возврат;
	КонецЕсли;
	
	СтрокаТаблицы[Контекст.ИменаПолей.Картинка] = бг_МаркируемаяПродукцияКлиентСервер.КартинкаПоТипуУпаковкиШтрихкода(
		СтрокаТаблицы[Контекст.ИменаПолей.ТипУпаковки],
		Контекст.ЗначенияТиповУпаковок,
		Контекст.КартинкиТиповУпаковок);

КонецПроцедуры

Функция КонтекстЗаполненияДанныхТиповУпаковок(Форма, ИмяПоляШтрихкод, ИмяПоляТипУпаковки, ИмяПоляКартинка) Экспорт

	КонтекстЗаполненияДанныхТиповУпаковок = Новый Структура;
	
	КонтекстЗаполненияДанныхТиповУпаковок.Вставить("ЗначенияТиповУпаковок", Форма.ЗначенияТиповУпаковок);
	КонтекстЗаполненияДанныхТиповУпаковок.Вставить("КартинкиТиповУпаковок", Форма.КартинкиТиповУпаковок);
	КонтекстЗаполненияДанныхТиповУпаковок.Вставить("ДлиныШтрихкодовМарок", Форма.ДлиныШтрихкодовМарок);
	
	КонтекстЗаполненияДанныхТиповУпаковок.Вставить(
		"ИменаПолей",
		Новый Структура("Штрихкод, ТипУпаковки, Картинка",
			ИмяПоляШтрихкод,
			ИмяПоляТипУпаковки,
			ИмяПоляКартинка));
	
	Возврат КонтекстЗаполненияДанныхТиповУпаковок;

КонецФункции

#КонецОбласти
