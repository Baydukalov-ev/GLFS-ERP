Процедура ЗаполнитьТекстыЗапросовУсловиями(ТекстЗапроса, ТекстЗапросаТаблицаКлючей, ПараметрыЗапроса, НастройкаВыгрузки, Объект, СтандартнаяОбработка) Экспорт
	ТекстПоиска =
	"ВыгружаемыйОбъект.Продукция.Номенклатура.Код КАК Номенклатура_ЗначениеРеквизитаКод";
	
	ТекстЗамены = "ВЫБОР
			|			КОГДА ВыгружаемыйОбъект.Продукция.бг_УпаковкаПаллета = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
			|					ИЛИ ВыгружаемыйОбъект.Продукция.бг_УпаковкаПаллета.бг_КодЕК_Номенклатуры = """"
			|				ТОГДА ВыгружаемыйОбъект.Продукция.Номенклатура.Код
			|			ИНАЧЕ ВыгружаемыйОбъект.Продукция.бг_УпаковкаПаллета.бг_КодЕК_Номенклатуры
			|		КОНЕЦ КАК Номенклатура_ЗначениеРеквизитаКод";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстПоиска, ТекстЗамены);
КонецПроцедуры

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	ИменаРеквизитов = "Дата,Номер,Проведен,ПометкаУдаления,Организация,ДокументОснование,Подразделение,Статус,Комментарий,НачатьНеРанее,ДатаПотребности,
				|Продукция.Номенклатура,Продукция.бг_УпаковкаПаллета,Продукция.Количество,Продукция.Спецификация,Продукция.ДоляСтоимости,Продукция.Склад,Продукция.Отменено";
	ИменаРеквизитов = СтрЗаменить(ИменаРеквизитов, Символы.ПС, "");
	адаптер_НастройкиОбмена.ОставитьРеквизиты(РеквизитыИСвойства, РеквизитыИСвойства.МетаданныеОбъекта, ИменаРеквизитов);
	
КонецПроцедуры

Процедура ЗаполнитьПаллетыПоДаннымЗаказаКлиента(ДокументСсылка, ТаблицаТоваров) Экспорт
	Если ТипЗнч(ДокументСсылка) <> Тип("ДокументСсылка.ЗаказКлиента")
		Или ТаблицаТоваров.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(ЗаказКлиентаТовары.бг_УпаковкаПаллета) КАК Паллета
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|ГДЕ
	|	ЗаказКлиентаТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказКлиентаТовары.Номенклатура
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказКлиентаТовары.бг_УпаковкаПаллета) = 1 И
	|	МАКСИМУМ(ЗаказКлиентаТовары.бг_УпаковкаПаллета) <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	ТаблицаТоваров.Колонки.Добавить("бг_УпаковкаПаллета", Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения"));
	
	ОтборТовары = Новый Структура("Номенклатура");
	Пока Выборка.Следующий() Цикл
		ОтборТовары.Номенклатура = Выборка.Номенклатура;
		СтрокиТовары = ТаблицаТоваров.НайтиСтроки(ОтборТовары);
		
		Для Каждого СтрокаТовар Из СтрокиТовары Цикл
			СтрокаТовар.бг_УпаковкаПаллета = Выборка.Паллета;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры
