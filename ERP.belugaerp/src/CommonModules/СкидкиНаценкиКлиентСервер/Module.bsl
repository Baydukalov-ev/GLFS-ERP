
#Область ПрограммныйИнтерфейс

Функция бг_ИмяДинамическогоСписка(ИмяКоманды, ПрефиксКоманды, СуффиксКоманды) Экспорт
	
	Если СтрНачинаетсяС(ИмяКоманды, ПрефиксКоманды)
		И СтрЗаканчиваетсяНа(ИмяКоманды, СуффиксКоманды) Тогда
		
		Возврат Сред(ИмяКоманды, СтрДлина(ПрефиксКоманды) + 1, СтрДлина(ИмяКоманды) - СтрДлина(СуффиксКоманды) - СтрДлина(ПрефиксКоманды));
		
	Иначе
		
		Возврат "";
		
	КонецЕсли;
	
КонецФункции

Процедура бг_УстановитьПараметрыДинамическихСписковИсточников(Форма, ИменаСписков = "", СкидкаНаценка = Неопределено, ИменаПараметров = "") Экспорт
	
	МассивИменСписков    = бг_МассивИмен(ИменаСписков, Форма.бг_ИменаДоступныхИсточников);
	МассивИменПараметров = бг_МассивИмен(ИменаПараметров, "ТекущаяДата,Статус,СкидкаНаценка");
	
	Если СкидкаНаценка = Неопределено Тогда
		ЗначениеПараметра = ПредопределенноеЗначение("Справочник.СкидкиНаценки.ПустаяСсылка");
	Иначе
		ЗначениеПараметра = СкидкаНаценка;
	КонецЕсли;
	
	Для Каждого ИмяБлока Из МассивИменСписков Цикл
		
		ДинамическийСписок = Форма["бг_Использование" + ИмяБлока];
		
		Если МассивИменПараметров.Найти("ТекущаяДата") <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
				ДинамическийСписок,
				"ТекущаяДата",
				Форма.ДатаСреза,
				Истина
			);
		КонецЕсли;
		
		Если МассивИменПараметров.Найти("СкидкаНаценка") <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
				ДинамическийСписок,
				"СкидкаНаценка",
				ЗначениеПараметра,
				Истина
			);
		КонецЕсли;
		
		Если МассивИменПараметров.Найти("Статус") <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				ДинамическийСписок,
				"Статус",
				ПредопределенноеЗначение("Перечисление.СтатусыДействияСкидок.Действует"),
				ВидСравненияКомпоновкиДанных.Равно,
				,
				Форма[СтрШаблон("бг_Использование%1ВариантОтображенияСкидокНаценок", ИмяБлока)] <> "Все"
			);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура бг_ОбновитьДинамическиеСписки(Форма, ИменаСписков = "") Экспорт
	
	МассивИменСписков = бг_МассивИмен(ИменаСписков, Форма.бг_ИменаДоступныхИсточников);
	
	Для каждого ИмяСписка из МассивИменСписков Цикл
		
		Форма.Элементы["бг_Использование" + ИмяСписка].Обновить();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция бг_МассивИмен(Имена, ИменаПоУмолчанию)
	Если ПустаяСтрока(Имена) Тогда
		Возврат СтрРазделить(ИменаПоУмолчанию, ",");
	Иначе
		Возврат СтрРазделить(Имена, ",
		| 	", Ложь);
	КонецЕсли;
КонецФункции

#КонецОбласти
