
#Область СлужебныеПроцедурыИФункции

#Область ЭтапыПроведения

&ИзменениеИКонтроль("ТаблицыДляДвижений")
Функция бг_ТаблицыДляДвижений(Документ, МенеджерДокумента, ДвижимыеРегистры, ДопПараметры)
	
	Если ЗначениеЗаполнено(ДвижимыеРегистры) Тогда
		
		Если ДопПараметры = Неопределено Тогда
			ДопПараметры = ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
		КонецЕсли;
#Вставка
		Если бг_ТребуетсяЗаполнятьДопПараметрыПроведения(Документ) Тогда
			бг_ЗаполнитьДопПараметрыПроведения(ДопПараметры, Документ);
		КонецЕсли;
#КонецВставки		
		ТаблицыДляДвижений = МенеджерДокумента.ДанныеДокументаДляПроведения(Документ.Ссылка,
																			ДвижимыеРегистры,
																			ДопПараметры);
		
		Если Документ.ДополнительныеСвойства.ПроведениеДокументов.Свойство("ДополнительныеТаблицыДанныхДокумента") Тогда
			Для Каждого Таблица Из Документ.ДополнительныеСвойства.ПроведениеДокументов.ДополнительныеТаблицыДанныхДокумента Цикл
				Если ТаблицыДляДвижений.Свойство(Таблица.Ключ) Тогда
					ВызватьИсключение СтрШаблон(НСтр("ru = 'Пересечение имен таблиц данных документа: ""%1"".';
													|en = 'Intersection of the document data table names: ""%1"".'"), Таблица.Ключ);
				Иначе
					ТаблицыДляДвижений.Вставить(Таблица.Ключ, Таблица.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Возврат ТаблицыДляДвижений;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

Функция бг_ТребуетсяЗаполнятьДопПараметрыПроведения(ДокументОбъект)

	ТребуетсяЗаполнитьДопПараметры = Ложь;
	
	ТипДокумента = ТипЗнч(ДокументОбъект);
	
	Если ТипДокумента = Тип("ДокументОбъект.Сторно") И ДокументОбъект.Проведен Тогда
		ТребуетсяЗаполнитьДопПараметры = Истина;
	ИначеЕсли ТипДокумента = Тип("ДокументОбъект.ПриходныйОрдерНаТовары") Тогда
		ТребуетсяЗаполнитьДопПараметры = Истина;
	КонецЕсли;
	
	Возврат ТребуетсяЗаполнитьДопПараметры;
	
КонецФункции

Процедура бг_ЗаполнитьДопПараметрыПроведения(ДопПараметры, ДокументОбъект)
	
	ТипДокумента = ТипЗнч(ДокументОбъект);
	
	Если ТипДокумента = Тип("ДокументОбъект.Сторно") Тогда
		Документы.Сторно.бг_ЗаполнитьДопПараметрыПроведения(ДопПараметры, ДокументОбъект);
	ИначеЕсли ТипДокумента = Тип("ДокументОбъект.ПриходныйОрдерНаТовары")
		И ДокументОбъект.ДополнительныеСвойства.Свойство("бг_ТребуетсяОтложеннаяОбработка")
		И ДокументОбъект.ДополнительныеСвойства.бг_ТребуетсяОтложеннаяОбработка Тогда
		ДопПараметры.Вставить("бг_ТребуетсяОтложеннаяОбработка", Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

