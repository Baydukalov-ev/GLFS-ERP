
#Область ПрограммныйИнтерфейс

#Область Проведение

&После("ЗарегистрироватьУчетныеМеханизмы")
Процедура бг_ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента)
	
	МеханизмыДокумента.Добавить("бг_СкидкиНаценки");
	МеханизмыДокумента.Добавить("бг_РезервыТоваров");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
&После("ДополнитьТекстыЗапросовПроведения")
Процедура бг_ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры)
	
	бг_ДобавитьДвиженияНакопительныхСкидок(Запрос, ТекстыЗапроса, Регистры);
	бг_ТекстЗапросаТаблицаСуммовыеСкидиГлобальные(Запрос, ТекстыЗапроса, Регистры);
	бг_ДобавитьДвиженияРезервыТоваров(Запрос, ТекстыЗапроса, Регистры);
	
КонецПроцедуры

Процедура бг_ДобавитьДвиженияНакопительныхСкидок(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра      = Метаданные.РегистрыНакопления.бг_СуммовыеСкидкиЛокальные.Имя;
	МенеджерРегистра = РегистрыНакопления[ИмяРегистра];
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Запрос.Параметры.Свойство("бг_ПунктНазначения") Тогда
		ПунктНазначения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запрос.Параметры.Ссылка, "бг_ПунктНазначения");
		Запрос.УстановитьПараметр("ПунктНазначения", ПунктНазначения);
	КонецЕсли;
	Запрос.УстановитьПараметр("СуммоваяСкидкаCRM", бг_КонстантыПовтИсп.ЗначениеКонстанты("СуммоваяСкидкаCRM"));
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&Контрагент КАК Контрагент,
	|	&ПунктНазначения КАК ПунктНазначения,
	|	С.СкидкаНаценка КАК СкидкаНаценка,
	|	СУММА(С.бг_СуммаНачислено) КАК Начислено
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.СкидкиНаценки КАК С
	|		ПО Т.Ссылка = С.Ссылка
	|			И Т.КлючСвязи = С.КлючСвязи
	|			И (Т.Ссылка = &Ссылка)
	|			И (С.СкидкаНаценка.бг_Накопительная)
	|			И (С.СкидкаНаценка <> &СуммоваяСкидкаCRM)
	|			И (НЕ Т.Отменено)
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.ДатаОтгрузки,
	|	С.СкидкаНаценка
	|
	|ИМЕЮЩИЕ
	|	СУММА(С.бг_СуммаНачислено) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Организация,
	|	&Контрагент,
	|	&ПунктНазначения,
	|	С.СкидкаНаценка,
	|	СУММА(С.бг_СуммаСписанияНакопительнойСкидки)
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК Т
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.СкидкиНаценки КАК С
	|		ПО Т.Ссылка = С.Ссылка
	|			И Т.КлючСвязи = С.КлючСвязи
	|			И (Т.Ссылка = &Ссылка)
	|			И (С.СкидкаНаценка <> &СуммоваяСкидкаCRM)
	|			И (НЕ Т.Отменено)
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.ДатаОтгрузки,
	|	С.СкидкаНаценка
	|
	|ИМЕЮЩИЕ
	|	СУММА(С.бг_СуммаСписанияНакопительнойСкидки) <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура бг_ТекстЗапросаТаблицаСуммовыеСкидиГлобальные(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "бг_СуммовыеСкидки";
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	Запрос.УстановитьПараметр("СуммоваяСкидкаCRM", бг_КонстантыПовтИсп.ЗначениеКонстанты("СуммоваяСкидкаCRM"));
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗаказКлиентаСкидкиНаценки.Ссылка.Контрагент КАК Контрагент,
	|	ЗаказКлиентаСкидкиНаценки.Ссылка КАК ДокументРезерва,
	|	0 КАК СуммаСкидки,
	|	СУММА(ЗаказКлиентаСкидкиНаценки.Сумма) КАК СуммаРезерва
	|ИЗ
	|	Документ.ЗаказКлиента.СкидкиНаценки КАК ЗаказКлиентаСкидкиНаценки
	|ГДЕ
	|	ЗаказКлиентаСкидкиНаценки.Ссылка = &Ссылка
	|	И ЗаказКлиентаСкидкиНаценки.СкидкаНаценка = &СуммоваяСкидкаCRM
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказКлиентаСкидкиНаценки.Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
КонецПроцедуры

Процедура бг_ДобавитьДвиженияРезервыТоваров(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра      = Метаданные.РегистрыНакопления.бг_РезервыТоваров.Имя;
	МенеджерРегистра = РегистрыНакопления[ИмяРегистра];
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	//создание резерва на складе из свободного остатка
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ДД.Ссылка КАК ДолгосрочныйРезерв,
	|	НЕОПРЕДЕЛЕНО КАК ЗаказКлиента,
	|	НЕОПРЕДЕЛЕНО КАК ЗаказПоставщику,
	|	ДД.Склад КАК Склад,
	|	Строки.Номенклатура КАК Номенклатура,
	|	Строки.бг_УпаковкаПаллета КАК УпаковкаПаллета,
	|	Строки.Количество КАК НаСкладе,
	|	0 КАК УПоставщика,
	|	0 КАК Поступило,
	|	0 КАК Выбыло,
	|	0 КАК Отменено
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК Строки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДД
	|		ПО Строки.Ссылка = ДД.Ссылка
	|ГДЕ
	|	ДД.Ссылка = &Ссылка
	|	И ДД.бг_ДолгосрочныйРезерв
	|	И Строки.бг_ЗаказПоставщикуИсточникОбеспечения = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	И Строки.ВариантОбеспечения В (ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада), ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	//создание резерва в пути
	|ВЫБРАТЬ
	|	&Период,
	|	Строки.Ссылка,
	|	НЕОПРЕДЕЛЕНО,
	|	Строки.бг_ЗаказПоставщикуИсточникОбеспечения,
	|	НЕОПРЕДЕЛЕНО,
	|	Строки.Номенклатура,
	|	Строки.бг_УпаковкаПаллета,
	|	0 КАК НаСкладе,
	|	Строки.Количество КАК УПоставщика,
	|	0 КАК Поступило,
	|	0 КАК Выбыло,
	|	0 КАК Отменено
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК Строки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДД
	|		ПО Строки.Ссылка = ДД.Ссылка
	|ГДЕ
	|	ДД.Ссылка = &Ссылка
	|	И ДД.бг_ДолгосрочныйРезерв
	|	И Строки.бг_ЗаказПоставщикуИсточникОбеспечения <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	| 
	// отмена резерва на складе
	|ВЫБРАТЬ
	|	&Период,
	|	ДД.Ссылка,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ДД.Склад,
	|	Строки.Номенклатура,
	|	Строки.бг_УпаковкаПаллета,
	|	0 КАК НаСкладе,
	|	0 КАК УПоставщика,
	|	0 КАК Поступило,
	|	0 КАК Выбыло,
	|	Строки.Количество КАК Отменено
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК Строки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДД
	|		ПО Строки.Ссылка = ДД.Ссылка
	|ГДЕ
	|	ДД.Ссылка = &Ссылка
	|	И Строки.Отменено
	|	И ДД.бг_ДолгосрочныйРезерв
	|	И Строки.бг_ЗаказПоставщикуИсточникОбеспечения = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	И Строки.ВариантОбеспечения В (ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада), ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	| 
	// отмена резерва поступившего на склад
	|ВЫБРАТЬ
	|	&Период,
	|	ДД.Ссылка,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ДД.Склад,
	|	Строки.Номенклатура,
	|	Строки.бг_УпаковкаПаллета,
	|	0 КАК НаСкладе,
	|	0 КАК УПоставщика,
	|	0 КАК Поступило,
	|	0 КАК Выбыло,
	|	Строки.Количество КАК Отменено
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК Строки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДД
	|		ПО Строки.Ссылка = ДД.Ссылка
	|ГДЕ
	|	ДД.Ссылка = &Ссылка
	|	И Строки.Отменено
	|	И ДД.бг_ДолгосрочныйРезерв
	|	И Строки.бг_ЗаказПоставщикуИсточникОбеспечения <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	И Строки.бг_ПриходныйОрдерОбособленногоОбеспечения <> ЗНАЧЕНИЕ(Документ.ПриходныйОрдерНаТовары.ПустаяСсылка)
	|	И Строки.ВариантОбеспечения В (ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада), ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// отмена резерва в пути
	|ВЫБРАТЬ
	|	&Период,
	|	ДД.Ссылка,
	|	НЕОПРЕДЕЛЕНО,
	|	Строки.бг_ЗаказПоставщикуИсточникОбеспечения,
	|	НЕОПРЕДЕЛЕНО,
	|	Строки.Номенклатура,
	|	Строки.бг_УпаковкаПаллета,
	|	0 КАК НаСкладе,
	|	0 КАК УПоставщика,
	|	0 КАК Поступило,
	|	0 КАК Выбыло,
	|	Строки.Количество КАК Отменено
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК Строки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДД
	|		ПО Строки.Ссылка = ДД.Ссылка
	|ГДЕ
	|	ДД.Ссылка = &Ссылка
	|	И Строки.Отменено
	|	И ДД.бг_ДолгосрочныйРезерв
	|	И Строки.бг_ЗаказПоставщикуИсточникОбеспечения <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// поступление на склад долгосрочного резерва
	|ВЫБРАТЬ
	|	&Период,
	|	Строки.Ссылка,
	|	НЕОПРЕДЕЛЕНО,
	|	Строки.бг_ЗаказПоставщикуИсточникОбеспечения,
	|	НЕОПРЕДЕЛЕНО,
	|	Строки.Номенклатура,
	|	Строки.бг_УпаковкаПаллета,
	|	0 КАК НаСкладе,
	|	0 КАК УПоставщика,
	|	Строки.Количество КАК Поступило,
	|	0 КАК Выбыло,
	|	0 КАК Отменено
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК Строки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДД
	|		ПО Строки.Ссылка = ДД.Ссылка
	|ГДЕ
	|	ДД.Ссылка = &Ссылка
	|	И ДД.бг_ДолгосрочныйРезерв
	|	И Строки.бг_ЗаказПоставщикуИсточникОбеспечения <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	И Строки.ВариантОбеспечения В (ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада), ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// если по заказу поставщика поступило под заказ клиента, а не под долгосрочный резерв, то уменьшаем поступление под резерв
	|ВЫБРАТЬ
	|	ДД.ДатаОтгрузки,
	|	Строки.бг_ЗаказКлиентаИсточникОбеспечения,
	|	НЕОПРЕДЕЛЕНО,
	|	Строки.бг_ЗаказПоставщикуИсточникОбеспечения,
	|	НЕОПРЕДЕЛЕНО,
	|	Строки.Номенклатура,
	|	Строки.бг_УпаковкаПаллета,
	|	0 КАК НаСкладе,
	|	0 КАК УПоставщика,
	|	- Строки.Количество КАК Поступило,
	|	0 КАК Выбыло,
	|	0 КАК Отменено
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК Строки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДД
	|		ПО Строки.Ссылка = ДД.Ссылка
	|ГДЕ
	|	ДД.Ссылка = &Ссылка
	|	И НЕ Строки.Отменено
	|	И НЕ ДД.бг_ДолгосрочныйРезерв
	|	И Строки.бг_ЗаказПоставщикуИсточникОбеспечения <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	И Строки.бг_ЗаказКлиентаИсточникОбеспечения <> ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|	И Строки.бг_ЗаказКлиентаИсточникОбеспечения.бг_ДолгосрочныйРезерв
	|	И Строки.бг_ПриходныйОрдерОбособленногоОбеспечения <> ЗНАЧЕНИЕ(Документ.ПриходныйОрдерНаТовары.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// перенос резерва в пути с долгосрочного на заказ клиента
	|ВЫБРАТЬ
	|	ДД.ДатаОтгрузки,
	|	Строки.бг_ЗаказКлиентаИсточникОбеспечения,
	|	Строки.Ссылка,
	|	Строки.бг_ЗаказПоставщикуИсточникОбеспечения,
	|	НЕОПРЕДЕЛЕНО,
	|	Строки.Номенклатура,
	|	Строки.бг_УпаковкаПаллета,
	|	0 КАК НаСкладе,
	|	0 КАК УПоставщика,
	|	0 КАК Поступило,
	|	Строки.Количество КАК Выбыло,
	|	0 КАК Отменено
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК Строки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДД
	|		ПО Строки.Ссылка = ДД.Ссылка
	|ГДЕ
	|	ДД.Ссылка = &Ссылка
	|	И НЕ Строки.Отменено
	|	И НЕ ДД.бг_ДолгосрочныйРезерв
	|	И Строки.бг_ЗаказПоставщикуИсточникОбеспечения <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	И Строки.бг_ЗаказКлиентаИсточникОбеспечения <> ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|	И Строки.бг_ЗаказКлиентаИсточникОбеспечения.бг_ДолгосрочныйРезерв
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// резервирование в пути на свободный остаток под заказ клиента
	|ВЫБРАТЬ
	|	ДД.ДатаОтгрузки,
	|	Строки.бг_ЗаказКлиентаИсточникОбеспечения,
	|	Строки.Ссылка,
	|	Строки.бг_ЗаказПоставщикуИсточникОбеспечения,
	|	НЕОПРЕДЕЛЕНО,
	|	Строки.Номенклатура,
	|	Строки.бг_УпаковкаПаллета,
	|	0 КАК НаСкладе,
	|	Строки.Количество КАК УПоставщика,
	|	0 КАК Поступило,
	|	Строки.Количество КАК Выбыло,
	|	0 КАК Отменено
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК Строки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДД
	|		ПО Строки.Ссылка = ДД.Ссылка
	|ГДЕ
	|	ДД.Ссылка = &Ссылка
	|	И НЕ Строки.Отменено
	|	И НЕ ДД.бг_ДолгосрочныйРезерв
	|	И Строки.бг_ЗаказПоставщикуИсточникОбеспечения <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	И Строки.бг_ЗаказКлиентаИсточникОбеспечения = ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// резервирование долгосрочного резерва со склада под заказ клиента
	|ВЫБРАТЬ
	|	ДД.ДатаОтгрузки,
	|	Строки.бг_ЗаказКлиентаИсточникОбеспечения,
	|	Строки.Ссылка,
	|	НЕОПРЕДЕЛЕНО,
	|	ДД.Склад,
	|	Строки.Номенклатура,
	|	Строки.бг_УпаковкаПаллета,
	|	0 КАК НаСкладе,
	|	0 КАК УПоставщика,
	|	0 КАК Поступило,
	|	Строки.Количество КАК Выбыло,
	|	0 КАК Отменено
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК Строки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДД
	|		ПО Строки.Ссылка = ДД.Ссылка
	|ГДЕ
	|	ДД.Ссылка = &Ссылка
	|	И НЕ Строки.Отменено
	|	И НЕ ДД.бг_ДолгосрочныйРезерв
	|	И Строки.бг_ЗаказКлиентаИсточникОбеспечения <> ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|	И Строки.бг_ЗаказКлиентаИсточникОбеспечения.бг_ДолгосрочныйРезерв
	|	И Строки.бг_ЗаказПоставщикуИсточникОбеспечения = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	И Строки.ВариантОбеспечения В (ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада), ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить))";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
