#Область ПрограммныйИнтерфейс

Процедура ВыполнитьРегламентноеЗадание() Экспорт

	Перем адаптер_ПодпискиНаСобытияВызовСервера, адаптер_НастройкиОбменаПовтИсп;
	адаптер_ПодпискиНаСобытияВызовСервера = ОбщегоНазначения.ОбщийМодуль("адаптер_ПодпискиНаСобытияВызовСервера");
	адаптер_НастройкиОбменаПовтИсп = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбменаПовтИсп");
	
	ПараметрыПодключения = адаптер_НастройкиОбменаПовтИсп.НастройкиПодключенияДляВыгрузки();
	Если ПараметрыПодключения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	НастройкиВыгрузки = адаптер_НастройкиОбменаПовтИсп.
		ПолучитьНастройкиВыгрузкиПоОбъекту(
		Метаданные.РегламентныеЗадания.битВыгрузкаДолгосрочныхРезервовКонтрагентов.ПолноеИмя());
	Если НастройкиВыгрузки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Метаданные", Метаданные.РегламентныеЗадания.битВыгрузкаДолгосрочныхРезервовКонтрагентов);

	УстановитьПривилегированныйРежим(Истина);
	Задания = РегламентныеЗадания.ПолучитьРегламентныеЗадания(Отбор);
	Если Задания.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОтборыДляВыгрузкиОстатков = СкладыДляВыгрузкиОстатковДолгосрочныхРезервов();
	
	Пока ОтборыДляВыгрузкиОстатков.Следующий() Цикл
	
		ИдентификаторСклада = ОтборыДляВыгрузкиОстатков.СкладРезерва.УникальныйИдентификатор();
		ИдентификаторОрганизации = ОтборыДляВыгрузкиОстатков.Организация.УникальныйИдентификатор();
		ДополнительныеСвойства = Новый Структура;
		ДополнительныеСвойства.Вставить("ИдентификаторСклада", ИдентификаторСклада);
		ДополнительныеСвойства.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
		ДополнительныеСвойства.Вставить("ТикерОрганизации", ОтборыДляВыгрузкиОстатков.ТикерОрганизации);
	
		адаптер_ПодпискиНаСобытияВызовСервера.ЗарегистрироватьИсходящееСообщение(
			Задания[0], ДополнительныеСвойства, НастройкиВыгрузки);
	
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства, адаптер_НастройкиОбмена);
	ДобавитьКлючевыеРеквизитыКВыгрузке(РеквизитыИСвойства, адаптер_НастройкиОбмена);

КонецПроцедуры

Процедура ЗаполнитьТекстыЗапросовУсловиями(ТекстЗапроса,
		ТекстЗапросаТаблицаКлючей, ПараметрыЗапроса, НастройкаВыгрузки, Объект, СтандартнаяОбработка) Экспорт

	СтандартнаяОбработка = Ложь;
	ТекстЗапросаТаблицаКлючей = "";
	ТекстЗапроса = "ВЫБРАТЬ 1";

КонецПроцедуры

Функция ПолучитьКлючМаршрутизацииИсходящегоСообщения(ДанныеСообщения, ИсточникОбъект = Неопределено) Экспорт

	//общий ключ, если нет свойства
	КлючМаршрутизации = "xml.ВыгрузкаДолгосрочныхРезервовКонтрагентов";
	
	Если ДанныеСообщения.ДополнительныеСвойстваОбъекта.Свойство("ТикерОрганизации")
		И ЗначениеЗаполнено(ДанныеСообщения.ДополнительныеСвойстваОбъекта.ТикерОрганизации) Тогда
			КлючМаршрутизации = "xml.ВыгрузкаДолгосрочныхРезервовКонтрагентов."
				+ ДанныеСообщения.ДополнительныеСвойстваОбъекта.ТикерОрганизации + "."
				+ ДанныеСообщения.ДополнительныеСвойстваОбъекта.ИдентификаторСклада;
	КонецЕсли;
	
	Возврат КлючМаршрутизации;

КонецФункции

Функция ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения) Экспорт

	Перем адаптер_РаботаСДаннымиИБ;
	адаптер_РаботаСДаннымиИБ = ОбщегоНазначения.ОбщийМодуль("адаптер_РаботаСДаннымиИБ");
	
	ПараметрыВыполненияЗапросов = адаптер_РаботаСДаннымиИБ.ПолучитьПараметрыВыполненияЗапросов(Объект, ДанныеСообщения);
	
	ДанныеВыгружаемогоОбъекта = Новый Структура;
	ДанныеВыгружаемогоОбъекта.Вставить("ПолноеИмя", ПараметрыВыполненияЗапросов.ПолноеИмя);
	ДанныеВыгружаемогоОбъекта.Вставить("Реквизиты", Новый Массив);
	
	ВыгружаемыеДанные = ВыгружаемыеДанные(ДанныеСообщения.ДополнительныеСвойстваОбъекта);
	
	Если ВыгружаемыеДанные.РезультатПоШапке.Пустой() Тогда
		Возврат ДанныеВыгружаемогоОбъекта;
	КонецЕсли;

	Реквизиты = адаптер_РаботаСДаннымиИБ.РезультатЗапросаВСтруктуруРеквизитов(
		ВыгружаемыеДанные.РезультатПоШапке,
		ПараметрыВыполненияЗапросов.ТаблицаКлючей,
		ДанныеСообщения);

	Если Реквизиты.Количество() > 0 Тогда
	
		Остатки = адаптер_РаботаСДаннымиИБ.РезультатЗапросаВСтруктуруРеквизитов(
			ВыгружаемыеДанные.РезультатПоТоварам,
			ПараметрыВыполненияЗапросов.ТаблицаКлючей,
			ДанныеСообщения);

		Реквизиты[0].Склад.Вставить("Остатки", Остатки);
	КонецЕсли;
	
	ДанныеВыгружаемогоОбъекта.Вставить("Реквизиты", Реквизиты);
	
	Возврат ДанныеВыгружаемогоОбъекта;

КонецФункции

#КонецОбласти 

#Область СлужебныеФункции

Функция СкладыДляВыгрузкиОстатковДолгосрочныхРезервов()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаказДР.Организация КАК Организация,
	|	ЗаказДР.Организация.бг_Тикер КАК ТикерОрганизации,
	|	ЗаказДР.Склад КАК СкладРезерва
	|ИЗ
	|	РегистрСведений.РаспределениеЗапасов КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказДР
	|		ПО ДД.ЗаказНаОтгрузку = ЗаказДР.Ссылка
	|ГДЕ
	|	ЗаказДР.бг_ДолгосрочныйРезерв
	|	И ДД.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ВРезерве)
	|	И ДД.Зарезервировано <> 0";

	Возврат Запрос.Выполнить().Выбрать();

КонецФункции

Процедура ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства, адаптер_НастройкиОбмена)

	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		,
		"Организация",
		Новый ОписаниеТипов("СправочникСсылка.Организации"));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		,
		"Склад",
		Новый ОписаниеТипов("СправочникСсылка.Склады"));

#Область ОстаткиДолгосрочныхРезервовКонтрагента

	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства, 
		Метаданные.Справочники.Склады,
		,
		"Остатки.Контрагент",
		Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));

	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Склады,
		,
		"Остатки.Номенклатура",
		Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));

	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Склады,
		,
		"Остатки.КоличествоРезерв",
			ОбщегоНазначения.ОписаниеТипаЧисло(15, 0));

#КонецОбласти

КонецПроцедуры

Процедура ДобавитьКлючевыеРеквизитыКВыгрузке(РеквизитыИСвойства, адаптер_НастройкиОбмена)

	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Организации,
		"бг_Тикер");

	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Склады,
		"Наименование");

	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Контрагенты,
		"Наименование");

	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Контрагенты,
		"ИНН");

	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Контрагенты,
		"КПП");

	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Номенклатура,
		"Наименование");

КонецПроцедуры

Функция ВыгружаемыеДанные(СтруктураПараметров)

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаВыгружаемыхДанных();
	Организация = Справочники.Организации.ПолучитьСсылку(
		Новый УникальныйИдентификатор(СтруктураПараметров.ИдентификаторОрганизации));
	Склад = Справочники.Склады.ПолучитьСсылку(
		Новый УникальныйИдентификатор(СтруктураПараметров.ИдентификаторСклада));

	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Склад", Склад);

	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	МаксимальныйИндексЗапроса = РезультатыЗапросов.ВГраница();

	ВыгружаемыеДанные = Новый Структура;
	ВыгружаемыеДанные.Вставить("РезультатПоШапке", РезультатыЗапросов[МаксимальныйИндексЗапроса - 1]);
	ВыгружаемыеДанные.Вставить("РезультатПоТоварам", РезультатыЗапросов[МаксимальныйИндексЗапроса]);

	Возврат ВыгружаемыеДанные;

КонецФункции

Функция ТекстЗапросаВыгружаемыхДанных()

	ЗапросТекст =
	"ВЫБРАТЬ
	|	ЗаказДолгосрочногоРезерва.Ссылка.Организация КАК Организация,
	|	ЗаказДолгосрочногоРезерва.Ссылка.Контрагент КАК Контрагент,
	|	ЗаказДолгосрочногоРезерва.Склад КАК Склад,
	|	РаспределениеЗапасов.Номенклатура КАК Номенклатура,
	|	СУММА(РаспределениеЗапасов.Зарезервировано) КАК КоличествоРезерв
	|ПОМЕСТИТЬ ДанныеВыгружаемогоОбъекта
	|ИЗ
	|	РегистрСведений.РаспределениеЗапасов КАК РаспределениеЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказДолгосрочногоРезерва
	|		ПО РаспределениеЗапасов.ЗаказНаОтгрузку = ЗаказДолгосрочногоРезерва.Ссылка
	|ГДЕ
	|	ЗаказДолгосрочногоРезерва.бг_ДолгосрочныйРезерв
	|	И РаспределениеЗапасов.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ВРезерве)
	|	И РаспределениеЗапасов.Зарезервировано <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	РаспределениеЗапасов.Номенклатура,
	|	ЗаказДолгосрочногоРезерва.Ссылка.Организация,
	|	ЗаказДолгосрочногоРезерва.Ссылка.Контрагент,
	|	ЗаказДолгосрочногоРезерва.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СкладыРезервов.Организация КАК Организация_ЗначениеРеквизитаИдентификатор,
	|	СкладыРезервов.Организация КАК Организация_ЗначениеРеквизитаТаблицаКлючей,
	|	СкладыРезервов.Организация.бг_Тикер КАК Организация_ЗначениеРеквизитабг_Тикер,
	|	СкладыРезервов.Склад КАК Склад_ЗначениеРеквизитаИдентификатор,
	|	СкладыРезервов.Склад КАК Склад_ЗначениеРеквизитаТаблицаКлючей,
	|	СкладыРезервов.Склад.Наименование КАК Склад_ЗначениеРеквизитаНаименование
	|ИЗ
	|	ДанныеВыгружаемогоОбъекта КАК СкладыРезервов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация_ЗначениеРеквизитабг_Тикер,
	|	Склад_ЗначениеРеквизитаНаименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиРезервовКонтрагентов.Контрагент КАК Контрагент_ЗначениеРеквизитаИдентификатор,
	|	ОстаткиРезервовКонтрагентов.Контрагент КАК Контрагент_ЗначениеРеквизитаТаблицаКлючей,
	|	ОстаткиРезервовКонтрагентов.Контрагент.Наименование КАК Контрагент_ЗначениеРеквизитаНаименование,
	|	ОстаткиРезервовКонтрагентов.Контрагент.ИНН КАК Контрагент_ЗначениеРеквизитаИНН,
	|	ОстаткиРезервовКонтрагентов.Контрагент.КПП КАК Контрагент_ЗначениеРеквизитаКПП,
	|	ОстаткиРезервовКонтрагентов.Номенклатура КАК Номенклатура_ЗначениеРеквизитаИдентификатор,
	|	ОстаткиРезервовКонтрагентов.Номенклатура КАК Номенклатура_ЗначениеРеквизитаТаблицаКлючей,
	|	ОстаткиРезервовКонтрагентов.Номенклатура.Наименование КАК Номенклатура_ЗначениеРеквизитаНаименование,
	|	ВЫРАЗИТЬ(ОстаткиРезервовКонтрагентов.КоличествоРезерв КАК ЧИСЛО(15, 0)) КАК КоличествоРезерв
	|ИЗ
	|	ДанныеВыгружаемогоОбъекта КАК ОстаткиРезервовКонтрагентов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Контрагент_ЗначениеРеквизитаНаименование,
	|	Номенклатура_ЗначениеРеквизитаНаименование";

	Возврат ЗапросТекст;

КонецФункции

#КонецОбласти