#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт
	ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства, ФорматСообщения);
КонецПроцедуры

Процедура ЗаполнитьТекстыЗапросовУсловиями(ТекстЗапроса, ТекстЗапросаТаблицаКлючей, ПараметрыЗапроса, НастройкаВыгрузки, Объект, СтандартнаяОбработка) Экспорт
	СтрокаКонецЗапроса = ") КАК Сырьё";
	ТекстЗапроса = Лев(ТекстЗапроса, СтрНайти(ТекстЗапроса, СтрокаКонецЗапроса) + СтрДлина(СтрокаКонецЗапроса) - 1);
	ТекстЗапроса = ТекстЗапроса + "
		|ИЗ
		|	Документ.битОтчетОПроизводствеЕГАИС КАК ВыгружаемыйОбъект
		|ГДЕ
		|	&УсловиеЗапроса";
КонецПроцедуры

Функция ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения) Экспорт
	
	Перем адаптер_РаботаСДаннымиИБ, адаптер_обработчикиСобытийСтандартный;
	адаптер_РаботаСДаннымиИБ = ОбщегоНазначения.ОбщийМодуль("адаптер_РаботаСДаннымиИБ");
	адаптер_обработчикиСобытийСтандартный = ОбщегоНазначения.ОбщийМодуль("адаптер_обработчикиСобытийСтандартный");
	
	ПараметрыВыполненияЗапросов = адаптер_РаботаСДаннымиИБ.ПолучитьПараметрыВыполненияЗапросов(Объект, ДанныеСообщения);
	ДанныеОбъекта = адаптер_обработчикиСобытийСтандартный.ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения);
	
	Если ДанныеОбъекта.Реквизиты.Количество() > 0 Тогда
		РеквизитыОбъекта = ДанныеОбъекта.Реквизиты[0];    
		ЗаполнитьСвязанныеРеквизиты(РеквизитыОбъекта, Объект);
		ДобавитьДанныеОМаркахУпаковках(РеквизитыОбъекта, Объект, ДанныеСообщения, ПараметрыВыполненияЗапросов);
	КонецЕсли;
	
	Возврат ДанныеОбъекта;
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьСвязанныеРеквизиты(РеквизитыОбъекта, Объект)
	Операции = Новый Массив;
	Операции.Добавить(Перечисления.ВидыДокументовЕГАИС.АктТТНПодтверждение);
	Операции.Добавить(Перечисления.ВидыДокументовЕГАИС.бг_ОтчетОПроизводствеЕГАИС);
	
	РеквизитыОбъекта.Вставить("бг_ЕстьДвиженияЕГАИС", 
		бг_ТТНВходящаяЕГАИСИнтеграция.ЕстьДвиженияЕАГИС(Объект));
	РеквизитыОбъекта.Вставить("ИдентификаторЗапросаУТМ", 
		бг_ТТНВходящаяЕГАИСИнтеграция.ИдентификаторЗапросаЕГАИС(Объект, Операции));
	РеквизитыОбъекта.Вставить("СтатусДокумента", 
		Строка(РегистрыСведений.СтатусыДокументовЕГАИС.ТекущееСостояние(Объект).Статус));
КонецПроцедуры

Процедура ДобавитьДанныеОМаркахУпаковках(РеквизитыОбъекта, Объект, ДанныеСообщения, ПараметрыВыполненияЗапросов)

	ДанныеМарокУпаковок = ДанныеМарокУпаковокПоЭтапуПроизводства(Объект);
	
	РеквизитыОбъекта.Вставить("Марки", ДиапазоныМарок(ДанныеМарокУпаковок.Марки));	
	бг_МаркируемаяПродукция.ДобавитьДанныеОбАкцизныхМаркахВРеквизитыСообщения(
		РеквизитыОбъекта, Объект, ДанныеМарокУпаковок);

КонецПроцедуры

Функция ДиапазоныМарок(ТаблицаМарок)
	
	Результат = Новый Массив;
	
	НачалоДиапазона = -2;
	КонецДиапазона = -2;
	Для каждого СтрокаМарки Из ТаблицаМарок Цикл
		Если СтрокаМарки.НомерМарки = КонецДиапазона + 1 Тогда
			КонецДиапазона = СтрокаМарки.НомерМарки;
		Иначе
			ДобавитьСтрокуДиапазона(Результат, НачалоДиапазона, КонецДиапазона);
			НачалоДиапазона = СтрокаМарки.НомерМарки;
			КонецДиапазона = СтрокаМарки.НомерМарки;
		КонецЕсли;
	КонецЦикла;
	ДобавитьСтрокуДиапазона(Результат, НачалоДиапазона, КонецДиапазона);
	
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьСтрокуДиапазона(МассивСтрок, НачалоДиапазона, КонецДиапазона)
	Если КонецДиапазона > 0 Тогда
		МассивСтрок.Добавить(СтруктураДиапазон(НачалоДиапазона, КонецДиапазона));
	КонецЕсли;
КонецПроцедуры

Функция СтруктураДиапазон(НачалоДипазона, КонецДипазона) 
	Результат = Новый Структура;
	НачалоДиапазонаСтрока = Формат(НачалоДипазона, "ЧГ=0");
	КонецДиапазонаСтрока = Формат(КонецДипазона, "ЧГ=0");
	НомерСимволаРазряд = 4;
	ДлинаСтрРазряда = 3;
	ДлинаСтрДиапазона = 8;
	Результат.Вставить("КлючСвязи", 1);
	Результат.Вставить("Разряд", Сред(НачалоДиапазонаСтрока, НомерСимволаРазряд, ДлинаСтрРазряда));
	Результат.Вставить("ДиапазонС", Прав(НачалоДиапазонаСтрока, ДлинаСтрДиапазона));
	Результат.Вставить("ДиапазонПо", Прав(КонецДиапазонаСтрока, ДлинаСтрДиапазона));
	Возврат Результат;
КонецФункции

Функция ДанныеМарокУпаковокПоЭтапуПроизводства(ОтчетОПроизводствеЕГАИС)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭтапПроизводства2_2.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втЭтапы
	|ИЗ
	|	Документ.битОтчетОПроизводствеЕГАИС КАК битОтчетОПроизводствеЕГАИС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
	|		ПО битОтчетОПроизводствеЕГАИС.ДокументОснование = ЭтапПроизводства2_2.Ссылка
	|			И (битОтчетОПроизводствеЕГАИС.Ссылка = &ОтчетОПроизводствеЕГАИС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	бг_ДвижениеМарок.КодУпаковки КАК НомерКоробки,
	|	бг_ИдентификаторыМарок.ИдентификаторМарки КАК КодАкцизнойМарки,
	|	бг_ИдентификаторыМарок.НомерМарки КАК НомерМарки
	|ПОМЕСТИТЬ втДанные
	|ИЗ
	|	втЭтапы КАК втЭтапы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.бг_ДвижениеМарок КАК бг_ДвижениеМарок
	|		ПО втЭтапы.Ссылка = бг_ДвижениеМарок.Регистратор
	|			И (бг_ДвижениеМарок.СтатусМарки = ЗНАЧЕНИЕ(Перечисление.бг_СтатусыАкцизныхМарок.ВключенаВЭтапПроизводства))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.бг_ИдентификаторыМарок КАК бг_ИдентификаторыМарок
	|		ПО (бг_ДвижениеМарок.ГУИДМарки = бг_ИдентификаторыМарок.ГУИДМарки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК КлючСвязи,
	|	втДанные.НомерКоробки КАК НомерКоробки,
	|	втДанные.КодАкцизнойМарки КАК КодАкцизнойМарки
	|ИЗ
	|	втДанные КАК втДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанные.НомерМарки КАК НомерМарки
	|ИЗ
	|	втДанные КАК втДанные
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерМарки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Упаковка КАК Упаковка,
	|	МИНИМУМ(ВложенныйЗапрос.ИерархияУпаковки) КАК ИерархияУпаковки
	|ИЗ
	|	(ВЫБРАТЬ
	|		втДанные.НомерКоробки КАК Упаковка,
	|		бг_СоставУпаковок.КодВышестоящейУпаковки КАК ИерархияУпаковки
	|	ИЗ
	|		втДанные КАК втДанные
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.бг_СоставУпаковок КАК бг_СоставУпаковок
	|			ПО втДанные.НомерКоробки = бг_СоставУпаковок.КодУпаковки
	|				И (бг_СоставУпаковок.Регистратор ССЫЛКА Документ.битДанныеСПроизводственнойЛинии)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		бг_СоставУпаковок.КодВышестоящейУпаковки,
	|		""""
	|	ИЗ
	|		втДанные КАК втДанные
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.бг_СоставУпаковок КАК бг_СоставУпаковок
	|			ПО втДанные.НомерКоробки = бг_СоставУпаковок.КодУпаковки
	|				И (бг_СоставУпаковок.Регистратор ССЫЛКА Документ.битДанныеСПроизводственнойЛинии)) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Упаковка";
	Запрос.УстановитьПараметр("ОтчетОПроизводствеЕГАИС", ОтчетОПроизводствеЕГАИС);	
	РезультатЗапросаПакет = Запрос.ВыполнитьПакет();
	
	Результат = Новый Структура;
	Результат.Вставить("Упаковки",		РезультатЗапросаПакет[РезультатЗапросаПакет.ВГраница()].Выгрузить());
	Результат.Вставить("Марки",			РезультатЗапросаПакет[РезультатЗапросаПакет.ВГраница() - 1].Выгрузить());
	Результат.Вставить("АкцизныеМарки",	РезультатЗапросаПакет[РезультатЗапросаПакет.ВГраница() - 2].Выгрузить());
	
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства, ФорматСообщения)
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	#Область ШапкаДокумента
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, РеквизитыИСвойства.МетаданныеОбъекта, ,
		"ИдентификаторЗапросаУТМ", ОбщегоНазначения.ОписаниеТипаСтрока(36));
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, РеквизитыИСвойства.МетаданныеОбъекта, ,
		"СтатусДокумента", ОбщегоНазначения.ОписаниеТипаСтрока(128));
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, РеквизитыИСвойства.МетаданныеОбъекта, ,
		"бг_ЕстьДвиженияЕГАИС", Новый ОписаниеТипов("Булево"));
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, РеквизитыИСвойства.МетаданныеОбъекта, ,
		"бг_ВариантВыгрузкиМарокУпаковок", ОбщегоНазначения.ОписаниеТипаСтрока(25));
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, РеквизитыИСвойства.МетаданныеОбъекта, ,
		"бг_ИмяФайлаВыгрузкиМарокУпаковок", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	#КонецОбласти 
	
	#Область ТабличнаяЧастьМарки
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, РеквизитыИСвойства.МетаданныеОбъекта,	, 
		"Марки.КлючСвязи", ОбщегоНазначения.ОписаниеТипаЧисло(10));
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, РеквизитыИСвойства.МетаданныеОбъекта,	, 
		"Марки.Разряд", ОбщегоНазначения.ОписаниеТипаСтрока(10));
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, РеквизитыИСвойства.МетаданныеОбъекта,	, 
		"Марки.ДиапазонС", ОбщегоНазначения.ОписаниеТипаСтрока(10));
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, РеквизитыИСвойства.МетаданныеОбъекта,	, 
		"Марки.ДиапазонПо", ОбщегоНазначения.ОписаниеТипаСтрока(10));
	#КонецОбласти 
	
	#Область ТабличнаяЧастьАкцизныеМарки
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, РеквизитыИСвойства.МетаданныеОбъекта,	, 
		"АкцизныеМарки.КодАкцизнойМарки", ОбщегоНазначения.ОписаниеТипаСтрока(160));
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, РеквизитыИСвойства.МетаданныеОбъекта,	, 
		"АкцизныеМарки.КлючСвязи", ОбщегоНазначения.ОписаниеТипаЧисло(5));
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, РеквизитыИСвойства.МетаданныеОбъекта,	, 
		"АкцизныеМарки.НомерКоробки", ОбщегоНазначения.ОписаниеТипаСтрока(120));
	#КонецОбласти 	
	
	#Область ТабличнаяЧастьУпаковки
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, РеквизитыИСвойства.МетаданныеОбъекта,	, 
		"Упаковки.Упаковка", ОбщегоНазначения.ОписаниеТипаСтрока(120));
	адаптер_НастройкиОбмена.ДобавитьРеквизит(РеквизитыИСвойства, РеквизитыИСвойства.МетаданныеОбъекта,	, 
		"Упаковки.ИерархияУпаковки", ОбщегоНазначения.ОписаниеТипаСтрока(120));
	#КонецОбласти 	
	
КонецПроцедуры

#КонецОбласти 
