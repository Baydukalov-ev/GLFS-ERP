#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьСоставВыгружаемыхОбъектов(РеквизитыИСвойства, ФорматСообщения, СтандартнаяОбработка) Экспорт
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	адаптер_НастройкиОбмена.ОставитьРеквизиты(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,
		ВыгружаемыеРеквизиты());
		
	ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства);
	ДобавитьКлючевыеПоляКВыгрузке(РеквизитыИСвойства);
	
КонецПроцедуры

Процедура ЗаполнитьТекстыЗапросовУсловиями(ТекстЗапроса, ТекстЗапросаТаблицаКлючей, ПараметрыЗапроса,
	НастройкаВыгрузки, Объект, СтандартнаяОбработка) Экспорт
	
	// Выгрузка объекта полностью переопределяется, поэтому стандартные запросы превращаются в фиктивные,
	// т.к., например, запрос к таблице ключей будет выполняться все равно, это время, а в данном случае
	// таблицы ключей не нужны ни для одного реквизита.
	ТекстЗапроса = "";
	ТекстЗапросаТаблицаКлючей = "";
	
КонецПроцедуры

Функция ПолучитьДанныеВыгружаемогоОбъекта(Объект, ДанныеСообщения) Экспорт
	
	Перем адаптер_РаботаСДаннымиИБ;
	адаптер_РаботаСДаннымиИБ = ОбщегоНазначения.ОбщийМодуль("адаптер_РаботаСДаннымиИБ");
	
	ПараметрыВыполненияЗапросов = адаптер_РаботаСДаннымиИБ.ПолучитьПараметрыВыполненияЗапросов(Объект, ДанныеСообщения);
	
	ДанныеВыгружаемогоОбъекта = Новый Структура;
	ДанныеВыгружаемогоОбъекта.Вставить("ПолноеИмя", ПараметрыВыполненияЗапросов.ПолноеИмя);
	ДанныеВыгружаемогоОбъекта.Вставить("Реквизиты", Новый Массив);
	
	ВыгружаемыеДанные = ВыгружаемыеДанные(Объект);
	
	Реквизиты = адаптер_РаботаСДаннымиИБ.РезультатЗапросаВСтруктуруРеквизитов(
		ВыгружаемыеДанные.РезультатПоШапке,
		ПараметрыВыполненияЗапросов.ТаблицаКлючей,
		ДанныеСообщения);
		
	ДанныеВыгружаемогоОбъекта.Вставить("Реквизиты", Реквизиты);
	
	ДанныеСообщения.RoutingKey = КлючМаршрутизации(Реквизиты[0], ДанныеСообщения);
	
	Возврат ДанныеВыгружаемогоОбъекта;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВыгружаемыеРеквизиты()
	
	ВыгружаемыеРеквизиты = Новый Массив;
	
	ВыгружаемыеРеквизиты.Добавить("бг_ДатаИнвойса");
	ВыгружаемыеРеквизиты.Добавить("бг_ДатаРегистрацииДвиженийЕГАИС");
	ВыгружаемыеРеквизиты.Добавить("бг_ДатаСлива");
	ВыгружаемыеРеквизиты.Добавить("бг_КонечныйНомерДиапазона");
	ВыгружаемыеРеквизиты.Добавить("бг_НачальныйНомерДиапазона");
	ВыгружаемыеРеквизиты.Добавить("бг_Номенклатура");
	ВыгружаемыеРеквизиты.Добавить("бг_НомерГТД");
	ВыгружаемыеРеквизиты.Добавить("бг_НомерДиапазонаВРулоне");
	ВыгружаемыеРеквизиты.Добавить("бг_НомерРулона");
	ВыгружаемыеРеквизиты.Добавить("бг_СерияМарки");
	ВыгружаемыеРеквизиты.Добавить("бг_Сертификат");
	ВыгружаемыеРеквизиты.Добавить("бг_СтранаНазначения");
	ВыгружаемыеРеквизиты.Добавить("бг_УпаковкаПаллета");
	ВыгружаемыеРеквизиты.Добавить("бг_ФактическаяКрепость");
	ВыгружаемыеРеквизиты.Добавить("ВидНоменклатуры");
	ВыгружаемыеРеквизиты.Добавить("ГоденДо");
	ВыгружаемыеРеквизиты.Добавить("ДатаПроизводства");
	ВыгружаемыеРеквизиты.Добавить("Наименование");
	ВыгружаемыеРеквизиты.Добавить("Номер");
	ВыгружаемыеРеквизиты.Добавить("ПометкаУдаления");
	ВыгружаемыеРеквизиты.Добавить("ПроизводительЕГАИС");
	ВыгружаемыеРеквизиты.Добавить("Справка2ЕГАИС");
	
	ВыгружаемыеРеквизиты = СтрСоединить(ВыгружаемыеРеквизиты, ",");
	
	Возврат ВыгружаемыеРеквизиты;
	
КонецФункции

Процедура ДобавитьСвязанныеРеквизитыКВыгрузке(РеквизитыИСвойства)
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	#Область РеквизитыПродукции
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"Спецификация",
		Новый ОписаниеТипов("СправочникСсылка.РесурсныеСпецификации"));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"бг_ЭтоГотоваяПродукция",
		Новый ОписаниеТипов("Булево"));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"бг_НомерСертифицированнойПартии",
		ОбщегоНазначения.ОписаниеТипаСтрока(20));
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"ЭтоМаркируемаяАлкогольнаяПродукция",
		Новый ОписаниеТипов("Булево"));

	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"КодВидаМарки",
		ОбщегоНазначения.ОписаниеТипаСтрока(20));
	#КонецОбласти // РеквизитыПродукции
	
	#Область РеквизитыФСМ
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"ЗаявлениеОВыдачеФСМ",
		Новый ОписаниеТипов("ДокументСсылка.битЗаявлениеОВыдачеФСМ"));
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"НакладнаяНаВыдачуФСМ",
		Новый ОписаниеТипов("ДокументСсылка.битНакладнаяНаВыдачуФСМ"));
				
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"ОрганизацияВладелец",
		Новый ОписаниеТипов("СправочникСсылка.Организации"));
	#КонецОбласти // РеквизитыФСМ
	
	#Область РеквизитыАлкогольнойПродукции
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"ЭтоАлкогольнаяПродукция",
		Новый ОписаниеТипов("Булево"));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"бг_ЭтоПиво",
		Новый ОписаниеТипов("Булево"));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"ДатаРозливаСправки1",
		ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"ДатаНачисленияАкцизаСправки1",
		ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"КрепостьСправки1",
		ОбщегоНазначения.ОписаниеТипаЧисло(5, 3));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"НомерГТДСправки1",
		Новый ОписаниеТипов("СправочникСсылка.НомераГТД"));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"СтранаПроисхождения",
		Новый ОписаниеТипов("СправочникСсылка.СтраныМира"));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"ВидАлкогольнойПродукцииСправки2",
		Новый ОписаниеТипов("СправочникСсылка.ВидыАлкогольнойПродукции"));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"ГрузоотправительСправки1",
		Новый ОписаниеТипов("СправочникСсылка.КлассификаторОрганизацийЕГАИС"));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"бг_АдресФайлаУдостоверенияКачества",
		ОбщегоНазначения.ОписаниеТипаСтрока(1024));
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"ДатаПодтвержденияЕГАИС",
		Новый ОписаниеТипов("Дата"));
	#КонецОбласти // РеквизитыАлкогольнойПродукции
	
	#Область ПрочиеРеквизиты
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		РеквизитыИСвойства.МетаданныеОбъекта,,
		"ОрганизацияЕГАИСВладелец",
		Новый ОписаниеТипов("СправочникСсылка.КлассификаторОрганизацийЕГАИС"));
	#КонецОбласти // ПрочиеРеквизиты
	
КонецПроцедуры

Процедура ДобавитьКлючевыеПоляКВыгрузке(РеквизитыИСвойства)
	
	Перем адаптер_НастройкиОбмена;
	адаптер_НастройкиОбмена = ОбщегоНазначения.ОбщийМодуль("адаптер_НастройкиОбмена");
	
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.УпаковкиЕдиницыИзмерения,
		"бг_КодЕК_Номенклатуры");
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.СертификатыНоменклатуры,
		"Номер");
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.НомераГТД,
		"Код");
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.ВидыАлкогольнойПродукции,
		"Код");
		
	адаптер_НастройкиОбмена.ДобавитьРеквизит(
		РеквизитыИСвойства,
		Метаданные.Справочники.Номенклатура,
		"КодЕК_ПОСМ",,
		ОбщегоНазначения.ОписаниеТипаСтрока(бг_НоменклатураИнтеграция.ДлинаКодаЕК_ПОСМ()));
		
КонецПроцедуры

Функция ВыгружаемыеДанные(Ссылка)
	
	ЗначенияКонстант = бг_КонстантыПовтИсп.ЗначенияКонстант("ИмпортныеССП, ВидДокументаУдостоверенияКачества");
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаВыгружаемыхДанных();
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ВидНоменклатурыИмпортныеССП", ЗначенияКонстант.ИмпортныеССП);
	Запрос.УстановитьПараметр("ВидДокументаУдостоверенияКачества", ЗначенияКонстант.ВидДокументаУдостоверенияКачества);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыгружаемыеДанные = Новый Структура;
	ВыгружаемыеДанные.Вставить("РезультатПоШапке", РезультатЗапроса);
	
	Возврат ВыгружаемыеДанные;
	
КонецФункции

Функция ТекстЗапросаВыгружаемыхДанных()
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЕСТЬNULL(бг_СопроводительныеДокументы.ПутьКФайлу, """") КАК бг_АдресФайлаУдостоверенияКачества,
	|	СерииНоменклатуры.бг_ДатаИнвойса КАК бг_ДатаИнвойса,
	|	СерииНоменклатуры.бг_ДатаРегистрацииДвиженийЕГАИС КАК бг_ДатаРегистрацииДвиженийЕГАИС,
	|	СерииНоменклатуры.бг_ДатаСлива КАК бг_ДатаСлива,
	|	СерииНоменклатуры.бг_КонечныйНомерДиапазона КАК бг_КонечныйНомерДиапазона,
	|	СерииНоменклатуры.бг_НачальныйНомерДиапазона КАК бг_НачальныйНомерДиапазона,
	|	СерииНоменклатуры.бг_Номенклатура КАК бг_Номенклатура_ЗначениеРеквизитаИдентификатор,
	|	Номенклатура.Код КАК бг_Номенклатура_ЗначениеРеквизитаКод,
	|	ВЫБОР
	|		КОГДА ВидыНоменклатуры.бг_ВариантСинхронизацииWMS = ЗНАЧЕНИЕ(Перечисление.бг_ВариантыСинхронизацииНоменклатурыWMS.ГУИД)
	|			ТОГДА ВЫРАЗИТЬ(Номенклатура.Код КАК СТРОКА(999))
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК бг_Номенклатура_ЗначениеРеквизитаКодЕК_ПОСМ,
	|	НомераГТДСерии.Ссылка КАК бг_НомерГТД_ЗначениеРеквизитаИдентификатор,
	|	ЕСТЬNULL(НомераГТДСерии.Код, """") КАК бг_НомерГТД_ЗначениеРеквизитаКод,
	|	СерииНоменклатуры.бг_НомерДиапазонаВРулоне КАК бг_НомерДиапазонаВРулоне,
	|	СерииНоменклатуры.бг_НомерРулона КАК бг_НомерРулона,
	|	СерииНоменклатуры.бг_НомерСертифицированнойПартии КАК бг_НомерСертифицированнойПартии,
	|	СерииНоменклатуры.бг_СерияМарки КАК бг_СерияМарки,
	|	СерииНоменклатуры.бг_Сертификат КАК бг_Сертификат_ЗначениеРеквизитаИдентификатор,
	|	ЕСТЬNULL(СертификатыНоменклатуры.Номер, """") КАК бг_Сертификат_ЗначениеРеквизитаНомер,
	|	СерииНоменклатуры.бг_СтранаНазначения КАК бг_СтранаНазначения_ЗначениеРеквизитаИдентификатор,
	|	ЕСТЬNULL(СтраныМираПоСтранеНазначения.КодАльфа2, """") КАК бг_СтранаНазначения_ЗначениеРеквизитаКодАльфа2,
	|	ЕСТЬNULL(СтраныМираПоСтранеНазначения.Код, """") КАК бг_СтранаНазначения_ЗначениеРеквизитаКод,
	|	СерииНоменклатуры.бг_УпаковкаПаллета КАК бг_УпаковкаПаллета_ЗначениеРеквизитаИдентификатор,
	|	ЕСТЬNULL(УпаковкиЕдиницыИзмерения.бг_КодЕК_Номенклатуры, """") КАК бг_УпаковкаПаллета_ЗначениеРеквизитабг_КодЕК_Номенклатуры,
	|	СерииНоменклатуры.бг_ФактическаяКрепость КАК бг_ФактическаяКрепость,
	|	СерииНоменклатуры.бг_ЭтоГотоваяПродукция КАК бг_ЭтоГотоваяПродукция,
	|	ВидыНоменклатуры.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Пиво) КАК бг_ЭтоПиво,
	|	ВидыНоменклатуры.бг_ВариантСинхронизацииWMS КАК ВариантСинхронизацииWMS,
	|	ЕСТЬNULL(КлассификаторАлкогольнойПродукцииЕГАИСПоСправке2.ВидПродукции, ЗНАЧЕНИЕ(Справочник.ВидыАлкогольнойПродукции.ПустаяСсылка)) КАК ВидАлкогольнойПродукцииСправки2_ЗначениеРеквизитаИдентификатор,
	|	ЕСТЬNULL(ВидыАлкогольнойПродукцииПоСправке2.Код, """") КАК ВидАлкогольнойПродукцииСправки2_ЗначениеРеквизитаКод,
	|	СерииНоменклатуры.ВидНоменклатуры КАК ВидНоменклатуры_ЗначениеРеквизитаИдентификатор,
	|	ВЫБОР
	|		КОГДА Номенклатура.СрокГодности = 0
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		ИНАЧЕ СерииНоменклатуры.ГоденДо
	|	КОНЕЦ КАК ГоденДо,
	|	Справки1ЕГАИС.Грузоотправитель КАК ГрузоотправительСправки1_ЗначениеРеквизитаИдентификатор,
	|	Справки1ЕГАИС.Грузоотправитель.Код КАК ГрузоотправительСправки1_ЗначениеРеквизитаКод,
	|	ЕСТЬNULL(Справки2ЕГАИС.бг_ДатаНачисленияАкциза, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаНачисленияАкцизаСправки1,
	|	ЕСТЬNULL(Справки2ЕГАИС.ДатаПодтвержденияЕГАИС, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПодтвержденияЕГАИС,
	|	СерииНоменклатуры.ДатаПроизводства КАК ДатаПроизводства,
	|	ЕСТЬNULL(Справки1ЕГАИС.ДатаРозлива, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаРозливаСправки1,
	|	СерииНоменклатуры.бг_ЗаявлениеОВыдачеФСМ КАК ЗаявлениеОВыдачеФСМ_ЗначениеРеквизитаИдентификатор,
	|	СерииНоменклатуры.Ссылка КАК Идентификатор,
	|	ЕСТЬNULL(Справки1ЕГАИС.бг_Крепость, 0) КАК КрепостьСправки1,
	|	СерииНоменклатуры.Наименование КАК Наименование,
	|	СерииНоменклатуры.бг_НакладнаяНаВыдачуФСМ КАК НакладнаяНаВыдачуФСМ_ЗначениеРеквизитаИдентификатор,
	|	СерииНоменклатуры.Номер КАК Номер,
	|	ЕСТЬNULL(Справки1ЕГАИС.бг_НомерГТД, ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)) КАК НомерГТДСправки1_ЗначениеРеквизитаИдентификатор,
	|	ЕСТЬNULL(НомераГТДСправки1.Код, """") КАК НомерГТДСправки1_ЗначениеРеквизитаКод,
	|	СерииНоменклатуры.бг_ОрганизацияЕГАИСВладелец КАК ОрганизацияЕГАИСВладелец_ЗначениеРеквизитаИдентификатор,
	|	КлассификаторОрганизацийЕГАИС.Код КАК ОрганизацияЕГАИСВладелец_ЗначениеРеквизитаКод,
	|	СерииНоменклатуры.бг_ОрганизацияВладелец КАК ОрганизацияВладелец_ЗначениеРеквизитаИдентификатор,
	|	СерииНоменклатуры.ПометкаУдаления КАК ПометкаУдаления,
	|	ЕСТЬNULL(СерииНоменклатуры.бг_ОрганизацияВладелец.Префикс, """") КАК ПрефиксОрганизации,
	|	СерииНоменклатуры.ПроизводительЕГАИС КАК ПроизводительЕГАИС_ЗначениеРеквизитаИдентификатор,
	|	КлассификаторОрганизацийЕГАИСПоПроизводителю.Код КАК ПроизводительЕГАИС_ЗначениеРеквизитаКод,
	|	СерииНоменклатуры.бг_Спецификация КАК Спецификация_ЗначениеРеквизитаИдентификатор,
	|	СерииНоменклатуры.Справка2ЕГАИС КАК Справка2ЕГАИС_ЗначениеРеквизитаИдентификатор,
	|	ЕСТЬNULL(СтраныМираПоГТДСерии.Ссылка, ЕСТЬNULL(СтраныМираПоПроизводителю.Ссылка, ЕСТЬNULL(СтраныМираПоНомеруГТДСправки1.Ссылка, ВЫБОР
	|					КОГДА СерииНоменклатуры.ВидНоменклатуры = &ВидНоменклатурыИмпортныеССП
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|				КОНЕЦ))) КАК СтранаПроисхождения_ЗначениеРеквизитаИдентификатор,
	|	ЕСТЬNULL(СтраныМираПоГТДСерии.КодАльфа2, ЕСТЬNULL(СтраныМираПоПроизводителю.КодАльфа2, ЕСТЬNULL(СтраныМираПоНомеруГТДСправки1.КодАльфа2, ВЫБОР
	|					КОГДА СерииНоменклатуры.ВидНоменклатуры = &ВидНоменклатурыИмпортныеССП
	|						ТОГДА """"
	|					ИНАЧЕ ""RU""
	|				КОНЕЦ))) КАК СтранаПроисхождения_ЗначениеРеквизитаКодАльфа2,
	|	ЕСТЬNULL(СтраныМираПоГТДСерии.Код, ЕСТЬNULL(СтраныМираПоПроизводителю.Код, ЕСТЬNULL(СтраныМираПоНомеруГТДСправки1.Код, ВЫБОР
	|					КОГДА СерииНоменклатуры.ВидНоменклатуры = &ВидНоменклатурыИмпортныеССП
	|						ТОГДА """"
	|					ИНАЧЕ ""643""
	|				КОНЕЦ))) КАК СтранаПроисхождения_ЗначениеРеквизитаКод,
	|	ЕСТЬNULL(ВидыАлкогольнойПродукцииПоНоменклатуре.ВидЛицензии = ЗНАЧЕНИЕ(Перечисление.ВидыЛицензийАлкогольнойПродукции.АлкогольнаяПродукция)
	|			И ВидыНоменклатуры.АлкогольнаяПродукция, ЛОЖЬ) КАК ЭтоАлкогольнаяПродукция,
	|	СерииНоменклатуры.ВидНоменклатуры = &ВидНоменклатурыИмпортныеССП КАК ЭтоИмпортныеССП,
	|	ЕСТЬNULL(ВидыАлкогольнойПродукцииПоНоменклатуре.Маркируемый, ЛОЖЬ) КАК ЭтоМаркируемаяАлкогольнаяПродукция,
	|	СерииНоменклатуры.бг_ДокументГенераторСерии КАК бг_ДокументГенераторСерии
	|ИЗ
	|	Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыАлкогольнойПродукции КАК ВидыАлкогольнойПродукцииПоНоменклатуре
	|			ПО Номенклатура.ВидАлкогольнойПродукции = ВидыАлкогольнойПродукцииПоНоменклатуре.Ссылка
	|		ПО СерииНоменклатуры.бг_Номенклатура = Номенклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИСПоПроизводителю
	|		ПО СерииНоменклатуры.ПроизводительЕГАИС = КлассификаторОрганизацийЕГАИСПоПроизводителю.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМираПоПроизводителю
	|		ПО (КлассификаторОрганизацийЕГАИСПоПроизводителю.КодСтраны = СтраныМираПоПроизводителю.бг_КодСтраныЧислом)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.бг_СопроводительныеДокументы КАК бг_СопроводительныеДокументы
	|		ПО СерииНоменклатуры.Ссылка = бг_СопроводительныеДокументы.Владелец
	|			И (бг_СопроводительныеДокументы.Вид = &ВидДокументаУдостоверенияКачества)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО СерииНоменклатуры.ВидНоменклатуры = ВидыНоменклатуры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК НомераГТДСерии
	|		ПО СерииНоменклатуры.бг_НомерГТД = НомераГТДСерии.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СертификатыНоменклатуры КАК СертификатыНоменклатуры
	|		ПО СерииНоменклатуры.бг_Сертификат = СертификатыНоменклатуры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМираПоСтранеНазначения
	|		ПО СерииНоменклатуры.бг_СтранаНазначения = СтраныМираПоСтранеНазначения.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
	|		ПО СерииНоменклатуры.бг_УпаковкаПаллета = УпаковкиЕдиницыИзмерения.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Справки2ЕГАИС КАК Справки2ЕГАИС
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИСПоСправке2
	|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыАлкогольнойПродукции КАК ВидыАлкогольнойПродукцииПоСправке2
	|				ПО КлассификаторАлкогольнойПродукцииЕГАИСПоСправке2.ВидПродукции = ВидыАлкогольнойПродукцииПоСправке2.Ссылка
	|			ПО Справки2ЕГАИС.АлкогольнаяПродукция = КлассификаторАлкогольнойПродукцииЕГАИСПоСправке2.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Справки1ЕГАИС КАК Справки1ЕГАИС
	|			ПО Справки2ЕГАИС.Справка1 = Справки1ЕГАИС.Ссылка
	|		ПО СерииНоменклатуры.Справка2ЕГАИС = Справки2ЕГАИС.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|		ПО СерииНоменклатуры.бг_ОрганизацияЕГАИСВладелец = КлассификаторОрганизацийЕГАИС.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМираПоГТДСерии
	|		ПО (НомераГТДСерии.СтранаПроисхождения = СтраныМираПоГТДСерии.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК НомераГТДСправки1
	|		ПО (Справки1ЕГАИС.бг_НомерГТД = НомераГТДСправки1.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМираПоНомеруГТДСправки1
	|		ПО (НомераГТДСправки1.СтранаПроисхождения = СтраныМираПоНомеруГТДСправки1.Ссылка)
	|ГДЕ
	|	СерииНоменклатуры.Ссылка = &Ссылка";
	
	// КодЕК_ПОСМ
	ТекстПоиска = "Номенклатура.Код КАК СТРОКА(999)";
	ТекстЗамены = СтрШаблон(
			"Номенклатура.Код КАК СТРОКА(%1)",
			Формат(бг_НоменклатураИнтеграция.ДлинаКодаЕК_ПОСМ(), "ЧГ=0"));
			
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ТекстПоиска, ТекстЗамены);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция КлючМаршрутизации(ДанныеШапки, ДанныеСообщения)
	
	Перем адаптер_ОбработчикиСобытийСтандартный;
	адаптер_ОбработчикиСобытийСтандартный = ОбщегоНазначения.ОбщийМодуль("адаптер_ОбработчикиСобытийСтандартный");
	
	Если ДанныеШапки.Свойство("ПрефиксОрганизации") Тогда
		ПрефиксОрганизации = СокрЛП(ДанныеШапки.ПрефиксОрганизации);
	Иначе
		ПрефиксОрганизации = "";
	КонецЕсли;
	
	Если ДанныеШапки.Свойство("ВариантСинхронизацииWMS") Тогда
		ВариантСинхронизацииWMS = ДанныеШапки.ВариантСинхронизацииWMS;
	Иначе
		ВариантСинхронизацииWMS = Неопределено;
	КонецЕсли;
	
	ДокументГенераторСерии = Неопределено;
	Если ДанныеШапки.Свойство("бг_ДокументГенераторСерии") Тогда
		ДокументГенераторСерии = ДанныеШапки.бг_ДокументГенераторСерии;
	КонецЕсли;
	
	КлючМаршрутизации = адаптер_ОбработчикиСобытийСтандартный.ПолучитьКлючМаршрутизацииИсходящегоСообщения(
		ДанныеСообщения);
	
	Если ЗначениеЗаполнено(ПрефиксОрганизации) Тогда
		КлючМаршрутизации = КлючМаршрутизации + "." + ПрефиксОрганизации;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВариантСинхронизацииWMS) Тогда
		КлючМаршрутизации = КлючМаршрутизации + ".WMS";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументГенераторСерии)
		И ТипЗнч(ДокументГенераторСерии) = Тип("ДокументСсылка.ТТНВходящаяЕГАИС") Тогда
		КлючМаршрутизации = КлючМаршрутизации + ".DocGenType_TTN";
	Иначе
		КлючМаршрутизации = КлючМаршрутизации + ".DocGenType_empty";
	КонецЕсли;
	
	Возврат КлючМаршрутизации;
	
КонецФункции

#КонецОбласти
