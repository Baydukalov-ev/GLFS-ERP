#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		Или ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;

	ВременнаяТаблицаЗаписейДоИзменения();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка
		Или ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;

	ВременнаяТаблицаИзменений();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВременнаяТаблицаЗаписейДоИзменения()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОперацииПоФакторингу.Период КАК Период,
	|	ОперацииПоФакторингу.Организация КАК Организация,
	|	ОперацииПоФакторингу.Контрагент КАК Контрагент,
	|	ОперацииПоФакторингу.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ОперацииПоФакторингу.Фактор КАК Фактор,
	|	ОперацииПоФакторингу.ДоговорФактора КАК ДоговорФактора,
	|	ОперацииПоФакторингу.ДокументРеализации КАК ДокументРеализации,
	|	ОперацииПоФакторингу.ВидРасчетов КАК ВидРасчетов,
	|	ОперацииПоФакторингу.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ОперацииПоФакторингу.ПервичныйДокумент КАК ПервичныйДокумент,
	|	ОперацииПоФакторингу.ВидОперации КАК ВидОперации,
	|	ОперацииПоФакторингу.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ОперацииПоФакторингу.СуммаРегл КАК СуммаРегл,
	|	РеквизитыФакторинга.Подразделение,
	|	РеквизитыФакторинга.СтатьяДоходов,
	|	РеквизитыФакторинга.СтатьяРасходов,
	|	РеквизитыФакторинга.КорСтатьяАктивовЗадолженностьКлиентов
	|ПОМЕСТИТЬ бг_ОперацииПоФакторингуПередЗаписью
	|ИЗ
	|	РегистрНакопления.бг_ОперацииПоФакторингу КАК ОперацииПоФакторингу
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.битФакторинг КАК РеквизитыФакторинга
	|		ПО ОперацииПоФакторингу.Регистратор = РеквизитыФакторинга.Ссылка
	|ГДЕ
	|	ОперацииПоФакторингу.Регистратор = &Регистратор";
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ВременнаяТаблицаИзменений()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Период КАК Период,
	|	ТаблицаИзменений.Организация КАК Организация,
	|	ТаблицаИзменений.Контрагент КАК Контрагент,
	|	ТаблицаИзменений.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ТаблицаИзменений.Фактор КАК Фактор,
	|	ТаблицаИзменений.ДоговорФактора КАК ДоговорФактора,
	|	ТаблицаИзменений.ДокументРеализации КАК ДокументРеализации,
	|	ТаблицаИзменений.ВидРасчетов КАК ВидРасчетов,
	|	ТаблицаИзменений.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ТаблицаИзменений.ПервичныйДокумент КАК ПервичныйДокумент,
	|	ТаблицаИзменений.ВидОперации КАК ВидОперации,
	|	ТаблицаИзменений.Подразделение КАК Подразделение,
	|	ТаблицаИзменений.СтатьяДоходов КАК СтатьяДоходов,
	|	ТаблицаИзменений.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаИзменений.КорСтатьяАктивовЗадолженностьКлиентов КАК КорСтатьяАктивовЗадолженностьКлиентов,
	|	СУММА(ТаблицаИзменений.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов,
	|	СУММА(ТаблицаИзменений.СуммаРегл) КАК СуммаРегл
	|ПОМЕСТИТЬ ТаблицаИзмененийбг_ОперацииПоФакторингу
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОперацииПоФакторингу.Период КАК Период,
	|		ОперацииПоФакторингу.Организация КАК Организация,
	|		ОперацииПоФакторингу.Контрагент КАК Контрагент,
	|		ОперацииПоФакторингу.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|		ОперацииПоФакторингу.Фактор КАК Фактор,
	|		ОперацииПоФакторингу.ДоговорФактора КАК ДоговорФактора,
	|		ОперацииПоФакторингу.ДокументРеализации КАК ДокументРеализации,
	|		ОперацииПоФакторингу.ВидРасчетов КАК ВидРасчетов,
	|		ОперацииПоФакторингу.ОбъектРасчетов КАК ОбъектРасчетов,
	|		ОперацииПоФакторингу.ПервичныйДокумент КАК ПервичныйДокумент,
	|		ОперацииПоФакторингу.ВидОперации КАК ВидОперации,
	|		ОперацииПоФакторингу.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|		ОперацииПоФакторингу.СуммаРегл КАК СуммаРегл,
	|		ОперацииПоФакторингу.Подразделение КАК Подразделение,
	|		ОперацииПоФакторингу.СтатьяДоходов КАК СтатьяДоходов,
	|		ОперацииПоФакторингу.СтатьяРасходов КАК СтатьяРасходов,
	|		ОперацииПоФакторингу.КорСтатьяАктивовЗадолженностьКлиентов КАК КорСтатьяАктивовЗадолженностьКлиентов
	|	ИЗ
	|		бг_ОперацииПоФакторингуПередЗаписью КАК ОперацииПоФакторингу
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ОперацииПоФакторингу.Период КАК Период,
	|		ОперацииПоФакторингу.Организация,
	|		ОперацииПоФакторингу.Контрагент,
	|		ОперацииПоФакторингу.ДоговорКонтрагента,
	|		ОперацииПоФакторингу.Фактор,
	|		ОперацииПоФакторингу.ДоговорФактора,
	|		ОперацииПоФакторингу.ДокументРеализации,
	|		ОперацииПоФакторингу.ВидРасчетов,
	|		ОперацииПоФакторингу.ОбъектРасчетов,
	|		ОперацииПоФакторингу.ПервичныйДокумент,
	|		ОперацииПоФакторингу.ВидОперации,
	|		-ОперацииПоФакторингу.СуммаВзаиморасчетов,
	|		-ОперацииПоФакторингу.СуммаРегл,
	|		РеквизитыФакторинга.Подразделение,
	|		РеквизитыФакторинга.СтатьяДоходов,
	|		РеквизитыФакторинга.СтатьяРасходов,
	|		РеквизитыФакторинга.КорСтатьяАктивовЗадолженностьКлиентов
	|	ИЗ
	|		РегистрНакопления.бг_ОперацииПоФакторингу КАК ОперацииПоФакторингу
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.битФакторинг КАК РеквизитыФакторинга
	|			ПО ОперацииПоФакторингу.Регистратор = РеквизитыФакторинга.Ссылка
	|	ГДЕ
	|		ОперацииПоФакторингу.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Период,
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.Контрагент,
	|	ТаблицаИзменений.ДоговорКонтрагента,
	|	ТаблицаИзменений.Фактор,
	|	ТаблицаИзменений.ДоговорФактора,
	|	ТаблицаИзменений.ДокументРеализации,
	|	ТаблицаИзменений.ВидРасчетов,
	|	ТаблицаИзменений.ОбъектРасчетов,
	|	ТаблицаИзменений.ПервичныйДокумент,
	|	ТаблицаИзменений.ВидОперации,
	|	ТаблицаИзменений.Подразделение,
	|	ТаблицаИзменений.СтатьяДоходов,
	|	ТаблицаИзменений.СтатьяРасходов,
	|	ТаблицаИзменений.КорСтатьяАктивовЗадолженностьКлиентов
	|ИМЕЮЩИЕ
	|	НЕ СУММА(ТаблицаИзменений.СуммаВзаиморасчетов) = 0
	|	ИЛИ НЕ СУММА(ТаблицаИзменений.СуммаРегл) = 0";
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли