#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает текст запроса для определения состояние набора перез записью.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстЗапросаСостояниеНабораПередЗаписью() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	АкцизПоПриобретеннымЦенностям.Период КАК Период,
	|	АкцизПоПриобретеннымЦенностям.Регистратор КАК Регистратор,
	|	АкцизПоПриобретеннымЦенностям.НомерСтроки КАК НомерСтроки,
	|	АкцизПоПриобретеннымЦенностям.Активность КАК Активность,
	|	АкцизПоПриобретеннымЦенностям.ВидДвижения КАК ВидДвижения,
	|	АкцизПоПриобретеннымЦенностям.Организация КАК Организация,
	|	АкцизПоПриобретеннымЦенностям.Номенклатура КАК Номенклатура,
	|	АкцизПоПриобретеннымЦенностям.СерияНоменклатуры КАК СерияНоменклатуры,
	|	АкцизПоПриобретеннымЦенностям.Сделка КАК Сделка,
	|	АкцизПоПриобретеннымЦенностям.СостояниеСырья КАК СостояниеСырья,
	|	АкцизПоПриобретеннымЦенностям.СтатусАкциза КАК СтатусАкциза,
	|	АкцизПоПриобретеннымЦенностям.Продукция КАК Продукция,
	|	АкцизПоПриобретеннымЦенностям.СерияПродукции КАК СерияПродукции,
	|	АкцизПоПриобретеннымЦенностям.Количество КАК Количество
	|ПОМЕСТИТЬ Движениябг_АкцизПоПриобретеннымЦенностям
	|ИЗ
	|	РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям КАК АкцизПоПриобретеннымЦенностям
	|ГДЕ
	|	АкцизПоПриобретеннымЦенностям.Регистратор = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст запроса для определения измений нового набора относительно текущего с учетом накопленных изменений
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстЗапросаИзменениеНабораОтносительноДанныхПередЗаписью() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МИНИМУМ(ВложенныйЗапрос.Период) КАК Период,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.СерияНоменклатуры КАК СерияНоменклатуры,
	|	ВложенныйЗапрос.Сделка КАК Сделка,
	|	ВложенныйЗапрос.Продукция КАК Продукция,
	|	ВложенныйЗапрос.СерияПродукции КАК СерияПродукции,
	|	ВложенныйЗапрос.СостояниеСырья КАК СостояниеСырья,
	|	ВложенныйЗапрос.СтатусАкциза КАК СтатусАкциза,
	|	СУММА(ВложенныйЗапрос.КоличествоИзменение) КАК КоличествоИзменение,
	|	ВложенныйЗапрос.Количество КАК Количество
	|ПОМЕСТИТЬ Движениябг_АкцизПоПриобретеннымЦенностямИзменения
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаИзменений.Период КАК Период,
	|		ТаблицаИзменений.Организация КАК Организация,
	|		ТаблицаИзменений.Номенклатура КАК Номенклатура,
	|		ТаблицаИзменений.СерияНоменклатуры КАК СерияНоменклатуры,
	|		ТаблицаИзменений.Сделка КАК Сделка,
	|		ТаблицаИзменений.Продукция КАК Продукция,
	|		ТаблицаИзменений.СерияПродукции КАК СерияПродукции,
	|		ТаблицаИзменений.СостояниеСырья КАК СостояниеСырья,
	|		ТаблицаИзменений.СтатусАкциза КАК СтатусАкциза,
	|		СУММА(ТаблицаИзменений.КоличествоИзменение) КАК КоличествоИзменение,
	|		СУММА(ТаблицаИзменений.Количество) КАК Количество
	|	ИЗ
	|		(ВЫБРАТЬ
	|			КОНЕЦПЕРИОДА(Таблица.Период, МЕСЯЦ) КАК Период,
	|			Таблица.Организация КАК Организация,
	|			Таблица.Номенклатура КАК Номенклатура,
	|			Таблица.СерияНоменклатуры КАК СерияНоменклатуры,
	|			Таблица.Сделка КАК Сделка,
	|			Таблица.СостояниеСырья КАК СостояниеСырья,
	|			Таблица.СтатусАкциза КАК СтатусАкциза,
	|			Таблица.Продукция КАК Продукция,
	|			Таблица.СерияПродукции КАК СерияПродукции,
	|			ВЫБОР Таблица.ВидДвижения
	|				КОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|					ТОГДА Таблица.Количество
	|				ИНАЧЕ -Таблица.Количество
	|			КОНЕЦ КАК КоличествоИзменение,
	|			0 КАК Количество
	|		ИЗ
	|			Движениябг_АкцизПоПриобретеннымЦенностям КАК Таблица
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			КОНЕЦПЕРИОДА(Таблица.Период, МЕСЯЦ),
	|			Таблица.Организация,
	|			Таблица.Номенклатура,
	|			Таблица.СерияНоменклатуры,
	|			Таблица.Сделка,
	|			Таблица.СостояниеСырья,
	|			Таблица.СтатусАкциза,
	|			Таблица.Продукция,
	|			Таблица.СерияПродукции,
	|			0,
	|			ВЫБОР Таблица.ВидДвижения
	|				КОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|					ТОГДА -Таблица.Количество
	|				ИНАЧЕ Таблица.Количество
	|			КОНЕЦ
	|		ИЗ
	|			РегистрНакопления.бг_АкцизПоПриобретеннымЦенностям КАК Таблица
	|		ГДЕ
	|			Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаИзменений.Период,
	|		ТаблицаИзменений.Организация,
	|		ТаблицаИзменений.Номенклатура,
	|		ТаблицаИзменений.СерияНоменклатуры,
	|		ТаблицаИзменений.Сделка,
	|		ТаблицаИзменений.Продукция,
	|		ТаблицаИзменений.СерияПродукции,
	|		ТаблицаИзменений.СостояниеСырья,
	|		ТаблицаИзменений.СтатусАкциза
	|	
	|	ИМЕЮЩИЕ
	|		СУММА(ТаблицаИзменений.КоличествоИзменение) <> 0) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.СерияНоменклатуры,
	|	ВложенныйЗапрос.Сделка,
	|	ВложенныйЗапрос.Продукция,
	|	ВложенныйЗапрос.СерияПродукции,
	|	ВложенныйЗапрос.СостояниеСырья,
	|	ВложенныйЗапрос.СтатусАкциза,
	|	ВложенныйЗапрос.Количество";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
		Или ДополнительныеСвойства.Свойство("ПересчетДвижений") Тогда
		Возврат;
	КонецЕсли;
	
	БлокироватьДляИзменения = Истина;
	МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	Если МенеджерВременныхТаблиц.Таблицы.Найти("Движениябг_АкцизПоПриобретеннымЦенностям") = Неопределено Тогда
		Регистратор = Отбор.Регистратор.Значение;
		
		ПараметрыКонтроля = Новый Структура;
		Запрос = Новый Запрос(ТекстЗапросаСостояниеНабораПередЗаписью());
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Ссылка", Регистратор);
		Запрос.Выполнить();
		
		ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
			"Движениябг_АкцизПоПриобретеннымЦенностямИзменения", Ложь, ПараметрыКонтроля);
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	Если ОбменДанными.Загрузка
		Или Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
		Или ДополнительныеСвойства.Свойство("ПересчетДвижений") Тогда
		Возврат;
	КонецЕсли;
	
	Регистратор = Отбор.Регистратор.Значение;
	
	Запрос = Новый Запрос(ТекстЗапросаИзменениеНабораОтносительноДанныхПередЗаписью());
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	ДополнительныеСвойства.ТаблицыКонтроля.Движениябг_АкцизПоПриобретеннымЦенностямИзменения.ЕстьЗаписиВТаблице =
																Выборка.Следующий() И Выборка.Количество > 0;
КонецПроцедуры

#КонецОбласти

#КонецЕсли
