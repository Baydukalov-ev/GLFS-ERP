#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

&После("ПриЗаписи")
Процедура бг_ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка
		И Не (ДополнительныеСвойства.Свойство("ЗаменаСсылок") И ДополнительныеСвойства.ЗаменаСсылок) Тогда
		Возврат;
	КонецЕсли;

	УстановитьПривилегированныйРежим(Истина);
	
	Если ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		бг_ЗаписатьИзмененияСвободныхОстатковДляИМ();
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура бг_ЗаписатьИзмененияСвободныхОстатковДляИМ()

	Если Не ДополнительныеСвойства.ТаблицыКонтроля.ДвиженияРаспределениеЗапасовДвиженияИзменение.ЕстьЗаписиВТаблице Тогда
		Возврат;	
	КонецЕсли;	
	
	Если Не бг_КонстантыПовтИсп.ФункциональнаяОпцияИспользуется("ИспользоватьВыгрузкуСвободныхОстатковВSAP") Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИзмененияДвижений.Номенклатура КАК Номенклатура,
	|	ИзмененияДвижений.Характеристика КАК Характеристика,
	|	ИзмененияДвижений.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(ИзмененияДвижений.ЗаказНаОтгрузку КАК Документ.ЗаказКлиента).бг_ИсточникЗаказа = ЗНАЧЕНИЕ(Перечисление.бг_ИсточникиЗагрузкиЗаказовКлиентов.WINELAB_HYBRIS)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.бг_ПричиныИзмененияСвободныхОстатковДляИМ.ИнтернетЗаказ)
	|		КОГДА ВЫРАЗИТЬ(ИзмененияДвижений.ЗаказНаОтгрузку КАК Документ.ЗаказКлиента).бг_ИсточникЗаказа = ЗНАЧЕНИЕ(Перечисление.бг_ИсточникиЗагрузкиЗаказовКлиентов.WINELAB_SAP_СONSOLIDATED)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.бг_ПричиныИзмененияСвободныхОстатковДляИМ.ИнтернетЗаказ)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.бг_ПричиныИзмененияСвободныхОстатковДляИМ.ПрочиеОперации)
	|	КОНЕЦ КАК ПричинаИзменения,
	|	СУММА(ИзмененияДвижений.Поступило - (ИзмененияДвижений.Отгрузить + ИзмененияДвижений.Резервировать)) КАК Свободно
	|ИЗ
	|	ДвиженияРаспределениеЗапасовДвиженияИзменение КАК ИзмененияДвижений
	|ГДЕ
	|	НЕ ЕСТЬNULL(ВЫРАЗИТЬ(ИзмененияДвижений.ЗаказНаОтгрузку КАК Документ.ЗаказКлиента).бг_ДолгосрочныйРезерв, ЛОЖЬ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмененияДвижений.Номенклатура,
	|	ИзмененияДвижений.Характеристика,
	|	ИзмененияДвижений.Склад,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(ИзмененияДвижений.ЗаказНаОтгрузку КАК Документ.ЗаказКлиента).бг_ИсточникЗаказа = ЗНАЧЕНИЕ(Перечисление.бг_ИсточникиЗагрузкиЗаказовКлиентов.WINELAB_HYBRIS)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.бг_ПричиныИзмененияСвободныхОстатковДляИМ.ИнтернетЗаказ)
	|		КОГДА ВЫРАЗИТЬ(ИзмененияДвижений.ЗаказНаОтгрузку КАК Документ.ЗаказКлиента).бг_ИсточникЗаказа = ЗНАЧЕНИЕ(Перечисление.бг_ИсточникиЗагрузкиЗаказовКлиентов.WINELAB_SAP_СONSOLIDATED)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.бг_ПричиныИзмененияСвободныхОстатковДляИМ.ИнтернетЗаказ)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.бг_ПричиныИзмененияСвободныхОстатковДляИМ.ПрочиеОперации)
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	СУММА(ИзмененияДвижений.Поступило - (ИзмененияДвижений.Отгрузить + ИзмененияДвижений.Резервировать)) <> 0";

	Таблица = Запрос.Выполнить().Выгрузить();
	Таблица.Колонки.Добавить("РазделительЗаписи");
	РазделительЗаписи = Новый УникальныйИдентификатор();
	Таблица.ЗаполнитьЗначения(РазделительЗаписи, "РазделительЗаписи");
	Таблица.Колонки.Добавить("ДатаЗаписи");
	Таблица.ЗаполнитьЗначения(ТекущаяДатаСеанса(), "ДатаЗаписи");
	
	Набор = РегистрыСведений.бг_ИзмененияСвободныхОстатковДляИМ.СоздатьНаборЗаписей();
	Набор.Загрузить(Таблица);
	Набор.Отбор.РазделительЗаписи.Установить(РазделительЗаписи);
	Набор.Записать(Ложь);
	
КонецПроцедуры	

#КонецОбласти

#КонецЕсли

