#Область СлужебныеПроцедурыИФункции

&Вместо("ИспользованиеЭлементов")
Функция бг_ИспользованиеЭлементов(ПереданныйОбъект, РежимВидимостиПоказатьТолькоВажные, РежимПроверкиИспользованияРеквизитов)
	
	Результат = ПродолжитьВызов(ПереданныйОбъект, РежимВидимостиПоказатьТолькоВажные, РежимПроверкиИспользованияРеквизитов);
	
	ВидНоменклатуры = Неопределено;
	
	Если ТипЗнч(ПереданныйОбъект.Ссылка) = Тип("СправочникСсылка.Номенклатура") Тогда
		ВидНоменклатуры = ПереданныйОбъект.ВидНоменклатуры;
	ИначеЕсли ТипЗнч(ПереданныйОбъект.Ссылка) = Тип("СправочникСсылка.ВидыНоменклатуры") Тогда  	
		ВидНоменклатуры = ПереданныйОбъект.Ссылка;
	КонецЕсли; 
	
	Если ВидНоменклатуры = бг_КонстантыПовтИсп.ЗначениеКонстанты("ФедеральнаяСпецМарка") Тогда
		
		Результат.ВидАлкогольнойПродукции = Истина;
		Результат.ОбъемДАЛ                = Истина;
		Результат.Крепость                = Истина;
		
		Если Не РежимПроверкиИспользованияРеквизитов Тогда	
			Результат.ДекорацияДекалитры = Результат.ОбъемДАЛ;
			Результат.ДекорацияПроцентыКрепости = Результат.ВидАлкогольнойПродукции;
		КонецЕсли;
		
	КонецЕсли; 	
	
	Возврат Результат;
	
КонецФункции

Функция бг_ЧастиНаименования() Экспорт
	
	ЧастиНаименования = Новый Массив;
	
	ЧастиНаименования.Добавить("бг_ОсновноеНаименование");
	ЧастиНаименования.Добавить("бг_Вариативность");
	ЧастиНаименования.Добавить("бг_ГодУрожая");
	ЧастиНаименования.Добавить("Крепость");
	ЧастиНаименования.Добавить("ВидНоменклатуры");	
	
	Возврат ЧастиНаименования;
	
КонецФункции

Функция бг_НаименованиеИзЧастей(ОбъектИлиЗначенияЧастей) Экспорт
		
	бг_НеФормироватьНаименованиеПоШаблону = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
												ОбъектИлиЗначенияЧастей.ВидНоменклатуры,
												"бг_НеФормироватьНаименованиеПоШаблону"); 
	
	Если бг_НеФормироватьНаименованиеПоШаблону Тогда 
		
		Наименование = ОбъектИлиЗначенияЧастей.бг_ОсновноеНаименование;
		
	Иначе		
		
		Если ЗначениеЗаполнено(ОбъектИлиЗначенияЧастей.бг_ОсновноеНаименование)
			И ЗначениеЗаполнено(ОбъектИлиЗначенияЧастей.бг_Вариативность) Тогда
			
			ЧастиНаименования = Новый Массив;
			ЧастиНаименования.Добавить(ОбъектИлиЗначенияЧастей.бг_ОсновноеНаименование);
			ЧастиНаименования.Добавить(ОбъектИлиЗначенияЧастей.бг_Вариативность);
			
			Наименования = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ЧастиНаименования, "Наименование");
			
			ОсновноеНаименование = Наименования.Получить(ОбъектИлиЗначенияЧастей.бг_ОсновноеНаименование);
			Вариативность = Наименования.Получить(ОбъектИлиЗначенияЧастей.бг_Вариативность);
			
		ИначеЕсли ЗначениеЗаполнено(ОбъектИлиЗначенияЧастей.бг_ОсновноеНаименование) Тогда
			ОсновноеНаименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектИлиЗначенияЧастей.бг_ОсновноеНаименование, "Наименование");
			Вариативность = "";
		ИначеЕсли ЗначениеЗаполнено(ОбъектИлиЗначенияЧастей.бг_Вариативность) Тогда
			ОсновноеНаименование = "";
			Вариативность = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектИлиЗначенияЧастей.бг_Вариативность, "Наименование");
		Иначе
			ОсновноеНаименование = "";
			Вариативность = "";
		КонецЕсли;
		
		МассивЧастей = Новый Массив;
		
		Если ЗначениеЗаполнено(ОсновноеНаименование) Тогда
			МассивЧастей.Добавить(ОсновноеНаименование + " ");
		Иначе
			МассивЧастей.Добавить("");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОбъектИлиЗначенияЧастей.бг_ГодУрожая) Тогда
			МассивЧастей.Добавить(Формат(ОбъектИлиЗначенияЧастей.бг_ГодУрожая, "ЧГ="));
		Иначе
			МассивЧастей.Добавить("");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОбъектИлиЗначенияЧастей.Крепость) Тогда
			МассивЧастей.Добавить("" + ОбъектИлиЗначенияЧастей.Крепость + "%");
		Иначе
			МассивЧастей.Добавить("");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Вариативность) Тогда
			МассивЧастей.Добавить(Вариативность);
		Иначе
			МассивЧастей.Добавить("");
		КонецЕсли;
		
		Наименование = СтрСоединить(МассивЧастей, "/");
		
	КонецЕсли;
	
	Возврат Наименование;
	
КонецФункции

&Вместо("ТаблицаНастроекРеквизитов")
Функция бг_ТаблицаНастроекРеквизитов(ВидНоменклатуры,
								ТипНоменклатуры,
								ОсобенностьУчета,
								ИспользованиеХарактеристик,
								ИспользоватьСрокГодностиСерии,
								ВладелецРеквизитов) Экспорт
	

	Таблица = ПродолжитьВызов(ВидНоменклатуры,
								ТипНоменклатуры,
								ОсобенностьУчета,
								ИспользованиеХарактеристик,
								ИспользоватьСрокГодностиСерии,
								ВладелецРеквизитов);								
								
								
	НайденнаяСтрока = Таблица.Найти("бг_УпаковкаПаллета", "ИмяРеквизита");							
	Если НайденнаяСтрока <> Неопределено Тогда
		// Реквизита "бг_УпаковкаПаллета" нет у номенклатуры.
		Таблица.Удалить(НайденнаяСтрока);
	КонецЕсли;
	
	Возврат Таблица;
	
КонецФункции

Функция бг_ВыгружаетсяВWMS(ВидНоменклатуры) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(ВидНоменклатуры) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВариантСинхронизацииWMS = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидНоменклатуры, "бг_ВариантСинхронизацииWMS");
	
	Возврат ЗначениеЗаполнено(ВариантСинхронизацииWMS);

КонецФункции

Функция бг_ШтучнаяУпаковка(Номенклатура) Экспорт

	Если Не ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УпаковкиЕдиницыИзмерения.Ссылка КАК Упаковка
	|ИЗ
	|	Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|		ПО (УпаковкиЕдиницыИзмерения.Владелец = Номенклатура.Ссылка
	|				И Номенклатура.Ссылка = &Номенклатура
	|				И Номенклатура.ЕдиницаИзмерения = УпаковкиЕдиницыИзмерения.ЕдиницаИзмерения)
	|ГДЕ
	|	НЕ УпаковкиЕдиницыИзмерения.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.Упаковка;
	КонецЦикла;

КонецФункции

Функция бг_ПредставлениеТемпературыХранения(ТемператураОт, ТемператураДо, Влажность) Экспорт
	ШаблонТемператураОт = "";
	ШаблонТемператураДо = "";
	ШаблонВлажность = "";
	Если ЗначениеЗаполнено(ТемператураОт) Тогда
		Если ЗначениеЗаполнено(ТемператураДо) Тогда
			ШаблонТемператураОт = СтрШаблон(НСтр("ru = 'От %1°C'"), ТемператураОт);
		Иначе
			ШаблонТемператураОт = СтрШаблон(НСтр("ru = 'Не менее %1°C'"), ТемператураОт);
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(ТемператураДо) Тогда
		Если ЗначениеЗаполнено(ТемператураОт) Тогда
			ШаблонТемператураДо = СтрШаблон(НСтр("ru = 'до %1°C'"), ТемператураДо);
		Иначе
			ШаблонТемператураДо = СтрШаблон(НСтр("ru = 'Не более %1°C'"), ТемператураДо);
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(Влажность) Тогда
		ШаблонВлажность = СтрШаблон(НСтр("ru = ', при относительной влажности %1%2'"), Влажность, "%");
	КонецЕсли;
	ПредставлениеТемпературыХранения = СтрШаблон("%1 %2%3", 
			ШаблонТемператураОт, ШаблонТемператураДо, ШаблонВлажность);
	Возврат СокрЛП(СтрЗаменить(ПредставлениеТемпературыХранения, "  ", " "));
	
КонецФункции

#КонецОбласти
