#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверкаУникальностиИспользуемойНастройкиПоПечатнойФорме(Отказ);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверкаУникальностиИспользуемойНастройкиПоПечатнойФорме(Отказ)
	
	Если ПометкаУдаления 
		Или Не Используется Тогда
		Возврат;	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиФиксацииПечатныхФорм.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.бг_НастройкиФиксацииПечатныхФорм КАК НастройкиФиксацииПечатныхФорм
		|ГДЕ
		|	НастройкиФиксацииПечатныхФорм.ПечатнаяФорма = &ПечатнаяФорма
		|	И НастройкиФиксацииПечатныхФорм.Используется
		|	И НЕ НастройкиФиксацииПечатныхФорм.ПометкаУдаления
		|	И НастройкиФиксацииПечатныхФорм.Ссылка <> &Ссылка";

	Запрос.УстановитьПараметр("ПечатнаяФорма", ПечатнаяФорма);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонОшибки = НСтр("ru='Уже есть активная настройка для печатной формы %1. 
						|Пометьте на удаление дублирующиеся элементы справочника 
						|или отключите использование настройки'");
	
	ОбщегоНазначения.СообщитьПользователю(
		СтрШаблон(ШаблонОшибки, ПечатнаяФорма),, 
		"Объект.ПечатнаяФорма",, 
		Отказ); 
	
КонецПроцедуры

#КонецОбласти
	
#КонецЕсли