#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
	
// Функция - Отбирает документы по настройкам фиксации
//
// Параметры:
//  РаспечатанныеДокументы	- ТаблицаЗначений - Список всех распечатанных объектов
//	АдресПовторноФиксируемыхДокументов - Неопределено - Адрес, по которому поместятся 
//		вторично фиксируемые документы
// 
// Возвращаемое значение:
// ТаблицаЗначений 
//
Функция ДокументыПоНастройкамФиксации(РаспечатанныеДокументы, АдресПовторноФиксируемыхДокументов) Экспорт
	
	ФиксироватьПовторнуюПечать = Пользователи.РолиДоступны("бг_ФиксироватьПечатьДокументовБезКонтроля");
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОтобранныеДокументы = ИнициализироватьТаблицуОтобранныхПоНастройкамДокументов(РаспечатанныеДокументы);	
		
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	СформироватьВременнуюТаблицуСвязанныхДокументов(МенеджерВременныхТаблиц, РаспечатанныеДокументы);
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СвязанныеДокументы.Документ КАК Документ,
	|	СвязанныеДокументы.ОбъектПечати КАК ОбъектПечати,
	|	СвязанныеДокументы.ИдентификаторМакета КАК ИдентификаторМакета,
	|	СвязанныеДокументы.КоличествоСтраниц КАК КоличествоСтраниц,
	|	СвязанныеДокументы.ДокументКорректировки КАК ДокументКорректировки,
	|	СвязанныеДокументы.СчетФактураВыданный КАК СчетФактураВыданный,
	|	ФиксируемыеПечатныеФормы.ТипДокумента КАК ТипДокумента,
	|	ИдентификаторыМакетов.Ссылка КАК ПечатнаяФорма
	|ПОМЕСТИТЬ НайденныеПечатныеФормы
	|ИЗ
	|	СвязанныеДокументы КАК СвязанныеДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.бг_ФиксируемыеПечатныеФормы.ИдентификаторыМакетовПечатныхФорм КАК ИдентификаторыМакетов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.бг_ФиксируемыеПечатныеФормы КАК ФиксируемыеПечатныеФормы
	|			ПО ИдентификаторыМакетов.Ссылка = ФиксируемыеПечатныеФормы.Ссылка
	|		ПО СвязанныеДокументы.ИдентификаторМакета = ИдентификаторыМакетов.ИдентификаторМакета
	|			И (НЕ ФиксируемыеПечатныеФормы.ПометкаУдаления)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.бг_НастройкиФиксацииПечатныхФорм КАК НастройкиФиксацииПечатныхФорм
	|		ПО (ИдентификаторыМакетов.Ссылка = НастройкиФиксацииПечатныхФорм.ПечатнаяФорма)
	|			И (НастройкиФиксацииПечатныхФорм.Используется)
	|			И (НЕ НастройкиФиксацииПечатныхФорм.ПометкаУдаления)
	|ГДЕ
	|	(ВЫБОР
	|		КОГДА ФиксируемыеПечатныеФормы.КодТипаДокумента = &КодИсправленногоСчетаФактуры 
	|			ИЛИ ФиксируемыеПечатныеФормы.КодТипаДокумента = &КодКорректировочногоСчетаФактуры 
	|			ИЛИ ФиксируемыеПечатныеФормы.КодТипаДокумента = &КодСчетаФактуры
	|			ТОГДА ВЫБОР
	|				КОГДА СвязанныеДокументы.СчетФактураВыданный.Исправление
	|					ТОГДА ФиксируемыеПечатныеФормы.КодТипаДокумента = &КодИсправленногоСчетаФактуры
	|				КОГДА СвязанныеДокументы.СчетФактураВыданный.Корректировочный
	|					ТОГДА ФиксируемыеПечатныеФормы.КодТипаДокумента = &КодКорректировочногоСчетаФактуры
	|				ИНАЧЕ ФиксируемыеПечатныеФормы.КодТипаДокумента = &КодСчетаФактуры
	|				КОНЕЦ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ)
	|
	|СГРУППИРОВАТЬ ПО
	|	СвязанныеДокументы.ОбъектПечати,
	|	СвязанныеДокументы.ИдентификаторМакета,
	|	СвязанныеДокументы.КоличествоСтраниц,
	|	СвязанныеДокументы.Документ,
	|	СвязанныеДокументы.ДокументКорректировки,
	|	СвязанныеДокументы.СчетФактураВыданный,
	|	ФиксируемыеПечатныеФормы.ТипДокумента,
	|	ИдентификаторыМакетов.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НайденныеПечатныеФормы.Документ КАК Документ,
	|	НайденныеПечатныеФормы.ДокументКорректировки КАК ДокументКорректировки,
	|	НайденныеПечатныеФормы.СчетФактураВыданный КАК СчетФактураВыданный,
	|	НайденныеПечатныеФормы.ПечатнаяФорма КАК ПечатнаяФорма,
	|	НайденныеПечатныеФормы.ПечатнаяФорма.Наименование КАК ПечатнаяФормаНаименование,
	|	НайденныеПечатныеФормы.ОбъектПечати КАК ОбъектПечати,
	|	ВЫБОР
	|		КОГДА НайденныеПечатныеФормы.ОбъектПечати ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(НайденныеПечатныеФормы.ОбъектПечати КАК Документ.РеализацияТоваровУслуг).Организация
	|		КОГДА НайденныеПечатныеФормы.ОбъектПечати ССЫЛКА Документ.КорректировкаРеализации
	|			ТОГДА ВЫРАЗИТЬ(НайденныеПечатныеФормы.ОбъектПечати КАК Документ.КорректировкаРеализации).Организация
	|		КОГДА НайденныеПечатныеФормы.ОбъектПечати ССЫЛКА Документ.СчетФактураВыданный
	|			ТОГДА ВЫРАЗИТЬ(НайденныеПечатныеФормы.ОбъектПечати КАК Документ.СчетФактураВыданный).Организация
	|		КОГДА НайденныеПечатныеФормы.ОбъектПечати ССЫЛКА Документ.КорректировкаПриобретения
	|			ТОГДА ВЫРАЗИТЬ(НайденныеПечатныеФормы.ОбъектПечати КАК Документ.КорректировкаПриобретения).Организация
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	КОНЕЦ КАК Организация,
	|	ВЫБОР
	|		КОГДА НайденныеПечатныеФормы.ТипДокумента = &ИмяТипаДокументаСчетаФактуры
	|			ТОГДА ВЫРАЗИТЬ(НайденныеПечатныеФормы.СчетФактураВыданный КАК Документ.СчетФактураВыданный).ПредставлениеНомера
	|		КОГДА НайденныеПечатныеФормы.ТипДокумента = &ИмяТипаДокументаРеализации
	|			ТОГДА ВЫРАЗИТЬ(НайденныеПечатныеФормы.Документ КАК Документ.РеализацияТоваровУслуг).Номер
	|		КОГДА НайденныеПечатныеФормы.ТипДокумента = &ИмяТипаДокументаКорректировкиПриобретения
	|			ТОГДА ВЫРАЗИТЬ(НайденныеПечатныеФормы.ОбъектПечати КАК Документ.КорректировкаПриобретения).Номер
	|		КОГДА НайденныеПечатныеФормы.ТипДокумента = &ИмяТипаДокументаКорректировкиРеализации
	|			ТОГДА ВЫРАЗИТЬ(НайденныеПечатныеФормы.ДокументКорректировки КАК Документ.КорректировкаРеализации).Номер
	|	КОНЕЦ КАК РаспечатанныйНомер,
	|	НайденныеПечатныеФормы.ИдентификаторМакета КАК ИдентификаторМакета,
	|	НайденныеПечатныеФормы.КоличествоСтраниц КАК КоличествоСтраниц,
	|	ВЫБОР
	|		КОГДА 1 В
	|				(ВЫБРАТЬ
	|					1
	|				ИЗ
	|					РегистрСведений.бг_ФиксацияПечатиДокументов.СрезПоследних(, (Документ, ПечатнаяФорма) В
	|						(ВЫБРАТЬ
	|							НайденныеПечатныеФормы.Документ,
	|							НайденныеПечатныеФормы.ПечатнаяФорма
	|						ИЗ
	|							НайденныеПечатныеФормы)) КАК ФиксацияПечатиДокументовСрезПоследних
	|				ГДЕ
	|					ФиксацияПечатиДокументовСрезПоследних.Документ = НайденныеПечатныеФормы.Документ
	|					И ФиксацияПечатиДокументовСрезПоследних.ПечатнаяФорма = НайденныеПечатныеФормы.ПечатнаяФорма)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ПервичнаяПечать
	|ИЗ
	|	НайденныеПечатныеФормы КАК НайденныеПечатныеФормы";
	
	Запрос.УстановитьПараметр("РаспечатанныеДокументы", РаспечатанныеДокументы);
	Запрос.УстановитьПараметр("КодИсправленногоСчетаФактуры", "ИСФ");
	Запрос.УстановитьПараметр("КодКорректировочногоСчетаФактуры", "КСФ");
	Запрос.УстановитьПараметр("КодСчетаФактуры", "СФ");
	Запрос.УстановитьПараметр(
		"ИмяТипаДокументаРеализации", 
		Метаданные.Документы.РеализацияТоваровУслуг.Имя);
	Запрос.УстановитьПараметр(
		"ИмяТипаДокументаСчетаФактуры", 
		Метаданные.Документы.СчетФактураВыданный.Имя);
	Запрос.УстановитьПараметр(
		"ИмяТипаДокументаКорректировкиПриобретения", 
		Метаданные.Документы.КорректировкаПриобретения.Имя);
	Запрос.УстановитьПараметр(
		"ИмяТипаДокументаКорректировкиРеализации", 
		Метаданные.Документы.КорректировкаРеализации.Имя);

	Выборка = Запрос.Выполнить().Выбрать();
	
	ПовторноФиксируемыеДокументы = ОтобранныеДокументы.СкопироватьКолонки();
	
	Пока Выборка.Следующий() Цикл
		
		Если Не Выборка.ПервичнаяПечать Тогда
			
			Если ФиксироватьПовторнуюПечать Тогда
				ЗаполнитьЗначенияСвойств(ПовторноФиксируемыеДокументы.Добавить(), Выборка);	
			КонецЕсли;
			
			Продолжить;
			
		КонецЕсли;	
		
		ИспользоватьФиксациюПечати = Ложь;
		
		Если ЗначениеЗаполнено(Выборка.Организация) Тогда
			ИспользоватьФиксациюПечати = бг_КонстантыПовтИсп.ЗначениеКонстанты(
				"ФиксироватьПечатьДокументов", 
				Выборка.Организация);
		КонецЕсли;
			
		Если ИспользоватьФиксациюПечати Тогда
			ЗаполнитьЗначенияСвойств(ОтобранныеДокументы.Добавить(), Выборка);	
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПовторноФиксируемыеДокументы.Количество() <> 0 Тогда
		
		ДанныеПовторноФиксируемыхДокументов = ОбщегоНазначения.ТаблицаЗначенийВМассив(
			ПовторноФиксируемыеДокументы);
			
		АдресПовторноФиксируемыхДокументов = ПоместитьВоВременноеХранилище(ДанныеПовторноФиксируемыхДокументов);
		
	КонецЕсли;
	
	Возврат ОтобранныеДокументы;
		
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьВременнуюТаблицуСвязанныхДокументов(МенеджерВременныхТаблиц, РаспечатанныеДокументы)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РаспечатанныеДокументы.ОбъектПечати КАК ОбъектПечати,
	|	РаспечатанныеДокументы.ИдентификаторМакета КАК ИдентификаторМакета,
	|	РаспечатанныеДокументы.КоличествоСтраниц КАК КоличествоСтраниц
	|ПОМЕСТИТЬ РаспечатанныеДокументы
	|ИЗ
	|	&РаспечатанныеДокументы КАК РаспечатанныеДокументы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Документ,
	|	РаспечатанныеДокументы.ОбъектПечати КАК ОбъектПечати,
	|	РаспечатанныеДокументы.ИдентификаторМакета КАК ИдентификаторМакета,
	|	РаспечатанныеДокументы.КоличествоСтраниц КАК КоличествоСтраниц,
	|	NULL КАК ДокументКорректировки,
	|	СчетаФактурыВыданные.Ссылка КАК СчетФактураВыданный
	|ПОМЕСТИТЬ СвязанныеДокументы
	|ИЗ
	|	РаспечатанныеДокументы КАК РаспечатанныеДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО ((ВЫРАЗИТЬ(РаспечатанныеДокументы.ОбъектПечати КАК Документ.РеализацияТоваровУслуг)) = РеализацияТоваровУслуг.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МАКСИМУМ(СчетФактураВыданныйДокументыОснования.Ссылка) КАК Ссылка,
	|			ВЫРАЗИТЬ(СчетФактураВыданныйДокументыОснования.ДокументОснование КАК Документ.РеализацияТоваровУслуг) КАК Реализация
	|		ИЗ
	|			Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ГДЕ
	|			ВЫРАЗИТЬ(СчетФактураВыданныйДокументыОснования.ДокументОснование КАК Документ.РеализацияТоваровУслуг) В
	|					(ВЫБРАТЬ
	|						РаспечатанныеДокументы.ОбъектПечати
	|					ИЗ
	|						РаспечатанныеДокументы
	|					ГДЕ
	|						РаспечатанныеДокументы.ОбъектПечати ССЫЛКА Документ.РеализацияТоваровУслуг)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ВЫРАЗИТЬ(СчетФактураВыданныйДокументыОснования.ДокументОснование КАК Документ.РеализацияТоваровУслуг)) КАК СчетаФактурыВыданные
	|		ПО ((ВЫРАЗИТЬ(РаспечатанныеДокументы.ОбъектПечати КАК Документ.РеализацияТоваровУслуг)) = СчетаФактурыВыданные.Реализация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокКорректировкаРеализации.ДокументОснование,
	|	РаспечатанныеДокументы.ОбъектПечати,
	|	РаспечатанныеДокументы.ИдентификаторМакета,
	|	РаспечатанныеДокументы.КоличествоСтраниц,
	|	ДокКорректировкаРеализации.Ссылка,
	|	СчетаФактурыВыданные.Ссылка
	|ИЗ
	|	РаспечатанныеДокументы КАК РаспечатанныеДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК ДокКорректировкаРеализации
	|		ПО ((ВЫРАЗИТЬ(РаспечатанныеДокументы.ОбъектПечати КАК Документ.КорректировкаРеализации)) = ДокКорректировкаРеализации.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МАКСИМУМ(СчетФактураВыданныйДокументыОснования.Ссылка) КАК Ссылка,
	|			ВЫРАЗИТЬ(СчетФактураВыданныйДокументыОснования.ДокументОснование КАК Документ.КорректировкаРеализации) КАК КорректировкаРеализации
	|		ИЗ
	|			Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ГДЕ
	|			ВЫРАЗИТЬ(СчетФактураВыданныйДокументыОснования.ДокументОснование КАК Документ.КорректировкаРеализации) В
	|					(ВЫБРАТЬ
	|						РаспечатанныеДокументы.ОбъектПечати
	|					ИЗ
	|						РаспечатанныеДокументы
	|					ГДЕ
	|						РаспечатанныеДокументы.ОбъектПечати ССЫЛКА Документ.КорректировкаРеализации)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ВЫРАЗИТЬ(СчетФактураВыданныйДокументыОснования.ДокументОснование КАК Документ.КорректировкаРеализации)) КАК СчетаФактурыВыданные
	|		ПО ((ВЫРАЗИТЬ(РаспечатанныеДокументы.ОбъектПечати КАК Документ.КорректировкаРеализации)) = СчетаФактурыВыданные.КорректировкаРеализации)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СчетаФактурыВыданные.ДокументРеализации ЕСТЬ NULL
	|			ТОГДА ВЫРАЗИТЬ(СчетаФактурыВыданные.КорректировкаРеализации.ДокументОснование КАК Документ.РеализацияТоваровУслуг)
	|		КОГДА СчетаФактурыВыданные.КорректировкаРеализации ЕСТЬ NULL
	|			ТОГДА ВЫРАЗИТЬ(СчетаФактурыВыданные.ДокументРеализации КАК Документ.РеализацияТоваровУслуг)
	|	КОНЕЦ,
	|	РаспечатанныеДокументы.ОбъектПечати,
	|	РаспечатанныеДокументы.ИдентификаторМакета,
	|	РаспечатанныеДокументы.КоличествоСтраниц,
	|	СчетаФактурыВыданные.КорректировкаРеализации,
	|	СчетаФактурыВыданные.Ссылка
	|ИЗ
	|	РаспечатанныеДокументы КАК РаспечатанныеДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			СчетФактураВыданныйДокументыОснования.Ссылка КАК Ссылка,
	|			МАКСИМУМ(ВЫРАЗИТЬ(СчетФактураВыданныйДокументыОснования.ДокументОснование КАК Документ.РеализацияТоваровУслуг)) КАК ДокументРеализации,
	|			NULL КАК КорректировкаРеализации
	|		ИЗ
	|			РаспечатанныеДокументы КАК РаспечатанныеДокументы
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|				ПО ((ВЫРАЗИТЬ(РаспечатанныеДокументы.ОбъектПечати КАК Документ.СчетФактураВыданный)) = СчетФактураВыданныйДокументыОснования.Ссылка)
	|		ГДЕ
	|			СчетФактураВыданныйДокументыОснования.ДокументОснование ССЫЛКА Документ.РеализацияТоваровУслуг
	|		
	|		СГРУППИРОВАТЬ ПО
	|			СчетФактураВыданныйДокументыОснования.Ссылка
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			СчетФактураВыданныйДокументыОснования.Ссылка,
	|			NULL,
	|			МАКСИМУМ(ВЫРАЗИТЬ(СчетФактураВыданныйДокументыОснования.ДокументОснование КАК Документ.КорректировкаРеализации))
	|		ИЗ
	|			РаспечатанныеДокументы КАК РаспечатанныеДокументы
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|				ПО ((ВЫРАЗИТЬ(РаспечатанныеДокументы.ОбъектПечати КАК Документ.СчетФактураВыданный)) = СчетФактураВыданныйДокументыОснования.Ссылка)
	|		ГДЕ
	|			СчетФактураВыданныйДокументыОснования.ДокументОснование ССЫЛКА Документ.КорректировкаРеализации
	|		
	|		СГРУППИРОВАТЬ ПО
	|			СчетФактураВыданныйДокументыОснования.Ссылка) КАК СчетаФактурыВыданные
	|		ПО ((ВЫРАЗИТЬ(РаспечатанныеДокументы.ОбъектПечати КАК Документ.СчетФактураВыданный)) = СчетаФактурыВыданные.Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвязанныйДокументРеализации.Документ,
	|	РаспечатанныеДокументы.ОбъектПечати,
	|	РаспечатанныеДокументы.ИдентификаторМакета,
	|	РаспечатанныеДокументы.КоличествоСтраниц,
	|	NULL,
	|	МАКСИМУМ(СчетФактураВыданныйДокументыОснования.Ссылка)
	|ИЗ
	|	РаспечатанныеДокументы КАК РаспечатанныеДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МАКСИМУМ(РеализацияТоваровУслугТовары.Ссылка) КАК Документ,
	|			РаспечатанныеДокументы.ОбъектПечати КАК ОбъектПечати,
	|			РаспечатанныеДокументы.ИдентификаторМакета КАК ИдентификаторМакета
	|		ИЗ
	|			РаспечатанныеДокументы КАК РаспечатанныеДокументы
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения КАК КорректировкаПриобретения
	|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
	|						ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК РеализацияТоваровУслугТовары
	|						ПО ((ВЫРАЗИТЬ(РеализацияТоваровУслугТовары.бг_ДокументОснованиеПоступлениеОтПоставщика КАК Документ.ПриобретениеТоваровУслуг)) = ПриобретениеТоваровУслуг.Ссылка)
	|					ПО ((ВЫРАЗИТЬ(КорректировкаПриобретения.ДокументОснование КАК Документ.ПриобретениеТоваровУслуг)) = ПриобретениеТоваровУслуг.Ссылка)
	|				ПО ((ВЫРАЗИТЬ(РаспечатанныеДокументы.ОбъектПечати КАК Документ.КорректировкаПриобретения)) = КорректировкаПриобретения.Ссылка)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			РаспечатанныеДокументы.ОбъектПечати,
	|			РаспечатанныеДокументы.ИдентификаторМакета) КАК СвязанныйДокументРеализации
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|			ПО (СвязанныйДокументРеализации.Документ = (ВЫРАЗИТЬ(СчетФактураВыданныйДокументыОснования.ДокументОснование КАК Документ.РеализацияТоваровУслуг)))
	|		ПО РаспечатанныеДокументы.ОбъектПечати = СвязанныйДокументРеализации.ОбъектПечати
	|			И РаспечатанныеДокументы.ИдентификаторМакета = СвязанныйДокументРеализации.ИдентификаторМакета
	|
	|СГРУППИРОВАТЬ ПО
	|	РаспечатанныеДокументы.ОбъектПечати,
	|	РаспечатанныеДокументы.ИдентификаторМакета,
	|	РаспечатанныеДокументы.КоличествоСтраниц,
	|	СвязанныйДокументРеализации.Документ";
	
	Запрос.УстановитьПараметр("РаспечатанныеДокументы", РаспечатанныеДокументы);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ИнициализироватьТаблицуОтобранныхПоНастройкамДокументов(РаспечатанныеДокументы)
	
	ОтобранныеДокументы = РаспечатанныеДокументы.СкопироватьКолонки();
	ОтобранныеДокументы.Колонки.Добавить(
		"ПечатнаяФорма", 
		Новый ОписаниеТипов("СправочникСсылка.бг_ФиксируемыеПечатныеФормы"));
		
	ОтобранныеДокументы.Колонки.Добавить(
		"ПечатнаяФормаНаименование", 
		ОбщегоНазначения.ОписаниеТипаСтрока(100));
		
	ОтобранныеДокументы.Колонки.Добавить(
		"Документ", 
		Новый ОписаниеТипов("ДокументСсылка.РеализацияТоваровУслуг"));
		
	ОтобранныеДокументы.Колонки.Добавить(
		"ДокументКорректировки", 
		Новый ОписаниеТипов("ДокументСсылка.КорректировкаРеализации"));
		
	ОтобранныеДокументы.Колонки.Добавить(
		"СчетФактураВыданный", 
		Новый ОписаниеТипов("ДокументСсылка.СчетФактураВыданный"));
		
	ОтобранныеДокументы.Колонки.Добавить(
		"РаспечатанныйНомер", 
		ОбщегоНазначения.ОписаниеТипаСтрока(12)); 
		
	Возврат ОтобранныеДокументы;
	
КонецФункции

#КонецОбласти

#КонецЕсли