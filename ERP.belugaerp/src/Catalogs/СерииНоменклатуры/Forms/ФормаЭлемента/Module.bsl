#Область ОбработчикиСобытийФормы

&НаСервере
Процедура бг_ПриЧтенииНаСервереПеред(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	бг_ДобавитьРеквизиты();
	бг_ЗаполнитьРеквизиты();
	
КонецПроцедуры

&НаСервере
Процедура бг_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		бг_ДобавитьРеквизиты();
		бг_ЗаполнитьРеквизиты();
	КонецЕсли;
	
	бг_СерииНоменклатуры.ДобавитьПоляФСМ(ЭтотОбъект, Элементы.ГруппаРеквизитыЛокализации, "Объект");
	бг_ДобавитьПоля();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	бг_УстановитьВидимостьДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ПриОткрытииПосле(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

&НаКлиенте
Процедура бг_ПослеЗаписиПосле(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура бг_ДатаПроизводстваПриИзмененииПосле(Элемент)
	
	бг_ДатаПроизводстваПриИзмененииПослеНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ДатаПроизводстваОбработкаВыбораПосле(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	бг_ДатаПроизводстваПриИзмененииПослеНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура бг_ДобавитьРеквизиты()
	
	РеквизитыКДобавлению = Новый Массив;
	
	РеквизитыКДобавлению.Добавить(
		Новый РеквизитФормы(
			"бг_ДатаПриемкиВСпиртохранилище",
			ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата),,
			НСтр("ru='Дата приемки в спиртохранилище'")));
			
	РеквизитыКДобавлению.Добавить(
		Новый РеквизитФормы( 
			"бг_ДатаРозлива",
			ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата),,
			НСтр("ru='Дата розлива'")));
			
	РеквизитыКДобавлению.Добавить(
		Новый РеквизитФормы( 
			"бг_НеограниченныйСрокГодности",
			ОбщегоНазначения.ОписаниеТипаСтрока(30),,
			НСтр("ru='Годен до'")));
	
	бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьРеквизитыНаФорму(ЭтотОбъект, РеквизитыКДобавлению);
	
КонецПроцедуры

&НаСервере
Процедура бг_ЗаполнитьРеквизиты()
	
	УстановитьПривилегированныйРежим(Истина);
	
	бг_ЗаполнитьРеквизитыПоДаннымСправки2ЕГАИС();
	бг_ЗаполнитьРеквизитГоденДо();
	
КонецПроцедуры

&НаСервере
Процедура бг_ДобавитьПоля()
	
	ГруппаАлкоголь = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьГруппуНаФорму(
		ЭтаФорма,
		"бг_ГруппаАлкоголь",
		Элементы.ГруппаРеквизитыЛокализации);
	ГруппаАлкоголь.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаАлкоголь.ОтображатьЗаголовок = Истина;
	ГруппаАлкоголь.Заголовок = НСтр("ru = 'Алкоголь'");
	
	ГруппаИмпорт = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьГруппуНаФорму(
		ЭтотОбъект, 
		"бг_ГруппаИмпорт",
		Элементы.ГруппаРеквизитыЛокализации);
	ГруппаИмпорт.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаИмпорт.ОтображатьЗаголовок = Истина;
	ГруппаИмпорт.Заголовок = НСтр("ru = 'Импорт'");
	
	ПолеВидМарки = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_ВидМарки",
		ГруппаАлкоголь,
		"Объект.бг_ВидМарки");
		
	ПолеДатаИнвойса = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_ДатаИнвойса",
		ГруппаИмпорт,
		"Объект.бг_ДатаИнвойса");
		
	ПолеДатаПриемкиВСпиртохранилище = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтаФорма,
		"бг_ДатаПриемкиВСпиртохранилище",
		ГруппаАлкоголь,
		"бг_ДатаПриемкиВСпиртохранилище");
		
	ПолеДатаРеализации = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_ДатаРеализации",
		ГруппаАлкоголь,
		"Объект.бг_ДатаРеализации");
		
	ПолеДатаРеализации = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_ДатаРегистрацииДвиженийЕГАИС",
		ГруппаАлкоголь,
		"Объект.бг_ДатаРегистрацииДвиженийЕГАИС");
		
	ПолеДатаРозлива = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_ДатаРозлива",
		ГруппаАлкоголь,
		"бг_ДатаРозлива");
		
	ПолеДатаСлива = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_ДатаСлива",
		ГруппаИмпорт,
		"Объект.бг_ДатаСлива");
		
	ПолеДокументВыпуска = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_ДокументВыпуска",
		ГруппаАлкоголь,
		"Объект.бг_ДокументВыпуска");
		
	ПолеИдентификаторСтрокиТТН = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтаФорма,
		"бг_ИдентификаторСтрокиТТН",
		ГруппаАлкоголь,
		"Объект.бг_ИдентификаторСтрокиТТН");
		
	ПолеНеограниченныйСрокГодности = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_НеограниченныйСрокГодности",
		Элементы.ГруппаНомерГоденДо,
		"бг_НеограниченныйСрокГодности");
	ПолеНеограниченныйСрокГодности.ТолькоПросмотр = Истина;
	ПолеНеограниченныйСрокГодности.Вид = ВидПоляФормы.ПолеНадписи;
	
	ПолеНоменклатура = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтаФорма,
		"бг_Номенклатура",
		ГруппаАлкоголь,
		"Объект.бг_Номенклатура");
		
	ПолеНомерГТД = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтаФорма,
		"бг_НомерГТД",
		ГруппаАлкоголь,
		"Объект.бг_НомерГТД");
		
	ПолеОрганизацияВладелец = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_ОрганизацияВладелец",
		ГруппаАлкоголь,
		"Объект.бг_ОрганизацияВладелец");
		
	ПолеОрганизацияЕГАИСВладелец = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_ОрганизацияЕГАИСВладелец",
		ГруппаАлкоголь,
		"Объект.бг_ОрганизацияЕГАИСВладелец");
		
	ПолеСерияРеализации = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_СерияРеализации",
		ГруппаАлкоголь,
		"Объект.бг_СерияРеализации");
		
	ПолеСертификат = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_Сертификат",
		ГруппаАлкоголь,
		"Объект.бг_Сертификат");
		
	ПолеСтранаНазначения = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтаФорма,
		"бг_СтранаНазначения",
		ГруппаАлкоголь,
		"Объект.бг_СтранаНазначения");
		
	ПолеУпаковкаПаллета = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_УпаковкаПаллета",
		ГруппаАлкоголь,
		"Объект.бг_УпаковкаПаллета");
		
	ПолеФактическаяКрепость = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтаФорма,
		"бг_ФактическаяКрепость",
		ГруппаАлкоголь,
		"Объект.бг_ФактическаяКрепость");
		
КонецПроцедуры

&НаСервере
Функция бг_ПараметрыОтображенияПолей()
	
	ПараметрыОтображенияПолей = Новый Структура;
	
	ПараметрыОтображенияПолей.Вставить("АлкогольнаяПродукция", Ложь);
	ПараметрыОтображенияПолей.Вставить("ВозвратОтПокупателя", Ложь); 
	ПараметрыОтображенияПолей.Вставить("ЕК_Номенклатура", Ложь);
	ПараметрыОтображенияПолей.Вставить(
		"ИзменениеДанныхОПервичнойРеализацииВСериях",
		РольДоступна("бг_ИзменениеДанныхОПервичнойРеализацииВСериях"));
	ПараметрыОтображенияПолей.Вставить(
		"ИмпортныеССП",
		Объект.ВидНоменклатуры = бг_КонстантыПовтИсп.ЗначениеКонстанты("ИмпортныеССП"));
	ПараметрыОтображенияПолей.Вставить("МаркируемаяАлкогольнаяПродукция", Ложь);
	ПараметрыОтображенияПолей.Вставить("ПолноправныйПользователь", Пользователи.ЭтоПолноправныйПользователь());
	ПараметрыОтображенияПолей.Вставить("СерияСпирта", бг_СерииНоменклатуры.ЭтоСерияСпирта(Объект));
	ПараметрыОтображенияПолей.Вставить("СрокГодности", Дата(1, 1, 1));
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат ПараметрыОтображенияПолей;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СерииНоменклатуры.бг_Номенклатура.АлкогольнаяПродукция, ЛОЖЬ) КАК АлкогольнаяПродукция,
	|	ЕСТЬNULL(СерииНоменклатуры.бг_Номенклатура.ВидАлкогольнойПродукции.Маркируемый, ЛОЖЬ) КАК МаркируемаяАлкогольнаяПродукция,
	|	ЕСТЬNULL(СерииНоменклатуры.бг_Номенклатура.СрокГодности, ЛОЖЬ) КАК СрокГодности,
	|	ЕСТЬNULL(СерииНоменклатуры.ВидНоменклатуры.бг_ВариантСинхронизацииWMS = ЗНАЧЕНИЕ(Перечисление.бг_ВариантыСинхронизацииНоменклатурыWMS.КодЕК_Номенклатуры), ЛОЖЬ) КАК ЕК_Номенклатура,
	|	СерииНоменклатуры.Справка2ЕГАИС КАК Справка2ЕГАИС
	|ИЗ
	|	Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|ГДЕ
	|	СерииНоменклатуры.Ссылка = &Серия";
	
	Запрос.УстановитьПараметр("Серия", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(ПараметрыОтображенияПолей, Выборка);
		
		ВидОперацииСправки2 = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.Справка2ЕГАИС, "ДокументОснование.ВидОперации");
		ПараметрыОтображенияПолей.ВозвратОтПокупателя = ВидОперацииСправки2 = Перечисления.ВидыОперацийТТНВходящейЕГАИС.ВозвратОтПокупателя;
	КонецЕсли;
	
	Возврат ПараметрыОтображенияПолей;
	
КонецФункции

&НаСервере
Процедура бг_УстановитьВидимостьДоступностьЭлементов()
	
	ПараметрыОтображения = бг_ПараметрыОтображенияПолей();
	
#Область Видимость

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_ВидМарки",
		"Видимость",
		ПараметрыОтображения.ЕК_Номенклатура);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_ДатаИнвойса",
		"Видимость",
		ПараметрыОтображения.ИмпортныеССП);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_ДатаПриемкиВСпиртохранилище",
		"Видимость",
		ПараметрыОтображения.СерияСпирта);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_ДатаРеализации",
		"Видимость",
		ПараметрыОтображения.АлкогольнаяПродукция
		И ПараметрыОтображения.ВозвратОтПокупателя);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_ДатаРозлива",
		"Видимость",
		ПараметрыОтображения.СерияСпирта);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_ДатаСлива",
		"Видимость",
		ПараметрыОтображения.ИмпортныеССП);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_ИдентификаторСтрокиТТН",
		"Видимость",
		ПараметрыОтображения.СерияСпирта);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_НеограниченныйСрокГодности",
		"Видимость",
		ПараметрыОтображения.СрокГодности = 0);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_НомерГТД",
		"Видимость",
		Ложь);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_СерияРеализации",
		"Видимость",
		ПараметрыОтображения.АлкогольнаяПродукция
		И ПараметрыОтображения.ВозвратОтПокупателя);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_Сертификат",
		"Видимость",
		ПараметрыОтображения.АлкогольнаяПродукция);

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_СтранаНазначения",
		"Видимость",
		ПараметрыОтображения.АлкогольнаяПродукция);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_ФактическаяКрепость",
		"Видимость",
		ПараметрыОтображения.СерияСпирта);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГоденДо",
		"Видимость",
		ПараметрыОтображения.СрокГодности <> 0);

#КонецОбласти

#Область Доступность

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_ДатаПриемкиВСпиртохранилище",
		"ТолькоПросмотр",
		Истина);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_ДатаРеализации",
		"ТолькоПросмотр",
		Не (ПараметрыОтображения.ПолноправныйПользователь
		Или ПараметрыОтображения.ИзменениеДанныхОПервичнойРеализацииВСериях));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_ДатаРегистрацииДвиженийЕГАИС",
		"ТолькоПросмотр",
		Истина);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_ДатаРозлива",
		"ТолькоПросмотр",
		Истина);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_ДокументВыпуска",
		"ТолькоПросмотр",
		Истина);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_ИдентификаторСтрокиТТН",
		"ТолькоПросмотр",
		Истина);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_Номенклатура",
		"ТолькоПросмотр",
		Истина);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_ОрганизацияВладелец",
		"ТолькоПросмотр",
		Истина);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_ОрганизацияЕГАИСВладелец",
		"ТолькоПросмотр",
		Истина);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_СерияРеализации",
		"ТолькоПросмотр",
		Не (ПараметрыОтображения.ПолноправныйПользователь
		Или ПараметрыОтображения.ИзменениеДанныхОПервичнойРеализацииВСериях));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_ФактическаяКрепость",
		"ТолькоПросмотр",
		Истина);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"бг_УпаковкаПаллета",
		"ТолькоПросмотр",
		Истина);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПроизводительЕГАИС",
		"ТолькоПросмотр",
		Истина);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Справка2ЕГАИС",
		"ТолькоПросмотр",
		Истина);
		
#КонецОбласти

КонецПРоцедуры

&НаСервере
Процедура бг_ЗаполнитьРеквизитыПоДаннымСправки2ЕГАИС()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Или Не ЗначениеЗаполнено(Объект.Справка2ЕГАИС) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ВЫРАЗИТЬ(Справки2ЕГАИС.ДокументОснование КАК Документ.ТТНВходящаяЕГАИС).ДатаРегистрацииДвижений, ДАТАВРЕМЯ(1, 1, 1)) КАК бг_ДатаПриемкиВСпиртохранилище,
	|	ЕСТЬNULL(ВЫРАЗИТЬ(Справки2ЕГАИС.ДокументОснование КАК Документ.ТТНВходящаяЕГАИС).ДатаОтгрузки, ДАТАВРЕМЯ(1, 1, 1)) КАК бг_ДатаРозлива
	|ИЗ
	|	Справочник.Справки2ЕГАИС КАК Справки2ЕГАИС
	|ГДЕ
	|	Справки2ЕГАИС.Ссылка = &Справка2ЕГАИС");
	
	Запрос.УстановитьПараметр("Справка2ЕГАИС", Объект.Справка2ЕГАИС);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ВыборкаДетальныеЗаписи);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура бг_ЗаполнитьРеквизитГоденДо()
	
	СрокГодности = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.бг_Номенклатура, "СрокГодности");
	Если СрокГодности = 0 Тогда
		Объект.ГоденДо = '39991231';
		ЭтаФорма.бг_НеограниченныйСрокГодности = Нстр("ru='Срок годности не ограничен'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура бг_ДатаПроизводстваПриИзмененииПослеНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.ДатаПроизводства) Или Не ЗначениеЗаполнено(Объект.бг_Номенклатура) Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ГоденДо = бг_Номенклатура.ГоденДо(Объект.бг_Номенклатура, Объект.ДатаПроизводства);
	
КонецПроцедуры

#КонецОбласти
