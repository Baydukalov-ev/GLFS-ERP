#Область ПрограммныйИнтерфейс

// Заменяет ссылку на справку1 в справках 2, связанных по номеру со справкой 1
// 
// Параметры:
//  Справка1 - СправочникСсылка.Справки1ЕГАИС - справка1, которую подставляем
//  
Процедура бг_ОбновитьСвязанныеСправки2(Справка1) Экспорт
	Справки2 = бг_СвязанныеПоНомеруСправки2(Справка1, "ТолькоНезаполненные");
	Для каждого Справка2 Из Справки2 Цикл
	    Попытка
			Справка2Объект = Справка2.ПолучитьОбъект();
			Справка2Объект.Справка1 = Справка1;
			Справка2Объект.Записать();
		Исключение
			ТекстОшибки = НСтр("ru='Не удалось обновить справку 2 %1 по причине %2'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, Справка2, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция бг_СвязанныеПоНомеруСправки2(Справка1, ОтборПоСправке1 = "")
	Если ОтборПоСправке1 = "Все" Или ОтборПоСправке1 = "" Тогда
		ТекстУсловия = "Истина";
	ИначеЕсли ОтборПоСправке1 = "ТолькоНезаполненные" Тогда
		ТекстУсловия = "Справки2ЕГАИС.Справка1 = Значение(Справочник.Справки1ЕГАИС.ПустаяСсылка)";
	Иначе 
		ТекстУсловия = "Ложь";                                                             
	КонецЕсли;
	Запрос = Новый Запрос; 
	Запрос.УстановитьПараметр("Справка1", Справка1);
	Запрос.Текст = СтрЗаменить(
		"ВЫБРАТЬ
		|	Справки2ЕГАИС.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Справки2ЕГАИС КАК Справки2ЕГАИС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Справки1ЕГАИС КАК Справки1ЕГАИС
		|		ПО Справки2ЕГАИС.НомерСправки1 = Справки1ЕГАИС.РегистрационныйНомер
		|			И (Справки1ЕГАИС.Ссылка = &Справка1)
		|			И (НЕ Справки2ЕГАИС.ПометкаУдаления)
		|			И (&ТекстУсловия)",
		"&ТекстУсловия",
		ТекстУсловия);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
КонецФункции

#КонецОбласти