#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

&После("ПриКопировании")
Процедура бг_ПриКопировании(ОбъектКопирования)
	
	бг_Тикер  = "";
	бг_КодНСИ = "";
	бг_КодSAP = "";
	
КонецПроцедуры

&После("ПередЗаписью")
Процедура бг_ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	бг_СинхронизироватьЗаданияФормированияЗаказовПередЗаписью();
	
КонецПроцедуры

&После("ПриЗаписи")
Процедура бг_ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	бг_СинхронизироватьЗаданияФормированияЗаказовПриЗаписи(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура бг_СинхронизироватьЗаданияФормированияЗаказовПередЗаписью()
	
	ПроверяемыеРеквизиты = Новый Структура;
	ПроверяемыеРеквизиты.Вставить("ПометкаУдаления");
	ПроверяемыеРеквизиты.Вставить("бг_Тикер");

	Если ЭтоНовый() Тогда
		ДополнительныеСвойства.Вставить("СинхронизироватьРегламентныеЗаданияФормированияЗаказов");
	Иначе
		ИзмененныеРеквизиты = бг_ОбщегоНазначенияСервер.ИзмененияОбъекта(
			ЭтотОбъект,
			ПроверяемыеРеквизиты);
			Если ИзмененныеРеквизиты.Свойство("Реквизиты")
				И ИзмененныеРеквизиты.Реквизиты.Количество() > 0 Тогда
			ДополнительныеСвойства.Вставить("СинхронизироватьРегламентныеЗаданияФормированияЗаказов");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура бг_СинхронизироватьЗаданияФормированияЗаказовПриЗаписи(Отказ)
	
	Если Не Отказ
		И ДополнительныеСвойства.Свойство("СинхронизироватьРегламентныеЗаданияФормированияЗаказов") Тогда
		бг_РегламентныеЗадания.СинхронизироватьРегламентныеЗаданияФормированияЗаказов();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
