#Область ПрограммныйИнтерфейс

// Возвращает параметры настройки счетов учета в документе.
// 
// Параметры:
// 	ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция документа.
// 	
// Возвращаемое значение:
// 	Массив - Массив параметров настройки счетов учета (См. НастройкаСчетовУчетаСервер.ПараметрыНастройкиСчетаУчетаОперации).
//
Функция ПараметрыНастройкиСчетовУчета(ХозяйственнаяОперация) Экспорт
	
	МассивПараметров = Новый Массив();
	
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект.ПрочиеРасходы";
	ПараметрыНастройки.СтатьяАктивовПассивов = "СтатьяАктивовПассивов";
	ПараметрыНастройки.Представление = "ПредставлениеОтраженияВРеглУчете";
	ПараметрыНастройки.ДоступностьПоОперации = ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеРасходы;
	
	ПараметрыНастройки.Организация = "";
	МассивПараметров.Добавить(ПараметрыНастройки);

	Возврат МассивПараметров;
	
КонецФункции

// Заполняет таблицу реквизитов, зависимых от хозяйственной операции
//
// Параметры:
//	ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - хозяйственная операция соглашения
//	МассивВсехРеквизитов - Массив - реквизиты, которые не зависят от хозяйственной операции
//	МассивРеквизитовОперации - Массив - реквизиты, которые зависят от хозяйственной операции.
//
Процедура ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(ХозяйственнаяОперация, МассивВсехРеквизитов, МассивРеквизитовОперации) Экспорт
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("ПрочиеРасходы.КорСтатьяРасходов");
	МассивВсехРеквизитов.Добавить("ПрочиеРасходы.СтатьяАктивовПассивов");
	МассивВсехРеквизитов.Добавить("ПрочиеРасходы.ПредставлениеОтраженияВРеглУчете");
	
	МассивРеквизитовОперации = Новый Массив;
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеРасходы Тогда
		МассивРеквизитовОперации.Добавить("ПрочиеРасходы.СтатьяАктивовПассивов");
		МассивРеквизитовОперации.Добавить("ПрочиеРасходы.ПредставлениеОтраженияВРеглУчете");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеклассификацияРасходов Тогда
		МассивРеквизитовОперации.Добавить("ПрочиеРасходы.КорСтатьяРасходов");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
