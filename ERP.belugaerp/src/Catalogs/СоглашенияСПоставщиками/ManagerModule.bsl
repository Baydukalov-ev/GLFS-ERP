
#Область ПрограммныйИнтерфейс

// Функция - Соглашение по партнеру и организации
//
// Параметры:
//  Партнер		 - СправочникСсылка.Партнеры - партнер соглашения
//  Организация	 - СправочникСсылка.Организации - организация соглашения
// 
// Возвращаемое значение:
//  СправочникСсылка.СоглашенияСПоставщиками - отобранное соглашение
//
Функция бг_СоглашениеПоПартнеруИОрганизации(Партнер, Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СоглашенияСПоставщиками.Ссылка КАК Ссылка,
		|	СоглашенияСПоставщиками.бг_ОсновноеСоглашение КАК бг_ОсновноеСоглашение
		|ИЗ
		|	Справочник.СоглашенияСПоставщиками КАК СоглашенияСПоставщиками
		|ГДЕ
		|	НЕ СоглашенияСПоставщиками.ПометкаУдаления
		|	И СоглашенияСПоставщиками.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСоглашенийСПоставщиками.Действует)
		|	И СоглашенияСПоставщиками.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОказаниеАгентскихУслуг)
		|			И СоглашенияСПоставщиками.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемНаХранениеСПравомПродажи)
		|	И СоглашенияСПоставщиками.Партнер = &Партнер
		|	И СоглашенияСПоставщиками.Организация = &Организация
		|
		|УПОРЯДОЧИТЬ ПО
		|	СоглашенияСПоставщиками.бг_ОсновноеСоглашение УБЫВ";	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Партнер", Партнер);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Соглашение = Неопределено;
	Если Не РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();

		Соглашение = ВыборкаДетальныеЗаписи.Ссылка;
	КонецЕсли;
	
	Возврат Соглашение;

КонецФункции

#КонецОбласти