
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбъектСервер = РеквизитФормыВЗначение("Объект");
	Для Каждого Макет Из ОбъектСервер.Метаданные().Макеты Цикл
		Элементы.ТипОбъекта.СписокВыбора.Добавить(Макет.Имя, Макет.Синоним);
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Если Элементы.ТипОбъекта.СписокВыбора.Количество() = 1 Тогда
			Объект.ТипОбъекта = Элементы.ТипОбъекта.СписокВыбора[0].Значение;
		КонецЕсли;
		
		ПриЧтенииСозданииНаСервере();
		
	КонецЕсли;
	
	Элементы.ТипОбъекта.Видимость = Элементы.ТипОбъекта.СписокВыбора.Количество() > 1;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.НастройкиКомпоновкиДанных = Новый ХранилищеЗначения(
		ТекущиеНастройкиКомпоновки.ПолучитьНастройки());
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипОбъектаПриИзменении(Элемент)
	
	ОбработатьИзменениеТипаОбъекта();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектаОчистка(Элемент, СтандартнаяОбработка)
	
	ОбработатьИзменениеТипаОбъекта();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиИзмененийРеквизитов

&НаКлиенте
Процедура ОбработатьИзменениеТипаОбъекта()
	
	Если КэшТипОбъекта = Объект.ТипОбъекта Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ТекущиеНастройкиКомпоновки.Настройки.Отбор.Элементы.Количество()
		Или ПустаяСтрока(КэшТипОбъекта) Тогда
		
		ЗагрузитьБазовыеНастройкиКомпоновщика();
		УстановитьДоступностьЭлементов(ЭтаФорма);
		Возврат;
		
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Условия отбора будут очищены. Продолжить?'",
		ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
		
	ОповещениеПослеВопроса = Новый ОписаниеОповещения("ТипОбъектаПриИзмененииЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОповещениеПослеВопроса, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОбъектаПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат = КодВозвратаДиалога.Да Тогда
		
		Объект.ТипОбъекта = КэшТипОбъекта;
		Возврат;
		
	КонецЕсли;
	
	КэшТипОбъекта = Объект.ТипОбъекта;
	ЗагрузитьБазовыеНастройкиКомпоновщика();
	УстановитьДоступностьЭлементов(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	ЗагрузитьБазовыеНастройкиКомпоновщика();
	
	ОбъектСервер = РеквизитФормыВЗначение("Объект");
	Настройки = ОбъектСервер.НастройкиКомпоновкиДанных.Получить();
	Если Не Настройки = Неопределено Тогда
		ТекущиеНастройкиКомпоновки.ЗагрузитьНастройки(Настройки);
	КонецЕсли;
	
	КэшТипОбъекта = Объект.ТипОбъекта;
	
	УстановитьДоступностьЭлементов(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьБазовыеНастройкиКомпоновщика()
	
	Если Не ЗначениеЗаполнено(Объект.ТипОбъекта) Тогда
		
		ТекущиеНастройкиКомпоновки.Настройки.Отбор.Элементы.Очистить();
		Возврат;
		
	КонецЕсли;
	
	СхемаКомпоновки = Справочники.бг_УсловияПечатиКомплектов.ПолучитьМакет(Объект.ТипОбъекта);
	АдресСхемы = ПоместитьВоВременноеХранилище(СхемаКомпоновки, УникальныйИдентификатор);
	ТекущиеНастройкиКомпоновки.Инициализировать(
		Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы));
	ТекущиеНастройкиКомпоновки.ЗагрузитьНастройки(СхемаКомпоновки.НастройкиПоУмолчанию);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьЭлементов(Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Элементы.ТекущиеНастройкиКомпоновкиНастройкиОтбор.Доступность
		= ЗначениеЗаполнено(Объект.ТипОбъекта);
	
КонецПроцедуры

#КонецОбласти
