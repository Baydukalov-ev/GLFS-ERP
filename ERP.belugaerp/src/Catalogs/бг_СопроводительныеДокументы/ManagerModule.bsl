
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	ОписаниеВидов = Справочники.бг_ВидыСопроводительныхДокументов.ОписаниеВидовСопроводительныхДокументов();
	Для Каждого ОписаниеВида Из ОписаниеВидов Цикл

		Если КоллекцияПечатныхФорм.Найти(ВРег(ОписаниеВида.Идентификатор), "ИмяВРЕГ") = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		бг_УправлениеПечатьюВызовСервера.ДополнитьКоллекциюФайлами(
			КоллекцияПечатныхФорм,
			ОбъектыПечати,
			ОписаниеВида.Идентификатор,
			ПодготовитьСопроводительныеДокументы(МассивОбъектов, ОписаниеВида.Вид, ПараметрыПечати));
			
	КонецЦикла;

КонецПроцедуры

Функция ПодготовитьСопроводительныеДокументы(МассивОбъектов, Вид, ПараметрыПечати)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Ссылка КАК Ссылка,
		|	Т.АдресФайла КАК АдресФайла		
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТаблицаТовары.Ссылка КАК Ссылка,
		|		Документы.ПутьКФайлу КАК АдресФайла
		|	ИЗ
		|		Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТовары
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.бг_СопроводительныеДокументы КАК Документы
		|			ПО ТаблицаТовары.Серия = Документы.Владелец
		|				И Документы.Вид = &ВидДокумента
		|	ГДЕ
		|		ТаблицаТовары.Ссылка В(&МассивОбъектов)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаСерии.Ссылка,
		|		Документы.ПутьКФайлу
		|	ИЗ
		|		Документ.РеализацияТоваровУслуг.Серии КАК ТаблицаСерии
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.бг_СопроводительныеДокументы КАК Документы
		|			ПО ТаблицаСерии.Серия = Документы.Владелец
		|				И Документы.Вид = &ВидДокумента
		|	ГДЕ
		|		ТаблицаСерии.Ссылка В(&МассивОбъектов)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаТовары.Ссылка,
		|		Документы.ПутьКФайлу
		|	ИЗ
		|		Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТовары
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.бг_СопроводительныеДокументы КАК Документы
		|			ПО ТаблицаТовары.Номенклатура = Документы.Владелец
		|				И Документы.Вид = &ВидДокумента
		|	ГДЕ
		|		ТаблицаТовары.Ссылка В(&МассивОбъектов)) КАК Т";

	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ВидДокумента", Вид);
	
	Если Не ПараметрыПечати.Группировать Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВЫБРАТЬ РАЗЛИЧНЫЕ", "ВЫБРАТЬ");
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#КонецЕсли