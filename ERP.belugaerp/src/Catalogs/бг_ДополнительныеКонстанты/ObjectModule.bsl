
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	Идентификатор = "";
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("НеВыполнятьПроверки")
		И ДополнительныеСвойства.НеВыполнятьПроверки Тогда
		
		Возврат;
	КонецЕсли;
	
	ПроверитьКорректностьИдентификатора(Отказ);
	ПроверитьУникальностьИдентификатора(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьКорректностьИдентификатора(Отказ)

	Если ПустаяСтрока(Идентификатор) Или Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеКонстанты = бг_КонстантыПовтИсп.ОписаниеКонстанты(Идентификатор);
	
	Если ОписаниеКонстанты = Неопределено Тогда
		
		ТекстОшибки = СтрШаблон(НСтр("ru='Запись константы с неизвестным идентификатором %1'"), Идентификатор);
				
		ОбщегоНазначения.СообщитьПользователю(
			ТекстОшибки,
			?(ЭтоНовый(), ЭтотОбъект, Ссылка),
			"Идентификатор",
			, // ПутьКДанным
			Отказ);
		
	КонецЕсли;

КонецПроцедуры

Процедура ПроверитьУникальностьИдентификатора(Отказ)
	
	Если ПустаяСтрока(Идентификатор) Или Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	бг_ДополнительныеКонстанты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.бг_ДополнительныеКонстанты КАК бг_ДополнительныеКонстанты
	|ГДЕ
	|	бг_ДополнительныеКонстанты.Ссылка <> &Ссылка
	|	И бг_ДополнительныеКонстанты.Идентификатор = &Идентификатор";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ТекстОшибки = СтрШаблон(НСтр("ru='Запись константы с неуникальным идентификатором %1'"), Идентификатор);
		
		ОбщегоНазначения.СообщитьПользователю(
			ТекстОшибки,
			?(ЭтоНовый(), ЭтотОбъект, Ссылка),
			"Идентификатор",
			, // ПутьКДанным
			Отказ);

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
	
#КонецЕсли