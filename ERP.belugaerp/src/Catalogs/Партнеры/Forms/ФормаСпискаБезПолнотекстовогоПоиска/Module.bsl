
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	бг_ДобавитьРеквизитыФормы();
	бг_ДобавитьЭлементыФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчкикКомандФормы

&НаКлиенте
&Вместо("ДинамическийСписокФильтрыПриАктивизацииСтроки")
Процедура бг_ДинамическийСписокФильтрыПриАктивизацииСтроки(Элемент)
	
	Если Элемент.Имя = "БизнесРегионы" И Элементы.СтраницыТипФильтра.ТекущаяСтраница <> Элементы.СтраницаБизнесРегионы Тогда
		Возврат;
	ИначеЕсли Элемент.Имя = "ГруппыДоступаПартнеров" И Элементы.СтраницыТипФильтра.ТекущаяСтраница <> Элементы.СтраницаГруппыДоступа Тогда
		Возврат;
	ИначеЕсли Элемент.Имя = "Менеджеры" И Элементы.СтраницыТипФильтра.ТекущаяСтраница <> Элементы.СтраницаМенеджеры Тогда
		Возврат;
	ИначеЕсли Элемент.Имя = "Свойства" И Элементы.СтраницыТипФильтра.ТекущаяСтраница <> Элементы.СтраницаСвойства Тогда
		Возврат;
	ИначеЕсли Элемент.Имя = "Категории" И Элементы.СтраницыТипФильтра.ТекущаяСтраница <> Элементы.СтраницаКатегории Тогда
		Возврат;
	ИначеЕсли Элемент.Имя = "бг_ГруппыПартнеров" И Элементы.СтраницыТипФильтра.ТекущаяСтраница <> Элементы.бг_СтраницаГруппыПартнеров Тогда
		Возврат;
	КонецЕсли;
	
	Если НеОтрабатыватьАктивизациюПанелиНавигации Тогда
		НеОтрабатыватьАктивизациюПанелиНавигации = Ложь;
	Иначе
		Если Элемент.Имя = "БизнесРегионы" И ТекущееЗначениеФильтра = Элементы.БизнесРегионы.ТекущаяСтрока Тогда
			Возврат;
		ИначеЕсли Элемент.Имя = "ГруппыДоступаПартнеров" И ТекущееЗначениеФильтра = Элементы.ГруппыДоступаПартнеров.ТекущаяСтрока Тогда
			Возврат;
		ИначеЕсли Элемент.Имя = "Менеджеры" И ТекущееЗначениеФильтра = Элементы.Менеджеры.ТекущаяСтрока Тогда
			Возврат;
		ИначеЕсли Элемент.Имя = "Свойства" И ТекущееЗначениеФильтра = Свойства.НайтиПоИдентификатору(Элементы.Свойства.ТекущаяСтрока) Тогда
			Возврат;
		ИначеЕсли Элемент.Имя = "Категории" И ТекущееЗначениеФильтра = Категории.НайтиПоИдентификатору(Элементы.Категории.ТекущаяСтрока) Тогда
			Возврат;
		ИначеЕсли Элемент.Имя = "бг_ГруппыПартнеров" И ТекущееЗначениеФильтра = ЭтотОбъект.бг_ГруппыПартнеров.НайтиПоИдентификатору(Элементы.бг_ГруппыПартнеров.ТекущаяСтрока) Тогда
			Возврат;
		КонецЕсли;
		
		ПодключитьОбработчикОжидания("ОбработатьАктивизациюСтрокиДинамическогоСписка",0.2,Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура бг_ДобавитьРеквизитыФормы()
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	ДобавляемыеРеквизиты.Добавить(
		Новый РеквизитФормы(
			"бг_ГруппыПартнеров", 
			Новый ОписаниеТипов("ДеревоЗначений"), 
			, 
			, 
			Истина));
			
	ДобавляемыеРеквизиты.Добавить(
		Новый РеквизитФормы(
			"Значение", 
			Новый ОписаниеТипов("СправочникСсылка.битГруппыПартнеров"), 
			"бг_ГруппыПартнеров", 
			, 
			Истина));
			
	ДобавляемыеРеквизиты.Добавить(
		Новый РеквизитФормы(
			"Представление", 
			ОбщегоНазначения.ОписаниеТипаСтрока(150), 
			"бг_ГруппыПартнеров", 
			, 
			Истина));
			
	бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьРеквизитыНаФорму(
		ЭтаФорма, 
		ДобавляемыеРеквизиты);
	
КонецПроцедуры

&НаСервере
Процедура бг_ДобавитьЭлементыФормы()
	
	бг_СтраницаГруппыПартнеров = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьГруппуНаФорму(
		ЭтотОбъект,
		"бг_СтраницаГруппыПартнеров",
		Элементы.СтраницыТипФильтра, 
		ВидГруппыФормы.Страница);
		
	бг_ГруппыПартнеров = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьТаблицуНаФорму(
		ЭтотОбъект,
		"бг_ГруппыПартнеров",
		бг_СтраницаГруппыПартнеров,
		"бг_ГруппыПартнеров");
		
	бг_ГруппыПартнеров.Отображение = ОтображениеТаблицы.Дерево;
	бг_ГруппыПартнеров.УстановитьДействие("ПриАктивизацииСтроки", "ДинамическийСписокФильтрыПриАктивизацииСтроки");
	
	бг_ГруппыПартнеровПредставление = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
										ЭтотОбъект,
										"бг_ГруппыПартнеровПредставление",
										бг_ГруппыПартнеров,
										"бг_ГруппыПартнеров.Представление");
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ТипФильтраПриИзмененииПосле(Элемент)
	
	Если ТипФильтра = "ГруппыПартнеров" Тогда
		Для каждого СтрокаДерева Из ЭтотОбъект.бг_ГруппыПартнеров.ПолучитьЭлементы() Цикл
			Элементы.бг_ГруппыПартнеров.Развернуть(СтрокаДерева.ПолучитьИдентификатор());
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти