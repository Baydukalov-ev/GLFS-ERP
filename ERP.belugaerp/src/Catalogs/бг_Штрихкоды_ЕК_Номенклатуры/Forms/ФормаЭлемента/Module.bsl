
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ) 
	
	ЗаполнитьТипШтрихкода(); 
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли; 
	
	Если Параметры.Ключ.Пустая()
		И Параметры.Свойство("Владелец") Тогда
		Объект.Владелец = Параметры.Владелец;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы 

&НаКлиенте
Процедура ШтрихкодПриИзменении(Элемент)
	
	ЗаполнитьТипШтрихкода();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НовыйШтрихкод(Команда)
	
	ОчиститьСообщения();

	Если Объект.ЕдиницаИзмерения = БазоваяЕдиницаИзмеренияКоробка() Тогда
		Объект.Штрихкод = СформироватьШтрихкодITF14();
		
	Иначе
		Объект.Штрихкод = СформироватьШтрихкодEAN13();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьТипШтрихкода()    
	
	ТипШтрихкода = МенеджерОборудованияКлиентСервер.ОпределитьТипШтрихкода(Объект.Штрихкод); 
		
КонецПроцедуры
	
#КонецОбласти

#Область Прочее

&НаСервереБезКонтекста
Функция СформироватьШтрихкодITF14()
	
	ШтрихкодEAN13 = СформироватьШтрихкодEAN13();
	КодЛогистическогоВарианта = НСтр("ru = '1'");
	
	ПредварительныйШтрихкодITF14 = КодЛогистическогоВарианта + Лев(ШтрихкодEAN13, 12);

	КонтрольныйСимволGTIN14 = МенеджерОборудованияКлиентСервер.РассчитатьКонтрольныйСимволGTIN14(
								ПредварительныйШтрихкодITF14);
	
	Возврат ПредварительныйШтрихкодITF14 + КонтрольныйСимволGTIN14;
	
КонецФункции

&НаСервереБезКонтекста
Функция СформироватьШтрихкодEAN13()
	
	Возврат Справочники.бг_Штрихкоды_ЕК_Номенклатуры.СформироватьШтрихкодEAN13();
	
КонецФункции

&НаСервереБезКонтекста
Функция БазоваяЕдиницаИзмеренияКоробка()
	
	КодЕдиницыИзмеренияКоробка = "8751";
	
	БазовыеЕдиницыИзмерения = Справочники.УпаковкиЕдиницыИзмерения.ЗаполнитьЕдиницыИзмеренияИзКлассификатора(
	КодЕдиницыИзмеренияКоробка);

	Возврат БазовыеЕдиницыИзмерения.Получить(КодЕдиницыИзмеренияКоробка);
	
КонецФункции


#КонецОбласти

