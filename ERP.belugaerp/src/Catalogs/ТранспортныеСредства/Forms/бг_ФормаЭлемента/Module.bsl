#Область ОбработчикиСобытийФормы
	
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	ОбновитьСвязанныеДанныеПоТС();
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ОбновитьСвязанныеДанныеПоТС();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_ТранспортноеСредство", , Объект.Ссылка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЭкипажПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;      
	Если Объект.Ссылка.Пустая() Тогда                    
		ТекстСообщения = НСтр("ru='Перед добавлением необходимо записать элемент'");
		ПоказатьПредупреждение( , ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если Копирование Тогда
		Отказ = Ложь;
	Иначе
		ПараметрыОткрытия = Новый Структура("ТС", Объект.Ссылка);
		ОткрытьФорму("РегистрСведений.бг_ЭкипажТС.ФормаЗаписи", ПараметрыОткрытия, ЭтотОбъект, , , ,
			Неопределено, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура КодПриИзменении(Элемент)
	КодПриИзмененииСервер();
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура КодПриИзмененииСервер()
	Справочники.ТранспортныеСредства.бг_ПроверитьПреобразоватьГосНомерКШаблону(Объект.Код);
	СформироватьНаименование();
КонецПроцедуры

&НаСервере
Процедура ОбновитьСвязанныеДанныеПоТС()
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Экипаж.Отбор, "ТС", Объект.Ссылка, , , Истина);
КонецПроцедуры

&НаСервере
Процедура СформироватьНаименование()
	Объект.Наименование = СокрЛП(Объект.Код);
КонецПроцедуры

#КонецОбласти