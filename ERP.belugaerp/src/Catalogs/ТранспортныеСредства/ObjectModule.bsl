#Область ОбработчикиСобытий
	
&После("ПередЗаписью")
Процедура бг_ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;  
	Справочники.ТранспортныеСредства.бг_ПроверитьПреобразоватьГосНомерКШаблону(Код, Отказ);
	бг_ПроверитьНаДубли(Отказ);
	бг_Организация = Неопределено;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
	
Процедура бг_ПроверитьНаДубли(Отказ)
	Если ПометкаУдаления Или Отказ Тогда
		Возврат;
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТранспортныеСредства.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ТранспортныеСредства КАК ТранспортныеСредства
	|ГДЕ
	|	ТранспортныеСредства.Код = &Код
	|	И НЕ ТранспортныеСредства.Ссылка = &Ссылка
	|	И НЕ ТранспортныеСредства.ПометкаУдаления";
	Запрос.УстановитьПараметр("Код", Код);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	ТекстОшибки = НСтр("ru='Транспортное средство с государственным номером %1 уже зарегистрировано.'");
	ТекстОшибки = СтрШаблон(ТекстОшибки, Код);
	Пока Выборка.Следующий() Цикл
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Выборка.Ссылка, , , Отказ);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
