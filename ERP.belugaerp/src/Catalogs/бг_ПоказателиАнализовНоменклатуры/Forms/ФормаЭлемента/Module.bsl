#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьСтраницуВидРезультатаПоказателя();
	
	Если Объект.ВидРезультата = Перечисления.бг_ВидыРезультатовЛабораторныхАнализов.ЗначениеСПогрешностью Тогда
		ЗаполнитьЗначениеИПогрешностьПоказателя();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВидРезультатаПриИзменении(Элемент)
	УстановитьСтраницуВидРезультатаПоказателя();
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеПоказателяПриИзменении(Элемент)
	ЗаполнитьМинимальноеМаксимальноеЗначениеПоЗначениюИПогрешности();
КонецПроцедуры

&НаКлиенте
Процедура ПогрешностьЗначенияПриИзменении(Элемент)
	ЗаполнитьМинимальноеМаксимальноеЗначениеПоЗначениюИПогрешности();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьСтраницуВидРезультатаПоказателя()
	Если Объект.ВидРезультата = Перечисления.бг_ВидыРезультатовЛабораторныхАнализов.ЗначениеИзСписка Тогда
		Элементы.СтраницыПоказателиАнализов.ТекущаяСтраница = Элементы.СтраницаПоказателиЗначениеИзСписка;
	ИначеЕсли Объект.ВидРезультата = Перечисления.бг_ВидыРезультатовЛабораторныхАнализов.ЗначениеСПогрешностью Тогда
		Элементы.СтраницыПоказателиАнализов.ТекущаяСтраница = Элементы.СтраницаПоказателиАнализовЗначение;
	ИначеЕсли Объект.ВидРезультата = Перечисления.бг_ВидыРезультатовЛабораторныхАнализов.Поддиапазон Тогда
		Элементы.СтраницыПоказателиАнализов.ТекущаяСтраница = Элементы.СтраницаПоказателиПоддиапазон;
	Иначе
		Элементы.СтраницыПоказателиАнализов.ТекущаяСтраница = Элементы.СтраницаПоказателиАнализовИнтервал;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьМинимальноеМаксимальноеЗначениеПоЗначениюИПогрешности()
	Объект.МинимальноеЗначение = ЗначениеПоказателя - ПогрешностьЗначения;
	Объект.МаксимальноеЗначение = ЗначениеПоказателя + ПогрешностьЗначения;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗначениеИПогрешностьПоказателя()
	ЗначениеПоказателя = (Объект.МинимальноеЗначение + Объект.МаксимальноеЗначение) / 2;
	ПогрешностьЗначения = Объект.МаксимальноеЗначение - ЗначениеПоказателя;
КонецПроцедуры

#КонецОбласти
