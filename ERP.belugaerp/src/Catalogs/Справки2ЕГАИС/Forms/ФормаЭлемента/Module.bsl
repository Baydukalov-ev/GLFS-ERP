#Область ОбработчикиСобытийФормы

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.ДокументОснование) И ЗначениеЗаполнено(Объект.бг_ДокументОснование) Тогда
		бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
			ЭтотОбъект, "бг_ДокументОснование",	Элементы.СтраницаОсновное, 
			"Объект.бг_ДокументОснование", "ПолеФормы", Элементы.ДокументОснование, "ПолеНадписи");
		
		Элементы.бг_ДокументОснование.Гиперссылка = Истина;
		Элементы.ДокументОснование.Видимость = Ложь;
	КонецЕсли;	
	
	бг_ДобавитьДанныеШтрихкодовНаФорму();
	бг_ДобавитьПолеДатаНачисленияАкциза();
	бг_ДобавитьИсториюДвижений();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура бг_ДобавитьПолеДатаНачисленияАкциза()
	бг_ДатаНачисленияАкциза = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_ДатаНачисленияАкциза",
		Элементы.СтраницаОсновное,
		"Объект.бг_ДатаНачисленияАкциза",
		,
		Элементы.Количество);
	бг_ДатаНачисленияАкциза.АвтоМаксимальнаяШирина = Ложь;
	бг_ДатаНачисленияАкциза.МаксимальнаяШирина = 25;
КонецПроцедуры

#Область ИсторияДвижений
	
&НаСервере
Процедура бг_ДобавитьИсториюДвижений()
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	бг_ИсторияДвиженийДобавитьРеквизиты();
	бг_ИсторияДвиженийУстановитьСвойстваСписка();
	бг_ИсторияДвиженийДобавитьЭлементыФормы();                     
	ЭтотОбъект["бг_ДатаАктуальностиДвижений"] = бг_ДатаАктуальностиДвижений();
КонецПроцедуры

&НаСервере
Процедура бг_ИсторияДвиженийДобавитьРеквизиты()
	НовыеРеквизиты = Новый Массив;
	РеквизитДатаАктуальностиДвижений = Новый РеквизитФормы(
						"бг_ДатаАктуальностиДвижений",
						ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя),
						,
						НСтр("ru='Дата актуальности движений'"));
	НовыеРеквизиты.Добавить(РеквизитДатаАктуальностиДвижений);
	РеквизитСписокИсторияДвижений = Новый РеквизитФормы(
						"бг_СписокИсторияДвижений",
						Новый ОписаниеТипов("ДинамическийСписок"),
						,
						НСтр("ru='История движений'"));
	НовыеРеквизиты.Добавить(РеквизитСписокИсторияДвижений);
	ИзменитьРеквизиты(НовыеРеквизиты);
КонецПроцедуры

&НаСервере
Процедура бг_ИсторияДвиженийДобавитьЭлементыФормы()
	СтраницаИсторияДвижений = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьСтраницуНаФорму(
						ЭтотОбъект,
						"бг_ИсторияДвижений",
						НСтр("ru='История движений'"),
						"Страницы");  
	ГруппаДанныеДвижений = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьГруппуНаФорму(
						ЭтотОбъект,
						"бг_ГруппаДанныеДвижений",
						СтраницаИсторияДвижений);
	ГруппаДанныеДвижений.ОтображатьЗаголовок = Ложь;
	ГруппаДанныеДвижений.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ПолеДатаАктуальностиДвижений = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
						ЭтотОбъект,
						"бг_ДатаАктуальностиДвижений",
	                    ГруппаДанныеДвижений,
						"бг_ДатаАктуальностиДвижений");
	ПолеДатаАктуальностиДвижений.ТолькоПросмотр = Истина;
	ПолеСписокДвижений = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьТаблицуНаФорму(
						ЭтотОбъект,
						"бг_СписокИсторияДвижений",
	                    СтраницаИсторияДвижений,
						"бг_СписокИсторияДвижений");
	ПолеСписокДвижений.ТолькоПросмотр = Истина;
	
	ПолеОрганизацияЕГАИС = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
						ЭтотОбъект,
						"бг_СписокДвиженийОрганизацияЕГАИС",
	                    ПолеСписокДвижений,
						"бг_СписокИсторияДвижений.ОрганизацияЕГАИС");
	ПолеРегистратор = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
						ЭтотОбъект,
						"бг_СписокДвиженийРегистратор",
	                    ПолеСписокДвижений,
						"бг_СписокИсторияДвижений.Регистратор");
	ПолеПериод = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
						ЭтотОбъект,
						"бг_СписокДвиженийПериод",
	                    ПолеСписокДвижений,
						"бг_СписокИсторияДвижений.Период");
	ПолеИдентификаторЕГАИС = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
						ЭтотОбъект,
						"бг_СписокДвиженийИдентификаторЕГАИС",
	                    ПолеСписокДвижений,
						"бг_СписокИсторияДвижений.ИдентификаторЕГАИС");
	ПолеКоличество = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
						ЭтотОбъект,
						"бг_СписокДвиженийКоличество",
	                    ПолеСписокДвижений,
						"бг_СписокИсторияДвижений.Количество");
КонецПроцедуры

&НаСервере
Процедура бг_ИсторияДвиженийУстановитьСвойстваСписка()
	СписокИсторияДвижений = ЭтотОбъект["бг_СписокИсторияДвижений"];
	СписокИсторияДвижений.ПроизвольныйЗапрос = Истина;
	СписокИсторияДвижений.ТекстЗапроса = бг_ТекстЗапросаДинамическогоСпискаИсторияДвижений();
	СписокИсторияДвижений.ОсновнаяТаблица = "РегистрНакопления.ОстаткиАлкогольнойПродукцииЕГАИС";
	СписокИсторияДвижений.ДинамическоеСчитываниеДанных = Ложь;
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокИсторияДвижений, "Справка2", Объект.Ссылка, Истина); 
КонецПроцедуры

&НаСервере
Функция бг_ТекстЗапросаДинамическогоСпискаИсторияДвижений()
	Возврат
	"ВЫБРАТЬ Разрешенные
	|	ОстаткиАлкогольнойПродукцииЕГАИС.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ОстаткиАлкогольнойПродукцииЕГАИС.Регистратор КАК Регистратор,
	|	ОстаткиАлкогольнойПродукцииЕГАИС.Период КАК Период,
	|	ВЫБОР
	|	КОГДА ОстаткиАлкогольнойПродукцииЕГАИС.Регистратор ССЫЛКА Документ.АктПостановкиНаБалансЕГАИС
	|		ТОГДА ВЫРАЗИТЬ(ОстаткиАлкогольнойПродукцииЕГАИС.Регистратор КАК Документ.АктПостановкиНаБалансЕГАИС).ИдентификаторЕГАИС
	|	КОГДА ОстаткиАлкогольнойПродукцииЕГАИС.Регистратор ССЫЛКА Документ.АктСписанияЕГАИС
	|		ТОГДА ВЫРАЗИТЬ(ОстаткиАлкогольнойПродукцииЕГАИС.Регистратор КАК Документ.АктСписанияЕГАИС).ИдентификаторЕГАИС
	|	КОГДА ОстаткиАлкогольнойПродукцииЕГАИС.Регистратор ССЫЛКА Документ.битОтчетОПроизводствеЕГАИС
	|		ТОГДА ВЫРАЗИТЬ(ОстаткиАлкогольнойПродукцииЕГАИС.Регистратор КАК Документ.битОтчетОПроизводствеЕГАИС).ИдентификаторЕГАИС
	|	КОГДА ОстаткиАлкогольнойПродукцииЕГАИС.Регистратор ССЫЛКА Документ.битОтчетОбИмпортеПродукцииЕГАИС
	|		ТОГДА ВЫРАЗИТЬ(ОстаткиАлкогольнойПродукцииЕГАИС.Регистратор КАК Документ.битОтчетОбИмпортеПродукцииЕГАИС).ИдентификаторЕГАИС
	|	КОГДА ОстаткиАлкогольнойПродукцииЕГАИС.Регистратор ССЫЛКА Документ.ТТНВходящаяЕГАИС
	|		ТОГДА ВЫРАЗИТЬ(ОстаткиАлкогольнойПродукцииЕГАИС.Регистратор КАК Документ.ТТНВходящаяЕГАИС).ИдентификаторЕГАИС
	|	КОГДА ОстаткиАлкогольнойПродукцииЕГАИС.Регистратор ССЫЛКА Документ.ТТНИсходящаяЕГАИС
	|		ТОГДА ВЫРАЗИТЬ(ОстаткиАлкогольнойПродукцииЕГАИС.Регистратор КАК Документ.ТТНИсходящаяЕГАИС).ИдентификаторЕГАИС
	|	ИНАЧЕ """"
	|	КОНЕЦ КАК ИдентификаторЕГАИС,
	|	Выбор 
	|		Когда ОстаткиАлкогольнойПродукцииЕГАИС.ВидДвижения = Значение(ВидДвиженияНакопления.Приход) 
	|			Тогда 	ОстаткиАлкогольнойПродукцииЕГАИС.СвободныйОстаток
	|		Иначе - ОстаткиАлкогольнойПродукцииЕГАИС.СвободныйОстаток
	|	Конец КАК Количество
	|ИЗ
	|	РегистрНакопления.ОстаткиАлкогольнойПродукцииЕГАИС КАК ОстаткиАлкогольнойПродукцииЕГАИС
	|ГДЕ
	|	ОстаткиАлкогольнойПродукцииЕГАИС.Справка2 = &Справка2
	|	И НЕ ОстаткиАлкогольнойПродукцииЕГАИС.СвободныйОстаток = 0
	|	И ОстаткиАлкогольнойПродукцииЕГАИС.Активность
	|";
КонецФункции

&НаСервере
Функция бг_ДатаАктуальностиДвижений()
	Результат = Дата(1, 1, 1);
	Запрос = Новый  Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОтчетЕГАИС.Справка2 КАК Справка2,
	|	ЕСТЬNULL(МАКСИМУМ(ОтчетЕГАИС.Дата), ДАТАВРЕМЯ(1, 1, 1)) КАК Дата
	|ИЗ
	|	Документ.ОтчетЕГАИС КАК ОтчетЕГАИС
	|ГДЕ
	|	ОтчетЕГАИС.Справка2 = &Справка2
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетЕГАИС.Справка2";
	Запрос.УстановитьПараметр("Справка2", Объект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Дата;
	КонецЕсли;
	Возврат Результат;
КонецФункции   

#КонецОбласти

#Область СтраницаШтрихкоды

&НаСервере
Процедура бг_ДобавитьДанныеШтрихкодовНаФорму()
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	бг_ШтрихкодыДобавитьРеквизиты();
	СписокШтрихкоды = ЭтотОбъект["бг_СписокШтрихкоды"];
	бг_ШтрихкодыУстановитьСвойстваСписка(СписокШтрихкоды);
	бг_ШтрихкодыДобавитьЭлементыФормы();
	бг_ЗаполнитьДанныеОтчетаЕГАИС(ЭтотОбъект);
	ОтчетЕГАИС = ЭтотОбъект["бг_ОтчетЕГАИС"];
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокШтрихкоды, "ОтчетЕГАИС", ОтчетЕГАИС, Истина); 
КонецПроцедуры

&НаСервере
Процедура бг_ШтрихкодыДобавитьРеквизиты()
	НовыеРеквизиты = Новый Массив;
	РеквизитДатаОстатковШтрихкодов = Новый РеквизитФормы(
						"бг_ДатаОстатковШтрихкодов",
						ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя),
						,
						НСтр("ru='Штрихкоды на дату'"));
	НовыеРеквизиты.Добавить(РеквизитДатаОстатковШтрихкодов);
	РеквизитКоличествоШтрихкодов = Новый РеквизитФормы(
						"бг_КоличествоШтрихкодов",
						ОбщегоНазначения.ОписаниеТипаЧисло(6),
						,
						НСтр("ru='Всего штрихкодов'"));
	НовыеРеквизиты.Добавить(РеквизитКоличествоШтрихкодов);
	РеквизитОтчетЕГАИС = Новый РеквизитФормы(
						"бг_ОтчетЕГАИС",
						Новый ОписаниеТипов("ДокументСсылка.ОтчетЕГАИС"),
						,
						НСтр("ru='Отчет ЕГАИС'"));
	НовыеРеквизиты.Добавить(РеквизитОтчетЕГАИС);
	РеквизитСписокШтрихкоды = Новый РеквизитФормы(
						"бг_СписокШтрихкоды",
						Новый ОписаниеТипов("ДинамическийСписок"),
						,
						НСтр("ru='Штрихкоды'"));
	НовыеРеквизиты.Добавить(РеквизитСписокШтрихкоды);
	ИзменитьРеквизиты(НовыеРеквизиты);
КонецПроцедуры

&НаСервере
Процедура бг_ШтрихкодыДобавитьЭлементыФормы()
	СтраницаОстаткиШтрихкодов = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьСтраницуНаФорму(
						ЭтотОбъект,
						"бг_ОстаткиШтрихкодов",
						НСтр("ru='Остатки штрихкодов'"),
						"Страницы");  
	СтраницаОстаткиШтрихкодов.ПутьКДаннымЗаголовка = "бг_КоличествоШтрихкодов";
	ГруппаДанныеОтчета = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьГруппуНаФорму(
						ЭтотОбъект,
						"бг_ГруппаДанныеОтчета",
						СтраницаОстаткиШтрихкодов);
	ГруппаДанныеОтчета.ОтображатьЗаголовок = Ложь;
	ГруппаДанныеОтчета.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ПолеДатаОстатковШтрихкодов = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
						ЭтотОбъект,
						"бг_ДатаОстатковШтрихкодов",
	                    ГруппаДанныеОтчета,
						"бг_ДатаОстатковШтрихкодов");
	ПолеДатаОстатковШтрихкодов.ТолькоПросмотр = Истина;
	ПолеКоличествоШтрихкодов = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
						ЭтотОбъект,
						"бг_КоличествоШтрихкодов",
	                    ГруппаДанныеОтчета,
						"бг_КоличествоШтрихкодов");
	ПолеКоличествоШтрихкодов.ТолькоПросмотр = Истина;
	ПолеСписокШтрихкоды = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьТаблицуНаФорму(
						ЭтотОбъект,
						"бг_СписокШтрихкоды",
	                    СтраницаОстаткиШтрихкодов,
						"бг_СписокШтрихкоды");
	ПолеСписокШтрихкоды.ТолькоПросмотр = Истина;
	ПолеКодАкцизнойМарки = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
						ЭтотОбъект,
						"бг_СписокШтрихкодыКодАкцизнойМарки",
	                    ПолеСписокШтрихкоды,
						"бг_СписокШтрихкоды.КодАкцизнойМарки");
КонецПроцедуры

&НаСервереБезКонтекста
Процедура бг_ЗаполнитьДанныеОтчетаЕГАИС(Форма)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОтчетЕГАИС.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ОтчетЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ОтчетЕГАИС.Справка2 КАК Справка2,
	|	МАКСИМУМ(ОтчетЕГАИС.Дата) КАК ДатаОстатков
	|ПОМЕСТИТЬ ДатыОтчетовЕГАИС
	|ИЗ
	|	Документ.ОтчетЕГАИС КАК ОтчетЕГАИС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО ОтчетЕГАИС.Ссылка = СтатусыДокументовЕГАИС.Документ
	|			И (СтатусыДокументовЕГАИС.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиОтчетаЕГАИС.ПолученОтчет))
	|			И (ОтчетЕГАИС.ВидДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыДокументовЕГАИС.ЗапросОтчетаОстаткиВРегистре3))
	|			И (ОтчетЕГАИС.Справка2 = &Справка2)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетЕГАИС.ОрганизацияЕГАИС,
	|	ОтчетЕГАИС.АлкогольнаяПродукция,
	|	ОтчетЕГАИС.Справка2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтчетЕГАИС.Ссылка КАК Ссылка,
	|	ДатыОтчетовЕГАИС.ДатаОстатков КАК ДатаОстатков
	|ПОМЕСТИТЬ втОтчетЕГАИС
	|ИЗ
	|	ДатыОтчетовЕГАИС КАК ДатыОтчетовЕГАИС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетЕГАИС КАК ОтчетЕГАИС
	|		ПО ДатыОтчетовЕГАИС.ОрганизацияЕГАИС = ОтчетЕГАИС.ОрганизацияЕГАИС
	|			И ДатыОтчетовЕГАИС.АлкогольнаяПродукция = ОтчетЕГАИС.АлкогольнаяПродукция
	|			И ДатыОтчетовЕГАИС.Справка2 = ОтчетЕГАИС.Справка2
	|			И ДатыОтчетовЕГАИС.ДатаОстатков = ОтчетЕГАИС.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОтчетЕГАИС.Ссылка КАК Ссылка,
	|	втОтчетЕГАИС.ДатаОстатков КАК ДатаОстатков,
	|	ДанныеОМарках.Количество КАК Количество
	|ИЗ
	|	втОтчетЕГАИС КАК втОтчетЕГАИС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			КОЛИЧЕСТВО(*) КАК Количество
	|		ИЗ
	|			Документ.ОтчетЕГАИС.АкцизныеМарки КАК АкцизныеМарки
	|		ГДЕ
	|			АкцизныеМарки.Ссылка В
	|					(ВЫБРАТЬ
	|						втОтчетЕГАИС.Ссылка
	|					ИЗ
	|						втОтчетЕГАИС КАК втОтчетЕГАИС)) КАК ДанныеОМарках
	|		ПО (ИСТИНА)";
	Запрос.УстановитьПараметр("Справка2", Форма.Объект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Форма.бг_ДатаОстатковШтрихкодов = Выборка.ДатаОстатков;
		Форма.бг_ОтчетЕГАИС = Выборка.Ссылка;
		Форма.бг_КоличествоШтрихкодов = Выборка.Количество;
	Иначе
		Форма.бг_ДатаОстатковШтрихкодов = Дата(1, 1, 1);
		Форма.бг_ОтчетЕГАИС = Документы.ОтчетЕГАИС.ПустаяСсылка();
		Форма.бг_КоличествоШтрихкодов = 0;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура бг_ШтрихкодыУстановитьСвойстваСписка(СписокШтрихкоды)
	СписокШтрихкоды.ПроизвольныйЗапрос = Истина;
	СписокШтрихкоды.ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОтчетЕГАИСАкцизныеМарки.КодАкцизнойМарки КАК КодАкцизнойМарки
	|ИЗ
	|	Документ.ОтчетЕГАИС.АкцизныеМарки КАК ОтчетЕГАИСАкцизныеМарки
	|ГДЕ
	|	ОтчетЕГАИСАкцизныеМарки.Ссылка = &ОтчетЕГАИС";
	СписокШтрихкоды.ОсновнаяТаблица = "Документ.ОтчетЕГАИС.АкцизныеМарки";
	СписокШтрихкоды.ДинамическоеСчитываниеДанных = Ложь;
КонецПроцедуры

#КонецОбласти

#КонецОбласти
