#Область ОбработчикиСобытийФормы
	
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	Если Параметры.Свойство("Справки2") Тогда 
		ЗаполнитьДанныеПоСправкам2(Параметры.Справки2);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьЗначения(Команда)
	ЗаписатьДатыНачисленияАкцизаНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьДатыНачисленияАкцизаНаСервере()
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Справка2 КАК Справка2,
	|	Таблица.ДатаНачисленияАкциза КАК ДатаНачисленияАкциза
	|ПОМЕСТИТЬ втИсходныеДанные
	|ИЗ
	|	&ИсходныеДанные КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втИсходныеДанные.Справка2 КАК Справка2,
	|	втИсходныеДанные.ДатаНачисленияАкциза КАК ДатаНачисленияАкциза
	|ИЗ
	|	втИсходныеДанные КАК втИсходныеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Справки2ЕГАИС КАК Справки2ЕГАИС
	|		ПО втИсходныеДанные.Справка2 = Справки2ЕГАИС.Ссылка
	|			И (НЕ втИсходныеДанные.ДатаНачисленияАкциза = ДАТАВРЕМЯ(1, 1, 1))
	|			И (НЕ втИсходныеДанные.ДатаНачисленияАкциза = Справки2ЕГАИС.бг_ДатаНачисленияАкциза)";
	Запрос.УстановитьПараметр("ИсходныеДанные", Данные.Выгрузить());

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Справка2Объект = Выборка.Справка2.ПолучитьОбъект();
		Справка2Объект.бг_ДатаНачисленияАкциза = Выборка.ДатаНачисленияАкциза;
		Справка2Объект.Записать();
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеПоСправкам2(Справки2)
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Справки2ЕГАИС.Ссылка КАК Справка2,
	|	Справки2ЕГАИС.бг_ДатаНачисленияАкциза КАК ДатаНачисленияАкциза
	|ИЗ
	|	Справочник.Справки2ЕГАИС КАК Справки2ЕГАИС
	|ГДЕ
	|	Справки2ЕГАИС.Ссылка В(&Справки2)";
	Запрос.УстановитьПараметр("Справки2", Справки2);
	Данные.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры

#КонецОбласти
