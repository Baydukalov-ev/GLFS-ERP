#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("Код");
	Поля.Добавить("Наименование");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Представление = СтрШаблон("%1 (%2)", СокрЛП(Данные.Код), СокрЛП(Данные.Наименование));
	
КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СтрокаПоиска") И Не ПустаяСтрока(Параметры.СтрокаПоиска) Тогда
	
		СтандартнаяОбработка = Ложь;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СтрокаПоискаКода",               Параметры.СтрокаПоиска + "%");
		Запрос.УстановитьПараметр("СтрокаПоискаНаименования", "%" + Параметры.СтрокаПоиска + "%");
	    Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 51
		|	бг_КлассификаторТиповМарок.Ссылка КАК Ссылка,
		|	бг_КлассификаторТиповМарок.Представление КАК Представление
		|ИЗ
		|	Справочник.бг_КлассификаторТиповМарок КАК бг_КлассификаторТиповМарок
		|ГДЕ
		|	(бг_КлассификаторТиповМарок.Код ПОДОБНО &СтрокаПоискаКода
		|			ИЛИ бг_КлассификаторТиповМарок.Наименование ПОДОБНО &СтрокаПоискаНаименования)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Представление";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		ДанныеВыбора = Новый СписокЗначений;
		Пока Выборка.Следующий() Цикл
			ДанныеВыбора.Добавить(Выборка.Ссылка, Выборка.Представление);
		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

#КонецОбласти
