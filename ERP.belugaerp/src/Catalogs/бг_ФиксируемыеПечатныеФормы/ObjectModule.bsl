#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверкаУникальностиЭлементаПоКодуТипаДокумента(Отказ);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверкаУникальностиЭлементаПоКодуТипаДокумента(Отказ)
	
	Если ПометкаУдаления Тогда
		Возврат;	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФиксируемыеПечатныеФормы.Наименование КАК Наименование
		|ИЗ
		|	Справочник.бг_ФиксируемыеПечатныеФормы КАК ФиксируемыеПечатныеФормы
		|ГДЕ
		|	ФиксируемыеПечатныеФормы.КодТипаДокумента = &КодТипаДокумента
		|	И НЕ ФиксируемыеПечатныеФормы.ПометкаУдаления
		|	И ФиксируемыеПечатныеФормы.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("КодТипаДокумента", КодТипаДокумента);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонОшибки = НСтр("ru='Код типа документа уже выбран для: %1. 
						|Пометьте на удаление дублирующиеся элементы справочника'");
		
	Выборка = РезультатЗапроса.Выбрать();
	
	ПредставлениеЭлементов = "";
	
	Пока Выборка.Следующий() Цикл
		ПредставлениеЭлементов = ПредставлениеЭлементов + Символы.ПС + Выборка.Наименование;		
	КонецЦикла;
	
	ОбщегоНазначения.СообщитьПользователю(
		СтрШаблон(ШаблонОшибки, ПредставлениеЭлементов),, 
		"Объект.КодТипаДокумента",, 
		Отказ); 
	
КонецПроцедуры

#КонецОбласти
	
#КонецЕсли