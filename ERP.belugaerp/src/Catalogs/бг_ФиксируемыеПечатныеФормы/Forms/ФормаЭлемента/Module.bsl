#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписокВыбораТиповДокументов();
	АктуализироватьСписокВыбораПечатныхФорм();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипДокументаПриИзменении(Элемент)
		
	АктуализироватьСписокВыбораПечатныхФорм();
	
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИдентификаторыМакетовПечатныхФорм

&НаКлиенте
Процедура ИдентификаторыМакетовПечатныхФормПредставлениеПриИзменении(Элемент)
	
	ЗаполнитьСтрокуТабличнойЧасти(Элемент, "ИдентификаторМакета");	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура АктуализироватьСписокВыбораПечатныхФорм()
	
	Элементы.ИдентификаторыМакетовПечатныхФормПредставление.СписокВыбора.Очистить();
	
	ТипДокумента = Объект.ТипДокумента;
	
	Если Не ПустаяСтрока(ТипДокумента) Тогда
		
		КоллекцияКомандПечати = УправлениеПечатью.СоздатьКоллекциюКомандПечати();
		Документы[ТипДокумента].ДобавитьКомандыПечати(КоллекцияКомандПечати);
		Справочники.бг_ВидыСопроводительныхДокументов.ДобавитьКомандыПечати(КоллекцияКомандПечати);
		
		Для Каждого КомандаПечати Из КоллекцияКомандПечати Цикл
			
			Если ЗначениеЗаполнено(КомандаПечати.Обработчик) Тогда
				Продолжить;
			КонецЕсли;
	
			Элементы.ИдентификаторыМакетовПечатныхФормПредставление.СписокВыбора.Добавить(
				КомандаПечати.Идентификатор, КомандаПечати.Представление);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораТиповДокументов()
	
	МетаданныеДоступныхДокументов = Новый Массив;

	МетаданныеДоступныхДокументов.Добавить(Метаданные.Документы.КорректировкаПриобретения);
	МетаданныеДоступныхДокументов.Добавить(Метаданные.Документы.КорректировкаРеализации);
	МетаданныеДоступныхДокументов.Добавить(Метаданные.Документы.РеализацияТоваровУслуг);
	МетаданныеДоступныхДокументов.Добавить(Метаданные.Документы.СчетФактураВыданный);
	
	Для Каждого МетаданныеДокумента Из МетаданныеДоступныхДокументов Цикл
		Элементы.ТипДокумента.СписокВыбора.Добавить(
			МетаданныеДокумента.Имя, 
			МетаданныеДокумента.Представление());
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСтрокуТабличнойЧасти(Элемент, РеквизитЗначения)

	ТекущиеДанные = Элемент.Родитель.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;	
	
	ВыбранноеЗначение = ТекущиеДанные.Представление;
	
	ЭлементСпискаВыбора = Элемент.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
	
	ТекущиеДанные[РеквизитЗначения] = ВыбранноеЗначение;
	ТекущиеДанные.Представление = ЭлементСпискаВыбора.Представление;
	
КонецПроцедуры	

#КонецОбласти