#Область ОбработчикиСобытийФормы

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	бг_ДобавитьПолеПунктРазгрузки();
	
КонецПроцедуры

#КонецОбласти

#Область СопоставлениеПартнеров

&НаСервере
&Вместо("СопоставитьПоИННКППСервер")
Функция бг_СопоставитьПоИННКППСервер()
	
	Если Объект.СоответствуетОрганизации Тогда
		
		НайденнаяОрганизация = ИнтеграцияИС.ОрганизацияПоИННКПП(Объект.ИНН, Объект.КПП);
		Если ЗначениеЗаполнено(НайденнаяОрганизация) Тогда
			Объект.Контрагент = НайденнаяОрганизация;
		КонецЕсли;
//#Вставка
		РезультатПоиска = Неопределено;
		ИнтеграцияЕГАИСПереопределяемый.КонтрагентТорговыйОбъектПоИННКПП(РезультатПоиска, Объект.ИНН, Объект.КПП);
		Если ЗначениеЗаполнено(РезультатПоиска) Тогда		
			Объект.бг_ПунктРазгрузки = РезультатПоиска.Контрагент;			
		КонецЕсли;
//#КонецВставки		
		ПриИзмененииРеквизитаСопоставленияНаСервере();

		Возврат Истина;
		
	Иначе
		
		РезультатПоиска = Неопределено;
		ИнтеграцияЕГАИСПереопределяемый.КонтрагентТорговыйОбъектПоИННКПП(РезультатПоиска, Объект.ИНН, Объект.КПП);
		Если ЗначениеЗаполнено(РезультатПоиска) Тогда
			
			Объект.Контрагент     = РезультатПоиска.Контрагент;
			Объект.ТорговыйОбъект = РезультатПоиска.ТорговыйОбъект;
			
			ПриИзмененииРеквизитаСопоставленияНаСервере();
			
			Возврат Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаСервере
Процедура бг_ДобавитьПолеПунктРазгрузки()
	
	бг_ПунктРазгрузки = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_ПунктРазгрузки",
		Элементы.ГруппаСобственнаяОрганизацияДанные,
		"Объект.бг_ПунктРазгрузки",
		, // ТипЭлемента
		, // Элемент (предшествующий)
		"ПолеВвода");
	бг_ПунктРазгрузки.АвтоМаксимальнаяШирина = Ложь;
	бг_ПунктРазгрузки.МаксимальнаяШирина = 28;
	
КонецПроцедуры


#КонецОбласти