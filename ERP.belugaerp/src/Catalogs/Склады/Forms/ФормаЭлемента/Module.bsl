#Область ОбработчикиСобытий

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	бг_ДобавитьПолеОрганизация();
	бг_ДобавитьПолеТипВнешнейСкладскойСистемы();
	бг_ДобавитьПолеПунктРазгрузки();
	бг_ДобавитьПолеПунктНазначения();
	бг_ДобавитьПолеОбязательнаяАгрегацияПриОтгрузке();
	бг_ДобавитьПолеКодКатегорииSolvo();
	бг_ДобавитьПолеРазрешитьПланированиеОтгрузокБезУказанияСерий();
	бг_ДобавитьПолеКонтролироватьСерии();
	бг_ДобавитьПолеАвтоматическиСоздаватьСкладскиеОрдераПриОформленииПеремещенийТоваров();
	
	бг_УстановитьВидимостьПолейWMS(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ТипВнешнейСкладскойСистемыПриИзмененииНаКлиенте(Элемент)
	
	бг_УстановитьВидимостьПолейWMS(ЭтаФорма);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура бг_ДобавитьПолеОрганизация()
	бг_Организация = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_Организация",
		Элементы.ГруппаПодразделение,
		"Объект.бг_Организация",
		,
		Элементы.Подразделение);
	бг_Организация.АвтоМаксимальнаяШирина = Ложь;
	бг_Организация.МаксимальнаяШирина = 35;
КонецПроцедуры

&НаСервере
Процедура бг_ДобавитьПолеТипВнешнейСкладскойСистемы()

	бг_ТипВнешнейСкладскойСистемы = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_ТипВнешнейСкладскойСистемы",
		бг_WMS.СтраницаWMS(ЭтотОбъект),
		"Объект.бг_ТипВнешнейСкладскойСистемы",
		, // ТипЭлемента
		, // Элемент (предшествующий)
		"ПолеВвода");
		
	бг_ТипВнешнейСкладскойСистемы.УстановитьДействие(
		"ПриИзменении",
		"бг_ТипВнешнейСкладскойСистемыПриИзмененииНаКлиенте");

КонецПроцедуры
	
&НаСервере
Процедура бг_ДобавитьПолеОбязательнаяАгрегацияПриОтгрузке()

	бг_ОбязательнаяАгрегацияПриОтгрузке = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_ОбязательнаяАгрегацияПриОтгрузке",
		бг_WMS.ГруппаMobileSmarts(ЭтотОбъект),
		"Объект.бг_ОбязательнаяАгрегацияПриОтгрузке",
		, // ТипЭлемента
		, // Элемент (предшествующий)
		"ПолеВвода");
		
	бг_ОбязательнаяАгрегацияПриОтгрузке.Вид = ВидПоляФормы.ПолеФлажка;
	бг_ОбязательнаяАгрегацияПриОтгрузке.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;

КонецПроцедуры

&НаСервере
Процедура бг_ДобавитьПолеПунктРазгрузки()

	бг_ПунктРазгрузки = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_ПунктРазгрузки",
		бг_WMS.СтраницаWMS(ЭтотОбъект),
		"Объект.бг_ПунктРазгрузки",
		, // ТипЭлемента
		, // Элемент (предшествующий)
		"ПолеВвода");
	
КонецПроцедуры

&НаСервере
Процедура бг_ДобавитьПолеПунктНазначения()

	бг_ПунктНазначения = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_ПунктНазначения",
		бг_WMS.СтраницаWMS(ЭтотОбъект),
		"Объект.бг_ПунктНазначения",
		, // ТипЭлемента
		, // Элемент (предшествующий)
		"ПолеВвода");
		
	СвязиПараметровВыбора = Новый Массив;
	СвязиПараметровВыбора.Добавить(Новый СвязьПараметраВыбора("Отбор.Клиент", "Объект.бг_Организация"));
	бг_ПунктНазначения.СвязиПараметровВыбора = Новый ФиксированныйМассив(СвязиПараметровВыбора);
	
КонецПроцедуры
	
&НаСервере
Процедура бг_ДобавитьПолеКодКатегорииSolvo()

	бг_КодКатегорииСкладаSolvo = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_КодКатегорииСкладаSolvo",
		бг_WMS.ГруппаSolvo(ЭтотОбъект),
		"Объект.бг_КодКатегорииСкладаSolvo",
		, // ТипЭлемента
		, // Элемент (предшествующий)
		"ПолеВвода");

КонецПроцедуры

&НаСервере
Процедура бг_ДобавитьПолеРазрешитьПланированиеОтгрузокБезУказанияСерий()

	бг_РазрешитьПланированиеОтгрузокБезУказанияСерий = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_РазрешитьПланированиеОтгрузокБезУказанияСерий",
		бг_WMS.СтраницаWMS(ЭтотОбъект),
		"Объект.бг_РазрешитьПланированиеОтгрузокБезУказанияСерий",
		, // ТипЭлемента
		, // Элемент (предшествующий)
		"ПолеВвода");
		
	бг_РазрешитьПланированиеОтгрузокБезУказанияСерий.Вид = ВидПоляФормы.ПолеФлажка;
	бг_РазрешитьПланированиеОтгрузокБезУказанияСерий.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;

КонецПроцедуры

&НаСервере
Процедура бг_ДобавитьПолеКонтролироватьСерии()

	бг_КонтролироватьСерии = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_КонтролироватьСерии",
		бг_WMS.СтраницаWMS(ЭтотОбъект),
		"Объект.бг_КонтролироватьСерии",
		, // ТипЭлемента
		, // Элемент (предшествующий)
		"ПолеВвода");
		
	бг_КонтролироватьСерии.Вид = ВидПоляФормы.ПолеФлажка;
	бг_КонтролироватьСерии.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;

КонецПроцедуры

&НаСервере
Процедура бг_ДобавитьПолеАвтоматическиСоздаватьСкладскиеОрдераПриОформленииПеремещенийТоваров()

	бг_КонтролироватьСерии = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
		ЭтотОбъект,
		"бг_АвтоматичеcкиСоздаватьСкладскиеОрдераПриОформленииПеремещенийТоваров",
		бг_WMS.СтраницаWMS(ЭтотОбъект),
		"Объект.бг_АвтоматичеcкиСоздаватьСкладскиеОрдераПриОформленииПеремещенийТоваров",,,
		"ПолеВвода");
		
	бг_КонтролироватьСерии.Вид = ВидПоляФормы.ПолеФлажка;
	бг_КонтролироватьСерии.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура бг_УстановитьВидимостьПолейWMS(Форма)
	
	ТипWMS = Форма.Объект.бг_ТипВнешнейСкладскойСистемы;
	
	ЭтоТипMobileSmarts = ТипWMS = ПредопределенноеЗначение("Перечисление.бг_ТипыВнешнихСкладскихСистем.MobileSmarts");
	ЭтоТипSolvo = ТипWMS = ПредопределенноеЗначение("Перечисление.бг_ТипыВнешнихСкладскихСистем.Solvo");

	Форма.Элементы.бг_ОбязательнаяАгрегацияПриОтгрузке.Видимость = ЭтоТипMobileSmarts;
	Форма.Элементы.бг_КодКатегорииСкладаSolvo.Видимость = ЭтоТипSolvo;
	
КонецПроцедуры

#КонецОбласти
