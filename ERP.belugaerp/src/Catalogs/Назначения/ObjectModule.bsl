#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

&После("ПередЗаписью")
Процедура бг_ПередЗаписью(Отказ)
	бг_СформироватьНаименованиеПоЭтапуПроизводства();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура бг_СформироватьНаименованиеПоЭтапуПроизводства()
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ЗначениеЗаполнено(Заказ)
		Или ТипЗнч(Заказ) <> Тип("ДокументСсылка.ЭтапПроизводства2_2") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЭтапПроизводства.Номер КАК Номер,
	|	ЭтапПроизводства.Дата КАК Дата,
	|	ПРЕДСТАВЛЕНИЕ(РесурсныеСпецификации.ОсновноеИзделиеНоменклатура) КАК ОсновноеИзделие,
	|	ПРЕДСТАВЛЕНИЕ(ЭтапПроизводства.Подразделение) КАК Подразделение
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
	|		ПО (ЭтапПроизводства.Ссылка = &Ссылка)
	|			И ЭтапПроизводства.Спецификация = РесурсныеСпецификации.Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Заказ);
	Результат = Запрос.Выполнить();
	Выборка   = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		ШаблонНаименованиеНазначения = НСтр("ru = '%1, %2, %3, %4 (Этап производства)'");
		Наименование = СтрШаблон(
							ШаблонНаименованиеНазначения,
							Выборка.Номер,
							Выборка.ОсновноеИзделие,
							Формат(Выборка.Дата, "ДФ=dd.MM.yyyy"),
							Выборка.Подразделение);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли
