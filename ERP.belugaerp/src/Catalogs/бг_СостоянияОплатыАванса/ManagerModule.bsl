#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция СостояниеОплатыАванса(Период, Статус, НалоговаяСтавка = 0, ПродажаНаЭкспорт = Ложь) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СостоянияОплатыАванса.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.бг_СостоянияОплатыАванса КАК СостоянияОплатыАванса
	|ГДЕ
	|	СостоянияОплатыАванса.Статус = &Статус
	|	И СостоянияОплатыАванса.НалоговаяСтавка = &НалоговаяСтавка
	|	И СостоянияОплатыАванса.ПродажаНаЭкспорт = &ПродажаНаЭкспорт
	|	И СостоянияОплатыАванса.Период = &Период
	|	И НЕ СостоянияОплатыАванса.ПометкаУдаления";
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Статус", Статус);
	Запрос.УстановитьПараметр("НалоговаяСтавка", НалоговаяСтавка);
	Запрос.УстановитьПараметр("ПродажаНаЭкспорт", ПродажаНаЭкспорт);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	СостояниеОплатыАванса = Справочники.бг_СостоянияОплатыАванса.СоздатьЭлемент();
	СостояниеОплатыАванса.Статус = Статус;
	СостояниеОплатыАванса.Период = Период;
	СостояниеОплатыАванса.НалоговаяСтавка = НалоговаяСтавка;
	СостояниеОплатыАванса.Валюта = Константы.ВалютаРегламентированногоУчета.Получить();
	СостояниеОплатыАванса.ПродажаНаЭкспорт = ПродажаНаЭкспорт;
	СостояниеОплатыАванса.Записать();
	
	Возврат СостояниеОплатыАванса.Ссылка;
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("Статус");
	Поля.Добавить("НалоговаяСтавка");
	Поля.Добавить("Валюта");
	Поля.Добавить("ПродажаНаЭкспорт");
	Поля.Добавить("Период");
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если Данные.Статус = Перечисления.бг_СтатусыНачисленияАвансов.НачисленАванс Тогда
		Шаблон = НСтр("ru = '%1 %2 (ставка %3 %4)%5'");
	Иначе
		Шаблон = НСтр("ru = '%2 (аванс %1 ставка %3 %4 %5)'");
	КонецЕсли;
	
	Представление = СтрШаблон(
			Шаблон,
			Формат(Данные.Период, "ДФ=MM.yyyy"),
			Данные.Статус,
			Формат(Данные.НалоговаяСтавка, "ЧДЦ=2"),
			Данные.Валюта,
			?(Данные.ПродажаНаЭкспорт, НСтр("ru = ' экспорт'"), ""));
КонецПроцедуры

#КонецОбласти

#КонецЕсли
