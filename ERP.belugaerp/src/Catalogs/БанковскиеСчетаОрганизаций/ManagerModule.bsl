
#Область ПрограммныйИнтерфейс

&ИзменениеИКонтроль("ПолучитьРеквизитыБанковскогоСчетаОрганизации")
Функция бг_ПолучитьРеквизитыБанковскогоСчетаОрганизации(БанковскийСчет)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
#Вставка
	|	ВЫБОР КОГДА БанковскиеСчета.Банк.Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|		ТОГДА ЛОЖЬ
	|	ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК бг_ЭтоМеждународныйСчет,
#КонецВставки
	|	БанковскиеСчета.Владелец                          КАК Организация,
	|	БанковскиеСчета.ВалютаДенежныхСредств             КАК Валюта,
	|	БанковскиеСчета.РазрешитьПлатежиБезУказанияЗаявок КАК РазрешитьПлатежиБезУказанияЗаявок,
	|	БанковскиеСчета.НомерСчета                        КАК НомерСчета,
	|	БанковскиеСчета.ТипСчета                          КАК ТипСчета,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка
	|			ТОГДА БанковскиеСчета.НаименованиеБанка
	|		ИНАЧЕ БанковскиеСчета.Банк
	|	КОНЕЦ КАК Банк,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка
	|			ТОГДА БанковскиеСчета.НаименованиеБанка
	|		ИНАЧЕ БанковскиеСчета.Банк.Наименование
	|	КОНЕЦ КАК НаименованиеБанка,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка
	|			ТОГДА БанковскиеСчета.БИКБанка
	|		ИНАЧЕ БанковскиеСчета.Банк.Код
	|	КОНЕЦ КАК БИК,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка
	|			ТОГДА БанковскиеСчета.КоррСчетБанка
	|		ИНАЧЕ БанковскиеСчета.Банк.КоррСчет
	|	КОНЕЦ КАК КоррСчет,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка
	|			ТОГДА БанковскиеСчета.АдресБанка
	|		ИНАЧЕ БанковскиеСчета.Банк.Адрес
	|	КОНЕЦ КАК АдресБанка,
	|	БанковскиеСчета.Банк.БИКРКЦ КАК БИКРКЦ
	|ИЗ
	|	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Ссылка = &БанковскийСчет
	|";
	
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	
	РеквизитыСчета = Новый Структура;
	РеквизитыСчета.Вставить("Организация", Неопределено);
	РеквизитыСчета.Вставить("Валюта", Неопределено);
	РеквизитыСчета.Вставить("РазрешитьПлатежиБезУказанияЗаявок", Ложь);
	РеквизитыСчета.Вставить("НомерСчета", "");
	РеквизитыСчета.Вставить("ТипСчета", Неопределено);
	РеквизитыСчета.Вставить("Банк", Неопределено);
	РеквизитыСчета.Вставить("НаименованиеБанка", "");
	РеквизитыСчета.Вставить("БИК", "");
	РеквизитыСчета.Вставить("КоррСчет", "");
	РеквизитыСчета.Вставить("АдресБанка", "");
	РеквизитыСчета.Вставить("БИКРКЦ", Неопределено);
#Вставка
	РеквизитыСчета.Вставить("бг_ЭтоМеждународныйСчет", Ложь);
#КонецВставки
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(РеквизитыСчета, Выборка);
	КонецЕсли;
	
	Возврат РеквизитыСчета;
	
КонецФункции

#КонецОбласти
