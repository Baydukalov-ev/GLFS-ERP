#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	бг_ОбщегоНазначенияСервер.ПередЗаписьюСправочникаНСИ(Отказ, ЭтотОбъект);
	
	КодSAPУжеСуществует();
	
    бг_СинхронизироватьНоменклатуруКонтрагентовПоКодуSAPПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	бг_СинхронизироватьНоменклатуруКонтрагентовПоКодуSAPПриЗаписи();

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	КодНоменклатурыSAP = "";

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура бг_СинхронизироватьНоменклатуруКонтрагентовПоКодуSAPПередЗаписью()

	Если ЭтоНовый() Тогда
		СтарыйКодНоменклатурыSAP = "";			
	Иначе	
	    СтарыйКодНоменклатурыSAP = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "КодНоменклатурыSAP");
	КонецЕсли;	
	ДополнительныеСвойства.Вставить("СтарыйКодНоменклатурыSAP", СтарыйКодНоменклатурыSAP);

КонецПроцедуры

Процедура бг_СинхронизироватьНоменклатуруКонтрагентовПоКодуSAPПриЗаписи()

	Если ДополнительныеСвойства.СтарыйКодНоменклатурыSAP <> КодНоменклатурыSAP Тогда
		бг_НоменклатураКонтрагентов.СинхронизироватьНоменклатуруКонтрагентовПоКодуSAP(Ссылка);	
	КонецЕсли;
	
КонецПроцедуры

Процедура КодSAPУжеСуществует()
	
	Если Не ЗначениеЗаполнено(КодНоменклатурыSAP) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	бг_ЕК_СКЮ_СкюМТ.Наименование КАК Наименование,
	|	бг_ЕК_СКЮ_СкюМТ.Код КАК Код
	|ИЗ
	|	Справочник.бг_ЕК_СКЮ_СкюМТ КАК бг_ЕК_СКЮ_СкюМТ
	|ГДЕ
	|	бг_ЕК_СКЮ_СкюМТ.КодНоменклатурыSAP = &КодНоменклатурыSAP";
	Запрос.УстановитьПараметр("КодНоменклатурыSAP", КодНоменклатурыSAP);
	Если Не ЭтоНовый() Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И бг_ЕК_СКЮ_СкюМТ.Ссылка <> &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	ТекстСообщенияОбОшибке = "";
	
	Пока Выборка.Следующий() Цикл
		ТекстСообщенияОбОшибке = СтрШаблон("%1 %2 %3 %4 %5", 
			ТекстСообщенияОбОшибке, Выборка.Наименование, " с кодом ", Выборка.Код, Символы.ПС);
	КонецЦикла;
	
	ШаблонСообщенияОбОшибке = НСтр("ru = 'В СКЮ МТ: %1 уже указан такой код SAP'");
	
	ВызватьИсключение СтрШаблон(ШаблонСообщенияОбОшибке, ТекстСообщенияОбОшибке);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
