
#Область ПрограммныйИнтерфейс

&ИзменениеИКонтроль("ПолучитьРеквизитыБанковскогоСчета")
Функция бг_ПолучитьРеквизитыБанковскогоСчета(БанковскийСчет)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
#Вставка
	|	ВЫБОР КОГДА БанковскиеСчета.Банк.Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|		ТОГДА ЛОЖЬ
	|	ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК бг_ЭтоМеждународныйСчет,
#КонецВставки
	|	БанковскиеСчета.ВалютаДенежныхСредств КАК Валюта,
	|	БанковскиеСчета.ТекстНазначения КАК ТекстНазначения,
	|	БанковскиеСчета.НомерСчета КАК НомерСчета,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка
	|			ТОГДА БанковскиеСчета.НаименованиеБанка
	|		ИНАЧЕ БанковскиеСчета.Банк
	|	КОНЕЦ КАК Банк,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка
	|			ТОГДА БанковскиеСчета.БИКБанка
	|		ИНАЧЕ БанковскиеСчета.Банк.Код
	|	КОНЕЦ КАК БИК,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка
	|			ТОГДА БанковскиеСчета.КоррСчетБанка
	|		ИНАЧЕ БанковскиеСчета.Банк.КоррСчет
	|	КОНЕЦ КАК КоррСчет,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка
	|			ТОГДА БанковскиеСчета.АдресБанка
	|		ИНАЧЕ БанковскиеСчета.Банк.Адрес
	|	КОНЕЦ КАК АдресБанка,
	|	БанковскиеСчета.Банк.БИКРКЦ КАК БИКРКЦ
	|ИЗ
	|	Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Ссылка = &БанковскийСчет";
	
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Валюта 			= Выборка.Валюта;
		ТекстНазначения = Выборка.ТекстНазначения;
		НомерСчета 		= Выборка.НомерСчета;
		Банк 			= Выборка.Банк;
		БИК 			= Выборка.БИК;
		КоррСчет 		= Выборка.КоррСчет;
		АдресБанка 		= Выборка.АдресБанка;
		БИКРКЦ 			= Выборка.БИКРКЦ;
#Вставка
		бг_ЭтоМеждународныйСчет = Выборка.бг_ЭтоМеждународныйСчет;
#КонецВставки
	Иначе
#Вставка
		бг_ЭтоМеждународныйСчет 		= Ложь;
#КонецВставки
		Валюта 			= Справочники.Валюты.ПустаяСсылка();
		ТекстНазначения = "";
		НомерСчета 		= "";
		Банк 			= Неопределено;
		БИК 			= "";
		КоррСчет 		= "";
		АдресБанка 		= "";
		БИКРКЦ 			= Выборка.БИКРКЦ;
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Валюта", Валюта);
	СтруктураРеквизитов.Вставить("ТекстНазначения", ТекстНазначения);
	СтруктураРеквизитов.Вставить("НомерСчета", НомерСчета);
	СтруктураРеквизитов.Вставить("Банк", Банк);
	СтруктураРеквизитов.Вставить("БИК", БИК);
	СтруктураРеквизитов.Вставить("КоррСчет", КоррСчет);
	СтруктураРеквизитов.Вставить("АдресБанка", АдресБанка);
	СтруктураРеквизитов.Вставить("БИКРКЦ", БИКРКЦ);
#Вставка
	СтруктураРеквизитов.Вставить("бг_ЭтоМеждународныйСчет", бг_ЭтоМеждународныйСчет);
#КонецВставки
	
	Возврат СтруктураРеквизитов;

КонецФункции

#КонецОбласти

