#Область бг_ОбработчикиСобытийФормы

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	бг_Организация = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(ЭтотОбъект,
									"бг_Организация", Элементы.ГруппаДополнительно, "Объект.бг_Организация");
	бг_Организация.АвтоМаксимальнаяШирина = Ложь;
	бг_Организация.МаксимальнаяШирина = 28;
	
	бг_Экспорт = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(ЭтотОбъект,
									"бг_Экспорт", Элементы.ГруппаДополнительно, "Объект.бг_Экспорт", , , "ПолеФлажка");
	бг_Экспорт.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	
	бг_ОсновноеИзделиеУпаковкаПаллета = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(ЭтотОбъект,
									"бг_ОсновноеИзделиеУпаковкаПаллета", Элементы.ГруппаКоличествоУпаковкаБрак,
									"Объект.бг_ОсновноеИзделиеУпаковкаПаллета", ,Элементы.ОсновноеИзделиеПроцентБрака);
	бг_ОсновноеИзделиеУпаковкаПаллета.АвтоМаксимальнаяШирина = Ложь;
	бг_ОсновноеИзделиеУпаковкаПаллета.МаксимальнаяШирина = 12;
КонецПроцедуры

&НаСервере
Процедура бг_ОбработкаПроверкиЗаполненияНаСервереПосле(Отказ, ПроверяемыеРеквизиты)
	
	бг_ПроверитьЗаполнениеСтатьиКалькуляции(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область МатериалыИУслуги

#Область Прочее

&НаКлиенте
&После("ПриИзмененииНоменклатурыВСтрокеМатериалов")
Процедура бг_ПриИзмененииНоменклатурыВСтрокеМатериалов(ТекущиеДанные)
	
	бг_УстановитьСтатьюКалькуляцииИзНоменклатуры("МатериалыИУслуги");
	
КонецПроцедуры

&НаСервере
&После("ОбработкаВыбораПодборНаСервере")
Процедура бг_ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение)
	
	бг_УстановитьСтатьюКалькуляцииИзНоменклатурыНаСервере("МатериалыИУслуги");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

&НаКлиенте
Процедура бг_УстановитьСтатьюКалькуляцииИзНоменклатуры(ИмяТЧ)
	
	ТекущиеДанные = Элементы[ИмяТЧ].ТекущиеДанные;
	Если Не ТекущиеДанные = Неопределено И ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) Тогда
		МассивИдентификаторовСтрок = Новый Массив;
		МассивИдентификаторовСтрок.Добавить(ТекущиеДанные.ПолучитьИдентификатор());
		бг_УстановитьСтатьюКалькуляцииИзНоменклатурыНаСервере(ИмяТЧ, МассивИдентификаторовСтрок);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура бг_УстановитьСтатьюКалькуляцииИзНоменклатурыНаСервере(ИмяТЧ, МассивИдентификаторовСтрок = Неопределено)
	
	Если ИмяТЧ = "МатериалыИУслуги" Тогда
		бг_Номенклатура.ЗаполнитьСтатьюКалькуляцииВТЧИзНоменклатуры(Объект, ИмяТЧ, МассивИдентификаторовСтрок);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура бг_ПроверитьЗаполнениеСтатьиКалькуляции(Отказ)
	
	бг_Номенклатура.ПроверитьСтатьюКалькуляцииВТЧ(Объект, "МатериалыИУслуги", Отказ);
	
КонецПроцедуры

#КонецОбласти
