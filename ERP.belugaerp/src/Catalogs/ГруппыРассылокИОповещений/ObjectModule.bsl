#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

&ИзменениеИКонтроль("ОбработкаПроверкиЗаполнения")
Процедура бг_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	МассивНепроверяемыхРеквизитов = Новый Массив;

	Если Не ПредназначенаДляЭлектронныхПисем И Не ПредназначенаДляSMS Тогда

		ТекстОшибки = НСтр("ru = 'Не указан ни один вид доставки сообщений.';
							|en = 'No message delivery kind is specified.'");

		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			ЭтотОбъект,
			"ПредназначенаДляЭлектронныхПисем",
			,
			Отказ);

	КонецЕсли;

	Если Не ПредназначенаДляЭлектронныхПисем Тогда

		МассивНепроверяемыхРеквизитов.Добавить("УчетнаяЗапись");
		МассивНепроверяемыхРеквизитов.Добавить("ВидКонтактнойИнформацииПартнераДляПисем");
		МассивНепроверяемыхРеквизитов.Добавить("ВидКонтактнойИнформацииКонтактногоЛицаДляПисем");

	КонецЕсли;

	Если Не ПредназначенаДляSMS Тогда

		МассивНепроверяемыхРеквизитов.Добавить("ВидКонтактнойИнформацииПартнераДляSMS");
		МассивНепроверяемыхРеквизитов.Добавить("ВидКонтактнойИнформацииКонтактногоЛицаДляSMS");

	КонецЕсли;

	Если Принудительная И Не ОтправлятьПартнеру Тогда

		МассивНепроверяемыхРеквизитов.Добавить("ВидКонтактнойИнформацииПартнераДляПисем");
		МассивНепроверяемыхРеквизитов.Добавить("ВидКонтактнойИнформацииПартнераДляSMS");

	КонецЕсли;

	Если Принудительная И (Не ОтправлятьКонтактнымЛицамРоли) И (Не ОтправлятьКонтактномуЛицуОбъектаОповещения) Тогда

		МассивНепроверяемыхРеквизитов.Добавить("ВидКонтактнойИнформацииКонтактногоЛицаДляПисем");
		МассивНепроверяемыхРеквизитов.Добавить("ВидКонтактнойИнформацииКонтактногоЛицаДляSMS");

	КонецЕсли;

	Если Не ОтправлятьКонтактнымЛицамРоли Тогда

		МассивНепроверяемыхРеквизитов.Добавить("РольКонтактногоЛица");

	КонецЕсли;

#Вставка
	Если Не Принудительная Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("бг_АдресацияЭлектронныхПисем");
		МассивНепроверяемыхРеквизитов.Добавить("бг_ГруппаПользователей");

	КонецЕсли;
#КонецВставки
	Если Принудительная Тогда

#Вставка
		Если Не бг_АдресацияЭлектронныхПисем = "ГруппеПользователей" Тогда
			МассивНепроверяемыхРеквизитов.Добавить("бг_ГруппаПользователей");			
		КонецЕсли;
#КонецВставки
		Если Не ОтправлятьПартнеру
			 И Не ОтправлятьКонтактномуЛицуОбъектаОповещения
#Вставка
			 И ПустаяСтрока(бг_АдресацияЭлектронныхПисем)
#КонецВставки
			 И Не ОтправлятьКонтактнымЛицамРоли Тогда

			ТекстОшибки = НСтр("ru = 'Необходимо указать хотя бы один вид адресатов.';
								|en = 'Specify at least one recipient kind.'");

			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект,
#Удаление
				"ОтправлятьПартнеру",
#КонецУдаления
#Вставка
				"бг_АдресацияЭлектронныхПисем",
#КонецВставки
				,
				Отказ);

		КонецЕсли;

	КонецЕсли;

	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);

КонецПроцедуры

#КонецОбласти

#КонецЕсли
