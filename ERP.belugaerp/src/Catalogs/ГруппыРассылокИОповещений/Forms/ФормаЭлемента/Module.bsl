
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	// В связи с переделкой поведения формы, надо установить заголовок типового поля ввода.
	Элементы.РольКонтактногоЛица.Заголовок = НСтр("ru = 'Роль'", ОбщегоНазначения.КодОсновногоЯзыка());

	бг_СоздатьЭлементыФормы();
	бг_ЗаполнитьСпискиВыбораРеквизитов();
	бг_УстановитьВидимостьЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура бг_ПриЧтенииНаСервереПосле(ТекущийОбъект)
	
	Если Не Элементы.Найти("бг_ГруппаПользователей") = Неопределено Тогда
		бг_УстановитьВидимостьЭлементовФормы();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура бг_АдресацияЭлектронныхПисемПриИзменении(Элемент)
	
	Объект.ОтправлятьПартнеру = Объект.бг_АдресацияЭлектронныхПисем = "ОтправлятьПартнеру";
	Объект.ОтправлятьКонтактнымЛицамРоли = Объект.бг_АдресацияЭлектронныхПисем = "КонтактнымЛицамРоли";
	Объект.ОтправлятьКонтактномуЛицуОбъектаОповещения = Объект.бг_АдресацияЭлектронныхПисем = "КонтактномуЛицуОбъектаОповещения";
	бг_УстановитьВидимостьЭлементовФормы();
	ЗаполнитьВидыКИПоУмолчанию();
	УправлениеДоступностью(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ТипПодпискиПриИзмененииПосле(Элемент)
	бг_УстановитьВидимостьЭлементовФормы();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура бг_СоздатьЭлементыФормы()

	бг_АдресацияЭлектронныхПисем = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
				ЭтотОбъект,
				"бг_АдресацияЭлектронныхПисем",
				Элементы.ГруппаНастройкиАдресатовПринудительно,
				"Объект.бг_АдресацияЭлектронныхПисем");
	бг_АдресацияЭлектронныхПисем.Заголовок = НСтр("ru = 'Отправлять'", ОбщегоНазначения.КодОсновногоЯзыка());
	бг_АдресацияЭлектронныхПисем.РежимВыбораИзСписка = Истина;
	бг_АдресацияЭлектронныхПисем.УстановитьДействие("ПриИзменении", "бг_АдресацияЭлектронныхПисемПриИзменении");
	Элементы.Переместить(бг_АдресацияЭлектронныхПисем, Элементы.ГруппаНастройкиАдресатовПринудительно,
		Элементы.ОтправлятьПартнеру);
	
	бг_ГруппаПользователей = бг_МодификацияИнтерфейсовОбщегоНазначенияСервер.ДобавитьПолеНаФорму(
				ЭтотОбъект,
				"бг_ГруппаПользователей",
				Элементы.ГруппаНастройкиАдресатовПринудительно,
				"Объект.бг_ГруппаПользователей");
	бг_ГруппаПользователей.Заголовок = НСтр("ru = 'Группа'", ОбщегоНазначения.КодОсновногоЯзыка());

КонецПроцедуры

&НаСервере
Процедура бг_ЗаполнитьСпискиВыбораРеквизитов()
	
	Если Элементы.ОтправлятьПартнеру.Видимость Тогда
		 Элементы.бг_АдресацияЭлектронныхПисем.СписокВыбора.Добавить("ОтправлятьПартнеру", "Партнеру");
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРолиКонтактныхЛицПартнеров") Тогда
		 Элементы.бг_АдресацияЭлектронныхПисем.СписокВыбора.Добавить("КонтактнымЛицамРоли", "Контактным лицам роли");
	КонецЕсли;
	
	Если Элементы.ОтправлятьКонтактномуЛицуОбъектаОповещения.Видимость Тогда
		 Элементы.бг_АдресацияЭлектронныхПисем.СписокВыбора.Добавить("КонтактномуЛицуОбъектаОповещения",
			"Контактному лицу объекта оповещения");
	КонецЕсли;
	
	 Элементы.бг_АдресацияЭлектронныхПисем.СписокВыбора.Добавить("ГруппеПользователей",
		"Группе пользователей");
	
	ОбъектСервер = РеквизитФормыВЗначение("Объект");
	Для Каждого Макет Из ОбъектСервер.Метаданные().Макеты Цикл
		Элементы.бг_АдресацияЭлектронныхПисем.СписокВыбора.Добавить(Макет.Имя, Макет.Синоним);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура бг_УстановитьВидимостьЭлементовФормы()
	
	Если Объект.Принудительная Тогда
		
		Элементы.ОтправлятьПартнеру.Видимость = Ложь;
		Элементы.ОтправлятьКонтактнымЛицамРоли.Видимость = Ложь;
		Элементы.РольКонтактногоЛица.Видимость = Объект.бг_АдресацияЭлектронныхПисем = "КонтактнымЛицамРоли";
		Элементы.бг_ГруппаПользователей.Видимость
			= Объект.бг_АдресацияЭлектронныхПисем = "ГруппеПользователей";
		Элементы.ОтправлятьКонтактномуЛицуОбъектаОповещения.Видимость = Ложь;
		Элементы.ДекорацияКонтактноеЛицоПартнераПояснение.Видимость
			= Объект.бг_АдресацияЭлектронныхПисем = "КонтактномуЛицуОбъектаОповещения";
	
	Иначе
		Элементы.бг_ГруппаПользователей.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти