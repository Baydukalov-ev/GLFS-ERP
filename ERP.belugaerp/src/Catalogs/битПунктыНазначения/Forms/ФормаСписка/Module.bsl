
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Партнер") Тогда
		Партнер = Параметры.Партнер;
		СписокПартнеров = Новый СписокЗначений;
		ПартнерыИКонтрагенты.ЗаполнитьСписокПартнераСРодителями(Партнер, СписокПартнеров);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"Партнер",
			СписокПартнеров,
			ВидСравненияКомпоновкиДанных.ВСписке,
			,
			Истина);
			
	ИначеЕсли Параметры.Отбор.Свойство("Клиент") Тогда
		Клиент = Параметры.Отбор.Клиент;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, "Клиент",
			"Видимость", Ложь);
			
	ИначеЕсли Параметры.Отбор.Свойство("Организация") Тогда
		Клиент = Параметры.Отбор.Организация;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"Клиент",
			Клиент,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			Истина);
			
	КонецЕсли;
	
	УстановитьВидимостьКомандыУстановкиМенеджера();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьМенеджера(Команда)
	
	ОткрытьФорму(
		"Справочник.битПунктыНазначения.Форма.ФормаВыбораМенеджера", 
		Новый Структура, 
		ЭтаФорма, 
		,
		,
		,
		Новый ОписаниеОповещения(
			"УстановитьМенеджераЗавершение", 
			ЭтаФорма));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьМенеджераЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМенеджераВПунктахНазначения(ВыбранноеЗначение);
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьМенеджераВПунктахНазначения(ДанныеВыбора)
	
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	Для Каждого ПунктНазначения Из ВыделенныеСтроки Цикл
		РегистрыСведений.бг_МенеджерыПунктовНазначения.СоздатьЗаписьМенеджераПунктаНазначения(
			ПунктНазначения, ДанныеВыбора.Менеджер, , , ДанныеВыбора.ДатаНачалаДействия);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКомандыУстановкиМенеджера()
	
	Элементы.УстановитьМенеджера.Видимость = Пользователи.РолиДоступны("бг_ГрупповоеИзменениеМенеджеровПунктовНазначения");
	
КонецПроцедуры

#КонецОбласти