
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьТекущуюЛицензиию();
	ЗаполнитьДанныеКлиента();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если БылиОшибкиГеокодирования Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("НеОпределятьКоординаты");
	ИначеЕсли ТекущийОбъект.Широта = 0 И ТекущийОбъект.Долгота = 0 Тогда
		Попытка
			ГеокодироватьКоординатыСервер(ТекущийОбъект);
			БылиОшибкиГеокодирования = Ложь;
		Исключение
			БылиОшибкиГеокодирования = Истина;
			ТекстСообщения = НСтр("ru='Не удалось определить координаты по причине %1.'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, ОписаниеОшибки());
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ГеокодироватьКоординаты(Команда)
	Если Не ЗначениеЗаполнено(Объект.Адрес) И Не ЗначениеЗаполнено(Объект.АдресГеокодирования) Тогда
		ТекстСообщения = НСтр("ru='Для выполнения геокодирования необходимо указать адрес.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Объект.АдресГеокодирования");
		Возврат;
	КонецЕсли;                                                    
	Попытка
		ГеокодироватьКоординатыСервер();
		БылиОшибкиГеокодирования = Ложь;
	Исключение
		БылиОшибкиГеокодирования = Истина;
		ТекстСообщения = НСтр("ru='Не удалось определить координаты по причине %1.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, ОписаниеОшибки());
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОрганизациюЕГАИС(Команда)
	
	ДанныеОрганизацииЕГАИС = ДанныеОрганизацииЕГАИСПоДаннымКлиента(Объект.Клиент);
	
	Если ДанныеОрганизацииЕГАИС = Неопределено Тогда
		ИНН = ИННКлиента(Объект.Клиент);
		ЗапроситьОрганизацииЕГАИСПоИНН(ИНН);
	Иначе
		Объект.ОрганизацияЕГАИС = ДанныеОрганизацииЕГАИС.ОрганизацияЕГАИС;
		Объект.КодЕГАИС = ДанныеОрганизацииЕГАИС.КодОрганизацииЕГАИС;
		
		ЭтаФорма.Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

#Область Адрес

&НаКлиенте
Процедура АдресПриИзменении(Элемент)
	
	ДоставкаТоваровКлиент.ПриИзмененииПредставленияАдреса(
		Элемент,
		Объект.Адрес,
		Объект.АдресЗначенияПолей);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресОчистка(Элемент, СтандартнаяОбработка)
	
	ДоставкаТоваровКлиент.ОчиститьПредставлениеАдреса(
		Объект.Адрес,
		Объект.АдресЗначенияПолей,
		Объект.АдресЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДоставкаТоваровКлиент.ОткрытьФормуВыбораАдресаИОбработатьРезультат(
		Элемент,
		Объект,
		Элемент.Имя,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	АдресПриИзменении(Элемент);
	
	ДоставкаТоваровКлиент.АдресДоставкиОбработкаВыбора(
		Элементы,
		Объект,
		Элемент.Имя,
		ВыбранноеЗначение);
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти // Адрес

#Область Прочее

&НаКлиенте
Процедура КонтактноеЛицоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Отбор = Новый Структура("Владелец", ПартнерКонтрагента(Объект.Клиент));
	ОткрытьФорму(
		"Справочник.КонтактныеЛицаПартнеров.ФормаВыбора", 
		Новый Структура("Отбор", Отбор), 
		Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура КлиентПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Клиент) Тогда
		ДанныеКлиента = ДанныеЗаполненияКлиента(Объект.Клиент);
		ЗаполнитьЗначенияСвойств(Объект, ДанныеКлиента);
	Иначе
		Объект.КПП = "";
		Объект.КодЕГАИС = "";
		Объект.ОрганизацияЕГАИС = Неопределено;
		Объект.ОбособленноеПодразделение = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // Прочее

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ГеокодироватьКоординатыСервер(ТекущийОбъект = Неопределено)
	Если Не ТекущийОбъект = Неопределено Тогда
		ОбъектДляИзменения = ТекущийОбъект;
	Иначе
		ОбъектДляИзменения = Объект;
	КонецЕсли;
    Если Не ЗначениеЗаполнено(ОбъектДляИзменения.АдресГеокодирования) Тогда
		ОбъектДляИзменения.АдресГеокодирования = ОбъектДляИзменения.Адрес; 
		Модифицированность = Истина;
	КонецЕсли;
	Справочники.битПунктыНазначения.ГеокодироватьАдрес(ОбъектДляИзменения.АдресГеокодирования, 
		ОбъектДляИзменения.Широта, ОбъектДляИзменения.Долгота);
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТекущуюЛицензиию()
	
	ТекущаяЛицензия = РегистрыСведений.бг_ЛицензииПунктовНазначения.ТекущаяЛицензияПунктаНазначения(Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеКлиента()
	
	ДанныеПараметровВыбора = Параметры.ПараметрыВыбора;
	
	Если ДанныеПараметровВыбора.Свойство("Клиент") 
		И ЗначениеЗаполнено(ДанныеПараметровВыбора.Клиент)
		И (ТипЗнч(ДанныеПараметровВыбора.Клиент) = Тип("СправочникСсылка.Контрагенты")
			ИЛИ ТипЗнч(ДанныеПараметровВыбора.Клиент) = Тип("СправочникСсылка.Организации")) Тогда
		Объект.Клиент = ДанныеПараметровВыбора.Клиент;
		ДанныеКлиента = ДанныеЗаполненияКлиента(Объект.Клиент);
		ЗаполнитьЗначенияСвойств(Объект, ДанныеКлиента);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеЗаполненияКлиента(Клиент)
	
	ДанныеКлиента = Новый Структура("КПП, КодЕГАИС, ОбособленноеПодразделение, НаименованиеПолное, ОрганизацияЕГАИС");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Клиент);
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Справочник.Ссылка КАК Ссылка,
	|	Справочник.КПП КАК КПП,
	|	&НаименованиеПолное КАК НаименованиеПолное,
	|	Справочник.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	ЕСТЬNULL(КлассификаторОрганизацийЕГАИС.Код, """") КАК КодЕГАИС,
	|	ЕСТЬNULL(КлассификаторОрганизацийЕГАИС.Ссылка, ЗНАЧЕНИЕ(Справочник.КлассификаторОрганизацийЕГАИС.ПустаяСсылка)) КАК ОрганизацияЕГАИС
	|ИЗ
	|	&ИмяСправочника КАК Справочник
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|		ПО Справочник.Ссылка = КлассификаторОрганизацийЕГАИС.Контрагент
	|ГДЕ
	|	Справочник.Ссылка = &Ссылка";
	
	Если ТипЗнч(Клиент) = Тип("СправочникСсылка.Контрагенты") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяСправочника", "Справочник.Контрагенты");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НаименованиеПолное", "Справочник.ГоловнойКонтрагент.НаименованиеПолное");
	ИначеЕсли ТипЗнч(Клиент) = Тип("СправочникСсылка.Организации") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяСправочника", "Справочник.Организации");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НаименованиеПолное", """""");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(ДанныеКлиента, Выборка);
	
	Возврат ДанныеКлиента;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПартнерКонтрагента(Контрагент)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "Партнер");
	
КонецФункции

&НаСервереБезКонтекста
Функция ДанныеОрганизацииЕГАИСПоДаннымКлиента(Клиент)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Организации.ИНН КАК ИНН,
		|	Организации.КПП КАК КПП
		|ПОМЕСТИТЬ втИННиКПП
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.Ссылка = &Клиент
		|	И Организации.ИНН <> """"
		|	И Организации.КПП <> """"
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Контрагенты.ИНН,
		|	Контрагенты.КПП
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.Ссылка = &Клиент
		|	И Контрагенты.ИНН <> """"
		|	И Контрагенты.КПП <> """"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КлассификаторОрганизацийЕГАИС.Ссылка КАК ОрганизацияЕГАИС,
		|	КлассификаторОрганизацийЕГАИС.Код КАК КодОрганизацииЕГАИС
		|ИЗ
		|	втИННиКПП КАК втИННиКПП
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
		|		ПО втИННиКПП.ИНН = КлассификаторОрганизацийЕГАИС.ИНН
		|			И втИННиКПП.КПП = КлассификаторОрганизацийЕГАИС.КПП
		|ГДЕ
		|	НЕ КлассификаторОрганизацийЕГАИС.ПометкаУдаления";
	Запрос.УстановитьПараметр("Клиент", Клиент);
	РезультатЗапроса = Запрос.Выполнить();
	
	ДанныеОрганизацииЕГАИС = Неопределено;
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		
		ДанныеОрганизацииЕГАИС = Новый Структура("ОрганизацияЕГАИС, КодОрганизацииЕГАИС");
		ЗаполнитьЗначенияСвойств(ДанныеОрганизацииЕГАИС, ВыборкаДетальныеЗаписи);
	КонецЕсли;
	
	Возврат ДанныеОрганизацииЕГАИС;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИННКлиента(Клиент)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Клиент, "ИНН");
	
КонецФункции

&НаКлиенте
Процедура ЗапроситьОрганизацииЕГАИСПоИНН(ИНН)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить(
		"Операция", ПредопределенноеЗначение("Перечисление.ВидыДокументовЕГАИС.ЗапросДанныхОрганизации"));
	ПараметрыФормы.Вставить("ИмяПараметра", "ИНН");
	ПараметрыФормы.Вставить("ЗначениеПараметра", ИНН);
	
	ОткрытьФорму(
		"ОбщаяФорма.ФормированиеИсходящегоЗапросаЕГАИС",
		ПараметрыФормы,
		ЭтаФорма,
		Ложь);
	
КонецПроцедуры

#КонецОбласти