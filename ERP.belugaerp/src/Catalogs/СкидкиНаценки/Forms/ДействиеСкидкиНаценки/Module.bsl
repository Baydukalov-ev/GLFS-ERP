
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура бг_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)
	
	СкидкиНаценкиСервер.бг_ДобавитьДинамическиеСпискиДополнительныхИсточников(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура бг_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	СкидкиНаценкиКлиентСервер.бг_УстановитьПараметрыДинамическихСписковИсточников(ЭтаФорма,, СкидкаНаценка);
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ОбработкаОповещенияПосле(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ДействиеСкидокНаценок" И Параметр.СкидкаНаценка.Найти(СкидкаНаценка) <> Неопределено Тогда
		
		СкидкиНаценкиКлиентСервер.бг_ОбновитьДинамическиеСписки(ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура бг_ДатаСрезаПриИзмененииПосле(Элемент)
	
	СкидкиНаценкиКлиентСервер.бг_УстановитьПараметрыДинамическихСписковИсточников(ЭтаФорма,, СкидкаНаценка, "ТекущаяДата");
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ДатаСрезаОчисткаПосле(Элемент, СтандартнаяОбработка)
	
	СкидкиНаценкиКлиентСервер.бг_УстановитьПараметрыДинамическихСписковИсточников(ЭтаФорма,, СкидкаНаценка, "ТекущаяДата");
	
КонецПроцедуры

&НаКлиенте
Процедура бг_ИспользованиеВариантОтображенияСкидокНаценокПриИзменении(Элемент)
	
	ИмяСписка = СкидкиНаценкиКлиентСервер.бг_ИмяДинамическогоСписка(
		Элемент.Имя,
		"бг_Использование",
		"ВариантОтображенияСкидокНаценок"
	);
	
	Если НЕ ПустаяСтрока(ИмяСписка) Тогда
		СкидкиНаценкиКлиентСервер.бг_УстановитьПараметрыДинамическихСписковИсточников(ЭтаФорма, ИмяСписка, СкидкаНаценка, "Статус");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура бг_ИсторияДействия(Команда)
	
	Источники = СкидкиНаценкиКлиент.бг_ВыделенныеИсточники(ЭтаФорма, Команда);
	
	Если НЕ Источники.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуИстории(Источники[0]);
	
КонецПроцедуры

&НаКлиенте
Процедура бг_УстановитьСтатусДействует(Команда)
	
	Источники = СкидкиНаценкиКлиент.бг_ВыделенныеИсточники(ЭтаФорма, Команда);
	
	Если НЕ Источники.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуИзмененияСтатуса(
		Источники,
		ПредопределенноеЗначение("Перечисление.СтатусыДействияСкидок.Действует")
	);
	
КонецПроцедуры

&НаКлиенте
Процедура бг_УстановитьСтатусНеДействует(Команда)
	
	Источники = СкидкиНаценкиКлиент.бг_ВыделенныеИсточники(ЭтаФорма, Команда);
	
	Если НЕ Источники.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуИзмененияСтатуса(
		Источники,
		ПредопределенноеЗначение("Перечисление.СтатусыДействияСкидок.НеДействует")
	);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
&После("УстановитьУсловноеОформление")
Процедура бг_УстановитьУсловноеОформление()
	
	СкидкиНаценкиСервер.бг_ДополнитьУсловноеОформлениеСписковДополнительныхИсточников(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
&После("ОбновитьДанныеНаСервере")
Процедура бг_ОбновитьДанныеНаСервере()
	
	СкидкиНаценкиКлиентСервер.бг_УстановитьПараметрыДинамическихСписковИсточников(ЭтаФорма,, СкидкаНаценка);
	
КонецПроцедуры

#КонецОбласти
